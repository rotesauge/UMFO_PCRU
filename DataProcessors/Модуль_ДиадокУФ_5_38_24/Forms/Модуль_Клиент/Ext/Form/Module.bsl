#Если ВебКлиент Тогда

ВызватьИсключение НСтр("ru = 'Недопустимый режим работы (Веб-клиент).'");

#Иначе

#Область ПЕРМЕННЫЕ_МОДУЛЯ

&НаКлиенте
Перем МассивСтруктурКонтент;
&НаКлиенте
Перем МаксимальноеКоличествоЗаданий;
&НаКлиенте
Перем КэшПротоКонтента;
&НаКлиенте
Перем КэшМетрики_Клиент;

#КонецОбласти

#Область ПЕРМЕННЫЕ_ПЛАТФОРМЫ

&НаКлиенте
Перем Платформа Экспорт, Манифест Экспорт;

&НаКлиенте
Перем НомерИтерацииВызоваМодуля;

&НаСервере
Перем ОбработкаОбъект;

#КонецОбласти

#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ПЛАТФОРМЫ

&НаКлиенте
Функция МетодКлиента(ИмяМодуля= "", ИмяМетода, 
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL)
	
	Возврат  Платформа.МетодКлиента(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4, 
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция МетодСервера(Знач ИмяМодуля= "", Знач ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL)
	
	Возврат ОбработкаОбъект().МетодСервера(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4, 
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция ОбработкаОбъект()
	
	Если ОбработкаОбъект = Неопределено Тогда
		
		СтруктураОбработки= ПолучитьИзВременногоХранилища(Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
		
		Если СтруктураОбработки <> Неопределено Тогда
			ОбработкаОбъект= СтруктураОбработки.ОбработкаОбъект;
		КонецЕсли;
		
		Если ОбработкаОбъект = Неопределено Тогда
			
			ОбработкаОбъект= РеквизитФормыВЗначение("Объект");
			
			Попытка
				ПоместитьВоВременноеХранилище(Новый Структура("ОбработкаОбъект", ОбработкаОбъект), Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
			Исключение КонецПопытки;
		
		Иначе
			ОбработкаОбъект.ПараметрыКлиентСервер= Объект.ПараметрыКлиентСервер;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Процедура Инициализировать(ИмяМодуля) Экспорт
	
	Если НомерИтерацииВызоваМодуля = Неопределено Тогда
		НомерИтерацииВызоваМодуля= 0;
	КонецЕсли;
	
	НомерИтерацииВызоваМодуля= НомерИтерацииВызоваМодуля + 1;
	
	Если Манифест = Неопределено Тогда
		Платформа.ЗаполнитьМанифест(ЭтаФорма, ИмяМодуля);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПеременные() Экспорт
	
	// Разрывается циклическая ссылка, для того чтобы исключить утечку памяти.
	// Модуль может быть вызван повторно во вложенных методах,
	// поэтому очищаем переменные, ТОЛЬКО если это начальная итерация вызова модуля.
	
	НомерИтерацииВызоваМодуля= НомерИтерацииВызоваМодуля - 1;
	
	Если НомерИтерацииВызоваМодуля = 0 Тогда
		Платформа= 					  Неопределено;
		Объект.ПараметрыКлиентСервер= Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область МАНИФЕСТ

&НаКлиенте
Функция ФункцииМодуля() Экспорт
	
	Результат = Новый Структура;
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "СтрокаКоличествоДокументов");
	Платформа.ДобавитьФункциюВМанифест(Результат, "СформироватьПредставлениеКнопкиСоздатьДокументВ1С");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеПродавца");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеПокупателя");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеПодразделения");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеЭД", "ЭДОбъект");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьСтруктуруПредставленияЭД");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредсталениеНомераЭД");
	Платформа.ДобавитьФункциюВМанифест(Результат, "РасшифровкаСтатуса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеСтатуса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеСтатусаРоуминг");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеСуммы");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеСуммыНДС");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеПериодаДД");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеТипаРезолюции");
	Платформа.ДобавитьФункциюВМанифест(Результат, "СформироватьHTMLПредставлениеОшибок");
	Платформа.ДобавитьФункциюВМанифест(Результат, "СформироватьHTMLПредставлениеРезолюций");
	Платформа.ДобавитьФункциюВМанифест(Результат, "СформироватьТекстОшибкиДиадок");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЭтоИнформацияОбОшибкахВалидации");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеAuthor");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеTargetUser");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеСтатусаСогласования");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьФИОСИнициалами");
	Платформа.ДобавитьФункциюВМанифест(Результат, "НайтиСтрокиВМассивеСтруктур");
	Платформа.ДобавитьФункциюВМанифест(Результат, "BoxID_2_Организация_Форма");
	Платформа.ДобавитьФункциюВМанифест(Результат, "CounteragentBoxID_2_Контрагент_Форма");
	Платформа.ДобавитьФункциюВМанифест(Результат, "GetCounteragentListByStatus", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьОсновнуюФормуОбъекта");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьОписаниеФормы");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ВернутьМассивВыбранныхФайлов");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьИмяФормыДокумента");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьDocumentID_2_Документ");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЭтоФормализованныйДокумент");
	Платформа.ДобавитьФункциюВМанифест(Результат, "НайтиОрганизациюВИерархииОрганизацийDiadoc", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "НайтиОрганизациюВИерархииОрганизацийDiadocРекурсия");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ТекстИнформацииОНовомФормате", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "СписокОбработокДереваКонфигурации", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ДатаГоризонта", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ИдентификаторСвойства", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПроверитьФорматДатыПрото");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьСписокФильтровДляДокументовДиадок");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьФилиаловDepartmentId");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьDepartmentByKpp");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ВернутьМассивDepartmentId");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьСтруктуруЗадачи");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьМассивАсинхронныхЗадач");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ВернутьВыборкуРНКИСчетовФактурДиадок");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьЗначениеКонтентаКэш");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьЯщикДиадокДляОрганизации");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьОрганизацииНезаблокированныеПоAPI");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЭтоОшибкаОтсутствиеРегистрацииФНС", ,"НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ИспользуетсяСертификатПоГОСТ2001");
	Платформа.ДобавитьФункциюВМанифест(Результат, "НайтиСтрокиКонтекстДиадокаПоОтбору");
	Платформа.ДобавитьФункциюВМанифест(Результат, "РазвернутьGUID");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьURLНаДокументДиадока");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьПутьКWEBСерверу");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ОтправитьНаОбработку");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьКраткоеИмяФайла");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ОтправитьПринятьПриглашениеКонтрагенту");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадокСервер");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадок");
	Платформа.ДобавитьФункциюВМанифест(Результат, "НайтиПодходящийСчетФактуруИзРНК");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьПодходящуюСФ");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьМассивТиповДокументов");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ВариантыИспользованияПроксиСервера");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЗначениеРеквизитаОбъекта");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЗначенияРеквизитовОбъекта");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЗначениеРеквизитаОбъектов");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЗначенияРеквизитовОбъектов");
	Платформа.ДобавитьФункциюВМанифест(Результат, "СравнитьВерсии");
	Платформа.ДобавитьФункциюВМанифест(Результат, "НеобходимоОграничениеНаОтправку", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ТипДокументаУПД");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ТипДокументаУКД");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ТипДокументаИУПД");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ФункцияУПД");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЭтоУПД_ТипаСЧФДОП");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЭтоУПД_ТипаДОП");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЭтоУПД_ТипаСЧФ");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЭтоУКД_ТипаКСЧФДИС");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЭтоУКД_ТипаКСЧФ");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ОбластьПолномочийУПД_ОтветственныйЗаПодписаниеСЧФ");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ОбластьПолномочийУПД_ЛицоСовершившееСделку");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ОбластьПолномочийУПД_ОтветственныйЗаОформление");
	Платформа.ДобавитьФункциюВМанифест(Результат, "УдалитьЛишниеНулиИНН");
	Платформа.ДобавитьФункциюВМанифест(Результат, "СтруктураФИО");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЗаменитьНедопустимыеСимволыИмениФайла");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ОформитьОшибкиВHTML");
	Платформа.ДобавитьФункциюВМанифест(Результат, "HTMLТекстОшибкиОтправки");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ТолькоЦифрыВСтроке");
	Платформа.ДобавитьФункциюВМанифест(Результат, "НовыйОбъектКомпонентыДиадока");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПодключитьКомпонентуДиадока");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЭтоТестоваяОрганизация");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Torg12SellerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Torg12BuyerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_AcceptanceCertificateSellerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_AcceptanceCertificateBuyerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_InvoiceContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_InvoiceCorrectionContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_UtdSellerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_UtdBuyerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_UcdSellerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_UcdBuyerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_TovTorgSellerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_TovTorgBuyerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Act552SellerContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Act552BuyerContent");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_OrganizationInfo");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_ExtendedOrganizationInfo");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Shipper");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_ShipperOrConsigneeInfo");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_AddressInfo");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_AdditionalInfoId");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_StructedAdditionalInfo");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_InvoiceForCorrectionInfo");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_InvoiceRevisionInfo");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_OriginalInvoice");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_PaymentDocument");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Torg12Item");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_AcceptanceCertificateItem");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_InvoiceItem");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_InvoiceCorrectionItem");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_InvoiceTable");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_ExtendedInvoiceItem");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_InvoiceCorrectionTable");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_ExtendedInvoiceCorrectionItem");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_TovTorgTable");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_TovTorgItem");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Act552WorkDescription");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Act552WorkItem");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_CustomDeclaration");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_InvoiceItemFields");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_TransferInfo");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_TovTorgTransferInfo");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Act552TransferInfo");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_TransferBase");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Waybill");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_EventContent");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_CorrectionBase");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_GroundInfo");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Organization");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Signer");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_ExtendedSigner");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_ExtendedSignerDetails");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Signature");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_PersonalCertificate");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_ExtendedSignerDetailsToPost");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Official");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Employee");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_OtherIssuer");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Attorney");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Torg12Commons");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Torg12Totals");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_InvoiceTotals");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_AmountsDiff");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_NonformalizedDocumentToSend");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_ServiceDetails");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_NonformalizedProforma");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_PriceList");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_AcceptanceCertificate");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Torg12");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Новый_Contract");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "ВыделитьСлово");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЭДО_БиблиотекаКартинок", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьЗначениеРеквизитаОбъекта");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЗаполненныйДиалогВыбораФайла");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ДокументНеСогласованРанее");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ДокументПроведен");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьСвязанныйInvoice");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЕстьСвойствоCOMОбъекта");
	Платформа.ДобавитьФункциюВМанифест(Результат, "БазовыйФорматВерсииКонтента");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьТипКонтента");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ФункцияДокументаДляСбораКонтента");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ЗапросНаПолучениеФайла");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьProto");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПротоКонтентДокумента");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПолучитьСтруктуруСодержанияДокумента");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ОрганизацияПоУмолчанию");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ДействиеПриОбновленииМодуляФайловыйВариант");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "Kontur_API_PortalBills_Metas");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Kontur_API_PortalBills_pdf");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "Компонента_HttpTransport");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Преобразование_МассивВСтроку");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Преобразование_СтрокуВМассивСлов");
	Платформа.ДобавитьФункциюВМанифест(Результат, "СвойствоСтруктуры");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПараметрыОповещенияВСтрокуHTMLСообщения");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПараметрыОповещенияИзСтрокиHTMLСообщения");
	Платформа.ДобавитьФункциюВМанифест(Результат, "МассивСтруктур_Найти_НаКлиенте");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "ТехническаяИнформация", , "НаВремяСеанса"); 
	Платформа.ДобавитьФункциюВМанифест(Результат, "АдресаИнтернетРесурсов", , "НаВремяСеанса");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "БазовыеФорматы", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ТипыКонтента", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "НовоеОписаниеФорматаЭлектронногоДокумента");
	
	Платформа.ДобавитьФункциюВМанифест(Результат, "НовоеОписаниеДокументаПакета");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ФорматЭлектронногоДокумента");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеТипаЭлектронногоДокумента");
	
	// {Метрики
	Платформа.ДобавитьФункциюВМанифест(Результат, "ТопикиМетрики", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ИспользоватьМетрики");
	Платформа.ДобавитьФункциюВМанифест(Результат, "МаксимальныйРазмерСтека");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ОписаниеПриложения", , "НаВремяСеанса");
	Платформа.ДобавитьФункциюВМанифест(Результат, "Метрика_Категории", , "НаВремяСеанса");
	// }Метрики
	
	// {Настройка формата отправки документов
	Платформа.ДобавитьФункциюВМанифест(Результат, "НастройкаФорматаОтправкиДокументов");
	Платформа.ДобавитьФункциюВМанифест(Результат, "НастройкаФорматаОтправкиДокументовПоУмолчанию");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ВариантОтправкиСчетовФактурПоКлючуНастройки");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеВариантаОтправкиСчетовФактур");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ВариантОтправкиНакладныхПоКлючуНастройки");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ВариантОтправкиНакладныхПоУмолчанию");
	Платформа.ДобавитьФункциюВМанифест(Результат, "ПредставлениеВариантаОтправкиНакладных");
	Платформа.ДобавитьФункциюВМанифест(Результат, "СписокВыбораВариантовОтправкиСчетовФактур");
	Платформа.ДобавитьФункциюВМанифест(Результат, "СписокВыбораВариантовОтправкиНакладных");
	// }Настройка формата отправки документов
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Описывает возможные значения базовых версий форматов контента.
// 
// Возвращаемое значение:
//  Структура - см. тело функции.
//
&НаКлиенте
Функция БазовыеФорматы() Экспорт
	
	Результат = МетодСервера(, "БазовыеФорматы");
	
	Возврат Результат;
	
КонецФункции

// Описывает возможные значения типов протоконтента.
// 
// Возвращаемое значение:
//  Структура - см. тело функции.
//
&НаКлиенте
Функция ТипыКонтента() Экспорт
	
	Результат = МетодСервера(, "ТипыКонтента");
	
	Возврат Результат;
	
КонецФункции

// Инициализирует структуру с описанием электронного документа,
// которая используется для передачи в форму документа
// 
// Возвращаемое значение:
//  Структура - см. тело функции.
//
&НаКлиенте
Функция НовоеОписаниеДокументаПакета() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("CounteragentBoxID");
	Результат.Вставить("НомерДокумента");
	Результат.Вставить("ДатаДокумента");
	Результат.Вставить("РасширениеФайлаДиадок");
	Результат.Вставить("DocumentType");
	Результат.Вставить("Документ1С");
	Результат.Вставить("ТипДокумента");
	Результат.Вставить("ДопСведения", "");
	Результат.Вставить("DocumentID");
	Результат.Вставить("Проведен", Истина);
	Результат.Вставить("IdСтроки");
	Результат.Вставить("ЭтоТекущиеДанные", Истина);
	Результат.Вставить("НомерЗаказа", "");
	Результат.Вставить("ОшибкаВалидации", "");
	Результат.Вставить("ЭтоФайл", Ложь);
	Результат.Вставить("ФункцияУПД");
	Результат.Вставить("ТипКонтента");
	Результат.Вставить("Вкл", Истина);
	Результат.Вставить("isTest", Ложь);
	Результат.Вставить("DepartmentID");
	Результат.Вставить("ВнешняяПечатнаяФорма");
	Результат.Вставить("СвойстваФайла");
	Результат.Вставить("ДанныеФайла");
	Результат.Вставить("ФорматОтправки");
	
	Возврат Результат;
	
КонецФункции

// Контракт на структуру, описывающую формат электронного документа.
// ВНИМАНИЕ! Результат функции должен полностью соответствовать методу
// Модуль_ИнтеграцияУниверсальный.НовоеОписаниеФорматаЭлектронногоДокумента()
//
// Возвращаемое значение:
//  Структура - см. тело функции.
//
&НаКлиенте
Функция НовоеОписаниеФорматаЭлектронногоДокумента() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяТипа");
	Результат.Вставить("ВерсияФормата");
	Результат.Вставить("ФункцияДокумента");
	Результат.Вставить("ТипКонтента");
	
	Возврат Результат;
	
КонецФункции

// Получает описание формата из объекта Document.
//
// Параметры:
//  ЭДОбъект - COMОбъект - Объект, производный от Document;
//
// Возвращаемое значение:
//  Структура - см. функцию НовоеОписаниеФорматаЭлектронногоДокумента.
//
&НаКлиенте
Функция ФорматЭлектронногоДокумента(ЭДОбъект) Экспорт
	
	Результат = НовоеОписаниеФорматаЭлектронногоДокумента();
	
	Результат.ВерсияФормата		 = ЭДОбъект.Version;
	Результат.ФункцияДокумента	 = ЭДОбъект.DocumentFunction;
	Результат.ИмяТипа			 = ЭДОбъект.TypeNamedId;
	
	Результат.ТипКонтента = МетодКлиента("Модуль_Клиент", "ПолучитьТипКонтента"
								, ЭДОбъект.Version
								, ЭДОбъект.type);
	
	Возврат Результат;
	
КонецФункции

// Возвращает пользовательское представление формата
// электронных документов.
//
// Параметры:
//  ФорматЭД - Структура - см. НовоеОписаниеФорматаЭлектронногоДокумента;
// 
// Возвращаемое значение:
//  Строка - представление формата.
//
&НаКлиенте
Функция ПользовательскоеПредставлениеФормата(ФорматЭД)
	
	ТипыДокументов = Новый Соответствие;
	ТипыДокументов.Вставить("UniversalTransferDocument"				, НСтр("ru = 'УПД'; en = 'UTD'")); // Universal transfer document
	ТипыДокументов.Вставить("UniversalTransferDocumentRevision"		, НСтр("ru = 'Исправление УПД'; en = 'Revision of UTD'"));
	ТипыДокументов.Вставить("UniversalCorrectionDocument"			, НСтр("ru = 'УКД'; en = 'UAD'")); // Universal adjustment document
	ТипыДокументов.Вставить("UniversalCorrectionDocumentRevision"	, НСтр("ru = 'Исправление УКД'; en = 'Revision of UAD'"));
	ТипыДокументов.Вставить("Invoice"								, НСтр("ru = 'Счет-фактура'; en='Tax invoice'"));
	ТипыДокументов.Вставить("InvoiceRevision"						, НСтр("ru = 'Исправление счета-фактуры'; en='Revision of tax invoice'"));
	ТипыДокументов.Вставить("InvoiceCorrection"						, НСтр("ru = 'Корректировочный счет-фактура'; en = 'Corrective tax invoice'"));
	ТипыДокументов.Вставить("InvoiceCorrectionRevision"				, НСтр("ru = 'Исправление корректировочного СФ'; en = 'Revision of corrective tax invoice'"));
	ТипыДокументов.Вставить("XmlTorg12"								, НСтр("ru = 'ТОРГ-12'; en='TORG-12'"));
	ТипыДокументов.Вставить("Torg12"								, НСтр("ru = 'ТОРГ-12'; en='TORG-12'"));
	ТипыДокументов.Вставить("XmlAcceptanceCertificate"				, НСтр("ru = 'Акт'; en='Act'"));
	ТипыДокументов.Вставить("AcceptanceCertificate"					, НСтр("ru = 'Акт'; en='Act'"));
	ТипыДокументов.Вставить("ProformaInvoice"						, НСтр("ru = 'Счет'; en='Proforma invoice'"));
	ТипыДокументов.Вставить("Nonformalized"							, НСтр("ru = 'Неформализованный документ'; en='Nonformalized document'"));
	ТипыДокументов.Вставить("TrustConnectionRequest"				, НСтр("ru = 'Приглашение к ЭДО'; en='Trust connection request'"));
	ТипыДокументов.Вставить("PriceList"								, НСтр("ru = 'Прайс-лист'; en='Price list'"));
	ТипыДокументов.Вставить("PriceListAgreement"					, НСтр("ru = 'Протокол согласования цены'; en='Price approval protocol'"));
	ТипыДокументов.Вставить("CertificateRegistry"					, НСтр("ru = 'Реестр сертификатов'; en='Certificate registry'"));
	ТипыДокументов.Вставить("ReconciliationAct"						, НСтр("ru = 'Акт сверки'; en='Reconciliation act'"));
	ТипыДокументов.Вставить("Contract"								, НСтр("ru = 'Договор'; en='Contract'"));
	ТипыДокументов.Вставить("Torg13"								, НСтр("ru = 'ТОРГ-13'; en='TORG-13'"));
	ТипыДокументов.Вставить("ServiceDetails"						, НСтр("ru = 'Детализация'; en='Service details'"));
	ТипыДокументов.Вставить("SupplementaryAgreement"				, НСтр("ru = 'Доп. соглашение к договору'; en='Supplementary agreement'"));
	ТипыДокументов.Вставить("StorageInventoryAcceptanceCertificate"	, НСтр("ru = 'Акт МХ-1'; en='Act MH-1'"));
	ТипыДокументов.Вставить("ReturnInventoryAcceptanceCertificate"	, НСтр("ru = 'Акт МХ-3'; en='Act MH-3'"));
	ТипыДокументов.Вставить("PowerOfAttorney"						, НСтр("ru = 'Доверенность'; en='Letter of authority'"));
	
	Результат = ТипыДокументов[ФорматЭД.ИмяТипа];
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Результат = ФорматЭД.ИмяТипа;
	КонецЕсли;
	
	ПредставлениеФункции = ПользовательскоеПредставлениеФункцииДокумента(ФорматЭД.ФункцияДокумента);
	
	Если ЗначениеЗаполнено(ПредставлениеФункции) Тогда
		
		Результат = Результат + " " + ПредставлениеФункции;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает представление типа электронных документов
// Для неформализованного документа возвращается имя файла
// Дополняет имя признаками тестовый и зашифрован
//
// Параметры:
//  Document - ComОбъект - объект электронного документа
// 
// Возвращаемое значение:
//  Строка - представление типа
//
&НаКлиенте
Функция ПредставлениеТипаЭлектронногоДокумента(Document) Экспорт
	
	ФорматЭД = ФорматЭлектронногоДокумента(Document);
	
	Если ФорматЭД.ИмяТипа = "Nonformalized" Тогда
		Результат = Document.FileName;
	Иначе
		Результат = ПользовательскоеПредставлениеФормата(ФорматЭД);
	КонецЕсли;
	
	Результат = Результат + ДополнениеКПредставлениюЭД(Document);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтразитьДокументВУчете(ФормаВладелец, ЭДОбъект) Экспорт 
	
	ПротоКонтент = МетодКлиента("Модуль_Клиент", "ПолучитьProto", ЭДОбъект, Ложь);
	
	Отказ = Ложь;
	
	ПроверитьКонтентПередОтражениемВУчете(ПротоКонтент, Отказ);
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	ИдентификаторЭД = ПолныйИдентификаторДокумента(ЭДОбъект.documentId, ЭДОбъект.OrganizationId);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Контрагент"		, ФормаВладелец.Контрагент);
	ПараметрыФормы.Вставить("Организация"		, ФормаВладелец.Организация);
	ПараметрыФормы.Вставить("ПротоКонтент"		, ПротоКонтент);
	ПараметрыФормы.Вставить("ИдентификаторЭД"	, ИдентификаторЭД);
	
	МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтраженияДокументаВУчете", ПараметрыФормы, ФормаВладелец, "СоздатьДокументУчетаЗавершение");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПользовательскоеПредставлениеФункцииДокумента(ФункцияДокумента)
	
	ФункцииДокументов = Новый Соответствие;
	ФункцииДокументов.Вставить("СЧФДОП"	, НСтр("ru = 'Счф Доп'; en='Invoice and basic'"));
	ФункцииДокументов.Вставить("СЧФ"	, НСтр("ru = 'Счф'; en='Invoice'"));
	ФункцииДокументов.Вставить("ДОП"	, НСтр("ru = 'Доп'; en='Basic'"));
	ФункцииДокументов.Вставить("КСЧФДИС", НСтр("ru = 'Ксчф Дис'; en='Invoice and basic'"));
	ФункцииДокументов.Вставить("КСЧФ"	, НСтр("ru = 'Ксчф'; en='Invoice'"));
	ФункцииДокументов.Вставить("ДИС"	, НСтр("ru = 'Дис'; en='Basic'"));
	
	Результат = ФункцииДокументов[ФункцияДокумента];
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьКонтентПередОтражениемВУчете(ПротоКонтент, Отказ)
	
	ТипКонтента = ВРег(ПротоКонтент.ТипКонтента);
	
	Если ТипКонтента = ВРег("InvoiceCorrectionContent") Тогда 
		
		ТекстСообщения = НСтр("ru = 'Отражение в учете счета-фактуры в устаревшем формате не поддерживается!'");
		ПоказатьПредупреждение(, ТекстСообщения, 60);
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

// Функция - Полный идентификатор документа
//
// Параметры:
//   ИдентификаторТранзакции	 - Строка - messageId+entityId;
//   ИдентификаторОрганизации	 - Строка - boxId;
// 
// Возвращаемое значение:
//  Структура - см. тело функции.
//
&НаКлиентеНаСервереБезКонтекста
Функция ПолныйИдентификаторДокумента(ИдентификаторТранзакции, ИдентификаторОрганизации)
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторТранзакции"	, ИдентификаторТранзакции);
	Результат.Вставить("ИдентификаторОрганизации"	, ИдентификаторОрганизации);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПодключениеВнешнейКомпоненты

// Подключает компоненту Диадока к процессу программы
// 
// Возвращаемое значение:
//  Булево - Истина, если после установки компоненты требуется перезапуск программы
//
&НаКлиенте
Функция ПодключитьКомпонентуДиадока() Экспорт
	
	ТребуемаяВерсия = МетодСервера(, "ВерсияВнешнейКомпоненты");
	КомпонентаУжеПодключена = КомпонентаУжеПодключенаЧерезРегистрацию();
	
	Если КомпонентаУжеПодключена Тогда
		
		ТекущаяВерсия			 = ВерсияУстановленнойКомпонентыДиадока();
		ТребуетсяПерезапуск		 = (ТекущаяВерсия <> ТребуемаяВерсия);
		ИспользуетсяРегистрация	 = (ТекущаяВерсия = ТребуемаяВерсия);
		
	Иначе
		
		Попытка
			ТребуетсяПерезапуск = ПодключитьКомпонентуДиадокаБезРегистрации(ТребуемаяВерсия);
			ИспользуетсяРегистрация = Ложь;
		Исключение
			ТребуетсяПерезапуск = ПодключитьКомпонентуДиадокаЧерезРегистрацию(ТребуемаяВерсия);
			ИспользуетсяРегистрация = Истина;
		КонецПопытки;
		
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ВерсияКомпоненты"		, ТребуемаяВерсия);
	Результат.Вставить("ТребуетсяПерезапуск"	, ТребуетсяПерезапуск);
	Результат.Вставить("ИспользуетсяРегистрация", ИспользуетсяРегистрация);
	
	Возврат Результат;
	
КонецФункции

// Создает новый com объект внешней компоненты Диадока
//
// Возвращаемое значение:
//  COMОбъект - Объект компоненты Диадока
//
&НаКлиенте
Функция НовыйОбъектКомпонентыДиадока() Экспорт
	
	ИдентификаторКласса = ИдентификаторКлассаКомпонентыДиадока();
	
	Результат = Новый COMОбъект(ИдентификаторКласса);
	
	Возврат Результат;
	
КонецФункции

// Подключает компоненту Диадока без регистрации в реестре MS Windows
//
// Параметры:
//  ТребуемаяВерсия - Строка - версия компоненты, которую нужно подключить
// 
// Возвращаемое значение:
//  Булево - Истина, если после установки компоненты требуется перезапуск программы
//
&НаКлиенте
Функция ПодключитьКомпонентуДиадокаБезРегистрации(ТребуемаяВерсия)
	
	ИмяФайла		 = ИмяФайлаКомпонентыДиадока(ТребуемаяВерсия);
	ИмяКомпоненты	 = ИмяКомпонентыДиадока();
	
	ЗаписатьФайлКомпонентыДиадокаНаДиск(ИмяФайла);
	
	КомпонентаПодключена = ПодключитьВнешнююКомпоненту(ИмяФайла, ИмяКомпоненты, ТипВнешнейКомпоненты.Native);
	
	Если Не КомпонентаПодключена Тогда
		ТекстОшибки = НСтр("ru = 'Не удалось подключить внешнюю компоненту'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ТекущаяВерсия = ВерсияУстановленнойКомпонентыДиадока();
	
	ТребуетсяПерезапуск = ТекущаяВерсия <> ТребуемаяВерсия;
	
	Возврат ТребуетсяПерезапуск;
	
КонецФункции

// Регистрирует компоненту Диадока в реестре MS Windows,
// а затем подключает её к процессу программы
//
// Параметры:
//  ТребуемаяВерсия - Строка - версия компоненты, которую нужно подключить
// 
// Возвращаемое значение:
//  Булево - Истина, если после установки компоненты требуется перезапуск программы
//
&НаКлиенте
Функция ПодключитьКомпонентуДиадокаЧерезРегистрацию(ТребуемаяВерсия)
	
	ИмяФайла = ИмяФайлаКомпонентыДиадока(ТребуемаяВерсия);
	ИдентификаторКомпоненты = ИдентификаторОбъектаКомпонентыДиадока();
	
	// Пробуем подключить компоненту по ProgID, чтобы узнать её версию
	КомпонентаПодключена = ПодключитьВнешнююКомпоненту(ИдентификаторКомпоненты);
	ТекущаяВерсия		 = ВерсияУстановленнойКомпонентыДиадока();
	
	ТребуетсяПерезапуск	 = ЗначениеЗаполнено(ТекущаяВерсия) И ТекущаяВерсия <> ТребуемаяВерсия;
	ТребуетсяРегистрация = (ТекущаяВерсия <> ТребуемаяВерсия);
	
	Если ТребуетсяРегистрация Тогда
		
		ДождатьсяЗавершенияРегистрации = Не ТребуетсяПерезапуск;
		
		ЗаписатьФайлКомпонентыДиадокаНаДиск(ИмяФайла);
		
		ЗарегистрироватьВнешнююКомпоненту(ИмяФайла, ДождатьсяЗавершенияРегистрации);
		
		Если Не ТребуетсяПерезапуск Тогда
			
			// Проверяем что компоненту удалось зарегистрировать в реестре
			КомпонентаПодключена = ПодключитьВнешнююКомпоненту(ИдентификаторКомпоненты);
			
			Если Не КомпонентаПодключена Тогда
				ТекстОшибки = НСтр("ru = 'Не удалось подключить внешнюю компоненту'");
				ВызватьИсключение ТекстОшибки;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТребуетсяПерезапуск;
	
КонецФункции

// Регистрирует внешнюю компоненту в профиль пользователя реестра MS Windows
//
// Параметры:
//  ИмяФайла            - Строка - имя файла внешней компоненты
//  ДождатьсяЗавершения - Булево - Истина, если необходимо дождаться
//                                 завершения регистрации компоненты
//
&НаКлиенте
Процедура ЗарегистрироватьВнешнююКомпоненту(ИмяФайла, ДождатьсяЗавершения)
	
	// Компонента регистрируется только в профиль пользователя
	ЗапуститьПриложение("RegSvr32 """ + ИмяФайла + """ /s /n /i:user");
	
	Если ДождатьсяЗавершения Тогда
		
		// В Windows XP процесс RegSvr32 после регистрации не завершается,
		// поэтому использовать параметр ДождатьсяЗавершения
		// метода ЗапуститьПриложение() нельзя
		Таймаут(5000);
		
	КонецЕсли;
	
КонецПроцедуры

// Записывает на диск файл внешней компоненты
// Добавляет в имя файла версию и архитектуру компоненты
//
// Параметры:
//  ИмяФайла - Строка - полное имя файла, в который нужно записать
//                      двоичные данные компоненты
//
&НаКлиенте
Процедура ЗаписатьФайлКомпонентыДиадокаНаДиск(ИмяФайла)
	
	Файл = Новый Файл(ИмяФайла);
	
	Если Файл.Существует() Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьКаталог(Файл.Путь);
	
	ИмяМакета = ИмяМакетаКомпонентыДиадока();
	
	ДвоичныеДанные = МетодСервера(, "МакетМодуля", ИмяМакета);
	
	ДвоичныеДанные.Записать(ИмяФайла);
	
КонецПроцедуры

// Проверяет наличие подключенной к процессу программы компоненты
// зарегестрированной в реестре MS Windows
// 
// Возвращаемое значение:
//  Булево - Истина, если внешняя компонента была подключена
//
&НаКлиенте
Функция КомпонентаУжеПодключенаЧерезРегистрацию()
	
	ИдентификаторКомпоненты = ИдентификаторОбъектаКомпонентыДиадока();
	
	Попытка
		ОбъектКомпоненты = Новый(ИдентификаторКомпоненты);
	Исключение
		Ошибка = ИнформацияОбОшибке();
	КонецПопытки;
	
	Результат = ОбъектКомпоненты <> Неопределено;
	
	Возврат Результат;
	
КонецФункции

// Возвращает версию внешней компоненты,
// которая была подключена к процессу программы ранее
// или зарегистрирована в реестре MS Windows
// 
// Возвращаемое значение:
//  Строка       - версия внешней компоненты;
//  Неопределено - если компонента не подключена;
//
&НаКлиенте
Функция ВерсияУстановленнойКомпонентыДиадока()
	
	Результат = Неопределено;
	
	Попытка
		ОбъектКомпоненты = НовыйОбъектКомпонентыДиадока();
		Результат = ОбъектКомпоненты.GetVersion();
	Исключение
		Ошибка = ИнформацияОбОшибке();
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Возвращает архитектуру компоненты, которую нужно использовать
// зависит от разрядности программы
// 
// Возвращаемое значение:
//  Строка
//
&НаКлиенте
Функция АрхитектураКомпонентыДиадока()
	
	СисИнфо = Новый СистемнаяИнформация;
	
	Если Найти(Строка(СисИнфо.ТипПлатформы), "64") > 0 Тогда
		Результат = "x86_64";
	Иначе
		Результат = "x86";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Имя файла компоненты во временном каталоге пользователя,
// файл расположен в папке с номером версии и архитектурой
//
// Параметры:
//  ВерсияКомпоненты - Строка - версия, которую нужно записать
//                              в имя папки с файлом компоненты
// 
// Возвращаемое значение:
//  Строка
//
&НаКлиенте
Функция ИмяФайлаКомпонентыДиадока(ВерсияКомпоненты)
	
	Архитектура = АрхитектураКомпонентыДиадока();
	ИмяКомпоненты = ИмяКомпонентыДиадока();
	
	// Имя файла должно быть точно таким же как в манифесте внутри компоненты,
	// поэтому версию и архитектуру записываем в имя папки
	ИмяФайла = ИмяКомпоненты + ".dll";
	ИмяПапки = ИмяКомпоненты + "_" + СтрЗаменить(ВерсияКомпоненты, ".", "_") + "_" + Архитектура;
	
	Результат = ОбъединитьПути(КаталогВременныхФайлов(), ИмяПапки, ИмяФайла);
	
	Возврат Результат;
	
КонецФункции

// Имя макета компоненты с учетом архитектуры программы
//
// Возвращаемое значение:
//  Строка
//
&НаКлиенте
Функция ИмяМакетаКомпонентыДиадока()
	
	Архитектура = АрхитектураКомпонентыДиадока();
	ИмяКомпоненты = ИмяКомпонентыДиадока();
	
	Результат = ИмяКомпоненты + "_" + Архитектура;
	
	Возврат Результат;
	
КонецФункции

// Имя компоненты, как оно записано в манифесте файла dll
// 
// Возвращаемое значение:
//  Строка
//
&НаКлиенте
Функция ИмяКомпонентыДиадока()
	
	Возврат "AddInDiadocAPI";
	
КонецФункции

// Идентификатор объекта внешней компоненты
// в виде ProgID реестра MS Windows
// 
// Возвращаемое значение:
//  Строка
//
&НаКлиенте
Функция ИдентификаторОбъектаКомпонентыДиадока()
	
	Возврат "AddIn.DiadocInvoiceAPI";
	
КонецФункции

// Идентификатор класса внешней компоненты
// в виде ProgID класса COM
// 
// Возвращаемое значение:
//  Строка
//
&НаКлиенте
Функция ИдентификаторКлассаКомпонентыДиадока()
	
	Возврат "Diadoc.Api.InvoiceApi";
	
КонецФункции

#КонецОбласти

//{ УПРАВЛЕНИЕ ФОРМОЙ

	&НаКлиенте
	Функция СформироватьПредставлениеКнопкиСоздатьДокументВ1С(Документ1С, ЭДОбъект, МассивСсылокРНК, ПодходящаяСФ) Экспорт 
		
		ПредставлениеКнопки=	"";
		
		Если НЕ ЗначениеЗаполнено(Документ1С) Тогда
			
			Если ЭДОбъект.Direction = "Inbound" Тогда
				Если ЛЕВ(ЭДОбъект.Type, 7) = "Invoice" ИЛИ ЭДОбъект.Type = "UniversalTransferDocument" И ТипДокументаУПД(ЭДОбъект.Function) = ТипДокументаУПД("СЧФ") Тогда
					Если ЭДОбъект.Type = "InvoiceRevision" Тогда
						СокращенноеИмяДокумента= "ИСФ";
					ИначеЕсли ЭДОбъект.Type = "InvoiceCorrection" Тогда
						СокращенноеИмяДокумента= "КСФ";
					ИначеЕсли ЭДОбъект.Type = "InvoiceCorrectionRevision" Тогда
						СокращенноеИмяДокумента= "ИКСФ";
					Иначе
						СокращенноеИмяДокумента= "СФ";
					КонецЕсли;
						
					Если ЗначениеЗаполнено(ПодходящаяСФ) Тогда
						ПредставлениеКнопки= "Сопоставить с "+СокращенноеИмяДокумента+" в 1С";
					ИначеЕсли МассивСсылокРНК.Количество() > 0 И (ЭДОбъект.Type = "Invoice") Тогда
						ПредставлениеКнопки= "Создать на основании накладной";
					Иначе
						ПредставлениеКнопки= "Создать "+СокращенноеИмяДокумента+" в 1С";
					КонецЕсли;
					
				Иначе
					ПредставлениеКнопки= "Создать документ в 1С";
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат ПредставлениеКнопки;
		
	КонецФункции
	
//} УПРАВЛЕНИЕ ФОРМОЙ

//{ ПРЕДСТАВЛЕНИЯ

	Функция СтрокаКоличествоДокументов(КоличествоДокументов) Экспорт
		
		ЧислоПрописью	 = ЧислоПрописью(КоличествоДокументов, ,",,,,,,,,0");
		ОбщаяСтрока		 = ЧислоПрописью(КоличествоДокументов, ,"документ, документа, документов, м,,,,,0");
		Результат		 = Строка(КоличествоДокументов) + " " + СтрЗаменить(ОбщаяСтрока, ЧислоПрописью, "");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция ПредставлениеПродавца(ЭДОбъект, НаименованиеЯщика=неопределено) Экспорт
		Если ЭДОбъект.Direction = "Outbound"  тогда 
			Возврат ?(НаименованиеЯщика=неопределено, ЭДОбъект.Organization.name, НаименованиеЯщика);
		Иначе 
			Возврат ЭДОбъект.CounterAgent.name;
		КонецЕсли;
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеПокупателя(ЭДОбъект, НаименованиеЯщика=неопределено) Экспорт
		Если ЭДОбъект.Direction = "Outbound"  тогда
			Возврат ЭДОбъект.CounterAgent.name;
		Иначе 
			Возврат ?(НаименованиеЯщика=неопределено, ЭДОбъект.Organization.name, НаименованиеЯщика);
		КонецЕсли;
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеПодразделения(ЭДОбъект) Экспорт
		
		Если ЭДОбъект.Department = Неопределено Тогда
			Возврат "";
		Иначе
			Возврат  ЭДОбъект.Department.Name;
		КонецЕсли;
		
	КонецФункции
	
	// Формирует представление электронного документа по его типу
	//
	// Параметры:
	//	Document	- ComОбъект	- электронный документ
	//
	// Возвращаемое значение:
	//	Строка
	&НаКлиенте
	Функция ПредставлениеЭД(Document) Экспорт
		
		ФорматЭД = ФорматЭлектронногоДокумента(Document);
		
		Если 	ФорматЭД.ИмяТипа = "TrustConnectionRequest" 
			ИЛИ ФорматЭД.ИмяТипа = "SupplementaryAgreement" Тогда 
			
			ПредставлениеЭД = Document.FileName;
		
		ИначеЕсли СтрНайти(ФорматЭД.ИмяТипа, "Revision") > 0 Тогда
			
			ПредставлениеЭД = ПредставлениеИсправленияЭД(ФорматЭД.ИмяТипа, Document);
						
		Иначе
			
			ПользовательскоеПредставление = ПользовательскоеПредставлениеФормата(ФорматЭД);
						
			ПредставлениеЭД = ПользовательскоеПредставление
								+ " № " + Document.DocumentNumber
								+ " от " + Формат(Document.DocumentDate, "ДФ=dd.MM.yyyy");
							
		КонецЕсли;
		
				
		Если ЗначениеЗаполнено(ПредставлениеЭД) Тогда	
			
			ПредставлениеЭД = ПредставлениеЭД + ДополнениеКПредставлениюЭД(Document);
			
		Иначе
			
			ПредставлениеЭД = "Тип документа не определен";
			
		КонецЕсли;
						
		Возврат ПредставлениеЭД;
		
	КонецФункции // ПредставлениеЭД()
	
	&НаКлиенте
	Функция ПредставлениеИсправленияЭД(ИмяТипа, Document)
		
		Если ИмяТипа = "InvoiceRevision" Тогда
			
			ИмяПервичного	= "счета-фактуры";
			НомерПервичного	= Document.OriginalDocumentNumber;
			ДатаПервичного	= Формат(Document.OriginalDocumentDate, "ДФ=dd.MM.yyyy");
													
		ИначеЕсли ИмяТипа = "InvoiceCorrectionRevision" Тогда
			
			ИмяПервичного	= "корректировочного счета-фактуры";
			НомерПервичного	= Document.OriginalInvoiceCorrectionNumber;
			ДатаПервичного	= Формат(Document.OriginalInvoiceCorrectionDate, "ДФ=dd.MM.yyyy");
												
		ИначеЕсли ИмяТипа = "UniversalTransferDocumentRevision" Тогда
			
			ИмяПервичного	= "УПД";
			НомерПервичного = Document.OriginalDocumentNumber;
			ДатаПервичного	= Формат(Document.OriginalDocumentDate, "ДФ=dd.MM.yyyy");
													
		Иначе
			
			ИмяПервичного	= "";
			НомерПервичного	= "";
			ДатаПервичного	= "";
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИмяПервичного) Тогда
			
			ДатаДокумента = Формат(Document.DocumentDate, "ДФ=dd.MM.yyyy");
			
			Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Исправление № %1 от %2 %3 № %4 от %5'"),
				Document.DocumentNumber,
				ДатаДокумента,
				ИмяПервичного,
				НомерПервичного,
				ДатаПервичного	);
				
		Иначе
			
			Результат = "";
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции // ПредставлениеИсправленияЭД()
	
	&НаКлиенте
	Функция ПолучитьСтруктуруПредставленияЭД(док) Экспорт
		
		Box = Док.Organization;
		
		СтруктураЭД = Новый Структура;
		
		СтруктураЭД.Вставить("ЭДОбъект", док);
		СтруктураЭД.Вставить("BoxID", док.OrganizationID);
		СтруктураЭД.Вставить("DocumentId", док.DocumentId);
		СтруктураЭД.Вставить("Организация", BoxID_2_Организация_Форма(док.OrganizationID));
		СтруктураЭД.Вставить("Контрагент", CounteragentBoxID_2_Контрагент_Форма(док.CounterAgent.Id, док.OrganizationID));
		СтруктураЭД.Вставить("Дата", док.DocumentDate);
		СтруктураЭД.Вставить("Номер", док.DocumentNumber);

		Если док.Direction = "Outbound"  тогда 
			СтруктураЭД.Вставить("Получатель", док.CounterAgent.name);
			СтруктураЭД.Вставить("Отправитель", Box.name);
		Иначе 
			СтруктураЭД.Вставить("Отправитель", док.CounterAgent.name);
			СтруктураЭД.Вставить("Получатель", Box.name);
		КонецЕсли;	
		СтруктураЭД.Вставить("Сумма", ПредставлениеСуммы(док));
		
		Если док.Type = "Invoice" Тогда
			СтруктураЭД.Вставить("ДатаУчета", док.ConfirmationDate);
		Иначе
			СтруктураЭД.Вставить("ДатаУчета", док.Timestamp);
		КонецЕсли;
		
		Возврат СтруктураЭД;
		
	КонецФункции
	
	&НаКлиенте
	Функция ДополнениеКПредставлениюЭД(Document)
		
		Стр			= "";
		Разделитель = "";
		
		Если Document.IsTest Тогда
			Стр = "тестовый";
			Разделитель = ", ";
		КонецЕсли;
		
		Если Document.IsEncryptedContent Тогда
			Стр = Стр + Разделитель + "зашифрован";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Стр) Тогда
			Стр = " ("+Стр+")";
		КонецЕсли;
		
		Возврат Стр;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредсталениеНомераЭД(ЭДОбъект) Экспорт
		
		Возврат ЭДОбъект.DocumentNumber;
		
	КонецФункции
	
	&НаКлиенте
	функция РасшифровкаСтатуса(СтатусСтрокой, СтатусАннулированияСтрокой = "")
		
		Если СтатусАннулированияСтрокой = "RevocationAccepted" Тогда
			РасшифровкаСтатуса=	"Аннулирован";
		Иначе
			Если СтатусСтрокой = "OutboundWaitingForInvoiceReceipt" Тогда
				РасшифровкаСтатуса=	"Ожидается извещение от покупателя"
			ИначеЕсли СтатусСтрокой = "OutboundNotFinished" или статусСтрокой = "InboundNotFinished" Тогда
				РасшифровкаСтатуса=	"Документооборот не завершен"
			ИначеЕсли СтатусСтрокой = "OutboundFinished" или статусСтрокой = "InboundFinished" 
				ИЛИ СтатусСтрокой = "Outbound" ИЛИ СтатусСтрокой = "Inbound" Тогда
				РасшифровкаСтатуса=	"Документооборот завершен"
			ИначеЕсли СтатусСтрокой = "OutboundWaitingForRecipientSignature" ИЛИ СтатусСтрокой = "InboundWaitingForRecipientSignature" Тогда
				РасшифровкаСтатуса=	"Требуется подпись";
			ИначеЕсли СтатусСтрокой = "OutboundWithRecipientSignature" ИЛИ СтатусСтрокой = "InboundWithRecipientSignature " ИЛИ СтатусСтрокой = "InboundWithRecipientSignature" Тогда   
				РасшифровкаСтатуса=	"Подписан";                                                                                                 
			ИначеЕсли СтатусСтрокой = "OutboundRecipientSignatureRequestRejected" ИЛИ СтатусСтрокой = "InboundRecipientSignatureRequestRejected" Тогда
				РасшифровкаСтатуса=	"Отказано в подписи";
			ИначеЕсли СтатусСтрокой = "OutboundWaitingForSenderSignature" Тогда
				РасшифровкаСтатуса=	"Требуется подписать и отправить";
			ИначеЕсли СтатусСтрокой = "InboundInvalidRecipientSignature" ИЛИ СтатусСтрокой = "OutboundInvalidSenderSignature" Тогда
				РасшифровкаСтатуса=	"Ошибка подписи";
			ИначеЕсли СтатусСтрокой = "InboundNoRecipientSignatureRequest" Тогда
				РасшифровкаСтатуса=	"Получен";
			ИначеЕсли СтатусСтрокой = "OutboundNoRecipientSignatureRequest" Тогда
				РасшифровкаСтатуса=	"Доставлен";
			ИначеЕсли СтатусСтрокой = "OutboundWaitingForInvoiceReceiptAndRecipientSignature" Тогда
				РасшифровкаСтатуса=	"Ожидается извещение и подпись от покупателя";
			Иначе
				РасшифровкаСтатуса=	"";
			КонецЕсли;
		КонецЕсли;
		
		Если СтатусАннулированияСтрокой = "RevocationIsRequestedByMe" Тогда
			РасшифровкаСтатусаАннулирования=	"Ожидается аннулирование";
		ИначеЕсли СтатусАннулированияСтрокой = "RequestsMyRevocation" Тогда
			РасшифровкаСтатусаАннулирования=	"Требуется аннулирование";
		ИначеЕсли СтатусАннулированияСтрокой = "RevocationRejected" Тогда
			РасшифровкаСтатусаАннулирования=	"Отказано в аннулировании";
		Иначе
			РасшифровкаСтатусаАннулирования=	"";
		КонецЕсли;
		
		Возврат РасшифровкаСтатуса + ?(ПустаяСтрока(РасшифровкаСтатусаАннулирования), "", ". " + РасшифровкаСтатусаАннулирования);
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеСтатуса(ЭДОбъект) Экспорт
		
		Статус = РасшифровкаСтатуса(ЭДОбъект.Status, ЭДОбъект.RevocationStatus);
		
		Если Лев(ЭДОбъект.Type, 7) = "Invoice" Тогда
			Если ЭДОбъект.Corrected Тогда
				Статус=	Статус + ". Откорректирован";
			ИначеЕсли ЭДОбъект.Revised Тогда
				Статус=	Статус + ". Исправлен";
			ИначеЕсли ЭДОбъект.AmendmentRequested Тогда
				Статус=	Статус + ?(ЭДОбъект.Direction = "Outbound", ". Требуется уточнение", ". Ожидается уточнение");
			КонецЕсли;
			
		ИначеЕсли ЭДОбъект.Type = "UniversalTransferDocument" ИЛИ ЭДОбъект.Type = "UniversalCorrectionDocument" Тогда 
			
			Если ЭДОбъект.AmendmentRequested
				И ЭДОбъект.RevocationStatus = "RevocationStatusNone" Тогда //добавляем "Требует уточнения" только если документ не аннулирован и не на стадии аннулирования.
				Статус = Статус + ?(ЭДОбъект.Direction = "Outbound", ". Требуется уточнение", ". Ожидается уточнение");
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат Статус;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеСтатусаРоуминг(ЭДОбъект) Экспорт
		
		RoamingNotificationStatus= ЭДОбъект.RoamingNotificationStatus;
		
		Если RoamingNotificationStatus= "RoamingNotificationStatusNone" Тогда
			Статус= "";
		ИначеЕсли RoamingNotificationStatus = "RoamingNotificationStatusError" Тогда
			Статус = Новый ФорматированнаяСтрока("Ошибка доставки документа через роуминг!",,WebЦвета.Красный,,RoamingNotificationStatus);
		ИначеЕсли RoamingNotificationStatus = "RoamingNotificationStatusSuccess" Тогда
			Статус = "Доставлен через роуминг.";
		ИначеЕсли RoamingNotificationStatus = "UnknownRoamingNotificationStatus" Тогда
			Статус = "Идет доставка через роуминг.";
		КонецЕсли;
		
		Возврат Статус;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеСуммы(Document) Экспорт
		
		Если 	Document.Type = "InvoiceCorrection"
			ИЛИ Document.Type = "InvoiceCorrectionRevision"
			ИЛИ Document.Type = "UniversalCorrectionDocument" Тогда
			
			стрУв = ?(Окр(Document.TotalInc, 2) <> 0, "+" + Формат(Окр(Document.TotalInc, 2), "ЧДЦ=2"), "");
			стрУв = СтрЗаменить(стрУв, Символы.НПП, " ");
			
			стрУм = ?(Окр(Document.TotalDec, 2) <> 0, "-" + Формат(Окр(Document.TotalDec, 2), "ЧДЦ=2"), "");
			стрУм = СтрЗаменить(стрУм, Символы.НПП, " ");
			
			Если НЕ ПустаяСтрока(стрУв) И НЕ ПустаяСтрока(стрУм) Тогда
				Возврат стрУв + " " + СтрУм;
			ИначеЕсли НЕ ПустаяСтрока(стрУв) Тогда
				Возврат стрУв;
			Иначе
				Возврат стрУм;
			КонецЕсли;

		ИначеЕсли 
				Document.Type = "Nonformalized"
			ИЛИ Document.Type = "ReconciliationAct"
			ИЛИ Document.Type = "CertificateRegistry"
			ИЛИ Document.Type = "ServiceDetails" 
			ИЛИ Document.Type = "Contract"
			ИЛИ Document.Type = "PriceListAgreement"
			ИЛИ Document.Type = "TrustConnectionRequest"
			ИЛИ Document.Type = "PriceList"
			ИЛИ Document.Type = "SupplementaryAgreement"

		Тогда
			Возврат "";
		Иначе
			
			Попытка
				Возврат СтрЗаменить(Формат(Окр(Document.Total, 2), "ЧДЦ=2"), Символы.НПП, " ");
			Исключение
			КонецПопытки;
			
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция СуммаПоDocument(Document)
		
		Если 	Document.Type = "InvoiceCorrection"
			ИЛИ Document.Type = "InvoiceCorrectionRevision"
			ИЛИ Document.Type = "UniversalCorrectionDocument" 
			
			ИЛИ	Document.Type = "Nonformalized"
			ИЛИ Document.Type = "ReconciliationAct"
			ИЛИ Document.Type = "CertificateRegistry"
			ИЛИ Document.Type = "ServiceDetails" 
			ИЛИ Document.Type = "Contract"
			ИЛИ Document.Type = "PriceListAgreement"
			ИЛИ Document.Type = "TrustConnectionRequest"
			ИЛИ Document.Type = "PriceList"
			ИЛИ Document.Type = "SupplementaryAgreement"
		Тогда
		
			Возврат 0;
		
		Иначе
			
			Попытка
				Возврат Окр(Document.Total, 2);
			Исключение
			КонецПопытки;
			
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеСуммыНДС(Document)
		
		Если Document.Type = "InvoiceCorrection"
			ИЛИ Document.Type = "InvoiceCorrectionRevision"
			Или Document.Type = "UniversalCorrectionDocument" Тогда
			
			стрУв = ?(Окр(Document.VatInc, 2) <> 0, "+" + Формат(Окр(Document.VatInc, 2), "ЧДЦ=2"), "");
			стрУм = ?(Окр(Document.VatDec, 2) <> 0, "-" + Формат(Окр(Document.VatDec, 2), "ЧДЦ=2"), "");
			Если НЕ ПустаяСтрока(стрУв) И НЕ ПустаяСтрока(стрУм) Тогда
				Возврат стрУв + " " + СтрУм;
			ИначеЕсли НЕ ПустаяСтрока(стрУв) Тогда
				Возврат стрУв;
			Иначе
				Возврат стрУм;
			КонецЕсли;
			
		Иначе
			
			Попытка
				Возврат Формат(Окр(Document.Vat, 2), "ЧДЦ=2");
			Исключение
			КонецПопытки;
			
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте 
	Функция ПредставлениеПериодаДД(ДатаНачала, Знач ДатаОкончания) Экспорт
		
		ДатаОкончания= ?(ДатаОкончания = МетодКлиента("Модуль_Клиент","ДатаГоризонта"), '00010101', ДатаОкончания);
		ПредставлениеПериодаДД= "";
		
		Если ЗначениеЗаполнено(ДатаОкончания) Тогда
			Если ЗначениеЗаполнено(ДатаНачала) Тогда
				ПредставлениеПериодаДД= ПредставлениеПериода(ДатаНачала, КонецДня(ДатаОкончания), "ФП = Истина");
			Иначе
				ПредставлениеПериодаДД= "по " + Формат(ДатаОкончания, "ДФ=dd.MM.yyyy");
			КонецЕсли;
		ИначеЕсли ЗначениеЗаполнено(ДатаНачала) Тогда
			ПредставлениеПериодаДД= "с " + Формат(Датаначала, "ДФ=dd.MM.yyyy");
		Иначе
			ПредставлениеПериодаДД= "Без ограничения";
		КонецЕсли;
		
		Возврат ПредставлениеПериодаДД;	
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеТипаРезолюции(ResolutionType) Экспорт
		
		Если ResolutionType = "ResolutionApprove" Тогда
			Действие=	"Согласовано";
		ИначеЕсли ResolutionType = "ResolutionDisapprove" Тогда
			Действие=	"Отказано в согласовании";
		ИначеЕсли ResolutionType = "ResolutionRequest" Тогда
			Действие=	"Отправлен запрос на согласование";
		ИначеЕсли ResolutionType = "SignatureApprove" Тогда
			Действие=	"Подписан";
		ИначеЕсли ResolutionType = "SignatureDisapprove" Тогда
			Действие=	"Отказано в подписи";
		ИначеЕсли ResolutionType = "SignatureRequest" Тогда
			Действие=	"Отправлен запрос на подпись";
		ИначеЕсли ResolutionType = "RevocationRequest" Тогда
			Действие=	"Отправлен запрос на аннулирование";
		ИначеЕсли ResolutionType = "RevocationApprove" Тогда
			Действие=	"Аннулирован";
		ИначеЕсли ResolutionType = "RevocationDisapprove" Тогда
			Действие=	"Отказано в аннулировании";
		ИначеЕсли ResolutionType = "InvoiceCorrectionRequest" Тогда
			Действие = "Отправлен запрос на уточнение";
		Иначе
			Действие=	"Тип согласования не определен";
		КонецЕсли;
		
		Возврат Действие;
		
	КонецФункции
	
	&НаКлиенте 
	Функция СформироватьHTMLПредставлениеОшибок(Document, Организация, МассивОшибок) Экспорт
		
		HTMLДокумент=	"";
		
		HTMLДокумент= 	"<style>
		|
		|p1 {font-family: 'Times New Roman', Times, serif; font-size: 11pt; }
		|p2 {font-family: 'Times New Roman', Times, serif; font-size: 10pt; }
		|p {font-family: 'Times New Roman', Times, serif; font-size: 9pt;}
		|
		|.left_block {
		|display: block;
		|float: left; 
		|}
		|
		|.right_block {
		|display: block;
		|float: right;
		|}
		|
		|.bottom_brd {
		|border-bottom-width: 1px;
		|border-bottom-style: solid;
		|border-bottom-color: black;
		|}
		|
		|.block_comment {
		|width: 98%;
		|float: right;
		|}
		|
		|</style>";
		
		
		HTMLДокумент=	HTMLДокумент + "<body>" +
		"<div>" +
		"<div>" +
		
		"<div>" + 
		
		"<div>" +
		"<div class=""bottom_brd"">" +
		"<h3>" + ПредставлениеЭД(Document) + "</h3>" +
		"<h4 style=""margin-bottom: -15px;"">" + ?(Document.Type = "Inbound", "Отправитель: ", "Получатель: ") + Document.Organization.Name + "</h4>" +
		"<h4>" + ?(Document.Type = "Inbound", "Получатель: ", "Отправитель: ") + Document.Counteragent.Name + "</h4>" +
		"</div>" +
		"</div>";
		
		НомерОшибки=				0;
		ПредыдущаяКатегорияОшибки=	"";
		
		Для каждого Ошибка Из МассивОшибок Цикл
			
			Если НЕ ПредыдущаяКатегорияОшибки = Ошибка.КатегорияОшибки Тогда
				
				НомерОшибки=				0;
				ПредыдущаяКатегорияОшибки=	Ошибка.КатегорияОшибки;
				
				HTMLДокумент=	HTMLДокумент +
				"<div>" +
				
				"<div>" +
				
				"<div style= ""color = red"">" +
				"<p2 align=""left"">" + Ошибка.КатегорияОшибки + "</p2>" + 
				"</div>"
				
				"</div>" +
				
				"</div>";
				
			КонецЕсли;
			
			НомерОшибки=	НомерОшибки + 1;
			
			HTMLДокумент=	HTMLДокумент +
			"<div>" + 
			"<div class=""block_comment"" style= ""white-space: normal; word-wrap: break-word"">" +
			"<p>" + НомерОшибки + ". " + Ошибка.ТекстОшибки + "</p>" +
			"</div>" + 
			"</div>";
			
		КонецЦикла;
		
		HTMLДокумент=	HTMLДокумент + "</div></div></div></<body>";
		
		Возврат HTMLДокумент;
		
	КонецФункции
	
	&НаКлиенте 
	Функция СформироватьHTMLПредставлениеРезолюций(Document, Организация) Экспорт
		
		//Верстальщик бы меня на рее повесил...
		
		HTMLДокумент=	"";
		
		HTMLДокумент= 	"<!DOCTYPE html PUBLIC """"-//W3C//DTD XHTML 1.0 Transitional//EN
		|http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd"""">
		|
		|<html xmlns=""""http://www.w3.org/1999/xhtml"""">
		|
		|<head>
		|<meta http-equiv=""""Content-Type"""" content=""""text/html; charset=utf-8""""/>
		|
		|<style>	
		|p1 {font-family: 'Times New Roman', Times, serif; font-size: 11pt; }
		|p2 {font-family: 'Times New Roman', Times, serif; font-size: 10pt; }
		|p {font-family: 'Times New Roman', Times, serif; font-size: 9pt; color = #828282; 
		|}
		|
		|.background.block_creationdate {
		|height: auto !important;
		|padding-bottom: 0;
		|right: 0;
		|width: 50%;
		|}
		|
		|.block_creationdate {
		|width: 30%;
		|padding-bottom: 0px;
		|text-align: right; 
		|}
		|
		|.block_resolutiontype {
		|width: 70%;
		|display: block;
		|float: left;
		|}
		|
		|.block_author {
		|width: 98%;
		|display: block;
		|float: left;
		|}
		|
		|.left_block {
		|display: block;
		|float: left; 
		|}
		|
		|.right_block {
		|display: block;
		|float: right;
		|}
		|
		|.bottom_brd {
		|border-bottom-width: 1px;
		|border-bottom-style: solid;
		|border-bottom-color: black;
		|}
		|
		|.block_comment {
		|width: 96%;
		|float: right; 
		|}
		|
		|</style>
		|
		|</head>";
		
		
		HTMLДокумент=	HTMLДокумент + "<body>" +
		"<div>" +
		"<div>" +
		
		"<div>" + 
		
		"<div>" +
		"<div class=""bottom_brd"">" +
		"<h3>" + ПредставлениеЭД(Document) + "</h3>" +
		"<h4 style=""margin-bottom: -15px;"">" + ?(Document.Type = "Inbound", "Отправитель: ", "Получатель: ") + Document.Organization.Name + "</h4>" +
		"<h4>" + ?(Document.Type = "Inbound", "Получатель: ", "Отправитель: ") + Document.Counteragent.Name + "</h4>" +
		"</div>" +
		"</div>";
		
		МассивРезолюций=	Document.Resolutions;
		
		Для Индекс = 0 По МассивРезолюций.Count - 1 Цикл
			
			Резолюция=	МассивРезолюций.GetItem(Индекс);
			
			Color=	"#0000FF";
			Если Резолюция.ResolutionType = "ResolutionApprove"
				ИЛИ Резолюция.ResolutionType = "SignatureApprove" Тогда
				Color=	"#00D403";
			ИначеЕсли  Резолюция.ResolutionType = "ResolutionDisapprove"
				ИЛИ Резолюция.ResolutionType = "SignatureDisapprove" Тогда
				Color=	"#FF0000";
			КонецЕсли;
			
			ResolutionType=	ПредставлениеТипаРезолюции(Резолюция.ResolutionType);
			
			Если Резолюция.ResolutionType = "ResolutionApprove" Тогда
				Пояснение=		"Согласовал: " + Резолюция.Author;
			ИначеЕсли Резолюция.ResolutionType = "ResolutionDisapprove" Тогда
				Пояснение=		"Отклонил: " + Резолюция.Author;
			ИначеЕсли Резолюция.ResolutionType = "SignatureApprove" Тогда
				Пояснение=		"Подписал: " + Резолюция.Author;
			ИначеЕсли Резолюция.ResolutionType = "SignatureDisapprove" Тогда
				Пояснение=		"Отказал: " + Резолюция.Author;
			ИначеЕсли Резолюция.ResolutionType = "ResolutionRequest" ИЛИ 
				Резолюция.ResolutionType = "SignatureRequest" ИЛИ
				Резолюция.ResolutionType = "RevocationRequest" Тогда
				ПолучательРезолюции=	?(Резолюция.TargetDepartment = Неопределено, "", " " + Резолюция.TargetDepartment.Name) + 
				?(Резолюция.TargetUser = Неопределено, "", ?(Резолюция.TargetDepartment = Неопределено, " ",", ") + ПолучитьФИОСИнициалами(Резолюция.TargetUser.Name));
				
				Пояснение=				"" + ПолучитьФИОСИнициалами(Резолюция.Author) + ?(НЕ ПустаяСтрока(ПолучательРезолюции), " &rarr; " + ПолучательРезолюции, "");
			Иначе
				Пояснение=		"";
			КонецЕсли;
			
			HTMLДокумент=	HTMLДокумент +
			"<div class=""block_left"">" +
			
			"<div>" +
			
			"<div class= ""right_block"">" +
			
			"<div class=""block_resolutiontype"" style= ""color: " + Color + """>" +
			"<p2 align=""left"">" + ResolutionType + "</p2>" + 
			"</div>"
			
			"<div class=""right_block block_creationdate"" >" + 
			"<div>" +
			"<p2 align=""right""  style=""vertical-align: baseline"" > &nbsp; " + Формат(Резолюция.CreationDate, "ДФ='dd.MM.yy HH:mm'") + "</p2>" +
			"</div>" +
			"</div>" +
			
			"</div>" +
			
			"</div>" +
			
			"<div>" +
			"<div class=""block_author"">" +
			"<p1>" + Пояснение + "</p1>" +
			"</div>" +
			"</div>" +
			
			"<div>" + 
			"<div class=""block_comment"" style= ""background-color: #FFFBF0; white-space: normal; word-wrap: break-word"">" +
			"<p><pre>" + Резолюция.Comment + "</pre></p>" +
			"</div>" + 
			
			"<div class=""block_comment"">" + 
			"<p>&nbsp</p>" +
			"</div>" + 
			
			"</div>"+
			
			"</div>";
			
		КонецЦикла;
		
		HTMLДокумент=	HTMLДокумент + "</div></div></div></div></<body>";
		
		Возврат HTMLДокумент;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеAuthor(ResolutionStatus)
		
		Возврат ?(ResolutionStatus.Author = Неопределено, "", ": " + ПолучитьФИОСИнициалами(ResolutionStatus.Author.Name));
		
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеTargetUser(ResolutionStatus)
		
		Возврат ?(ResolutionStatus.TargetDepartment = Неопределено, ": ", ResolutionStatus.TargetDepartment.Name)
		+ ?(ResolutionStatus.TargetUser = Неопределено, "", ?(ResolutionStatus.TargetDepartment = Неопределено, "", ", ") + ПолучитьФИОСИнициалами(ResolutionStatus.TargetUser.Name));
	КонецФункции
	
	&НаКлиенте
	Функция ПредставлениеСтатусаСогласования(Document) Экспорт
		
		СтатусСогласования=	"";
		ResolutionStatus=	Document.ResolutionStatus;
		
		Если НЕ ResolutionStatus = Неопределено Тогда
			
			Если ResolutionStatus.Type = "Approved" Тогда
				СтатусСогласования=	"Согласовано" + ПредставлениеAuthor(ResolutionStatus);
			ИначеЕсли ResolutionStatus.Type = "Disapproved" Тогда
				СтатусСогласования=	"Отказано в согласовании" + ПредставлениеAuthor(ResolutionStatus);
			ИначеЕсли ResolutionStatus.Type = "ApprovementRequested" Тогда
				СтатусСогласования=	"На согласовании" + ПредставлениеTargetUser(ResolutionStatus);
			ИначеЕсли ResolutionStatus.Type = "SignatureRequested" Тогда
				СтатусСогласования=	"На подписании" + ПредставлениеTargetUser(ResolutionStatus);
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат СтатусСогласования;			
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьФИОСИнициалами(ФИО) Экспорт
		
		СтрокаФИОСИнициалами=	"";
		
		ИсходнаяСтрока=			ФИО;
		ПозицияПослПробела=		Найти(ИсходнаяСтрока, " ");
		Фамилия=				Лев(ИсходнаяСтрока, ПозицияПослПробела);
		
		ИсходнаяСтрока=			Сред(ИсходнаяСтрока, ПозицияПослПробела + 1);
		ПозицияПослПробела=		Найти(ИсходнаяСтрока, " ");
		Имя=					Лев(ИсходнаяСтрока, ПозицияПослПробела);
		
		ИсходнаяСтрока=			Сред(ИсходнаяСтрока, ПозицияПослПробела + 1);
		Отчество=				ИсходнаяСтрока;
		ИсходнаяСтрока=			Сред(ИсходнаяСтрока, ПозицияПослПробела + 1);
		
		Если НЕ ПустаяСтрока(Фамилия) Тогда
			СтрокаФИОСИнициалами=	Фамилия + 
			?(НЕ ПустаяСтрока(Имя), Лев(Имя, 1) + ".", "") +
			?(НЕ ПустаяСтрока(Отчество), Лев(Отчество, 1) + ".", "");
		КонецЕсли;
		
		Возврат СтрокаФИОСИнициалами;
		
	КонецФункции
	
	&НаКлиенте
	Функция ЕстьСвойствоCOMОбъекта(COMОбъект, ИмяСвойства, ЗначениеСвойства = "") Экспорт 
		
		Попытка
			ЗначениеСвойства = COMОбъект[ИмяСвойства];
			Возврат Истина;
		Исключение
			Возврат Ложь;
		КонецПопытки;
		
	КонецФункции
	
//} ПРЕДСТАВЛЕНИЯ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

	&НаКлиентеНаСервереБезКонтекста
	Функция НайтиСтрокиВМассивеСтруктур(МассивСтруктур, ПараметрыОтбора) //вместо НайтиСтроки таблицы значений
		Результат = Новый Массив;
		Для Каждого Стр из МассивСтруктур Цикл
			БылоНесоответствие = Ложь;
			Для Каждого Стр1 из ПараметрыОтбора Цикл
				Если СокрЛП(ПараметрыОтбора[Стр1.Ключ]) <>  СокрЛП(Стр[Стр1.Ключ]) Тогда
					БылоНесоответствие = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если БылоНесоответствие = Ложь Тогда
				Результат.Добавить(Стр);
			КонецЕсли;
			
		КонецЦикла;
		Возврат Результат;
	КонецФункции
	
	&НаСервере
	Функция BoxID_2_Организация_Форма(BoxID, DepartmentKpp = "") Экспорт 
		Возврат МетодСервера(,"BoxID_2_Организация", BoxID, DepartmentKpp);
	КонецФункции
	
	&НаСервере
	Функция CounteragentBoxID_2_Контрагент_Форма(CounteragentID, BoxID = Неопределено) экспорт 
		Возврат МетодСервера(,"CounteragentBoxID_2_Контрагент", CounteragentID, BoxID);
	КонецФункции
		
	// Возвращает коллекцию COM-объектов типа Counteragent по запрошенной организации и статусу.
	//
	// Параметры:
	//  Организация – СправочникСсылка.Организации – организация, по которой необходимо получить список контрагентов;
	//  Status – Строка – статус, в котором находятся контрагенты; возможные статусы перечислены в документации в методе GetCounteragentListByStatus.
	// 
	// Возвращаемое значение:
	//  Коллекция COM-объектов типа Counteragent, Неопределено – если контрагенты в запрошенном статусе существуют, то возвращается их коллекция, в противном случае возвращается Неопределено.
	//
	&НаКлиенте
	Функция GetCounteragentListByStatus(Организация, Status) Экспорт
		
		Результат = Неопределено;
		
		Box = ПолучитьЯщикДиадокДляОрганизации(Организация);
		
		Если Box <> Неопределено Тогда
			Результат = Box.GetCounteragentListByStatus(Status);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции //GetCounteragentListByStatus()
	
	// Проверяет, что объект ИнформацияОбОшибке содержит сведения об ошибках валидации.
	// Проверка осуществляет путем парсинга текста ошибки. Если текст содержит HTML,
	// значит это ошибка, сгенерированная подсистемой валидации контента.
	//
	// Параметры:
	//  ИнформацияОбОшибке - ИнформацияОбОшибке - сведения об ошибке.
	// 
	// Возвращаемое значение:
	//  Булево.
	//
	&НаКлиенте
	Функция ЭтоИнформацияОбОшибкахВалидации(ИнформацияОбОшибке) Экспорт
		
		Результат = Ложь;
		
		ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		
		Если Найти(ВРег(ТекстОшибки), "<HTML>") = 1 Тогда
			Результат = Истина;
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Интерпретирует текст ошибки, возвращаемый исключением
	//
	// Параметры:
	//	ТекстОшибки	-	Строка	- интерпретируемый текст
	//
	// Возвращаемое значение:
	//	Строка
	&НаКлиенте
	Функция СформироватьТекстОшибкиДиадок(Знач ТекстОшибки) Экспорт
		
		КраткоеСообщение	= ТекстОшибки;
		КраткоеСообщение	= Сред(КраткоеСообщение, Найти(КраткоеСообщение, "##") + 1);
		КраткоеСообщение	= Сред(КраткоеСообщение, Найти(КраткоеСообщение, "[" ) + 1);
		КраткоеСообщение	= Сред(КраткоеСообщение, Найти(КраткоеСообщение, "]" ) + 1);
		
		НаименованиеСистемыРодительныйПадеж = Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыРодительныйПадеж;
		
		Если ЭтоОшибкаОтсутствиеРегистрацииФНС(КраткоеСообщение) Тогда
			
			ПодстрокаПоиска	= "is not registered in FNS for box";
			ДлинаПодстроки	= СтрДлина(ПодстрокаПоиска);
			
			BoxID	= Сред(КраткоеСообщение, Найти(КраткоеСообщение, ПодстрокаПоиска) + ДлинаПодстроки);
			BoxID	= Лев(BoxID, Найти(BoxID, ".") - 1);
			
			ОтпечатокСертификата	= Лев(КраткоеСообщение, Найти(КраткоеСообщение, ПодстрокаПоиска) - 1);
			
			ПодстрокаПоиска = "Signer certificate";
			ДлинаПодстроки	= СтрДлина(ПодстрокаПоиска);
			ОтпечатокСертификата = Сред(ОтпечатокСертификата, Найти(ОтпечатокСертификата, ПодстрокаПоиска) + ДлинаПодстроки);
			ОтпечатокСертификата = СокрЛП(ОтпечатокСертификата);
			
			Результат	=
			"Сертификат, которым подписано сообщение, не зарегистрирован в ФНС.
			|Для решения данной проблемы обратитесь в техподдержку.
			|Отладочная информация: 
			|Отпечаток сертификата: " + ОтпечатокСертификата + "
			|Идентификатор ящика организации: " + BoxID;
			
		ИначеЕсли МетодКлиента("Модуль_РаботаССерверомДиадок", "ЭтоОшибкаАпиНеНастроеныПараметрыПодписания", ТекстОшибки) Тогда
			
			Результат = "Для сертификата, которым подписано сообщение, не настроены параметры подписи.";
			
		ИначеЕсли Найти(ТекстОшибки, "##3") > 0 Тогда
			
			// ошибки крипто про
			
			Результат = "Произошла ошибка при работе с КриптоПро:
						 |" + КраткоеСообщение;
			
		ИначеЕсли Найти(ТекстОшибки, "##1") > 0 Тогда
			
			// ошибки сервера Диадок
			
			Результат = ТекстОшибкиДиадокСерии_100(ТекстОшибки, КраткоеСообщение); 
												   
		ИначеЕсли Найти(ТекстОшибки, "##2") > 0 Тогда
			
			// ошибки передачи данных
			
			Результат = ТекстОшибкиДиадокСерии_200(КраткоеСообщение);
								
		ИначеЕсли Найти(ТекстОшибки, "##9") > 0 Тогда
			
			Результат = КраткоеСообщение;
			
		Иначе
			
			Результат = ТекстОшибки;
			
		КонецЕсли;	
		
		Возврат Результат;
		
	КонецФункции // СформироватьТекстОшибкиДиадок()
	
	&НаКлиенте
	Функция ТекстОшибкиДиадокСерии_100(ТекстОшибки, КраткоеСообщение)
		
		НаименованиеСистемыРодительныйПадеж = Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыРодительныйПадеж;
		
		Если Найти(ТекстОшибки, "Код ошибки: 500") > 0 И Найти(ТекстОшибки, "GetTokenByCertificate") > 0 Тогда 
				
			Результат = "При работе программы произошла ошибка:
						 |Выбранный сертификат не имеет доступа в " + Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы;
			
		ИначеЕсли 	Найти(ТекстОшибки, "for entity that already signed") > 0 
				ИЛИ Найти(ТекстОшибки, "Receipt for document which already has one") > 0 Тогда
			
			Результат = "При работе программы произошла ошибка:
						 |данный документ уже подписан";
			
		ИначеЕсли 	  Найти(ТекстОшибки, "for entity that has requested signature rejection") > 0 
			 	  ИЛИ Найти(ТекстОшибки, "RequestedSignatureRejection for document that already has one") > 0
			 	  ИЛИ Найти(ТекстОшибки, "for entity that already have requested signature rejection") > 0 Тогда
			
			Результат = "При работе программы произошла ошибка:
						 |по данному документу уже сформирован отказ в подписи";
			
		ИначеЕсли Найти(ТекстОшибки, "Access to Box") > 0 И Найти(ТекстОшибки, "is denied") > 0 Тогда 
			
			Результат = "При работе программы произошла ошибка:
						 |у пользователя отсутствует доступ к ящику " + НаименованиеСистемыРодительныйПадеж;
			
		ИначеЕсли 	  Найти(ТекстОшибки, "Organization is not allowed to get counteragent certificates") > 0 
		 		  ИЛИ Найти(ТекстОшибки, "Organization is not allowed to send encrypted documents") > 0  Тогда 
			
			Результат = "Отправка зашифрованных документов невозможна! " + 
							"Для того чтобы включить опцию отправки зашифрованных документов, обратитесь в техподдержку.";
			
		ИначеЕсли Найти(КраткоеСообщение, "code:407") > 0 ИЛИ Найти(КраткоеСообщение, "code:401") > 0 Тогда
						   
			Результат = "Настройки прокси-сервера не позволяют установить соединение с сервером " 
							+ НаименованиеСистемыРодительныйПадеж + "
						 	|(" + КраткоеСообщение + ")";
			
		Иначе
			
			Результат = "Произошла ошибка при работе с сервером " + НаименованиеСистемыРодительныйПадеж + ":
						 |" + КраткоеСообщение;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции // ТекстОшибкиДиадокСерии_100()
	
	&НаКлиенте
	Функция ТекстОшибкиДиадокСерии_200(КраткоеСообщение)
		
		НаименованиеСистемыРодительныйПадеж = Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыРодительныйПадеж;
		
		Если Найти(КраткоеСообщение, "code:407") > 0 ИЛИ Найти(КраткоеСообщение, "code:401") > 0 Тогда
				
			Результат = "Настройки прокси-сервера не позволяют установить соединение с сервером " 
							+ НаименованиеСистемыРодительныйПадеж + "
						 	|(" + КраткоеСообщение + ")";
			
		Иначе 	
			
			Результат = "Невозможно установить соединение с сервером " + НаименованиеСистемыРодительныйПадеж + " по причине:
						 |" + КраткоеСообщение;
			
		КонецЕсли;
		
		Возврат Результат; 
		
	КонецФункции // ТекстОшибкиДиадокСерии_200()
	
	&НаСервере
	Функция ПолучитьОсновнуюФормуОбъекта(Объект1С)
		
		МетаданныеОбъекта=	Объект1С.ПолучитьОбъект().Метаданные();
		ИмяОбъекта1С=		МетаданныеОбъекта.Имя;
		
		Если Метаданные.Документы.Содержит(МетаданныеОбъекта) Тогда
			КореньОбъекта=	"Документ";
		ИначеЕсли Метаданные.Справочники.Содержит(МетаданныеОбъекта) Тогда
			КореньОбъекта=	"Справочник";
		КонецЕсли;
		
		Попытка
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
				Если ТипЗнч(Объект1С) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
					ФормыСчетаФактуры=			Новый ФиксированноеСоответствие(Документы.СчетФактураПолученный.ПолучитьСоответствиеВидовСчетаФактурыФормам());
					ИмяФормыОбъекта=			ФормыСчетаФактуры[Объект1С.ВидСчетаФактуры];
				ИначеЕсли ТипЗнч(Объект1С) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
					ФормыСчетаФактуры= 			Новый ФиксированноеСоответствие(Документы.СчетФактураВыданный.ПолучитьСоответствиеВидовСчетаФактурыФормам());
					ИмяФормыОбъекта=			ФормыСчетаФактуры[Объект1С.ВидСчетаФактуры];
				ИначеЕсли ТипЗнч(Объект1С) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
					ФормыДокументаНакладной=	Новый ФиксированноеСоответствие(Документы.РеализацияТоваровУслуг.ПолучитьСоответствиеВидовОперацийФормам());
					ИмяФормыОбъекта=			ФормыДокументаНакладной[Объект1С.ВидОперации];
				ИначеЕсли ТипЗнч(Объект1С) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
					ФормыДокументаНакладной=	Новый ФиксированноеСоответствие(Документы.ПоступлениеТоваровУслуг.ПолучитьСоответствиеВидовОперацийФормам());
					ИмяФормыОбъекта=			ФормыДокументаНакладной[Объект1С.ВидОперации];
				ИначеЕсли ТипЗнч(Объект1С) = Тип("ДокументСсылка.ВозвратТоваровОтПокупателя") тогда 
					ИмяФормыОбъекта= 			"ФормаДокументаОбщая";
				ИначеЕсли ТипЗнч(Объект1С) = Тип("ДокументСсылка.ОтчетКомитентуОПродажах") Тогда
					ФормыОтчетовКомитенту= 		Новый ФиксированноеСоответствие(Документы.ОтчетКомитентуОПродажах.ПолучитьСоответствиеВидовОтчетаКомитентуФормам());
					ИмяФормыОбъекта=			ФормыОтчетовКомитенту[Объект1С.ВидОперации];
				Иначе
					ИмяФормыОбъекта=			МетаданныеОбъекта.ОсновнаяФормаОбъекта.Имя;
				КонецЕсли;
			Иначе
				ИмяФормыОбъекта=			МетаданныеОбъекта.ОсновнаяФормаОбъекта.Имя;
			КонецЕсли;
		Исключение
			ИмяФормыОбъекта=			МетаданныеОбъекта.ОсновнаяФормаОбъекта.Имя;
		КонецПопытки;
		
		Возврат КореньОбъекта + "." + ИмяОбъекта1С + ".Форма." + ИмяФормыОбъекта;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьОписаниеФормы(Документ1С) Экспорт
		
		Возврат ПолучитьОсновнуюФормуОбъекта(Документ1С);
		
	КонецФункции
	
	&НаКлиенте
	Функция ВернутьМассивВыбранныхФайлов(МножественныйВыбор =  Ложь) Экспорт
		
		Попытка
			WshShell= 			Новый COMОбъект("WScript.Shell");
			КаталогФайлов=	WshShell.CurrentDirectory;
		Исключение
			КаталогФайлов=	"";
		КонецПопытки;
		
		ДиалогВыбора=						Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогВыбора.Заголовок=				?(МножественныйВыбор, "Выберите файлы", "Выберите файл");
		ДиалогВыбора.Каталог=				КаталогФайлов;
		ДиалогВыбора.Фильтр=				НСтр("ru = 'Все файлы(*.*)|*.*'"); 
		ДиалогВыбора.МножественныйВыбор=	МножественныйВыбор;
		
		Если ДиалогВыбора.Выбрать() Тогда
			Возврат ДиалогВыбора.ВыбранныеФайлы;
		Иначе
			Возврат Новый Массив();
		КонецЕсли;
		
	КонецФункции
	
	&НаСервере
	Процедура Установить_CounteragentBoxID_для_Контрагент(ВыбКонтрагент, CounteragentId)
		
		МетодСервера(,"Установить_CounteragentBoxID_для_Контрагент", ВыбКонтрагент, CounteragentId);
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьИмяФормыДокумента(Document) Экспорт
		
		Если ЭтоФормализованныйДокумент(Document.Type) И Document.HasCustomPrintForm = Ложь Тогда
			ИмяФормыПросмотра=	"ФормаДокумента";
		Иначе
			ИмяФормыПросмотра=	"ФормаДокументаБезВизуализации";
		КонецЕсли;
		
		Возврат ИмяФормыПросмотра;
		
	КонецФункции
	
	&НаСервере
	Функция ПолучитьDocumentID_2_Документ(DocumentID, BoxID)
		
		Возврат МетодСервера(,"DocumentID_2_Документ", DocumentID, BoxID);
		
	КонецФункции

	&НаКлиенте
	Функция ЭтоФормализованныйДокумент(DocumentType) Экспорт
		
		Возврат
		
			DocumentType = "XmlTorg12"
		ИЛИ DocumentType = "XmlAcceptanceCertificate"
		ИЛИ DocumentType = "UniversalTransferDocument"
		ИЛИ DocumentType = "UtdInvoice"
		ИЛИ DocumentType = "UtdTorg12"
		ИЛИ DocumentType = "UtdAcceptanceCertificate"
		ИЛИ DocumentType = "UniversalCorrectionDocument"
		ИЛИ DocumentType = "UniversalTransferDocumentRevision"
		ИЛИ DocumentType = "TovTorg"
		ИЛИ DocumentType = "Act552"
		ИЛИ ЛЕВ(DocumentType, 7) = "Invoice";
		
	КонецФункции
			
	&НаКлиенте
	Функция НайтиОрганизациюВИерархииОрганизацийDiadoc(BoxID, DepartmentId) Экспорт
		
		Возврат НайтиОрганизациюВИерархииОрганизацийDiadocРекурсия(BoxID, Платформа.ПараметрыКлиент.ИерархияОрганизацийDiadoc[DepartmentId]);
		
	КонецФункции
	
	&НаКлиенте
	Функция НайтиОрганизациюВИерархииОрганизацийDiadocРекурсия(BoxID, СтруктураПодразделения)
		
		Если СтруктураПодразделения = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Организация= МетодСервера(,"BoxID_2_Организация", BoxID, СтруктураПодразделения.DepartmentKpp);
		
		Если ЗначениеЗаполнено(Организация) Тогда
			Возврат Организация;
		КонецЕсли;
		
		Возврат НайтиОрганизациюВИерархииОрганизацийDiadocРекурсия(BoxID, Платформа.ПараметрыКлиент.ИерархияОрганизацийDiadoc[СтруктураПодразделения.ParentId]);
		
	КонецФункции
	
	&НаКлиенте
	Функция ТекстИнформацииОНовомФормате(ConfirmationDate) Экспорт
		
		ДатаПрименения = ?(ConfirmationDate<'20010101', ТекущаяДата(), ConfirmationDate);
		
		Если ДатаПрименения >= '20170701' Тогда // с 01.07.2017 СФ должен отправляться в формате УПД
			
			ТекстИнформации =
			"<HTML><HEAD>
			|<META content=""text/html; charset=utf-8"" http-equiv=Content-Type>
			|<META name=GENERATOR content=""MSHTML 11.00.10240.17202"">
			|<STYLE type=""text/css"">h3{margin-top:0.5em; margin-bottom:1em;} p{margin-top:0.2em; margin-bottom:0em;}</STYLE></HEAD>
			|<BODY>
			|<H3>С 1 июля 2017 года изменился формат счета-фактуры</H3>
			|<H4>После 1 июля налоговая может отказать в вычете НДС за старый формат</H4>
			|<P>Счет-фактура старого формата не считается выставленным, если он отправлен после 1 июля. Налоговая может отказать в вычете НДС получателю при представлении документов.</P>
			|<H4>Договоритесь с поставщиками о переходе на новый формат</H4>
			|<P>С 1 июля счета-фактуры нужно передавать в новом формате по <A href=""https://normativ.kontur.ru/document?moduleId=1&documentId=339569""> приказу №155 ФНС</A>. Если поставщик продолжает отправлять документы в старом формате, запросите аннулирование документов и попросите отправить их в новом формате.</P></BODY></HTML>";
			
		Иначе // с 14.04.2015 версия 5.02
			
			ТекстИнформации =
			"<HTML><HEAD>
			|<META content=""text/html; charset=utf-8"" http-equiv=Content-Type>
			|<META name=GENERATOR content=""MSHTML 11.00.10240.17202"">
			|<STYLE type=""text/css"">h3{margin-top:0.5em; margin-bottom:1em;} p{margin-top:0.2em; margin-bottom:0em;}</STYLE></HEAD>
			|<BODY>
			|<H3>14 апреля 2015 года изменился формат электронных счетов-фактур</H3>
			|<P>Согласно <A href=""http://publication.pravo.gov.ru/Document/View/0001201504030034"">приказу ФНС от 04.03.2015 № ММВ-7-6/93@</A> с 14 апреля 2015 года электронные счета-фактуры необходимо передавать в новом формате.</P>
			|<P>Как принимать к учету счета-фактуры в старом формате, переданные после 14 апреля, вы можете узнать в вашей ИФНС</P>
			|<P>На данный момент "+Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы+" поддерживает счета-фактуры как старого, так и нового форматов. Чтобы быть в курсе изменений в законодательстве, <A href=""http://www.diadoc.ru/blog"">подпишитесь на новости на нашем сайте</A></P></BODY></HTML>";
			
		КонецЕсли;
		
		Возврат ТекстИнформации;

	КонецФункции
	
	&НаСервере
	Функция СписокОбработокДереваКонфигурации() Экспорт
		
		СписокОбработок= 	 Новый СписокЗначений;
		МетаданныеОбработки= Метаданные.Обработки;
		
		Для Каждого МетаданныеОбработка ИЗ МетаданныеОбработки Цикл
			СписокОбработок.Добавить(МетаданныеОбработка.Имя, МетаданныеОбработка.Представление());
		КонецЦикла;
		
		Возврат СписокОбработок;
		
	КонецФункции
	
	&НаКлиенте
	Функция ДатаГоризонта() Экспорт
		
		Возврат ДобавитьМесяц(ТекущаяДата(), 12);
		
	КонецФункции
	
	&НаКлиенте
	Функция ИдентификаторСвойства(ИмяСвойства) Экспорт
		
		Возврат МетодСервера(,"ИдентификаторСвойства"+ИмяСвойства);
		
	КонецФункции
	
	&НаКлиенте
	Функция ВариантыИспользованияПроксиСервера() Экспорт
		
		ВариантыИспользованияПроксиСервера = Новый СписокЗначений;
		
		ВариантыИспользованияПроксиСервера.Добавить("НеИспользовать"		  , "Не использовать");
		ВариантыИспользованияПроксиСервера.Добавить("ИспользоватьПроксиСервер", "Использовать прокси-сервер");
		ВариантыИспользованияПроксиСервера.Добавить("ИспользоватьНастройкиIE" , "Использовать настройки Internet Explorer");
		
		Возврат ВариантыИспользованияПроксиСервера;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЭлементВРежимеВыбораИзСпискаПриИзменении(Форма, Элемент, ВыбранноеЗначение) Экспорт
		
		СократитьСписокВыбораДоВыбранногоЗначения(Элемент, ВыбранноеЗначение);
		
		СброситьКартинкуСпискаВыбора(Элемент);
		
		СинхронизироватьСпискиВыбораЭлементовПовторителей(Форма, Элемент);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СократитьСписокВыбораДоВыбранногоЗначения(Элемент, ВыбранноеЗначение) Экспорт
		
		ОбратныйИндекс= Элемент.СписокВыбора.Количество() - 1;
		
		Пока ОбратныйИндекс > -1 Цикл
			Если Элемент.СписокВыбора[ОбратныйИндекс].Значение <> ВыбранноеЗначение Тогда
				Элемент.СписокВыбора.Удалить(ОбратныйИндекс);
			КонецЕсли;
			ОбратныйИндекс= ОбратныйИндекс - 1;
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СброситьКартинкуСпискаВыбора(Элемент)
		
		Если Элемент.СписокВыбора.Количество() = 1 Тогда
			Элемент.СписокВыбора[0].Картинка= Новый Картинка;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СинхронизироватьСпискиВыбораЭлементовПовторителей(Форма, Элемент)
		
		МаскаИмени= Лев(Элемент.Имя, Найти(Элемент.Имя, "_") - 1);
		
		Для НомерЭлемента=1 ПО 10 Цикл
			
			ЭлементПовторитель= Форма.Элементы.Найти(МаскаИмени + "_" + Формат(НомерЭлемента, "ЧГ="));
			
			Если ЭлементПовторитель = Неопределено Тогда
				
				Прервать;
				
			ИначеЕсли ЭлементПовторитель <> Элемент Тогда
				
				ЭлементПовторитель.СписокВыбора.Очистить();
				
				Для Каждого ЭлементСписка ИЗ Элемент.СписокВыбора Цикл
					ЭлементПовторитель.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура ЗаписатьВЛог(ИдентификаторСобытия, Комментарий = "", ДокументСсылка = Неопределено) Экспорт
		
		ЗаписьЖурналаРегистрации(МетодСервера(,"ПолучитьСловарь").НаименованиеСистемы + "."+СтрЗаменить(ИдентификаторСобытия, " ", "_"),,,ДокументСсылка, Комментарий);
		
	КонецПроцедуры
	
	// Сравнить две строки версий.
	//
	// Параметры
	//  СтрокаВерсии1  - Строка - номер версии в формате РР.{П|ПП}.ЗЗ.СС
	//  СтрокаВерсии2  - Строка - второй сравниваемый номер версии
	//
	// Возвращаемое значение:
	//   Число   - больше 0, если СтрокаВерсии1 > СтрокаВерсии2; 0, если версии равны.
	//
	&НаКлиенте
	Функция СравнитьВерсии(Знач СтрокаВерсии1, Знач СтрокаВерсии2) Экспорт
		
		Строка1 = ?(ПустаяСтрока(СтрокаВерсии1), "0.0.0.0", СтрокаВерсии1);
		Строка2 = ?(ПустаяСтрока(СтрокаВерсии2), "0.0.0.0", СтрокаВерсии2);
		
		// Для составных номеров версий берем только первую часть.
		Строка1 = ПреобразоватьСтрокуВМассивПодстрок(Строка1, "/")[0];
		Строка2 = ПреобразоватьСтрокуВМассивПодстрок(Строка2, "/")[0];
		
		Версия1 = ПреобразоватьСтрокуВМассивПодстрок(Строка1, ".");
		Если Версия1.Количество() > 4 Тогда
			ВызватьИсключение СтрЗаменить(НСтр("ru = 'Неправильный формат параметра СтрокаВерсии1: %1'"), "%1", СтрокаВерсии1);
		Иначе
			Для Инекс = Версия1.Количество() По 4 Цикл
				Версия1.Добавить("0");
			КонецЦикла;
		КонецЕсли;
		Версия2 = ПреобразоватьСтрокуВМассивПодстрок(Строка2, ".");
		Если Версия2.Количество() > 4 Тогда
			ВызватьИсключение СтрЗаменить(НСтр("ru = 'Неправильный формат параметра СтрокаВерсии2: %1'"), "%1", СтрокаВерсии2);
		Иначе
			Для Инекс = Версия2.Количество() По 4 Цикл
				Версия2.Добавить("0");
			КонецЦикла;
		КонецЕсли;
		
		Результат = 0;
		Для Разряд = 0 По 3 Цикл
			Результат = Число(Версия1[Разряд]) - Число(Версия2[Разряд]);
			Если Результат <> 0 Тогда
				Возврат Результат;
			КонецЕсли;
		КонецЦикла;
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НеобходимоОграничениеНаОтправку(Документ1С, ВнешняяПечатнаяФорма, ПризнакПроведен) Экспорт
		
		ВозвращаемоеЗначение= Ложь;
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
			
			Если ЗначениеЗаполнено(ВнешняяПечатнаяФорма) = Истина Тогда //внешняя печатная форма всегда не проведена
				
				ДокументВалютный= МетодСервера("Модуль_ИнтеграцияБП30", "ЭтоВалютныйДокумент", Документ1С);
				Если ДокументВалютный И НЕ ДокументПроведен(Документ1С) Тогда
					ВозвращаемоеЗначение= Истина;	
				КонецЕсли;
				
			ИначеЕсли ЗначениеЗаполнено(ПризнакПроведен) = Истина И ПризнакПроведен = Ложь Тогда
				
				ДокументВалютный= МетодСервера("Модуль_ИнтеграцияБП30", "ЭтоВалютныйДокумент", Документ1С);
				Если ДокументВалютный = Истина Тогда
					ВозвращаемоеЗначение= Истина;	
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			ВозвращаемоеЗначение= Ложь;
		КонецЕсли;
		
		Возврат ВозвращаемоеЗначение;
		
	КонецФункции
	
	&НаСервереБезКонтекста
	Функция ДокументПроведен(ДокументНаПроверку) Экспорт
		
		Возврат ДокументНаПроверку.Проведен; 	
		
	КонецФункции

	&НаКлиенте
	Функция ВыделитьСлово(ИсходнаяСтрока, Разделитель = " ") Экспорт
		
		Буфер = СокрЛ(ИсходнаяСтрока);
		ПозицияПослПробела = Найти(Буфер, Разделитель);
		
		Если ПозицияПослПробела = 0 Тогда
			ИсходнаяСтрока = "";
			Возврат Буфер;
		КонецЕсли;
		
		ВыделенноеСлово = СокрЛП(Лев(Буфер, ПозицияПослПробела));
		ИсходнаяСтрока = Сред(ИсходнаяСтрока, ПозицияПослПробела + 1);
		
		Возврат ВыделенноеСлово;	
		
	КонецФункции
	
	&НаКлиенте
	Функция ЭДО_БиблиотекаКартинок() Экспорт
		
		Возврат МетодСервера(,"ЭДО_БиблиотекаКартинок");
		
	КонецФункции
	
	&НаКлиенте
	Функция ЗаполненныйДиалогВыбораФайла(МножественныйВыбор, Каталог) Экспорт
		
		ДиалогВыбора=						Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогВыбора.Заголовок=				НСтр("ru = 'Выбор " + ?(МножественныйВыбор, "файлов", "файла") + "'");
		ДиалогВыбора.Каталог=				Каталог;
		ДиалогВыбора.Фильтр=				НСтр("ru = 'Все файлы (*.*)|*.*'"); 
		ДиалогВыбора.МножественныйВыбор=	МножественныйВыбор;
		
		Возврат ДиалогВыбора;
		
	КонецФункции
	
	// Определяет базовую версию формата контента.
	//
	// Параметры:
	//  ВерсияФормата - Строка - версия вложения в терминах API (наример, utd820_05_01_01, tovtorg_05_01_04 и т.д.)
	// 
	// Возвращаемое значение:
	//  Строка - возможные значения определены в функции БазовыеФорматы()
	//
	&НаКлиенте
	Функция БазовыйФорматВерсииКонтента(ВерсияФормата) Экспорт
		
		БазовыеФорматы = БазовыеФорматы();
		
		Если НРег(Лев(ВерсияФормата, 7)) = "utd820_" Тогда
			
			Результат = БазовыеФорматы.utd820;
			
		ИначеЕсли НРег(Лев(ВерсияФормата, 4)) = "utd_" Тогда
			
			Результат = БазовыеФорматы.utd;
			
		ИначеЕсли НРег(Лев(ВерсияФормата, 4)) = "ucd_" Тогда
			
			Результат = БазовыеФорматы.ucd;
			
		ИначеЕсли НРег(Лев(ВерсияФормата, 8)) = "tovtorg_" тогда
			
			Результат = БазовыеФорматы.tovtorg;
			
		ИначеЕсли НРег(Лев(ВерсияФормата, 6)) = "rezru_" Тогда
			
			Результат = БазовыеФорматы.rezru;
			
		ИначеЕсли ВерсияФормата = "UniversalCorrectionDocument" Тогда
			
			Результат = БазовыеФорматы.ucd;
			
		ИначеЕсли ВерсияФормата = "UniversalTransferDocument" Тогда
			
			Результат = БазовыеФорматы.utd;
			
		ИначеЕсли НРег(Лев(ВерсияФормата, 8)) = "invoice_" Тогда
			
			Результат = БазовыеФорматы.invoice;
			
		ИначеЕсли НРег(Лев(ВерсияФормата, 11)) = "invoicecor_" Тогда
			
			Результат = БазовыеФорматы.invoicecor;
			
		ИначеЕсли НРег(Лев(ВерсияФормата, 7)) = "torg12_" Тогда
			
			Результат = БазовыеФорматы.torg12;
			
		ИначеЕсли НРег(Лев(ВерсияФормата, 4)) = "act_" Тогда
			
			Результат = БазовыеФорматы.act;
			
		ИначеЕсли НРег(Лев(ВерсияФормата, 16)) = "proformainvoice_" Тогда
			
			Результат = БазовыеФорматы.proformainvoice;
			
		ИначеЕсли НРег(ВерсияФормата) = "v1" Тогда
			
			Результат = БазовыеФорматы.v1;
			
		Иначе
			
			Результат = БазовыеФорматы.unknown; // Все остальное будем считать неформализованными
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Возвращает тип протоконтента электронного документа.
	//
	// Параметры:
	//  ВерсияФормата	 - Строка - версия вложения в терминах API (наример, utd820_05_01_01, tovtorg_05_01_04 и т.д.)
	//  ТипДокумента	 - Строка - имя типа документа в терминах компоненты. Это производные типы от Document.
	// 
	// Возвращаемое значение:
	//  Строка - тип контента для формализованных документов,
	//			 NonformalizedContent для неформализованного и любого полуформализованного.
	//
	&НаКлиенте
	Функция ПолучитьТипКонтента(ВерсияФормата, ТипДокумента) Экспорт
		
		БазовыйФормат = БазовыйФорматВерсииКонтента(ВерсияФормата);
		
		Результат = ТипКонтентаПоБазовомуФормату(БазовыйФормат);
		
		Если НЕ ЗначениеЗаполнено(Результат) Тогда
			Результат = ТипКонтентаПоТипуДокументаCOM(ТипДокумента);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Результат) Тогда
			
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Неизвестный тип контента (Version = %1, DocumentType = %2)!'")
				, ВерсияФормата
				, ТипДокумента);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Определяет значение параметра ФункцияУПД, используемого при
	// сборе контента по документам, отправляемым в формате УПД или УКД.
	//
	// Параметры:
	//  ФорматОтправки - Структура - см. функцию НовоеОписаниеФорматаЭлектронногоДокумента();
	// 
	// Возвращаемое значение:
	//  Строка - для накладных, актов или счетов-фактур в формате УПД/УКД.
	//  Неопределено - для всех остальных
	//
	&НаКлиенте
	Функция ФункцияДокументаДляСбораКонтента(ФорматОтправки) Экспорт
		
		ВерсияФормата = ФорматОтправки.ВерсияФормата;
		Если ВерсияФормата = "v1" Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Если ФорматОтправки.ФункцияДокумента <> "default" Тогда
			Возврат ФорматОтправки.ФункцияДокумента;
		КонецЕсли;
		
		БазовыеФорматы = БазовыеФорматы();
		БазовыйФормат = БазовыйФорматВерсииКонтента(ВерсияФормата);
		
		ТребуетсяВычислитьФункцию = Ложь;
		Если БазовыйФормат = БазовыеФорматы.utd820
			Или БазовыйФормат = БазовыеФорматы.utd
			Или БазовыйФормат = БазовыеФорматы.ucd Тогда
			ТребуетсяВычислитьФункцию = Истина;
		КонецЕсли;
		
		Если Не ТребуетсяВычислитьФункцию Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ТипДокумента = ФорматОтправки.ИмяТипа;
		
		Если ТипДокумента = "Invoice" 
			Или ТипДокумента = "InvoiceRevision" Тогда
			
			Результат = "СЧФ";
			
		ИначеЕсли ТипДокумента = "InvoiceCorrection" 
			Или ТипДокумента = "InvoiceCorrectionRevision" Тогда
			
			Результат = "КСЧФ";
			
		Иначе // Накладные или акты в формате УПД
			Результат = "ДОП";
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Определяет тип протоконтента по идентификатору базовой версии формата.
	//
	// Параметры:
	//  ВерсияФормата - Строка - см. функцию БазовыеФорматы;
	// 
	// Возвращаемое значение:
	//  Строка - 
	//
	&НаКлиенте
	Функция ТипКонтентаПоБазовомуФормату(БазовыйФормат)
		
		БазовыеФорматы	 = БазовыеФорматы();
		ТипыКонтента	 = ТипыКонтента();
		
		ТипыКонтентаБазовыхФорматов = Новый Соответствие;
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.utd820			, ТипыКонтента.Utd820SellerContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.utd				, ТипыКонтента.UtdSellerContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.ucd				, ТипыКонтента.UcdSellerContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.tovtorg			, ТипыКонтента.TovTorgSellerContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.torg12			, ТипыКонтента.Torg12SellerContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.rezru			, ТипыКонтента.Act552SellerContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.act				, ТипыКонтента.AcceptanceCertificateSellerContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.invoice			, ТипыКонтента.InvoiceContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.invoicecor		, ТипыКонтента.InvoiceCorrectionContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.proformainvoice	, ТипыКонтента.ProformaInvoiceContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.v1				, ТипыКонтента.NonformalizedContent);
		ТипыКонтентаБазовыхФорматов.Вставить(БазовыеФорматы.unknown			, ТипыКонтента.NonformalizedContent);
		
		Результат = ТипыКонтентаБазовыхФорматов[БазовыйФормат];
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ТипКонтентаПоТипуДокументаCOM(ТипДокумента)
		
		ТипыКонтента = ТипыКонтента();
		
		ТипыКонтентаДокументов = Новый Соответствие;
		ТипыКонтентаДокументов.Вставить("TovTorg"						, ТипыКонтента.TovTorgSellerContent);
		ТипыКонтентаДокументов.Вставить("Act552"						, ТипыКонтента.Act552SellerContent);
		ТипыКонтентаДокументов.Вставить("UniversalTransferDocument"		, ТипыКонтента.UtdSellerContent);
		ТипыКонтентаДокументов.Вставить("UniversalCorrectionDocument"	, ТипыКонтента.UcdSellerContent);
		ТипыКонтентаДокументов.Вставить("XmlTorg12"						, ТипыКонтента.Torg12SellerContent);
		ТипыКонтентаДокументов.Вставить("XmlAcceptanceCertificate"		, ТипыКонтента.AcceptanceCertificateSellerContent);
		ТипыКонтентаДокументов.Вставить("Invoice"						, ТипыКонтента.InvoiceContent);
		ТипыКонтентаДокументов.Вставить("InvoiceRevision"				, ТипыКонтента.InvoiceContent);
		ТипыКонтентаДокументов.Вставить("InvoiceCorrection"				, ТипыКонтента.InvoiceCorrectionContent);
		ТипыКонтентаДокументов.Вставить("InvoiceCorrectionRevision"		, ТипыКонтента.InvoiceCorrectionContent);
		
		Результат = ТипыКонтентаДокументов[ТипДокумента];
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	//выполнение GET запроса
	Функция ЗапросНаПолучениеФайла(ПутьКФайлу, ФайлРезультата = "", Заголовки = Неопределено) Экспорт
		
		Результат = Неопределено;
		
		Если ФайлРезультата = "" Тогда 
			ФайлРезультата = ПолучитьИмяВременногоФайла();
		КонецЕсли;	
		
		Ответ = Компонента_HttpTransport(ПутьКФайлу, Заголовки, ФайлРезультата);
		
		Если Ответ.StatusCode = 200 Тогда
			Результат = Ответ.HttpContent.FilePath;
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Готовит GET запрос в Билли по наличию выставленных счетов на оплату. Возвращает ответ от Билли.
	//
	// Параметры:
	//  ИНН  					- Строка - ИНН организации
	//  КПП  					- Строка - КПП организации.
	//  АвторизационныйТокен  	- Строка - Токен, под которым проходила авторизация в Диадок
	//
	// Возвращаемое значение:
	//   Структура, Строка   - если от сервера Билли вернулся корректный JSON, тогда возвращает структуру, в противном случае вернет непосредственно строку JSON.
	//
	&НаКлиенте
	Функция Kontur_API_PortalBills_Metas(ИНН, КПП = "", АвторизационныйТокен) Экспорт
	
		АдресЗапроса = "https://api.kontur.ru/renewals/api/v3/metas/byinn";
			 
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить("productId=diadoc");
		МассивПараметров.Добавить("source=widget");
		МассивПараметров.Добавить("inn=" + ИНН);
			
		Если ЗначениеЗаполнено(КПП) Тогда
			МассивПараметров.Добавить("kpp=" + КПП);
		КонецЕсли;
		 
		АдресЗапроса = АдресЗапроса + "?" + Преобразование_МассивВСтроку(МассивПараметров, "&");
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Accept"			, "application/json");
		Заголовки.Вставить("Content-Type"	, "application/json; charset=utf-8");
		Заголовки.Вставить("x-Auth-ddToken"	, АвторизационныйТокен);
		
		Ответ = Компонента_HttpTransport(АдресЗапроса, Заголовки);
		
		Если Ответ.StatusCode = 200 Тогда
			Результат = РазобранныйОтветАПИ(Ответ.HttpContent.DataString);
		КонецЕсли;
		
		Возврат Результат;
	
	КонецФункции // Kontur_API_PortalBills_Metas()
	
	//GET запрос в Билли по сохранению файла счета на оплату
	&НаКлиенте
	Функция Kontur_API_PortalBills_pdf(ИДТарифа, ИДЛицевогоСчета, АвторизационныйТокен) Экспорт
		
		АдресЗапроса = "https://api.kontur.ru/renewals/api/v2/pdf";
	
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить("billId=" + ИДТарифа);
		МассивПараметров.Добавить("accountId=" + ИДЛицевогоСчета);
		МассивПараметров.Добавить("productId=diadoc");
		МассивПараметров.Добавить("realm=diadoc");
						
		АдресЗапроса = АдресЗапроса + "?" + Преобразование_МассивВСтроку(МассивПараметров, "&");
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("x-Auth-ddToken"	, АвторизационныйТокен);
		
		Попытка
			Результат = ЗапросНаПолучениеФайла(АдресЗапроса, , Заголовки);
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		Возврат Результат;
		
	КонецФункции //Kontur_API_PortalBills_pdf()
	
	// Отправляет HTTP запрос, используя внешнюю компоненту
	//
	// Параметры:
	//  АдресЗапроса  	- Строка 		- адрес метода, к которому обращаемся
	//  Заголовки 		- Соответствие 	- заголовки для HTTP запроса
	//  ФайлРезультата	- Строка 		- полный путь до файла, в который необходимо вернуть результат запроса
	//
	// Возвращаемое значение:
	//   Структура   	- содержит ключи: DataString, FilePath
	//
	&НаКлиенте
	Функция Компонента_HttpTransport( АдресЗапроса 
									, Заголовки = Неопределено 
									, ФайлРезультата = "" 
									, ТелоЗапроса = "" 
									, МетодPost = Ложь	) Экспорт
		
		Ответ = Новый_HttpОтвет();
		
		DiadocConnection = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection;
		
		Если ТипЗнч(DiadocConnection) = Тип("COMОбъект") Тогда
			
			Transport 	= DiadocConnection.CreateHttpTransport();
			Factory 	= Transport.CreateContentFactory();
			
			Request		= Transport.CreateRequest();
			Request.Uri = АдресЗапроса;
			
			// По умолчанию используется метод GET
			Если МетодPost Тогда
				Request.Method = "POST";
			КонецЕсли;
			
			Если ТипЗнч(Заголовки) = Тип("Соответствие") Тогда
				Для каждого ЭлЗаголовок из Заголовки Цикл
					Request.AddHeader(ЭлЗаголовок.Ключ, ЭлЗаголовок.Значение);
				КонецЦикла;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТелоЗапроса) Тогда
				Request.RequestContent = Factory.CreateMemoryContentFromString(ТелоЗапроса);	
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ФайлРезультата) Тогда
				Request.ResponseContent	= Factory.CreateFileContent(ФайлРезультата);
			Иначе
				Request.ResponseContent = Factory.CreateMemoryContent();
			КонецЕсли;
						
			Response = Transport.Send(Request);
						
			Ответ.StatusCode = Response.StatusCode;
			ЗаполнитьЗначенияСвойств(Ответ.HttpContent, Request.ResponseContent);
			
		КонецЕсли;
						
		Возврат Ответ;
	
	КонецФункции // Компонента_HttpTransport()
	
	&НаКлиенте
	Функция Новый_HttpContent()
		
		Возврат Новый Структура("DataString, FilePath", "", Неопределено);
		
	КонецФункции //Новый_HttpContent()
	
	&НаКлиенте
	Функция Новый_HttpОтвет()
		
		Возврат Новый Структура("StatusCode, HttpContent", 0, Новый_HttpContent());
		
	КонецФункции //Новый_HttpОтвет()

	// Преобразует содержимое JSON в коллекцию
	//
	// Параметры:
	//  СтрокаJSON  - Строка - ответ сервера в формате JSON
	// Возвращаемое значение:
	//   Структура, Строка
	//
	&НаКлиенте
	Функция РазобранныйОтветАПИ(СтрокаJSON)
		
		Если НЕ ЗначениеЗаполнено(СтрокаJSON) Тогда
			Возврат Новый Структура;
		КонецЕсли;
		
		Если НЕ ПлатформаСтаршеЧем_8_3_6_1977() Тогда
		
			ИмяВремФайла = ПолучитьИмяВременногоФайла();
			
			ТД = Новый ТекстовыйДокумент;
			ТД.УстановитьТекст(СтрокаJSON);
			ТД.Записать(ИмяВремФайла, "UTF-8");
		
			Чтение = Новый("ЧтениеJSON");//8.3
			Чтение.ОткрытьФайл(ИмяВремФайла, "UTF-8");
			Попытка
				Результат = Вычислить("ПрочитатьJSON(Чтение)");
			Исключение
				Сообщить(ОписаниеОшибки());
				Возврат СтрокаJSON;  // некорректный JSON - сервер вернул текст ошибки. Вернем его просто как строку.
			КонецПопытки;
			Чтение.Закрыть();
			УдалитьФайлы(ИмяВремФайла);
			
		Иначе
			
			Результат = СтрокаJSON;
			
		КонецЕсли;
		
		Возврат Результат;
	
	КонецФункции // РазобранныйОтветАПИ()
	
	&НаКлиенте
	Функция ПлатформаСтаршеЧем_8_3_6_1977()

		СИ = Новый СистемнаяИнформация;
		ВерсияПлатформы = СИ.ВерсияПриложения;
		
		ВерсияПлатформыМассивом = Преобразование_СтрокуВМассивСлов(СИ.ВерсияПриложения, ".");
		ВтораяЦифраВерсии = Число(ВерсияПлатформыМассивом[1]);
		ТретьяЦифраВерсии = Число(ВерсияПлатформыМассивом[2]);
		
		Если ВтораяЦифраВерсии < 3
			ИЛИ (ВтораяЦифраВерсии = 3 И ТретьяЦифраВерсии < 6)  Тогда
			// 8.3.6 - самая первая официальная платформа на https://releases.1c.ru/project/Platform83
			// Предполагаем, что старше нее в линейке 8.3.6 ничего нет.
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	КонецФункции //ПлатформаСтаршеЧем_8_3_6_1977()
	
	// Объединяет элементы массива в строку с использованием разделителя
	//
	// Параметры:
	//  МассивСтрок	 - Массив 		- обрабатываемый массив 
	//  Разделитель	 - Произвольный	- добавляется между элементами массива
	// 
	// Возвращаемое значение:
	//   Строка - 
	//
	&НаКлиенте
	Функция Преобразование_МассивВСтроку(МассивСтрок, Знач Разделитель = Неопределено) Экспорт
	
		Результат = "";
		
		Если ТипЗнч(МассивСтрок) = Тип("Массив") Тогда
			
			Если Разделитель = Неопределено Тогда
				Разделитель = Символы.ПС;
			КонецЕсли;
			
			Для Каждого Эл Из МассивСтрок Цикл
				Результат = Результат + ?(СтрДлина(Результат) = 0, "", Разделитель) + Эл;
			КонецЦикла;		
			
		ИначеЕсли ТипЗнч(МассивСтрок) = Тип("Строка") Тогда
			Результат = МассивСтрок;		
		КонецЕсли;	
		
		Возврат Результат;
		
	КонецФункции //Преобразование_МассивВСтроку()
	
	// Разбивает строку на элементы, помещая их в массив
	//
	// Параметры:
	//  Строка			 - Строка		- исходная строка
	//  РазделителиСлов	 - Произвольный	- разделитель по которому происходит декомпозиция строки 
	// 
	// Возвращаемое значение:
	//   Массив 
	//
	&НаКлиенте
	Функция Преобразование_СтрокуВМассивСлов(Знач Строка, РазделителиСлов = ",") Экспорт

		Слова = Новый Массив;
		
		Для Сч = 1 По СтрДлина(РазделителиСлов) Цикл
			Строка = СтрЗаменить(Строка, Сред(РазделителиСлов, Сч, 1), Символы.ПС);
		КонецЦикла;
		
		Для Сч = 1 По СтрЧислоСтрок(Строка) Цикл
			
			ТекСлово = СокрЛП(СтрПолучитьСтроку(Строка, Сч));
			Если ТекСлово <> "" Тогда
				Слова.Добавить(ТекСлово);
			КонецЕсли;
			
		КонецЦикла;	
		
		Возврат Слова;
		
	КонецФункции //Служебные_СтрокуВМассивСлов()
	
	// Возвращает значение свойства структуры или фиксированной структуры.
	//
	// Параметры:
	//   Структура           - Структура, ФиксированнаяСтруктура - объект, из которого необходимо прочитать значение ключа.
	//   Ключ                - Строка       - имя свойства структуры, для которого необходимо прочитать значение.
	//   ЗначениеПоУмолчанию - Произвольный - необязательный, возвращается когда в структуре нет значения по указанному ключу.
	//
	// Возвращаемое значение:
	//   Произвольный        - значение свойства структуры или параметра "ЗначениеПоУмолчанию".
	//
	&НаКлиенте
	Функция СвойствоСтруктуры(Структура, Ключ, ЗначениеПоУмолчанию = Неопределено) Экспорт
		
		Результат = ЗначениеПоУмолчанию;

		ЭтоСтруктура = Ложь
			Или ТипЗнч(Структура) = Тип("Структура")
			Или ТипЗнч(Структура) = Тип("ФиксированнаяСтруктура");
		
		Если ЭтоСтруктура И Структура.Свойство(Ключ) Тогда
			Результат = Структура[Ключ];
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции //СвойствоСтруктуры()
		
	&НаКлиенте
	Функция ПолучитьСтруктуруСодержанияДокумента(Document) Экспорт
		
		ТипыКонтента = ТипыКонтента();
		
		Прото = ПолучитьProto(Document);
		
		ТипКонтента = Прото.ТипКонтента;
		Контент = Прото.Контент;
		
		Результат = Новый_СтруктураСодержаниеДокумента();
		Результат.Document	 = Document;
		Результат.Type		 = Document.Type;
		
		Если ТипКонтента = ТипыКонтента.Utd820SellerContent 
			Или ТипКонтента = ТипыКонтента.UtdSellerContent Тогда
			
			ЗаполнитьСтруктуруСодержанияДокументаПоUniversalTransferDocument(Результат, Прото);
			
		ИначеЕсли ТипКонтента = ТипыКонтента.Torg12SellerContent Тогда
			
			ЗаполнитьСтруктуруСодержанияДокументаПоXmlTorg12(Результат, Прото);
			
		ИначеЕсли ТипКонтента = ТипыКонтента.AcceptanceCertificateSellerContent Тогда
			
			ЗаполнитьСтруктуруСодержанияДокументаПоXmlAcceptanceCertificate(Результат, Прото);
			
		ИначеЕсли ТипКонтента = ТипыКонтента.InvoiceContent Тогда
			
			ЗаполнитьСтруктуруСодержанияДокументаПоInvoice(Результат, Прото);
			
		ИначеЕсли ТипКонтента = ТипыКонтента.TovTorgSellerContent Тогда
			
			ЗаполнитьСтруктуруСодержанияДокументаПоTovTorg(Результат, Прото);
			
		ИначеЕсли ТипКонтента = ТипыКонтента.Act552SellerContent Тогда
			
			ЗаполнитьСтруктуруСодержанияДокументаПоAct552(Результат, Прото);
			
		Иначе
			ВызватьИсключение "Неизвестный Type объекта ЭДОбъект: """ + Document.Type + """";
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьСтруктуруСодержанияДокументаПоXmlTorg12(стДокумент, Прото)
		
		тзТабличнаяЧасть = Новый Массив();
		ПротоКонтент	 = Прото.Контент;
		
		НалоговыйАгентПоНДС = Ложь;
		
		СуммаВсего = 0;
		НДСВсего = 0;
		
		Для Каждого Item Из ПротоКонтент.Items Цикл
			
			СтрокаТЧ = Новый_СтруктураСтрокаТЧ();
			
			СтрокаТЧ.Name		= ?(ЗначениеЗаполнено(Item.Product), Item.Product, Неопределено);
			СтрокаТЧ.Code		= ?(ЗначениеЗаполнено(Item.ProductCode), Item.ProductCode, Неопределено);
			СтрокаТЧ.NomenclatureArticle = ?(ЗначениеЗаполнено(Item.Article), Item.Article, Неопределено);
			СтрокаТЧ.UnitCode	= ?(ЗначениеЗаполнено(Item.UnitCode), Item.UnitCode, Неопределено);
			СтрокаТЧ.UnitName	= ?(ЗначениеЗаполнено(Item.UnitName), Item.UnitName, Неопределено);
			СтрокаТЧ.Quantity	= ЧислоИзКомпоненты(Item.Quantity);
			СтрокаТЧ.Price		= ЧислоИзКомпоненты(Item.Price);
			СтрокаТЧ.SubtotalWithVatExcluded = ЧислоИзКомпоненты(Item.TotalWithVatExcluded);
			СтрокаТЧ.Vat		= ЧислоИзКомпоненты(Item.Vat);
			СтрокаТЧ.Subtotal	= ЧислоИзКомпоненты(Item.Total);
			СтрокаТЧ.TaxRate	= ?(ЗначениеЗаполнено(Item.TaxRate), Item.TaxRate, Неопределено);
			СтрокаТЧ.VatIsCalculatedByTaxAgent = НДСИсчисляетсяНалоговымАгентом(Item.TaxRate);
			
			НалоговыйАгентПоНДС = НалоговыйАгентПоНДС Или СтрокаТЧ.VatIsCalculatedByTaxAgent;
			
			тзТабличнаяЧасть.Добавить(СтрокаТЧ);
			
			СуммаВсего = СуммаВсего + СтрокаТЧ.Subtotal;
			НДСВсего = НДСВсего + СтрокаТЧ.Vat;
			
		КонецЦикла;
		
		стДокумент.Items = тзТабличнаяЧасть;
		
		стДокумент.ДатаДоговора 		= ?(ЗначениеЗаполнено(ПротоКонтент.GroundDate), ПротоКонтент.GroundDate, "");
		стДокумент.НомерДоговора 		= ?(ЗначениеЗаполнено(ПротоКонтент.GroundNumber), ПротоКонтент.GroundNumber, "");
		стДокумент.НаименованиеДоговора = ?(ЗначениеЗаполнено(ПротоКонтент.GroundName), ПротоКонтент.GroundName, "");
		
		стДокумент.Total 	= СуммаВсего;
		стДокумент.Vat 		= НДСВсего;
		стДокумент.isTaxAgent 	= НалоговыйАгентПоНДС;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСтруктуруСодержанияДокументаПоXmlAcceptanceCertificate(стДокумент, Прото)
		
		тзТабличнаяЧасть = Новый Массив();
		ПротоКонтент	 = Прото.Контент;
		
		СуммаВсего = 0;
		НДСВсего = 0;
		
		Для Каждого Item Из ПротоКонтент.Items Цикл
			
			СтрокаТЧ = Новый_СтруктураСтрокаТЧ();
					
			Если ЗначениеЗаполнено(Item.Name) Тогда
				СтрокаТЧ.Name = Item.Name;
			ИначеЕсли ЗначениеЗаполнено(Item.Description) Тогда
				СтрокаТЧ.Name = Item.Description;
			Иначе
				СтрокаТЧ.Name = Неопределено;
			КонецЕсли;
			
			СтрокаТЧ.Code		= "";
			СтрокаТЧ.UnitCode	= ?(ЗначениеЗаполнено(Item.UnitCode), Item.UnitCode, Неопределено);
			СтрокаТЧ.UnitName	= ?(ЗначениеЗаполнено(Item.UnitName), Item.UnitName, Неопределено);
			СтрокаТЧ.Quantity	= ?(ЗначениеЗаполнено(Item.Quantity), ЧислоИзКомпоненты(Item.Quantity), 0);
			СтрокаТЧ.Price		= ?(ЗначениеЗаполнено(Item.Price), ЧислоИзКомпоненты(Item.Price), 0);
			СтрокаТЧ.SubtotalWithVatExcluded = ?(ЗначениеЗаполнено(Item.TotalWithVatExcluded), ЧислоИзКомпоненты(Item.TotalWithVatExcluded), 0);
			СтрокаТЧ.Vat		= ?(ЗначениеЗаполнено(Item.Vat), ЧислоИзКомпоненты(Item.Vat), 0);
			СтрокаТЧ.Subtotal	= ?(ЗначениеЗаполнено(Item.Total), ЧислоИзКомпоненты(Item.Total), 0);
			СтавкаНДС			= ОпределитьСтавкуНДСПоУслуге(?(ЗначениеЗаполнено(Item.TotalWithVatExcluded), Item.TotalWithVatExcluded, 0), ?(ЗначениеЗаполнено(Item.Vat), Item.Vat, 0));
			СтрокаТЧ.TaxRate	= ?(ЗначениеЗаполнено(СтавкаНДС), СтавкаНДС, Неопределено);
			
			Если СтрокаТЧ.TaxRate = "0" Тогда
				СтрокаТЧ.TaxRate = ""; //для того, чтобы выводилась ставка "Без НДС"	
			КонецЕсли;
			
			тзТабличнаяЧасть.Добавить(СтрокаТЧ);
			
			СуммаВсего = СуммаВсего + СтрокаТЧ.Subtotal;
			НДСВсего = НДСВсего + СтрокаТЧ.Vat;
			
		КонецЦикла;
		
		стДокумент.Items = тзТабличнаяЧасть;
		
		стДокумент.ДатаДоговора 		= "";
		стДокумент.НомерДоговора 		= "";
		стДокумент.НаименованиеДоговора = "";
		
		стДокумент.Total 	= СуммаВсего;
		стДокумент.Vat 		= НДСВсего;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСтруктуруСодержанияДокументаПоInvoice(стДокумент, Прото)
		
		тзТабличнаяЧасть = Новый Массив;
		ПротоКонтент	 = Прото.Контент;
		
		СуммаВсего = 0;
		НДСВсего = 0;
		
		Для Каждого Item Из ПротоКонтент.Items Цикл
			
			СтрокаТЧ = Новый_СтруктураСтрокаТЧ(Истина);
			
			СтрокаТЧ.Name			= Item.Product;
			СтрокаТЧ.UnitCode		= Item.UnitCode;
			СтрокаТЧ.Quantity		= ЧислоИзКомпоненты(Item.Quantity);
			СтрокаТЧ.Price			= ЧислоИзКомпоненты(Item.Price);
			СтрокаТЧ.SubtotalWithVatExcluded = Item.TotalWithVatExcluded;
			СтрокаТЧ.Subtotal		= Item.Total;
			СтрокаТЧ.Vat			= ЧислоИзКомпоненты(Item.Vat);
			СтрокаТЧ.TaxRate		= Item.TaxRate;
			СтрокаТЧ.CountryCode	= Item.CountriesOfOrigin;
			СтрокаТЧ.TDNumber		= Item.CustomsDeclarationNumbers;
			
			тзТабличнаяЧасть.Добавить(СтрокаТЧ);
			
			СуммаВсего = СуммаВсего + СтрокаТЧ.Subtotal;
			НДСВсего = НДСВсего + СтрокаТЧ.Vat;
			
		КонецЦикла;
		
		стДокумент.Items = тзТабличнаяЧасть;
		
		стДокумент.ДатаДоговора 		= "";
		стДокумент.НомерДоговора 		= "";
		стДокумент.НаименованиеДоговора = "";
		
		стДокумент.Total 	= СуммаВсего;
		стДокумент.Vat 		= НДСВсего;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСтруктуруСодержанияДокументаПоUniversalTransferDocument(стДокумент, Прото)
		
		ЭтоУПД820 = (Прото.ТипКонтента = ТипыКонтента().Utd820SellerContent);
		
		тзТабличнаяЧасть = Новый Массив();
		ПротоКонтент	 = Прото.Контент;
		
		НалоговыйАгентПоНДС = Ложь;
		
		СуммаВсего = 0;
		НДСВсего = 0;
		
		КоллекцияСтрок = ?(ЭтоУПД820, ПротоКонтент.Table.Items, ПротоКонтент.InvoiceTable.Items);
		
		Для Каждого Item Из КоллекцияСтрок Цикл
		
			СтрокаТЧ = Новый_СтруктураСтрокаТЧ(Истина);
			
			Если ЭтоУПД820 Тогда
				Артикул = Item.ItemVendorCode;
				КодЕдИзм = Item.Unit;
			Иначе
				Артикул = Item.VendorCode;
				КодЕдИзм = Item.UnitCode;
			КонецЕсли;
			
			СтрокаТЧ.NomenclatureArticle = ?(ЗначениеЗаполнено(Артикул), Артикул, Неопределено);
			СтрокаТЧ.Name		= Item.Product;
			СтрокаТЧ.UnitCode	= КодЕдИзм;
			СтрокаТЧ.UnitName	= ?(ЗначениеЗаполнено(Item.UnitName), Item.UnitName, Неопределено);
			СтрокаТЧ.Quantity	= ЧислоИзКомпоненты(Item.Quantity);
			СтрокаТЧ.Price		= ЧислоИзКомпоненты(Item.Price);
			СтрокаТЧ.SubtotalWithVatExcluded = Item.SubtotalWithVatExcluded;
			СтрокаТЧ.Subtotal	= Item.Subtotal;
			СтрокаТЧ.Vat		= ЧислоИзКомпоненты(Item.Vat);
			СтрокаТЧ.TaxRate	= Item.TaxRate;
			СтрокаТЧ.VatIsCalculatedByTaxAgent = НДСИсчисляетсяНалоговымАгентом(Item.TaxRate);
			
			НалоговыйАгентПоНДС = НалоговыйАгентПоНДС Или СтрокаТЧ.VatIsCalculatedByTaxAgent;
			
			CustomDeclarations 	= Item.CustomDeclarations;
			
			КоличествоCustomDeclarations = CustomDeclarations.Количество();
			Если КоличествоCustomDeclarations > 0 Тогда
				Если КоличествоCustomDeclarations = 1 Тогда
					СтрокаCustomDeclarations 	= CustomDeclarations[0];
					СтрокаТЧ.CountryCode		= СтрокаCustomDeclarations.CountryCode;
					СтрокаТЧ.TDNumber			= СтрокаCustomDeclarations.DeclarationNumber;
				Иначе
					Сообщить("" + СтрокаТЧ.Name + ", количество = " + СтрокаТЧ.Quantity + ", цена = " + СтрокаТЧ.Price + ": указано более одного значения ""CustomsDeclaration"", данные по ГТД в строке не заполнены!");
				КонецЕсли;
			КонецЕсли;
				
			тзТабличнаяЧасть.Добавить(СтрокаТЧ);
			
			СуммаВсего = СуммаВсего + СтрокаТЧ.Subtotal;
			НДСВсего = НДСВсего + СтрокаТЧ.Vat;
			
		КонецЦикла;
		
		стДокумент.Items = тзТабличнаяЧасть;
		
		стДокумент.ДатаДоговора 		= "";
		стДокумент.НомерДоговора 		= "";
		стДокумент.НаименованиеДоговора = "";
		
		стДокумент.Total 	= СуммаВсего;
		стДокумент.Vat 		= НДСВсего;
		стДокумент.isTaxAgent 	= НалоговыйАгентПоНДС;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСтруктуруСодержанияДокументаПоTovTorg(стДокумент, Прото)
		
		тзТабличнаяЧасть = Новый Массив();
		ПротоКонтент	 = Прото.Контент;
		
		НалоговыйАгентПоНДС = Ложь;
		
		СуммаВсего = 0;
		НДСВсего = 0;
		
		Для Каждого Item Из ПротоКонтент.Table.Items Цикл
			
			СтрокаТЧ = Новый_СтруктураСтрокаТЧ();
					
			СтрокаТЧ.Code		= ?(ЗначениеЗаполнено(Item.ProductCode ), Item.ProductCode, Неопределено);
			СтрокаТЧ.NomenclatureArticle = ?(ЗначениеЗаполнено(Item.VendorCode ), Item.VendorCode, Неопределено);
			СтрокаТЧ.Name		= Item.Product;
			СтрокаТЧ.UnitCode	= Item.Unit;
			СтрокаТЧ.UnitName	= ?(ЗначениеЗаполнено(Item.UnitName), Item.UnitName, Неопределено);
			СтрокаТЧ.Quantity	= ЧислоИзКомпоненты(Item.Net);
			СтрокаТЧ.Price		= ЧислоИзКомпоненты(Item.Price);
			СтрокаТЧ.SubtotalWithVatExcluded = Item.SubtotalWithVatExcluded;
			СтрокаТЧ.Subtotal	= Item.Subtotal;
			СтрокаТЧ.Vat		= ЧислоИзКомпоненты(Item.Vat);
			СтрокаТЧ.TaxRate	= Item.TaxRate;
			СтрокаТЧ.VatIsCalculatedByTaxAgent = НДСИсчисляетсяНалоговымАгентом(Item.TaxRate);
			
			НалоговыйАгентПоНДС = НалоговыйАгентПоНДС Или СтрокаТЧ.VatIsCalculatedByTaxAgent;
			
			тзТабличнаяЧасть.Добавить(СтрокаТЧ);
			
			СуммаВсего = СуммаВсего + СтрокаТЧ.Subtotal;
			НДСВсего = НДСВсего + СтрокаТЧ.Vat;
			
		КонецЦикла;
		
		стДокумент.Items=	тзТабличнаяЧасть;
		
		стДокумент.ДатаДоговора 		= "";
		стДокумент.НомерДоговора 		= "";
		стДокумент.НаименованиеДоговора = "";
		
		стДокумент.Total 	= СуммаВсего;
		стДокумент.Vat 		= НДСВсего;
		стДокумент.isTaxAgent 	= НалоговыйАгентПоНДС;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСтруктуруСодержанияДокументаПоAct552(стДокумент, Прото)
		
		тзТабличнаяЧасть = Новый Массив;
		ПротоКонтент	 = Прото.Контент;
		
		НалоговыйАгентПоНДС = Ложь;
		
		Works_TotalVat	= 0;
		Works_Total		= 0;
		
		Если ЗначениеЗаполнено(ПротоКонтент.Works) Тогда
			
			Work_0 = ПротоКонтент.Works[0];
			
			Для Каждого Item Из Work_0.Items Цикл
				
				СтрокаТЧ = Новый_СтруктураСтрокаТЧ();
				
				Если ЗначениеЗаполнено(Item.Name) Тогда
					СтрокаТЧ.Name = Item.Name;
				ИначеЕсли ЗначениеЗаполнено(Item.Description) Тогда
					СтрокаТЧ.Name = Item.Description;
				Иначе
					СтрокаТЧ.Name = Неопределено;
				КонецЕсли;
				
				СтрокаТЧ.Code		= "";
				СтрокаТЧ.UnitCode	= ?(ЗначениеЗаполнено(Item.UnitCode), Item.UnitCode, Неопределено);
				СтрокаТЧ.UnitName	= ?(ЗначениеЗаполнено(Item.UnitName), Item.UnitName, Неопределено);
				СтрокаТЧ.Quantity	= ?(ЗначениеЗаполнено(Item.Quantity), ЧислоИзКомпоненты(Item.Quantity), 0);
				СтрокаТЧ.Price		= ?(ЗначениеЗаполнено(Item.Price), ЧислоИзКомпоненты(Item.Price), 0);
				СтрокаТЧ.SubtotalWithVatExcluded = ?(ЗначениеЗаполнено(Item.SubtotalWithVatExcluded), ЧислоИзКомпоненты(Item.SubtotalWithVatExcluded), 0);
				СтрокаТЧ.Vat		= ?(ЗначениеЗаполнено(Item.Vat), ЧислоИзКомпоненты(Item.Vat), 0);
				СтрокаТЧ.Subtotal	= ?(ЗначениеЗаполнено(Item.Subtotal), ЧислоИзКомпоненты(Item.Subtotal), 0);
				СтрокаТЧ.TaxRate	= Item.TaxRate;
				СтрокаТЧ.VatIsCalculatedByTaxAgent = НДСИсчисляетсяНалоговымАгентом(Item.TaxRate);
				
				НалоговыйАгентПоНДС = НалоговыйАгентПоНДС Или СтрокаТЧ.VatIsCalculatedByTaxAgent;
				
				тзТабличнаяЧасть.Добавить(СтрокаТЧ);
				
			КонецЦикла;
			
			Works_TotalVat	= Work_0.TotalVat;
			Works_Total		= Work_0.Total;
			
		КонецЕсли;
				
		стДокумент.Items = тзТабличнаяЧасть;
		
		стДокумент.ДатаДоговора 		= "";
		стДокумент.НомерДоговора 		= "";
		стДокумент.НаименованиеДоговора = "";
		
		стДокумент.Total 		= Works_Total;
		стДокумент.Vat 			= Works_TotalVat;
		стДокумент.isTaxAgent 	= НалоговыйАгентПоНДС;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция Новый_СтруктураСодержаниеДокумента()

		СодержаниеДокумента = Новый Структура;
		
		СодержаниеДокумента.Вставить("Document", 		Неопределено);
		СодержаниеДокумента.Вставить("Type", 			Неопределено);
		СодержаниеДокумента.Вставить("Items", 			Неопределено);
		СодержаниеДокумента.Вставить("ДатаДоговора",	Неопределено);
		СодержаниеДокумента.Вставить("НомерДоговора", 	Неопределено);
		СодержаниеДокумента.Вставить("НаименованиеДоговора", Неопределено);
		СодержаниеДокумента.Вставить("Total", 			Неопределено);
		СодержаниеДокумента.Вставить("Vat", 			Неопределено);
		СодержаниеДокумента.Вставить("isTaxAgent", 		Неопределено); // Истина, если есть хотя бы одна строка ТЧ со ставкой "НДС исчисляется налоговым агентом"
		
		Возврат СодержаниеДокумента; 
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_СтруктураСтрокаТЧ(ЕстьГТД = Ложь)
		
		СтрокаТЧ = Новый Структура;
		
		СтрокаТЧ.Вставить("Name", 		Неопределено);
		СтрокаТЧ.Вставить("Code", 		Неопределено);
		СтрокаТЧ.Вставить("NomenclatureArticle", Неопределено);
		СтрокаТЧ.Вставить("Quantity", 	Неопределено);
		СтрокаТЧ.Вставить("UnitCode", 	Неопределено);
		СтрокаТЧ.Вставить("UnitName", 	Неопределено);
		СтрокаТЧ.Вставить("Price", 		Неопределено);
		СтрокаТЧ.Вставить("SubtotalWithVatExcluded", Неопределено);
		СтрокаТЧ.Вставить("Vat",		Неопределено);
		СтрокаТЧ.Вставить("Subtotal",	Неопределено);
		СтрокаТЧ.Вставить("TaxRate",	Неопределено);
		СтрокаТЧ.Вставить("VatIsCalculatedByTaxAgent", Неопределено);
		
		Если ЕстьГТД Тогда
			СтрокаТЧ.Вставить("CountryCode",	Неопределено);
			СтрокаТЧ.Вставить("TDNumber",		Неопределено);
		КонецЕсли;
		
		Возврат СтрокаТЧ;
		
	КонецФункции
	
	&НаКлиенте
	Функция ЧислоИзКомпоненты(Значение)
		
		Результат = 0;
		
		Если ЗначениеЗаполнено(Значение) И НРег(Значение) <> "без ндс" Тогда
			Результат = Число(Значение);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ОпределитьСтавкуНДСПоУслуге(СуммаБезНДС, СуммаНДС)
		
		Если СуммаБезНДС = 0 Тогда
			Возврат 0;
		Конецесли;
		
		РасчетнаяСтавка = 100 * СуммаНДС / СуммаБезНДС;
		Если РасчетнаяСтавка < 5 Тогда
			Возврат "0";
		ИначеЕсли РасчетнаяСтавка < 14 Тогда
			Возврат "10";
		ИначеЕсли РасчетнаяСтавка < 19 Тогда
			Возврат "18";	
		Иначе
			Возврат "20";
		КонецЕсли;
		
	КонецФункции
	
	// Признак того, что применяется обратное обложение НДС (п. 8 ст. 161 НК РФ)
	//
	// Параметры:
	//  TaxRate - Строка - Значение свойства TaxRate объекта <Имя коллекции>Item
	// 
	// Возвращаемое значение:
	//  Булево - Истина, если НДС исчисляется налоговым агентом
	//
	&НаКлиенте
	Функция НДСИсчисляетсяНалоговымАгентом(TaxRate)
		
		Возврат ВРег(TaxRate) = ВРег("ИсчНалАг");
		
	КонецФункции
	
	&НаКлиенте
	Процедура ДополнитьСтруктуруСоответствие(Приемник, Источник) Экспорт
		
		Для Каждого КлючИЗначение Из Источник Цикл
			Приемник.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ЗаменитьНедопустимыеСимволыИмениФайла(ИмяФайла) Экспорт
		
		Результат = ИмяФайла;
		
		НедопустимыеСимволы = "\/:*?""<>|";
		КоличествоНедопустимыхСимволов = СтрДлина(НедопустимыеСимволы);
		
		Для Сч = 1 По КоличествоНедопустимыхСимволов Цикл
			Результат = СтрЗаменить(Результат, Сред(НедопустимыеСимволы, Сч, 1), "_");
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции
	
	// Возвращает организацию 1С
	//
	// Возвращаемое значение:
	//   <СправочникСсылка>   - организация 1С, принятая как значение по умолчанию
	//
	&НаКлиенте
	Функция ОрганизацияПоУмолчанию() Экспорт
		
		ОрганизацияПоУмолчанию = Неопределено;
		
		Если ЗначениеЗаполнено(Платформа.ПараметрыКлиент.КонтекстДиадока)
				И Платформа.ПараметрыКлиент.КонтекстДиадока.Количество() = 1 Тогда
					
			ОрганизацияПоУмолчанию = Платформа.ПараметрыКлиент.КонтекстДиадока[0].Организация;
			
		КонецЕсли;
		
		Возврат ОрганизацияПоУмолчанию;
	
	КонецФункции // ОрганизацияПоУмолчанию()
	
	&НаКлиенте
	Функция ОформитьОшибкиВHTML(Ошибка, Заголовок = "") Экспорт
		
		Результат = "";
		
		Если ЗначениеЗаполнено(Ошибка) Тогда
			
			ТелоHTML = "<H2>" + Заголовок + "</H2>" + Ошибка;
			
			Если НЕ ЗначениеЗаполнено(Заголовок) Тогда
				
				// Если заголовок не указан, ставим рядом с сообщением пиктограмму ошибки.
				
				КартинкаВниманиеОшибка = Base64Строка(
				МетодКлиента("Модуль_Клиент", "ЭДО_БиблиотекаКартинок").ВниманиеОшибка.ПолучитьДвоичныеДанные());
				
				ТелоHTML = СтрЗаменить(ТелоHTML, "<p>", "<p><IMG src=""data:image/png;base64," + КартинкаВниманиеОшибка + """> ");
				
			КонецЕсли;
			
			Результат = "<HTML><HEAD>
			|<META content=""text/html; charset=utf-8"" http-equiv=Content-Type>
			|<STYLE type=text/css>H3{MARGIN-BOTTOM:0em;MARGIN-TOP:0.5em}P{MARGIN-BOTTOM:0em;MARGIN-TOP:0.2em}</STYLE></HEAD>
			|<BODY>" + ТелоHTML + "</BODY></HTML>";
			
		КонецЕсли;	
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция HTMLТекстОшибкиОтправки(ТекстОшибки) Экспорт
		
		Результат = "";
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			
			Результат = "<p>" + ТекстОшибки + "</p>";
			
			Если Найти(Результат, "не настроены параметры подписи") > 0 Тогда
				
				Результат = Результат + "
				|<a href=""НАСТРОЙКАПАРАМЕТРОВПОДПИСАНИЯ"">Настроить параметры подписи</a>";
				
			КонецЕсли;
			
			Результат = ОформитьОшибкиВHTML(Результат);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Осуществляет поиск значения в указанных колонках таблицы значений, представленной в виде массива структур.
	// Аналог метода ТаблицаЗначений.Найти(<Значение>, <Колонки>)
	//
	// Параметры:
	//  МассивСтруктур  - Массив     	- Все элементы массива - структуры с одинаковым набором ключей.
	//  Значение     	- Произвольный  - Искомое значение.
	//  Ключ       		- Строка     	- Список ключей структуры, разделенных запятыми, по которым производится поиск.
	//                                    	Если параметр не указан, поиск осуществляется по всем свойствам.
	// 
	// Возвращаемое значение:
	//  Структура   - Первый элемент массива, содержащий искомое значение.
	//  Неопределено - Если значение не найдено.
	//
	&НаКлиенте
	Функция МассивСтруктур_Найти_НаКлиенте(МассивСтруктур, Значение, Ключ) Экспорт
		
		Результат = Неопределено;
		
		Если ЗначениеЗаполнено(МассивСтруктур) Тогда
			
			Для Каждого Структура Из МассивСтруктур Цикл 
				
				Если Структура[Ключ] = Значение Тогда
					Результат = Структура;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции //МассивСтруктур_Найти_НаКлиенте()
	
	&НаКлиенте
	Функция ПараметрыОповещенияВСтрокуHTMLСообщения(ОбработчикОповещения, РезультатОповещения = Неопределено) Экспорт
		
		Результат = "Оповещение:" + ОбработчикОповещения;
		
		Если ЗначениеЗаполнено(РезультатОповещения) Тогда
			
			Результат = Результат + "?" + РезультатОповещения;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции //ПараметрыОповещенияВСтрокуHTMLСообщения()
	
	&НаКлиенте
	Функция ПараметрыОповещенияИзСтрокиHTMLСообщения(СтрокаОповещения) Экспорт
		
		ОбработчикОповещения = "";
		РезультатОповещения	 = "";
		
		Поз = Найти(НРег(СтрокаОповещения), НРег("Оповещение:"));
		СтрокаОповещенияБезПрефикса = Сред(СтрокаОповещения, Поз + СтрДлина("Оповещение:"));
		
		ЧастиСтроки = Преобразование_СтрокуВМассивСлов(СтрокаОповещенияБезПрефикса, "?");
		
		ОбработчикОповещения = ЧастиСтроки[0];
		
		Если ЧастиСтроки.Количество() > 1 Тогда
			ЧастиСтроки.Удалить(0); // Удаляем обработчик
			РезультатОповещения = Преобразование_МассивВСтроку(ЧастиСтроки, "?");
		КонецЕсли;
		
		Результат = Новый Структура;
		Результат.Вставить("ОбработчикОповещения"	, ОбработчикОповещения);
		Результат.Вставить("РезультатОповещения"	, РезультатОповещения);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Base64СтрокаИзДвоичныхДанных(ДвоичныеДанные)
		
		Результат = Base64Строка(ДвоичныеДанные);
		Результат = СтрЗаменить(Результат, Символы.ВК, "");
		Результат = СтрЗаменить(Результат, Символы.ПС, "");
		
		Возврат Результат;
		
	КонецФункции

	// Формирует строковое представление имени конфигурациии
	//
	// Возвращаемое значение:
	//	Строка
	&НаСервереБезКонтекста
	Функция ОписаниеКонфигурации()
		
		Результат = Метаданные.ПодробнаяИнформация + " (" + Метаданные.Версия + ")";
		
		Возврат Результат;
		
	КонецФункции //ОписаниеКонфигурации()
	
	&НаКлиенте
	Функция ВидКлиента()
		
		Результат = Неопределено;
		
		#Если ТонкийКлиент Тогда
			Результат = "Тонкий клиент";
		#ИначеЕсли ТолстыйКлиентОбычноеПриложение Тогда
			Результат = "Толстый клиент обычное приложение";
		#ИначеЕсли ТолстыйКлиентУправляемоеПриложение Тогда
			Результат = "Толстый клиент управляемые формы";
		#ИначеЕсли ВебКлиент Тогда
			Результат = "Веб клиент";
		#ИначеЕсли НаСервере Тогда
			Результат = "Сервер";
		#ИначеЕсли ВнешнееСоединение Тогда
			Результат = "Внешнее соединение";
		#КонецЕсли
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция МодульТиповой()
		
		Результат = "";
		
		СтруктураРасположенияМодуля	= МетодСервера(,"МестоРасположенияМодуля");
		
		ИмяФайлаМодуля				= СтруктураРасположенияМодуля.Путь;
		РасположениеМодуля			= СтруктураРасположенияМодуля.Место;
						
		Если РасположениеМодуля = "ВКонфигурации" Тогда
			Результат = "Неизвестно";
		Иначе
			
			Действие = ДействиеПриОбновленииМодуляФайловыйВариант(СтруктураРасположенияМодуля);
			
			Если Действие = "Скачать" Тогда
				Результат = "Да";
			ИначеЕсли НЕ ЗначениеЗаполнено(ИмяФайлаМодуля) Тогда
				Результат = "Неизвестно";
			Иначе
				Результат = "Нет";
			КонецЕсли;	
			
		КонецЕсли;	
		
		Возврат Результат;
		
	КонецФункции	
	
	// Функция - Описание организации
	//
	// Параметры:
	//  Организация - СправочникСсылка - организация, сведения о которой нужно получить
	// 
	// Возвращаемое значение:
	//  Структура - содержит поля:
	//   * Наименование - Строка - полное (если заполнено) или краткое наименование организации
	//   * ИНН - Строка - ИНН организации
	//   * КПП - Строка - КПП организации
	//
	&НаКлиенте
	Функция ОписаниеОрганизации(Организация)
		
		ОписаниеОрганизации = Новый Структура;
		
		Реквизиты = Новый Структура;
		Реквизиты.Вставить("Наименование");
		Реквизиты.Вставить("НаименованиеПолное");
		Реквизиты.Вставить("ИНН");
		Реквизиты.Вставить("КПП");
		
		ЗначенияРеквизитов = МетодСервера(, "ЗначенияРеквизитовОбъекта", Организация, Реквизиты);
		
		Если ЗначениеЗаполнено(ЗначенияРеквизитов.НаименованиеПолное) Тогда 
			ОписаниеОрганизации.Вставить("Наименование", ЗначенияРеквизитов.НаименованиеПолное);
		Иначе 
			ОписаниеОрганизации.Вставить("Наименование", ЗначенияРеквизитов.Наименование);
		КонецЕсли;
		
		ОписаниеОрганизации.Вставить("ИНН", ЗначенияРеквизитов.ИНН);
		ОписаниеОрганизации.Вставить("КПП", ЗначенияРеквизитов.КПП);
		
		Возврат ОписаниеОрганизации;
		
	КонецФункции //ОписаниеОрганизации()
	
	// Обертка для формирования ключей параметров, передаваемых в URL.
	// Используется при формировании URL для открытия онлайн-чата.
	// Параметры, используемые большим количеством сервисов,
	// должны иметь префикс "base/"
	//
	&НаКлиенте
	Функция СтрокаБазовогоПараметраОнлайнКонсультанта(ИмяПараметра)
		
		Возврат СтрЗаменить("base/%ИмяПараметра%", "%ИмяПараметра%", ИмяПараметра);
		
	КонецФункции

	// Обертка для формирования ключей параметров, передаваемых в URL.
	// Используется при формировании URL для открытия онлайн-чата.
	// Параметры, имеющие отношение только к конкретному сервису, 
	// должны иметь префикс "scope/%service_name%/".
	// В данном случае %service_name% = 1CDiadoc
	// 
	&НаКлиенте
	Функция СтрокаПродуктовогоПараметраОнлайнКонсультанта(ИмяПараметра)
		
		Возврат СтрЗаменить("scope/1CDiadoc/%ИмяПараметра%", "%ИмяПараметра%", ИмяПараметра);
		
	КонецФункции
	
	// Преобразует коллекцию элементов КлючИЗначение в строку, представляющую собой массив параметров URI
	//
	// Параметры:
	//  КоллекцияПараметров - Структура, Соответствие - коллекция элементов КлючИЗначение
	// 
	// Возвращаемое значение:
	//  Строка - коллекция KeyValue в формате: {"key1":"value1","key2":"value2",...,"keyN":"valueN"}
	//
	&НаКлиенте
	Функция СтруктуруВКоллекциюПараметровURI(КоллекцияПараметров)
		
		Результат = "";
		
		Для Каждого Параметр Из КоллекцияПараметров Цикл 
			
			Если Не ЗначениеЗаполнено(Параметр.Значение) Тогда 
				Продолжить;
			КонецЕсли;
			
			Ключ 		= Параметр.Ключ;
			Значение 	= СокрЛП(Параметр.Значение);
			Значение 	= СтрЗаменить(Значение, "\"	, "\\"	); // экранируем обратный слеш
			Значение 	= СтрЗаменить(Значение, """", "\"""	); // экранируем кавычки
			
			ПараметрСтрокой = """"+Ключ+""":"""+Значение+"""";
			
			Результат = Результат + ПараметрСтрокой + ",";
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(Результат) Тогда 
			Результат = Лев(Результат, СтрДлина(Результат)-1);
			Результат = СтрЗаменить("{%Параметры%}", "%Параметры%", Результат);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Преобразует коллекцию элементов КлючИЗначение в строку параметров URI
	//
	// Параметры:
	//  КоллекцияПараметров - Структура - коллекция элементов КлючИЗначение
	// 
	// Возвращаемое значение:
	//  Строка - строка параметров в формате ?paramName1=paramValue1&paramName2=paramValue2&...&paramNameN=paramValueN
	//
	&НаКлиенте
	Функция СтруктуруВСтрокуПараметровURI(КоллекцияПараметров)
		
		Результат = "";
		
		Для Каждого Параметр Из КоллекцияПараметров Цикл 
			
			Если Не ЗначениеЗаполнено(Параметр.Значение) Тогда 
				Продолжить;
			КонецЕсли;
			
			Результат = Результат 
						+ "" + Параметр.Ключ
						+ "=" + СокрЛП(Параметр.Значение)
						+ "&";
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(Результат) Тогда 
			Результат = "?" + Лев(Результат, СтрДлина(Результат)-1);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Разделяет строку на части по указанным символам-разделителям.
	// По аналогии с глобальным методом СтрРазделить, добавленным в платформу версии 8.3.6.
	//
	// Параметры:
	//  Строка         - Строка - разделяемая строка;
	//  Разделитель    - Строка - строка символов, каждый из которых является индивидуальным разделителем;
	//  ВключатьПустые - Булево - Ложь, если пустые части строки включать в результат не нужно.
	// 
	// Возвращаемое значение:
	//   Массив - массив со строками, которые получились в результате разделения исходной строки.
	//
	&НаКлиенте
	Функция РазделитьСтроку(Знач Строка, Разделитель = ",", ВключатьПустые = Истина)
		
		Результат = Новый Массив;
		
		МассивРазделителей = Новый Массив;
		
		Для Сч = 1 По СтрДлина(Разделитель) Цикл
			СимволРазделителя = Сред(Разделитель, Сч, 1);
			МассивРазделителей.Добавить(СимволРазделителя);
		КонецЦикла;
		
		Пока Истина Цикл
			
			Поз = 0;
			
			Для Каждого СимволРазделителя Из МассивРазделителей Цикл
				
				Поз = Найти(Строка, СимволРазделителя);
				
				Если Поз > 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если Поз = 0 Тогда
				Подстрока	 = СокрЛП(Строка);
				Строка		 = "";
			Иначе
				Подстрока	 = СокрЛП(Лев(Строка, Поз - 1));
				Строка		 = Сред(Строка, Поз + 1);
			КонецЕсли;
			
			Если ВключатьПустые ИЛИ ЗначениеЗаполнено(Подстрока) Тогда
				Результат.Добавить(Подстрока);
			КонецЕсли;
			
			Если Поз = 0 Тогда
				Прервать; // Нет разделителя, за которым могла бы быть следующая часть строки.
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции
	
	// Проверяет, содержит ли строка только цифры.
	//
	// Параметры:
	//  Значение - Строка - проверяемая строка.
	//
	// Возвращаемое значение:
	//  Булево - Истина, если строка содержит только цифры или пустая.
	//
	Функция ТолькоЦифрыВСтроке(Значение) Экспорт
		
		Результат	 = Истина;
		ДлинаСтроки	 = СтрДлина(Значение);
		
		Для НомерСимвола = 1 По ДлинаСтроки Цикл
			
			КодСимвола = КодСимвола(Значение, НомерСимвола);
			
			Если КодСимвола < 48 Или КодСимвола > 57 Тогда
				Результат = Ложь;
				Прервать;
			КонецЕсли;
		
		КонецЦикла;
		
		Возврат Результат;
				
	КонецФункции
	
	// Безопасно получает значение из массива по его индексу
	//
	// Параметры:
	//  Массив              - Массив       - массив, из которого нужно получить значение
	//  ИндексЗначения      - Число        - индекс значения
	//  ЗначениеПоУмолчанию - Произвольный - значение, которое нужно вернуть
	//                                       если указанный индекс выходит за границу массива
	// 
	// Возвращаемое значение:
	//  Произвольный - значение из массива. ЗначениеПоУмолчанию если индекс выходит за границы массива
	//
	&НаКлиенте
	Функция ЗначениеИзМассива(Массив, ИндексЗначения, ЗначениеПоУмолчанию = Неопределено)
		
		Результат = ЗначениеПоУмолчанию;
		
		Если ИндексЗначения <= Массив.ВГраница() Тогда
			Результат = Массив[ИндексЗначения];
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Добавляет значение в массив
	//
	// Параметры:
	//	Значение - Произвольный - преобразуемое значение
	//
	// Возвращаемое значение:
	//	Массив
	&НаКлиенте
	Функция ЗначениеВМассив(Значение)
	
		Если ТипЗнч(Значение) = Тип("Массив") Тогда
			
			Результат = Значение;
			
		Иначе
			
			Результат = Новый Массив;
			Результат.Добавить(Значение);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Подставляет параметры в строку, максимальное число параметров - 5.
	//
	// Параметры:
	//  ШаблонСтроки - Строка - шаблон содержащий маркеры подстановки вида: "%1..%N"
	//                        . Нумерация маркеров начинается с 1.
	//                          N не может быть более 5.
	//  Параметр<n>  - Строка - подставляемые параметры.
	//
	// Возвращаемое значение:
	//   Строка - строка шаблона с подставленными параметрами.
	//
	&НаКлиенте
	Функция ПодставитьПараметрыВСтроку(ШаблонСтроки, Параметр1
		, Параметр2 = Неопределено
		, Параметр3 = Неопределено
		, Параметр4 = Неопределено
		, Параметр5 = Неопределено) Экспорт
		
		Результат = СтрЗаменить(ШаблонСтроки, "%1", Параметр1);
		Результат = СтрЗаменить(Результат	, "%2", Параметр2);
		Результат = СтрЗаменить(Результат	, "%3", Параметр3);
		Результат = СтрЗаменить(Результат	, "%4", Параметр4);
		Результат = СтрЗаменить(Результат	, "%5", Параметр5);
		
		Возврат Результат;
		
	КонецФункции
	
	// Объединяет части пути через разделитель
	//
	Функция ОбъединитьПути(П1, П2, П3 = Неопределено, П4 = Неопределено) Экспорт
		
		Результат = П1;
		РазделительПути = ПолучитьРазделительПути();
		
		ЧастиПути = Новый Массив;
		ЧастиПути.Добавить(П2);
		ЧастиПути.Добавить(П3);
		ЧастиПути.Добавить(П4);
		
		Для Каждого Элемент Из ЧастиПути Цикл
			
			Если Не ЗначениеЗаполнено(Элемент) Тогда
				Прервать;
			КонецЕсли;
			
			Если Прав(Результат, 1) <> РазделительПути Тогда
				Результат = Результат + РазделительПути;
			КонецЕсли;
			
			Результат = Результат + Элемент;
			
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции
	
//} СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ ИНТЕГРАЦИЯ

	&НаКлиенте
	Функция ПроверитьФорматДатыПрото(ЗначениеПоля, ПредставлениеПоля, ОбязательноеЗаполнение= Ложь) Экспорт  
		
		если ОбязательноеЗаполнение и (ЗначениеЗаполнено(ЗначениеПоля)=Ложь) тогда 
			возврат  "<p>не указана "+ПредставлениеПоля+"</p>
			|"
		конецЕсли;
		
		Если ТипЗнч(ЗначениеПоля) = тип("Дата") тогда 
			Если (ЗначениеПоля < '18000101') или (ЗначениеПоля>='21000101') тогда 
				возврат "<p>"+ПредставлениеПоля+" ("+формат(ЗначениеПоля,"ДФ=dd.MM.yyyy")+") должна находиться в интервале 01.01.1800 - 31.12.2099</p>"
			КонецЕсли;
		ИначеЕсли ТипЗнч(ЗначениеПоля) = тип("Строка") тогда 	
			ДатаГод = прав(ЗначениеПоля, 4);
			Если (ДатаГод <"1800") или (ДатаГод >"2099") тогда 
				возврат "<p>"+ПредставлениеПоля+" ("+ЗначениеПоля+") должна находиться в интервале 01.01.1800 - 31.12.2099</p>"
			КонецЕсли;
		КонецЕсли;
		
		возврат ""
		
	КонецФункции

	&НаКлиенте
	Процедура ДобавитьВСписокФильтр(СписокФильтров, МассивТиповФильтруемыхДокументов, СтрокаФильтра)
		
		Для каждого ТипДокумента Из МассивТиповФильтруемыхДокументов Цикл
			
			СтруктураФильтра=	Новый Структура();
			СтруктураФильтра.Вставить("ТипДокумента", 	ТипДокумента);
			СтруктураФильтра.Вставить("Фильтр", 		СтрокаФильтра);
			
			СписокФильтров.Добавить(СтруктураФильтра);
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьСписокФильтровДляДокументовДиадок(Направление, ФильтрПоСостояниюДокументооборота, ФильтрПотипамДокументов) 
		
		//ФильтрПотипамДокументов =1 выводим РНК
		//ФильтрПотипамДокументов =2 выводим СФ
		//ФильтрПотипамДокументов =3 выводим Счета на оплату
		//ФильтрПотипамДокументов =4 выводим неформализованные 
		//ФильтрПотипамДокументов =5 выводим Акты сверки
		//ФильтрПотипамДокументов =0 выводим все 
		
		// ФильтрПоСостояниюДокументооборота = 0 все состояния
		// ФильтрПоСостояниюДокументооборота = 1 завершен
		// ФильтрПоСостояниюДокументооборота = 2 не завершен
		// ФильтрПоСостояниюДокументооборота = 3 прекращен
		
		фильтрDirection = направление;
		СписокФильтров = Новый Массив;
		
		Если Направление = "OutboundWaitingForSenderSignature" Тогда 
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), ФильтрDirection);
			
			Возврат СписокФильтров;
		КонецЕсли;
		
		Если ФильтрПоСостояниюДокументооборота = 0  Тогда // все состояния
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), ФильтрDirection);
		ИначеЕсли  ФильтрПоСостояниюДокументооборота = 1 Тогда // завершен
			
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 1), ФильтрDirection+"Finished"); //сф
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 3), фильтрDirection+""); //строго односторонние
	 	   	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 2), фильтрDirection+"WithRecipientSignature"); //строго двусторонние
	 	   	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 4), фильтрDirection+"WithRecipientSignature"); //односторонние или двусторонние
	 	   	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 4), фильтрDirection+"NoRecipientSignatureRequest"); //односторонние или двусторонние
			
		ИначеЕсли  ФильтрПоСостояниюДокументооборота = 2 Тогда // не завершен
			
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 1), фильтрDirection+"NotFinished"); //сф
 	   		ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 2), фильтрDirection+"WaitingForRecipientSignature"); //строго двусторонние
 	   		ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 4), фильтрDirection+"WaitingForRecipientSignature"); //односторонние или двусторонние

		ИначеЕсли  ФильтрПоСостояниюДокументооборота = 3 Тогда // прекращен                                                
						
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"RevocationAccepted");
			Если фильтрDirection = "Outbound" Тогда
				ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"InvalidSenderSignature");
	  		КонецЕсли;
	 	   	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 2), фильтрDirection+"RecipientSignatureRequestRejected"); //строго двусторонние
	 	   	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 4), фильтрDirection+"RecipientSignatureRequestRejected"); //односторонние или двусторонние
			
		ИначеЕсли	ФильтрПоСостояниюДокументооборота = 4 Тогда //Требуется уточнение
     		ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 1), фильтрDirection+"InvoiceAmendmentRequested"); //сф	
		ИначеЕсли	ФильтрПоСостояниюДокументооборота = 21 Тогда 
	    	ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"RevocationIsRequestedByMe");  //ожидается Аннулирование
		ИначеЕсли 	ФильтрПоСостояниюДокументооборота = 22 Тогда 
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"RequestsMyRevocation");  //запрошено Аннулирование
		ИначеЕсли 	ФильтрПоСостояниюДокументооборота = 23 Тогда 
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"RevocationAccepted");  //Документ аннулирован
		ИначеЕсли 	ФильтрПоСостояниюДокументооборота = 24 Тогда 
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"RevocationRejected");  //отказано в аннулировании
		ИначеЕсли 	ФильтрПоСостояниюДокументооборота = 11 Тогда 
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"WaitingForResolution");  //передано на согласование
		ИначеЕсли 	ФильтрПоСостояниюДокументооборота = 12 Тогда 
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"Approved");  //согласовано		
	   	ИначеЕсли 	ФильтрПоСостояниюДокументооборота = 13 Тогда 
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"Disapproved");  //отказано в согласованияя		
	    ИначеЕсли 	ФильтрПоСостояниюДокументооборота = 14 Тогда 
			ДобавитьВСписокФильтр(СписокФильтров, ПолучитьМассивТиповДокументов(ФильтрПотипамДокументов, 0), фильтрDirection+"SignatureRequestRejected");  //отказано в подписании		
		КонецЕсли;
				
		Возврат СписокФильтров;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьФилиаловDepartmentId()
		
		ФилиаловDepartmentId = Новый Массив;
		
		Возврат ФилиаловDepartmentId;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьDepartmentByKpp(Organization, DepartmentKpp)
		
		Для Индекс = 0 По Organization.Departments.Count - 1 Цикл
			dep=	Organization.Departments.GetItem(Индекс);
			Если dep.Kpp = DepartmentKpp Тогда
				Возврат dep.Id;
			КонецЕсли;
		КонецЦикла;
		
		Возврат "";
		
	Конецфункции
	
	&НаКлиенте
	Функция ВернутьМассивDepartmentId(стрЯщик, Box, ПодразделениеID, ВключатьПодчиненныеПодазделения)
		
		ПодразделениеID_текущее = ПодразделениеID;
		Если ПустаяСтрока(ПодразделениеID) Тогда
			Если НЕ ПустаяСтрока(стрЯщик.КПППодразделения) Тогда
				ПодразделениеID_текущее = ПолучитьDepartmentByKpp(Box, стрЯщик.КПППодразделения);   
			КонецЕсли;
		КонецЕсли;
		
		массивDepartmentId = Новый Массив;
		Если ПодразделениеID_текущее = "" Тогда
			ФилиаловDepartmentId = ПолучитьФилиаловDepartmentId();
			ЕстьПодразделенеКоторыеНужноОбходить = Ложь;
			массивDepartmentId.Добавить(Новый Структура("DepartmentID, вклДочерниеПодразделения", "", Ложь));
			Если ВключатьПодчиненныеПодазделения Тогда
				Для Индекс = 0 По Box.Departments.Count - 1 Цикл
					
					department=	Box.Departments.GetItem(Индекс);
					
					Если ФилиаловDepartmentId.Найти(department.Id) = Неопределено Тогда
						массивDepartmentId.Добавить(Новый Структура("DepartmentID, вклДочерниеПодразделения", department.Id, Истина));
					Иначе
						ЕстьПодразделенеКоторыеНужноОбходить = Истина;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Если ЕстьПодразделенеКоторыеНужноОбходить = Ложь Тогда
				массивDepartmentId.Очистить();
			КонецЕсли;
		Иначе
			массивDepartmentId.Добавить(Новый Структура("DepartmentID, вклДочерниеПодразделения", ПодразделениеID_текущее, ВключатьПодчиненныеПодазделения));
		КонецЕсли;
		
		Если массивDepartmentId.Количество()=0 Тогда 
			//добавим пустой фильтр 
			массивDepartmentId.Добавить(Новый Структура("DepartmentID, вклДочерниеПодразделения", "", ВключатьПодчиненныеПодазделения));
		КонецЕсли;	
		
		Возврат  массивDepartmentId;
		
	КонецФункции	
	
	&НаКлиенте
	Функция ПолучитьСтруктуруЗадачи(НаименованиеЯщика, Организация, AsyncTask)
		
		Задача=	Новый Структура();
		Задача.Вставить("НаименованиеЯщика", 	НаименованиеЯщика);
		Задача.Вставить("Организация", 			Организация);
		Задача.Вставить("Task", 				AsyncTask);
		
		Возврат Задача;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьМассивАсинхронныхЗадач(СписокФильтров, ДатаНачала, ДатаОкончания, СписокЯщиков, counterAgentBoxId, ПараметрыВыборки)
		
		ПодразделениеID=					?(ПараметрыВыборки.Свойство("ПодразделениеID"), ПараметрыВыборки.ПодразделениеID, "");
		ВключатьПодчиненныеПодазделения=	?(ПараметрыВыборки.Свойство("ВключатьПодчиненныеПодазделения"), ПараметрыВыборки.ВключатьПодчиненныеПодазделения, Истина);
		ОтбиратьПоДатеДокумента=			?(ПараметрыВыборки.Свойство("ОтбиратьПоДатеДокумента"), ПараметрыВыборки.ОтбиратьПоДатеДокумента, Ложь);
		
		МассивЗадач=	Новый Массив;
		
		Для Каждого стрЯщик Из СписокЯщиков Цикл
			
			ОбработкаПрерыванияПользователя();
			
			Box=	 			ПолучитьЯщикДиадокДляОрганизации(стрЯщик.Организация, стрЯщик.BoxId);
			НаименованиеЯщика=	Box.name;
			массивDepartmentId=	ВернутьМассивDepartmentId(стрЯщик, Box, ПодразделениеID, ВключатьПодчиненныеПодазделения);
			
			Для каждого ФильтрАпи Из СписокФильтров Цикл
				Для каждого depId Из массивDepartmentId Цикл
					
					ОбработкаПрерыванияПользователя();
					
					DocumentsTask=	Box.getDocumentsTask();
					
					Если ОтбиратьПоДатеДокумента Тогда
						DocumentsTask.FromDocumentDate=			ДатаНачала;
						Если НЕ ПараметрыВыборки.Направление = "OutboundWaitingForSenderSignature" Тогда
							DocumentsTask.ToDocumentDate=		КонецДня(Датаокончания);
						КонецЕсли;
					Иначе
						DocumentsTask.FromSendDate=			ДатаНачала;
						Если НЕ ПараметрыВыборки.Направление = "OutboundWaitingForSenderSignature" Тогда
							DocumentsTask.ToSendDate=		КонецДня(Датаокончания);
						КонецЕсли;
					КонецЕсли;
					DocumentsTask.Category=					ФильтрАпи.ТипДокумента + "." + ФильтрАпи.Фильтр;
					DocumentsTask.counterAgentId=			counterAgentBoxId;
					DocumentsTask.DepartmentId=				depId.DepartmentId;
					DocumentsTask.ExcludeSubdepartments=	НЕ depId.вклДочерниеПодразделения;
					
					AsyncTask=	DocumentsTask.GetDocumentsAsync();
					Задача=		ПолучитьСтруктуруЗадачи(НаименованиеЯщика, СтрЯщик.Организация, AsyncTask);
					МассивЗадач.Вставить(0, Задача);
					
				КонецЦикла;
			КонецЦикла;
		Конеццикла;  
		
		Возврат МассивЗадач;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьРезультатВыполненияЗадач(МассивЗадач, ПараметрыВыборки)
		
		Результат = Новый Массив;
		
		Пока ЗначениеЗаполнено(МассивЗадач) Цикл
			
			Для Сч = -МассивЗадач.ВГраница() По 0 Цикл
				
				Задача = МассивЗадач[-Сч];
				
				ПоказатьСостояниеОбработки(НСтр("ru = 'Заполнение списка документов'"));
				
				Если Задача.Task.IsCompleted Тогда
					
					DocumentsTask	 = Задача.Task.Result;
					ВГраница		 = DocumentsTask.Count - 1;
					
					Для ИндексДокумента = 0 По ВГраница Цикл
						
						ОбработкаПрерыванияПользователя();
						
						Document = DocumentsTask.GetItem(ИндексДокумента);
						
						Если Document.Counteragent <> Неопределено Тогда
							
							// Фильтры УПД, пока в API нет фильтров по Function.
							ФильтрПотипамДокументов = ПараметрыВыборки.ТипыДокументов;
							Если Не ПустаяСтрока(ФильтрПотипамДокументов) И Document.Type = "UniversalTransferDocument" Тогда
								
								ТипДокумента = ТипДокументаУПД(Document.Function);
								
								Если 	ТипДокумента = ТипДокументаУПД("СЧФДОП")
									И ФильтрПотипамДокументов <> "5"
								ИЛИ ТипДокумента = ТипДокументаУПД("СЧФ")
									И ФильтрПотипамДокументов <> "2"
									И ФильтрПотипамДокументов <> "24"
								ИЛИ ТипДокумента = ТипДокументаУПД("ДОП")
									И ФильтрПотипамДокументов <> "1"
									И ФильтрПотипамДокументов <> "13"
								Тогда
									Продолжить;
								КонецЕсли;
								
							КонецЕсли;
							
							// Фильтры УКД, пока в API нет фильтров по Function.
							Если Не ПустаяСтрока(ФильтрПотипамДокументов) И Document.Type = "UniversalCorrectionDocument" Тогда
								
								ТипДокумента = ТипДокументаУКД(Document.Function);
								
								Если 	ТипДокумента = ТипДокументаУКД("КСЧФДИС")
									И ФильтрПотипамДокументов <> "6"
								ИЛИ ТипДокумента = ТипДокументаУКД("КСЧФ")
									И ФильтрПотипамДокументов <> "2"
									И ФильтрПотипамДокументов <> "25"
								ИЛИ ТипДокумента = ТипДокументаУКД("ДИС")
									И ФильтрПотипамДокументов <> "1"
									И ФильтрПотипамДокументов <> "14"
								Тогда
									Продолжить;
								КонецЕсли;
								
							КонецЕсли;
							
							СтруктураДокумента	= Новый Структура;
							ЗаполнитьСтрокуТаблицыДокумента(Document, СтруктураДокумента, Задача.НаименованиеЯщика, Ложь);
							Результат.Добавить(СтруктураДокумента);
							
						КонецЕсли;
						
					КонецЦикла;
					
					МассивЗадач.Удалить(-Сч);
					
				КонецЕсли;
				
			КонецЦикла;
			
			СвернутьТаблицуДокументовДиадок(Результат);
			
		КонецЦикла;
		
		ДобавитьПервичныйДокумент(Результат);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ДобавитьПервичныйДокумент(МассивДокументов)
		
		Если НЕ ЗначениеЗаполнено(МассивДокументов) Тогда
			Возврат;
		КонецЕсли; 
		
		МассивДокументовСервер = Новый Массив;
		
		// Формируем сокращенную структуру документа для передачи её на сервер 1С.
		Для Каждого СтруктураДокумента Из МассивДокументов Цикл
			
			СтруктураДокументаСервер = Новый Структура;
			СтруктураДокументаСервер.Вставить("DocumentID"		 , СтруктураДокумента.DocumentID);
			СтруктураДокументаСервер.Вставить("BoxID"			 , СтруктураДокумента.BoxID);
			СтруктураДокументаСервер.Вставить("DocumentDirection", СтруктураДокумента.DocumentDirection);
			СтруктураДокументаСервер.Вставить("DocumentType"	 , СтруктураДокумента.DocumentType);
			СтруктураДокументаСервер.Вставить("DocumentFunction" , СтруктураДокумента.DocumentFunction);
			
			МассивДокументовСервер.Добавить(СтруктураДокументаСервер);
			
		КонецЦикла;
		
		ДобавитьПервичныйДокументНаСервере(МассивДокументовСервер);
		
		// В результате МассивДокументовСервер содержит только ИндексСтроки и ПервичныйДокумент.
		Для Каждого СтруктураДокумента Из МассивДокументовСервер Цикл
			МассивДокументов[СтруктураДокумента.ИндексСтроки].Вставить("ПервичныйДокумент", СтруктураДокумента.ПервичныйДокумент);
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура ДобавитьПервичныйДокументНаСервере(МассивДокументов)
		
		Для Каждого СтруктураДокумента Из МассивДокументов Цикл
			СтруктураДокумента.Вставить("ПервичныйДокумент");
		КонецЦикла;
		
		МетодСервера(,"ЗаполнитьТаблицуДокументовДиадокДокументами1С", МассивДокументов);
		
		// Оставляем в МассивДокументов только ИндексСтроки и ПервичныйДокумент
		ОбратныйИндекс = МассивДокументов.ВГраница();
		Пока ОбратныйИндекс > -1 Цикл
			
			ПервичныйДокумент = МассивДокументов[ОбратныйИндекс].ПервичныйДокумент;
			
			Если ЗначениеЗаполнено(ПервичныйДокумент) Тогда
				МассивДокументов[ОбратныйИндекс] = Новый Структура("ИндексСтроки, ПервичныйДокумент", ОбратныйИндекс, ПервичныйДокумент);
			Иначе
				МассивДокументов.Удалить(ОбратныйИндекс);
			КонецЕсли;
			
			ОбратныйИндекс = ОбратныйИндекс - 1;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СвернутьТаблицуДокументовДиадок(ТаблицаДокументовДиадок)
		
		// УПД - документ с квантовой запутанностью, может существовать сразу в двух статусах (InboundFinished и InboundWithRecipientSignature)
		// Поэтому в ТаблицаДокументовДиадок он может попадать дважды (DDSUPPORT-10335).
		// Вот здесь и будем искать дубли.
		// Алгоритм будет общий для ОФ и УФ, поэтому предполагаем, что в ТаблицаДокументовДиадок может быть как ТаблицаЗначений, так и Массив из Структур (&НаКлиенте).
		
		СоответствиеУПД = Новый Соответствие;
		
		ОбратныйИндекс = ТаблицаДокументовДиадок.Количество()-1;
		
		Пока ОбратныйИндекс > -1 Цикл
			
			Строка= ТаблицаДокументовДиадок[ОбратныйИндекс];
			
			Если Строка.DocumentType = "UniversalTransferDocument" Тогда
				
				DocumentIdBoxId = Строка.DocumentId + Строка.BoxId;
				
				// Если документ уже есть в СоответствиеУПД значит текущий можно удалить.
				Если СоответствиеУПД[DocumentIdBoxId] = Истина Тогда
					ТаблицаДокументовДиадок.Удалить(ОбратныйИндекс);
				Иначе
					СоответствиеУПД.Вставить(DocumentIdBoxId, Истина);
				КонецЕсли;
				
			КонецЕсли;
			
			ОбратныйИндекс = ОбратныйИндекс - 1;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ВернутьВыборкуРНКИСчетовФактурДиадок(ДатаНачала, ДатаОкончания, Организация = Неопределено, counterAgentBoxId = "", ПараметрыВыборки) Экспорт
		
		Результат = Новый Массив;
		
		МетодКлиента("Модуль_Клиент", "ПоказатьСостояниеОбработки", НСтр("ru = 'Заполнение списка документов'"));
		
		Направление				= ?(ПараметрыВыборки.Свойство("Направление"), ПараметрыВыборки.Направление, "Inbound");
		СписокДокументооборота	= ?(ПараметрыВыборки.Свойство("СписокДокументооборота"), ПараметрыВыборки.СписокДокументооборота, 0);
		ТипыДокументов			= ?(ПараметрыВыборки.Свойство("ТипыДокументов"), ПараметрыВыборки.ТипыДокументов, "");
		
		Если НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
			ДатаОкончания = Дата("20500101");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Организация) Тогда
			СписокЯщиков = МетодСервера(,"ПолучитьТаблицуЯщиковДиадокОрганизации", Организация);
		Иначе
			СопоставленныеОрганизации = МетодКлиента("Модуль_Клиент","ПолучитьОрганизацииНезаблокированныеПоAPI");
			СписокЯщиков = МетодСервера(,"ПолучитьТаблицуЯщиковДиадокОрганизации", СопоставленныеОрганизации);
		КонецЕсли;
		
		СписокФильтров	 = ПолучитьСписокФильтровДляДокументовДиадок(Направление, СписокДокументооборота, ТипыДокументов);
		МассивЗадач		 = ПолучитьМассивАсинхронныхЗадач(СписокФильтров, ДатаНачала, ДатаОкончания, СписокЯщиков, counterAgentBoxId, ПараметрыВыборки);
		Результат		 = ПолучитьРезультатВыполненияЗадач(МассивЗадач, ПараметрыВыборки);
				
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьМассивСтруктурКонтент() Экспорт
		
		МассивЗадачПолучениеКонтента = Новый Массив;
		
		Если НЕ МассивСтруктурКонтент = Неопределено Тогда
		
			Для каждого СтрокаМассива Из МассивСтруктурКонтент Цикл
				
				ОбработкаПрерыванияПользователя();
				
				Пока МассивЗадачПолучениеКонтента.Количество() = МаксимальноеКоличествоЗаданий Цикл
					ОбработатьМассивЗадачКонтента(МассивЗадачПолучениеКонтента, МассивСтруктурКонтент);
				КонецЦикла;

				Попытка
					ДобавитьЗадачуПолучитьКонтент(МассивЗадачПолучениеКонтента, СтрокаМассива.DocumentId, СтрокаМассива.BoxId, СтрокаМассива.Document);
				Исключение
										
				КонецПопытки;
				
			КонецЦикла;
			
			Пока НЕ МассивЗадачПолучениеКонтента.Количество() = 0 Цикл
				ОбработатьМассивЗадачКонтента(МассивЗадачПолучениеКонтента, МассивСтруктурКонтент);
			КонецЦикла;
			
		Иначе	
			
		КонецЕсли;
		
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ДобавитьЗадачуПолучитьКонтент(МассивЗадачПолучениеКонтента, DocumentId, BoxId, Document)
		
		Попытка
			
			ЭтоНеформализованныйДокумент = ?(НЕ Найти(Document.Type,"Nonformalized")=0 ИЛИ Document.Type="Contract", Истина, Ложь);
			Если НЕ ЭтоНеформализованныйДокумент Тогда 
			
				СтруктураЗадачи = Новый Структура();
				СтруктураЗадачи.Вставить("DocumentId", DocumentId);
				СтруктураЗадачи.Вставить("BoxId", BoxId);
				СтруктураЗадачи.Вставить("Task", Document.GetBase64ContentAsync());
			
				МассивЗадачПолучениеКонтента.Добавить(СтруктураЗадачи);
			
			КонецЕсли;
			
		Исключение
			
		КонецПопытки;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработатьМассивЗадачКонтента(МассивЗадачПолучениеКонтента, МассивСтруктурКонтент)
		
		МассивУдаляемых = Новый Массив();
		
		Для каждого ЗадачаПолучениеКонтента Из МассивЗадачПолучениеКонтента Цикл
			Если ЗадачаПолучениеКонтента.Task.IsCompleted Тогда
				
				Попытка
					
					ФайлXML = Base64Значение(ЗадачаПолучениеКонтента.Task.Result);
					ПротоКонтент = МетодСервера("ГенерацияXML", "XML_В_ProtoСтруктура", ФайлXML);
					
					СтрокаМассива = ПолучитьЗначениеКонтентаКэш(ЗадачаПолучениеКонтента.BoxId, ЗадачаПолучениеКонтента.DocumentId);
					
					Если НЕ СтрокаМассива = Неопределено Тогда
						СтрокаМассива.Content = ПротоКонтент;
					Иначе
						
					КонецЕсли;
														
				Исключение
					
				КонецПопытки;		
				
				МассивУдаляемых.Добавить(ЗадачаПолучениеКонтента);
				
			КонецЕсли;
		КонецЦикла;
		
		Для каждого УдаляемыйЭлемент Из МассивУдаляемых Цикл
			Индекс=	МассивЗадачПолучениеКонтента.Найти(УдаляемыйЭлемент);
			Если НЕ Индекс = Неопределено Тогда
				МассивЗадачПолучениеКонтента.Удалить(Индекс);
			КонецЕсли;
		КонецЦикла;
		
	КонецПроцедуры
		
	&НаКлиенте
	Функция ПолучитьЗначениеКонтентаКэш(BoxID, DocumentId) Экспорт
		
		Результат = Неопределено;
		
		Если ЗначениеЗаполнено(МассивСтруктурКонтент) Тогда
			
			Для Каждого СтрокаМассива ИЗ МассивСтруктурКонтент Цикл
				
				Если СтрокаМассива.DocumentId = DocumentId И СтрокаМассива.BoxID = BoxID Тогда
					Результат = СтрокаМассива;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьСтрокуТаблицыДокумента(Document, СтруктураДокумента, НаименованиеЯщика = Неопределено, ЗаполнятьПервичныйДокумент = Истина)
		
		ФорматЭД = ФорматЭлектронногоДокумента(Document);
		
		СтруктураДокумента.Вставить("ТипДокумента", 	 ПредставлениеТипаЭлектронногоДокумента(Document));
		СтруктураДокумента.Вставить("BoxID", 			 "" + Document.OrganizationID);
		СтруктураДокумента.Вставить("DocumentId", 		 "" + Document.DocumentId);
		СтруктураДокумента.Вставить("DocumentType", 	 Document.Type);
		СтруктураДокумента.Вставить("AttachmentVersion", БазовыйФорматВерсииКонтента(ФорматЭД.ВерсияФормата));
		СтруктураДокумента.Вставить("ContentType", 		 ФорматЭД.ТипКонтента);
		СтруктураДокумента.Вставить("DocumentFunction",  ФорматЭД.ФункцияДокумента);
		СтруктураДокумента.Вставить("DocumentDirection", Document.Direction);
		СтруктураДокумента.Вставить("CounteragentId", 	 Document.counteragent.id);
		СтруктураДокумента.Вставить("Дата", 			 ?(Document.DocumentDate<'20000101', "" ,Document.DocumentDate));
		СтруктураДокумента.Вставить("Номер", 			 ПредсталениеНомераЭД(Document));
		СтруктураДокумента.Вставить("Продавец", 		 ПредставлениеПродавца(Document, НаименованиеЯщика)); 
		СтруктураДокумента.Вставить("Покупатель", 		 ПредставлениеПокупателя(Document, НаименованиеЯщика)); 
		СтруктураДокумента.Вставить("Подразделение", 	 ПредставлениеПодразделения(Document));
		СтруктураДокумента.Вставить("Сумма", 			 ПредставлениеСуммы(Document));
		СтруктураДокумента.Вставить("СуммаДокументаЗначение", 				СуммаПоDocument(Document));
		СтруктураДокумента.Вставить("СостояниеДокументооборота", 			ПредставлениеСтатуса(Document));
		СтруктураДокумента.Вставить("СостояниеСогласования", 				ПредставлениеСтатусаСогласования(Document));
		СтруктураДокумента.Вставить("СостояниеПередачиЧерезРоуминг", 		ПредставлениеСтатусаРоуминг(Document));
		СтруктураДокумента.Вставить("СостояниеПередачиЧерезРоумингДетали", 	?(Document.RoamingNotificationStatus = "RoamingNotificationStatusError", Document.RoamingNotificationStatusDescription, ""));
		СтруктураДокумента.Вставить("Status", 			 "" + Document.Status);
		СтруктураДокумента.Вставить("RevocationStatus",	 "" + Document.RevocationStatus);
		СтруктураДокумента.Вставить("ЭтоТестовыйДок", 	 Document.IsTest);
		СтруктураДокумента.Вставить("FileName", 		 Document.FileName);
		СтруктураДокумента.Вставить("DepartmentKpp", 	 ?(Document.Department <> Неопределено, Document.Department.Kpp, ""));
		СтруктураДокумента.Вставить("DepartmentId", 	 ?(Document.Department <> Неопределено, Document.Department.Id, ""));
		
		Если Document.RoamingNotificationStatus <> "RoamingNotificationStatusNone" И Document.RoamingNotificationStatus <> "RoamingNotificationStatusError" Тогда
			СтруктураДокумента.СостояниеДокументооборота=		СтруктураДокумента.СостояниеПередачиЧерезРоуминг + ?(ЗначениеЗаполнено(СтруктураДокумента.СостояниеДокументооборота), " " + СтруктураДокумента.СостояниеДокументооборота, "");
			СтруктураДокумента.СостояниеПередачиЧерезРоуминг= 	"";
		КонецЕсли;
		
		Если МассивСтруктурКонтент = Неопределено Тогда
			МассивСтруктурКонтент= Новый Массив;
		КонецЕсли;
		
		МассивСтруктурКонтент.Добавить(Новый Структура("BoxID, DocumentId, Document, Content", Document.OrganizationID, Document.DocumentId, Document));
				
		Department= Document.Department;
		
		Если Найти(Document.Type,"Invoice")>0 Тогда
			СтруктураДокумента.Вставить("СуммаНДС", ПредставлениеСуммыНДС(Document));
			СтруктураДокумента.Вставить("ДатаУчета", Document.ConfirmationDate);
		Иначе
			СтруктураДокумента.Вставить("СуммаНДС", 0);
			СтруктураДокумента.Вставить("Валюта", "");
			СтруктураДокумента.Вставить("ДатаУчета", Document.Timestamp+4*60*60);  // Timestamp - по UTC, переводим в МСК +4 часа
		КонецЕсли;
		
		СтруктураДокумента.Вставить("MessageId", Сред(СокрЛП(Document.DocumentId), 1, 36));
		
		Если Document.Type = "NonformalizedProforma" Тогда
			СтруктураДокумента.Вставить("ПозицияСортировки", 1);
		ИначеЕсли 	Document.Type = "XmlTorg12" 
				ИЛИ Document.Type = "XmlAcceptanceCertificate" 
				ИЛИ (Document.Type = "UniversalTransferDocument" И ФорматЭД.ФункцияДокумента = "ДОП") Тогда
				
			СтруктураДокумента.Вставить("ПозицияСортировки", 2);
			
		ИначеЕсли 	Document.Type = "Invoice"
				ИЛИ (Document.Type = "UniversalTransferDocument" И ФорматЭД.ФункцияДокумента = "СЧФДОП")
				ИЛИ (Document.Type = "UniversalTransferDocument" И ФорматЭД.ФункцияДокумента = "СЧФ") Тогда
				
			СтруктураДокумента.Вставить("ПозицияСортировки", 4);
			
		Иначе
			СтруктураДокумента.Вставить("ПозицияСортировки", 3);
		КонецЕсли;
		
		СтруктураДокумента.Вставить("ИндексИконкиПакета", 0);
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьЯщикДиадокДляОрганизации(Организация, BoxID=неопределено, Connection = неопределено) Экспорт
		
		Результат = Неопределено;
		
		Если BoxId = Неопределено тогда
			BoxId = МетодСервера(,"Организация_2_BoxID", Организация);
		КонецЕсли;
		
		Если BoxId <> Неопределено Тогда
			
			СтрокаКонтекста = МетодКлиента("Модуль_РаботаССерверомДиадок", "СтрокаКонтекста", Организация);
			
			Если СтрокаКонтекста = Неопределено Тогда
				
				СтрокаКонтекста = НоваяСтрокаКонтекста(Организация, BoxId);
				
				Платформа.ПараметрыКлиент.КонтекстДиадока.Добавить(СтрокаКонтекста);
				
				ПоместитьДанныеСотрудникаВоВременноеХранилище(Организация, СтрокаКонтекста.ДанныеСотрудника);
				
				ИнициализироватьMagic();
				
			КонецЕсли;
			
			Результат = СтрокаКонтекста.Box;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НоваяСтрокаКонтекста(Организация, BoxId)
		
		Результат = Новый Структура;
		Результат.Вставить("BoxId"	   		   , BoxId);
		Результат.Вставить("Организация"	   , Организация);
		Результат.Вставить("Connection" 	   , Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection);
		Результат.Вставить("Box"		  	   , Результат.Connection.GetOrganizationById(BoxId));
		Результат.Вставить("ЗаблокированаПоAPI", НоваяСтрокаКонтекста_ЗаблокированаПоAPI(Результат.Box));
		Результат.Вставить("ДанныеСотрудника"  , НоваяСтрокаКонтекста_ДанныеСотрудника(Результат.Box, Результат.Connection, Результат.ЗаблокированаПоAPI));
		Результат.Вставить("ДанныеОрганизации" , НоваяСтрокаКонтекста_ДанныеОрганизации(Результат.Box));
		Результат.Вставить("ОшибкаСертификата" , НоваяСтрокаКонтекста_ОшибкаСертификата(Результат));
		Результат.Вставить("Magic");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НоваяСтрокаКонтекста_ЗаблокированаПоAPI(Box)
		
		//проверяем заблокированна ли организация. Для этого вызываем блокируемый метод по API
		
		Результат = Ложь;
		
		Попытка
			Permissions = Box.GetUserPermissions();
		Исключение
			Результат = Истина;
		КонецПопытки;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НоваяСтрокаКонтекста_ДанныеСотрудника(Box, Connection, ЗаблокированаПоAPI)
		
		Результат = Новый Структура;
		
		User = Connection.GetMyUser();
		Результат.Вставить("Id"		 , User.Id);
		Результат.Вставить("Фамилия" , User.LastName);
		Результат.Вставить("Имя"	 , User.FirstName);
		Результат.Вставить("Отчество", User.MiddleName);
		
		ФИО = "";
		Разделитель = "";
		Если ЗначениеЗаполнено(Результат.Фамилия) Тогда
			ФИО = ФИО + Разделитель + Результат.Фамилия;
			Разделитель = " ";
		КонецЕсли;
		Если ЗначениеЗаполнено(Результат.Имя) Тогда
			ФИО = ФИО + Разделитель + Результат.Имя;
			Разделитель = " ";
		КонецЕсли;
		Если ЗначениеЗаполнено(Результат.Отчество) Тогда
			ФИО = ФИО + Разделитель + Результат.Отчество;
			Разделитель = " ";
		КонецЕсли;
		Результат.Вставить("ФИО", ФИО);
		
		Если НЕ ЗаблокированаПоAPI Тогда
			
			UserPermissions = Box.GetUserPermissions();
			
			Результат.Вставить("Должность", UserPermissions.JobTitle);
			
			Результат.Вставить("ПраваДоступа", Новый Структура);
			Результат.ПраваДоступа.Вставить("IsAdministrator"	   , UserPermissions.IsAdministrator);
			Результат.ПраваДоступа.Вставить("CanSignDocuments"	   , UserPermissions.CanSignDocuments);
			Результат.ПраваДоступа.Вставить("CanAddResolutions"	   , UserPermissions.CanAddResolutions);
			Результат.ПраваДоступа.Вставить("CanRequestResolutions", UserPermissions.CanRequestResolutions);
			Результат.ПраваДоступа.Вставить("DocumentsAccessLevel" , UserPermissions.DocumentsAccessLevel);
			
		Иначе
			
			Результат.Вставить("Должность", "");
			
			Результат.Вставить("ПраваДоступа", Новый Структура);
			Результат.ПраваДоступа.Вставить("IsAdministrator"	   , Ложь);
			Результат.ПраваДоступа.Вставить("CanSignDocuments"	   , Ложь);
			Результат.ПраваДоступа.Вставить("CanAddResolutions"	   , Ложь);
			Результат.ПраваДоступа.Вставить("CanRequestResolutions", Ложь);
			Результат.ПраваДоступа.Вставить("DocumentsAccessLevel" , Ложь);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НоваяСтрокаКонтекста_ДанныеОрганизации(Box)
		
		Результат = Новый_Organization();
		
		ЗаполнитьСтруктуруПоКонтенту(Box, Результат);
		
		Если Результат.AuthenticateType = "Certificate" Тогда
			Результат.Certificate.Inn = УдалитьЛишниеНулиИНН(Результат.Certificate.Inn);
		Иначе
			Результат.Certificate = Неопределено;
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НоваяСтрокаКонтекста_ОшибкаСертификата(СтрокаКонтекста)
		
		Результат = "";
		
		ДанныеОрганизации = СтрокаКонтекста.ДанныеОрганизации;
			
		Если ДанныеОрганизации.AuthenticateType <> "Certificate" Тогда
			Возврат Результат;
		КонецЕсли;
		
		Если ИспользуетсяСертификатПоГОСТ2001()
			И ДатаЗапретаГОСТ2001_Наступила() Тогда
			
			Результат = ТекстОшибкиИспользованиеСертификатаГОСТ2001();
		
		ИначеЕсли НЕ СтрокаКонтекста.ЗаблокированаПоAPI
			И НЕ ЭтоТестоваяОрганизация(ДанныеОрганизации)Тогда
				
			Результат = СтрокаКонтекста.Box.CanSendInvoice(ДанныеОрганизации.Certificate.Thumbprint);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПоместитьДанныеСотрудникаВоВременноеХранилище(Организация, ДанныеСотрудника)
		
		ДанныеСотрудников = ПолучитьИзВременногоХранилища(Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресДанныеСотрудников);
		
		Если ДанныеСотрудников = Неопределено Тогда
			ДанныеСотрудников = Новый Соответствие;
		КонецЕсли;
		
		ДанныеСотрудников.Вставить(Организация, ДанныеСотрудника);
		
		ПоместитьВоВременноеХранилище(ДанныеСотрудников, Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресДанныеСотрудников);
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьОрганизацииНезаблокированныеПоAPI() Экспорт
		
		СписокОрганизаций =  Новый Массив;
		КонтекстДиадока = Платформа.ПараметрыКлиент.КонтекстДиадока;
		Если КонтекстДиадока <> Неопределено Тогда
			Для Каждого ТекОрганизация Из КонтекстДиадока Цикл
				Если НЕ ТекОрганизация.ЗаблокированаПоAPI Тогда
					СписокОрганизаций.Добавить(ТекОрганизация.Организация);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;	
		
		Возврат СписокОрганизаций;
		
	КонецФункции
		
	&НаКлиенте
	Процедура ИнициализироватьMagic() Экспорт
		
		ОбработанныеBoxId= Новый Соответствие;
		
		Для каждого СтрокаКонтекста Из Платформа.ПараметрыКлиент.КонтекстДиадока Цикл
			
			Если ОбработанныеBoxId[СтрокаКонтекста.BoxId] = Истина
				ИЛИ СтрокаКонтекста.ЗаблокированаПоAPI Тогда
				Продолжить;
			КонецЕсли;
			
			Если  СтрокаКонтекста.ДанныеОрганизации.AuthenticateType = "Certificate"
				И СтрокаКонтекста.Magic = Неопределено
				И НЕ ЗначениеЗаполнено(СтрокаКонтекста.ОшибкаСертификата) Тогда
				
				Попытка
					СтрокаКонтекста.Magic = СтрокаКонтекста.Box.GetReceiptGenerationProcess();
					СтрокаКонтекста.Magic.Start();
				Исключение КонецПопытки;
				
			КонецЕсли;
			
			ОбработанныеBoxId.Вставить(СтрокаКонтекста.BoxId, Истина);
			
		КонецЦикла;
		
	КонецПроцедуры
	
	// Определяет содержится ли в переданном тексте ошибки
	// признак того, что сертификат не зарегистрирован в ФНС
	//
	// Параметры:
	//	ТекстОшибки - Строка - текст полученной ошибки	
	&НаКлиенте
	Функция ЭтоОшибкаОтсутствиеРегистрацииФНС(ТекстОшибки) Экспорт
		
		Результат = Найти(ТекстОшибки, "FNS reg message was not sent for box") > 0
					ИЛИ Найти(ТекстОшибки, "is not registered in FNS for box") > 0
					ИЛИ Найти(ТекстОшибки, "No registration message to FNS found for boxId") > 0 
					ИЛИ Найти(ТекстОшибки, "Unknown FnsRegStatus") > 0;
		
		Возврат Результат;
		
	КонецФункции
	
	// Проверяет сертификат на ошибки в зависимости от принадлежности типа документа
	// к формализованному/неформализованному:
	// 	- если ошибка связана с тем, что сертификат неквалифицированный,
	// 	  то неформализованные документы могут подписываться, а формализованные - нет;
	// 	- если ошибка связана с тем, что сертификат выпущен по ГОСТ-2001, то
	//	  документы любого типа не могут быть подписаны
	//
	// Параметры:
	//	BoxId 						- Строка - идентификатор ящика организации в Диадоке	
	//	ЭтоФормализованныйДокумент 	- Булево - признак проверки для формализованного документа
	&НаКлиенте
	Процедура ПроверитьСертификат(BoxId, ЭтоФормализованныйДокумент) Экспорт
		
		СтрокаКонтекста = МетодКлиента("Модуль_РаботаССерверомДиадок", "СтрокаКонтекста", BoxId);
		
		Если НЕ ЗначениеЗаполнено(СтрокаКонтекста.ОшибкаСертификата) Тогда
			Возврат;
		КонецЕсли;
		
		Если НЕ ЭтоФормализованныйДокумент
			И Найти(СтрокаКонтекста.ОшибкаСертификата, "Сертификат подписанта не является квалифицированным") > 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если ЭтоОшибкаОтсутствиеРегистрацииФНС(СтрокаКонтекста.ОшибкаСертификата) Тогда
			
			СтрокаКонтекста.Box.SendFnsRegistrationMessage(СтрокаКонтекста.ДанныеОрганизации.Certificate.Thumbprint);
			
			Если Не ЭтоТестоваяОрганизация(СтрокаКонтекста.ДанныеОрганизации) Тогда
				СтрокаКонтекста.ОшибкаСертификата = СтрокаКонтекста.Box.CanSendInvoice(СтрокаКонтекста.ДанныеОрганизации.Certificate.Thumbprint);
			КонецЕсли;
			
			ИнициализироватьMagic();
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаКонтекста.ОшибкаСертификата) Тогда
			ВызватьИсключение "##9[]" + СтрокаКонтекста.ОшибкаСертификата;
		КонецЕсли;
		
	КонецПроцедуры
	
	// Проверяет возможность отправки формализованных и неформализованных документов пакета
	// 
	// Параметры:
	//	МассивДокументовПакета 	- Массив - массив структур, обязательное свойство "DocumentType"
	//	BoxId					- Строка - идентификатор ящика организации в Диадоке
	&НаКлиенте
	Процедура ПроверитьСертификатПоМассивуДокументов(МассивДокументовПакета, BoxId) Экспорт
		
		ФормализованныйПроверен		= Ложь;
		НеформализованныйПроверен 	= Ложь;
		
		Для Каждого СтруктураДокументВПакете Из МассивДокументовПакета Цикл
			
			ЭтоФормализованныйДокумент = ЭтоФормализованныйДокумент(СтруктураДокументВПакете.DocumentType);
			
			Если ЭтоФормализованныйДокумент И НЕ ФормализованныйПроверен Тогда
				ПроверитьСертификат(BoxId, ЭтоФормализованныйДокумент);
				ФормализованныйПроверен = Истина;
			ИначеЕсли НЕ ЭтоФормализованныйДокумент И НЕ НеформализованныйПроверен Тогда
				ПроверитьСертификат(BoxId, ЭтоФормализованныйДокумент);
				НеформализованныйПроверен = Истина;
			КонецЕсли;
				
			Если ФормализованныйПроверен И НеформализованныйПроверен Тогда
				Прервать;
			КонецЕсли;
				
		КонецЦикла;
		
	КонецПроцедуры
	
	// Формирует текст начала ошибки об использовании сертификата ГОСТ 2001
	//
	// Возвращаемое значение:
	//	Строка - текст начала ошибки
	&НаКлиенте
	Функция ОшибкаИспользованиеСертификатаГОСТ2001_Начало()
		
		DiadocConnection = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection;
		
		Certificate = DiadocConnection.Certificate;
		
		Результат = "Сертификат " + Certificate.Name + " (" 
					+ Формат(Certificate.BeginDate, "ДФ='dd.MM.yy'") + " - " 
					+ Формат(Certificate.EndDate, "ДФ='dd.MM.yy'")
					+ ") выпущен по устаревшему ГОСТ-2001.";
					
		Возврат Результат;
		
	КонецФункции
	
	// Формирует текст ошибки об использовании сертификата ГОСТ 2001
	// и выводит ее в форму ФормаВыводаHTMLДокумента
	&НаКлиенте
	Процедура ПоказатьПредупреждениеОбИспользованииСертификатаГОСТ2001() Экспорт
		
		Если ДатаЗапретаГОСТ2001_Наступила() Тогда
			ТекстОграничения = "Вы не можете подписывать документы этим сертификатом.";
		Иначе
			ТекстОграничения = "С 1 января 2020 года вы не сможете подписывать документы этим сертификатом.";	
		КонецЕсли;
		
		HTMLТекст = "<HTML><HEAD>
					|<META content=""text/html; charset=utf-8"" http-equiv=Content-Type>
					|<STYLE type=""text/css"">h3{margin-top:0.5em; margin-bottom:1em;} p{margin-top:0.2em; margin-bottom:0em;}</STYLE></HEAD>
					|<BODY>
					|<H4>#НачалоОшибки</H4>
					|<P>#ТекстОграничения</P>
					|<P>#ОкончаниеОшибки</P>		
					|</BODY></HTML>";
		
		HTMLТекст = СтрЗаменить(HTMLТекст, "#НачалоОшибки"		, ОшибкаИспользованиеСертификатаГОСТ2001_Начало());
		HTMLТекст = СтрЗаменить(HTMLТекст, "#ТекстОграничения"	, ТекстОграничения);
		HTMLТекст = СтрЗаменить(HTMLТекст, "#ОкончаниеОшибки"	, "Обновите сертификат на ГОСТ-2012 через свой сервисный центр.");
						
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗаголовокФормы", "Обновите сертификат на ГОСТ-2012");
		ПараметрыФормы.Вставить("HTMLДокумент"	, HTMLТекст);
		
		МетодКлиента(, "ОткрытьФормуОбработкиМодально", "ФормаВыводаHTMLДокумента", ПараметрыФормы, ЭтаФорма.ВладелецФормы);
	
	КонецПроцедуры
	
	// Формирует текст ошибки об использовании сертификата ГОСТ 2001
	// для показа во время выполнения запрещенных действий:
	//	- подписание исходящих/входящих;
	//	- отказ в подписи входящих;
	//	- аннулирование;
	//	- запрос уточнения.
	//
	// Возвращаемое значение:
	//	Строка - текст ошибки
	&НаКлиенте
	Функция ТекстОшибкиИспользованиеСертификатаГОСТ2001()
		
		Результат = СтрШаблон("%1
		|Вы не можете подписывать документы этим сертификатом."
		, ОшибкаИспользованиеСертификатаГОСТ2001_Начало());
		
		Возврат Результат;
		
	КонецФункции

	// Проверяет сертификат на выпуск по ГОСТ 2001
	//
	// Возвращаемое значение:
	//	Булево - является ли сертификат, под которым авторизован пользователь,
	//			 сертификатом, выпущенным по ГОСТ 2001
	&НаКлиенте
	Функция ИспользуетсяСертификатПоГОСТ2001() Экспорт
		
		Результат = Ложь;
		
		DiadocConnection = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection;
		
		Если DiadocConnection.AuthenticateType <> "Certificate" Тогда
			Возврат Результат;
		КонецЕсли;
		
		Certificate = DiadocConnection.Certificate;
		
		// OID алгоритма открытого ключа сертификата по ГОСТ 2001
		Если Certificate.AlgorithmOID = "1.2.643.2.2.19" Тогда 
			Результат = Истина;
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Проверяет, что текущая дата больше даты запрета использования сертификатов,
	// выпущенных по ГОСТ 2001
	//
	// Возвращаемое значение:
	//	Булево - текущая дата больше даты запрета использования сертификатов
	&НаКлиенте
	Функция ДатаЗапретаГОСТ2001_Наступила()
		
		Результат = Ложь;
		
		ДатаПерехода = Дата(2020, 1, 1);

		Если УниверсальноеВремя(ТекущаяДата()) > ДатаПерехода Тогда
			Результат = Истина;
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НайтиСтрокиКонтекстДиадокаПоОтбору(СтруктураОтбор) Экспорт
		Возврат НайтиСтрокиВМассивеСтруктур(Платформа.ПараметрыКлиент.КонтекстДиадока, СтруктураОтбор);//КонтекстДиадока.НайтиСтроки(СтруктураОтбор);
	КонецФункции
	
	&НаКлиенте
	Функция РазвернутьGUID(СтрНач) Экспорт
		
		СтрРез = "";
		Если СтрДлина(Стрнач) > 36 Тогда
			СтрРез = лев(Стрнач, 32);
			СтрРез = Сред(СтрРез, 1, 8) + "-" + Сред(СтрРез, 9, 4) + "-" + Сред(СтрРез, 13, 4) + "-" + Сред(СтрРез, 17, 4) + "-" + Сред(СтрРез, 21, 12);
		ИначеЕсли СтрДлина(Стрнач) < 36 Тогда
			СтрРез = Сред(Стрнач, 1, 8) + "-" + Сред(Стрнач, 9, 4) + "-" + Сред(Стрнач, 13, 4) + "-" + Сред(Стрнач, 17, 4) + "-" + Сред(Стрнач, 21, 12);
		Иначе
			СтрРез = СтрНач;
		КонецЕсли;
		
		Возврат СтрРез;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьURLНаДокументДиадока(BoxId, DocumentId)  
		
		MessageId = Сред(DocumentId, 1, 36);
		EntityId = Сред(DocumentId, 37, 36);
		
		Возврат  РазвернутьGUID(BoxId) + "/Document/Show?letterId=" + РазвернутьGUID(MessageId) + "&documentId=" + РазвернутьGUID(EntityId);
		
	КонецФункции
	
	&НаСервере
	Функция ПолучитьПутьКWEBСерверу()
		
		Возврат МетодСервера(,"ПолучитьПутьКWEBСерверу");
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОткрытьСтраницуВДиадоке(ПутьПослеСлэша)
		
		ТекстURL = ПолучитьПутьКWEBСерверу() + ПутьПослеСлэша;
		
		//// Открытие ссылки в IE
		//Попытка
		//	IE=	Новый COMОбъект("InternetExplorer.Application"); 
		//Исключение
		//	Сообщить("Ошибка при открытии приложения Internet Explorer");
		//	Сообщить(ОписаниеОшибки());
		//	Возврат;
		//КонецПопытки;
		//
		//IE.Visible=	True; 
		//IE.Navigate2(ТекстURL);
		
		ЗапуститьПриложение(текстURL);
		
	КонецПроцедуры 
	
	&НаКлиенте
	Процедура  ПоказатьДокументВДиадоке(OrganizationId, DocumentId) Экспорт
		
		ТекстURL = ПолучитьURLНаДокументДиадока(OrganizationId, DocumentId);
		ОткрытьСтраницуВДиадоке(ТекстURL);
		
	КонецПроцедуры 
	
	&НаКлиенте
	Процедура ОбработчикСозданиеДокумента(ДокументССылка, ЭДОбъект) Экспорт
		
		РезультатОбновления = МетодСервера(,"ОбновитьДанныеДокумента", ДокументССылка, ЭДОбъект.DocumentDate, ЭДОбъект.DocumentNumber);
		Если РезультатОбновления.ДанныеОбновлены = Истина Тогда
			ОтобразитьИзменениеДанных(ДокументССылка, ВидИзмененияДанных.Изменение);
		КонецЕсли;
		
		Если НЕ РезультатОбновления.ОписаниеОшибки = "" Тогда
			ПоказатьПростоеПредупреждение(РезультатОбновления.ОписаниеОшибки);
		КонецЕсли;
		
		МетодСервера(,"Установить_DocumentID_Для_Документ", ДокументССылка, ЭДОбъект.DocumentId, ЭДОбъект.OrganizationId);
		
		ПараметрыОповещения=	Новый Структура;
		ПараметрыОповещения.Вставить("ТипСущности", "Документ");
		ПараметрыОповещения.Вставить("BoxID", 		ЭДОбъект.OrganizationID);
		ПараметрыОповещения.Вставить("DocumentID", 	ЭДОбъект.DocumentID);
		ПараметрыОповещения.Вставить("Документ1С",	ДокументССылка);
		
		МетодКлиента(,"ОповеститьФормы", "ИзменениеСвязиДД1С", ПараметрыОповещения, ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьПодписанта(Content, Organization, ПараметрыОтправкиНаСогласование= Неопределено) Экспорт
		
		Если 	Content.Type = "UniversalTransferDocument" ИЛИ Лев(Content.Type, 3) = "Utd"
			ИЛИ Content.Type = "UniversalCorrectionDocument" ИЛИ Лев(Content.Type, 3) = "Ucd" Тогда 
					
			ExtendedSigner= Content.AddSigner();
			
			Если ПараметрыОтправкиНаСогласование <> Неопределено Тогда 
				
				Если ПараметрыОтправкиНаСогласование.ResolutionRequestType = "SignatureRequest" Тогда
					ФИОПодписанта= ПараметрыОтправкиНаСогласование.ФИОПолучателя;
					ДолжностьПодписанта = ПараметрыОтправкиНаСогласование.ДолжностьПолучателя;
				Иначе
					ФИОПодписанта= ПараметрыОтправкиНаСогласование.ФИОПодписанта;
					ДолжностьПодписанта = ПараметрыОтправкиНаСогласование.ДолжностьПодписанта;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ФИОПодписанта) Тогда
					ExtendedSigner.SignerDetails.Surname=	 ВыделитьСлово(ФИОПодписанта);
					ExtendedSigner.SignerDetails.FirstName=  ВыделитьСлово(ФИОПодписанта);
					ExtendedSigner.SignerDetails.Patronymic= ВыделитьСлово(ФИОПодписанта);
				Иначе
					ExtendedSigner.SignerDetails.Surname=	 "-";
					ExtendedSigner.SignerDetails.FirstName=	 "-";
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ДолжностьПодписанта) Тогда
					ExtendedSigner.SignerDetails.JobTitle = ДолжностьПодписанта;
				КонецЕсли;
				
				ExtendedSigner.SignerDetails.SignerType= ?(СтрДлина(УдалитьЛишниеНулиИНН(Organization.Inn)) = 12, "IndividualEntity", "LegalEntity");
				ExtendedSigner.SignerDetails.Status= 	 "InformationCreatorEmployee";
				
				ExtendedSigner.SignerDetails.Powers= 	 "PersonDocumentedOperation";
				ExtendedSigner.SignerDetails.Inn= 	 	 Organization.Inn;
				
			ИначеЕсли Organization.AuthenticateType = "Certificate" Тогда
				
				ExtendedSigner.BoxId= Organization.Id;
				ExtendedSigner.CertificateThumbprint= Organization.Certificate.thumbprint;
				
			Иначе
				
				UserPermissions= Organization.GetUserPermissions();
			
				Если НЕ UserPermissions.CanSignDocuments Тогда
					
					ExtendedSigner.SignerDetails.Surname=	 "-";
					ExtendedSigner.SignerDetails.FirstName=	 "-";
					
					ExtendedSigner.SignerDetails.SignerType= "IndividualEntity";
					ExtendedSigner.SignerDetails.Status= 	 "InformationCreatorEmployee";
					ExtendedSigner.SignerDetails.Powers= 	 "PersonDocumentedOperation";
					ExtendedSigner.SignerDetails.Inn= 	 	 "999999999950";
					
				Иначе
					
					User= МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьТекущегоПользователяДиадок");
					
					ExtendedSigner.SignerDetails.Surname=	 User.LastName;
					ExtendedSigner.SignerDetails.FirstName=	 User.FirstName;
					ExtendedSigner.SignerDetails.Patronymic= User.MiddleName ;
					
					ExtendedSigner.SignerDetails.SignerType= "LegalEntity";
					ExtendedSigner.SignerDetails.Status= 	 "SellerEmployee";
					ExtendedSigner.SignerDetails.JobTitle=   UserPermissions.JobTitle;
					ExtendedSigner.SignerDetails.Powers=   	 "ResponsibleForOperationAndSignerForInvoice";
					ExtendedSigner.SignerDetails.Inn= 		 Organization.Inn;
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			UserPermissions= Organization.GetUserPermissions();
			
			Если НЕ UserPermissions.CanSignDocuments Тогда
				
				Content.Signer.Surname=		"-";
				Content.Signer.FirstName=	"-";
				
			Иначе
				
				User= МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьТекущегоПользователяДиадок");
				
				Content.Signer.Surname=		User.LastName;
				Content.Signer.FirstName=	User.FirstName;
				Content.Signer.Patronymic=	User.MiddleName ;
				Content.Signer.JobTitle= 	UserPermissions.JobTitle;
				
			КонецЕсли;
			
			Content.Signer.Inn= Organization.Inn;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ТипДокументаУПД(ФункцияУПД) Экспорт
		
		Если ФункцияУПД = "СЧФДОП" ИЛИ ФункцияУПД = "InvoiceAndBasic" Тогда
			Возврат "УПД Счф Доп";
		ИначеЕсли ФункцияУПД = "ДОП" ИЛИ ФункцияУПД = "Basic" Тогда
			Возврат "УПД Доп";
		ИначеЕсли ФункцияУПД = "СЧФ" ИЛИ ФункцияУПД = "Invoice" Тогда
			Возврат "УПД Счф";
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция ТипДокументаУКД(ФункцияУКД) Экспорт
		
		Если ФункцияУКД = "КСЧФДИС" ИЛИ ФункцияУКД = "InvoiceAndBasic" Тогда
			Возврат "УКД Ксчф Дис";
		ИначеЕсли ФункцияУКД = "ДИС" ИЛИ ФункцияУКД = "Basic" Тогда
			Возврат "УКД Дис";
		ИначеЕсли ФункцияУКД = "КСЧФ" ИЛИ ФункцияУКД = "Invoice" Тогда
			Возврат "УКД Ксчф";
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция ТипДокументаИУПД(ФункцияИУПД) Экспорт
		
		Если ФункцияИУПД = "СЧФДОП" ИЛИ ФункцияИУПД = "InvoiceAndBasic" Тогда
			Возврат "Исправление УПД Счф Доп";
		ИначеЕсли ФункцияИУПД = "ДОП" ИЛИ ФункцияИУПД = "Basic" Тогда
			Возврат "Исправление УПД Доп";
		ИначеЕсли ФункцияИУПД = "СЧФ" ИЛИ ФункцияИУПД = "Invoice" Тогда
			Возврат "Исправление УПД Счф";
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция ФункцияУПД(ТипДокументаУПД) Экспорт
		
		Если ТипДокументаУПД = ТипДокументаУПД("СЧФДОП") ИЛИ ТипДокументаУПД = ТипДокументаУПД("СЧФДОП") + " (тестовый)" Тогда
			Возврат "СЧФДОП";
		ИначеЕсли ТипДокументаУПД = ТипДокументаУПД("ДОП") ИЛИ ТипДокументаУПД = ТипДокументаУПД("ДОП") + " (тестовый)" Тогда
			Возврат "ДОП";
		ИначеЕсли ТипДокументаУПД = ТипДокументаУПД("СЧФ") ИЛИ ТипДокументаУПД = ТипДокументаУПД("СЧФ") + " (тестовый)" Тогда
			Возврат "СЧФ";
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция ЭтоУПД_ТипаСЧФДОП(Document) Экспорт
		
		Возврат ?(Document <> Неопределено, Document.Type = "UniversalTransferDocument" И ТипДокументаУПД(Document.Function) = ТипДокументаУПД("СЧФДОП"), Ложь);
		
	КонецФункции
	
	&НаКлиенте
	Функция ЭтоУПД_ТипаДОП(Document) Экспорт
		
		Возврат ?(Document <> Неопределено, Document.Type = "UniversalTransferDocument" И ТипДокументаУПД(Document.Function) = ТипДокументаУПД("ДОП"), Ложь);
		
	КонецФункции
	
	&НаКлиенте
	Функция ЭтоУПД_ТипаСЧФ(Document) Экспорт
		
		Возврат ?(Document <> Неопределено, Document.Type = "UniversalTransferDocument" И ТипДокументаУПД(Document.Function) = ТипДокументаУПД("СЧФ"), Ложь);
		
	КонецФункции
	
	&НаКлиенте
	Функция ЭтоУКД_ТипаКСЧФДИС(Document) Экспорт
		
		Возврат
		
		Document <> Неопределено
		И Document.Type = "UniversalCorrectionDocument"
		И ТипДокументаУКД(Document.Function) = ТипДокументаУКД("КСЧФДИС");
		
	КонецФункции
	
	&НаКлиенте
	Функция ЭтоУКД_ТипаКСЧФ(Document) Экспорт
		
		Возврат
		
		Document <> Неопределено
		И Document.Type = "UniversalCorrectionDocument"
		И ТипДокументаУКД(Document.Function) = ТипДокументаУКД("КСЧФ");
		
	КонецФункции
	
	&НаКлиенте
	Функция ОбластьПолномочийУПД_ОтветственныйЗаПодписаниеСЧФ(Powers) Экспорт
		
		Возврат
		
			Powers = "InvoiceSigner"
		ИЛИ Powers = "MadeOperationAndSignedInvoice"
		ИЛИ Powers = "MadeAndResponsibleForOperationAndSignedInvoice"
		ИЛИ Powers = "ResponsibleForOperationAndSignerForInvoice"

	КонецФункции

	&НаКлиенте
	Функция ОбластьПолномочийУПД_ЛицоСовершившееСделку(Powers) Экспорт
		
		Возврат
		
			Powers = "PersonMadeOperation"
		ИЛИ Powers = "MadeAndSignOperation"
		ИЛИ Powers = "MadeOperationAndSignedInvoice"
		ИЛИ Powers = "MadeAndResponsibleForOperationAndSignedInvoice"

	КонецФункции

	&НаКлиенте
	Функция ОбластьПолномочийУПД_ОтветственныйЗаОформление(Powers) Экспорт
		
		Возврат
		
			Powers = "MadeAndSignOperation"
		ИЛИ Powers = "PersonDocumentedOperation" 
		ИЛИ Powers = "MadeAndResponsibleForOperationAndSignedInvoice"
		ИЛИ Powers = "ResponsibleForOperationAndSignerForInvoice"

	КонецФункции

	&НаКлиенте
	Процедура ЗаполнитьСтруктуруПоКонтенту(Content, СтруктураДанных, ОписаниеСтруктурыДанных = "") Экспорт
		
		Если Content = Неопределено 
			Или ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли; 
		
		Для Каждого Элемент Из СтруктураДанных Цикл
			
			Если Найти(Элемент.Ключ, "Ссылка") > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ШаблонЭлементаКоллекции = Неопределено;
			
			Если Элемент.Ключ = "Items" Тогда 
				
				Если ОписаниеСтруктурыДанных = "UniversalTransferDocument.InvoiceTable" Тогда
					ШаблонЭлементаКоллекции = Новый_ExtendedInvoiceItem();
				ИначеЕсли ОписаниеСтруктурыДанных = "UniversalCorrectionDocument.InvoiceCorrectionTable" Тогда
					ШаблонЭлементаКоллекции = Новый_ExtendedInvoiceCorrectionItem();
				ИначеЕсли ОписаниеСтруктурыДанных = "TovTorg.Table" Тогда
					ШаблонЭлементаКоллекции = Новый_TovTorgItem();
				ИначеЕсли ОписаниеСтруктурыДанных = "XmlAcceptanceCertificate552.Works" Тогда
					ШаблонЭлементаКоллекции = Новый_Act552WorkItem();
				Иначе
					Если Content.Type = "InvoiceContent" Тогда
						ШаблонЭлементаКоллекции = Новый_InvoiceItem();
					ИначеЕсли Content.Type = "InvoiceCorrectionContent" Тогда
						ШаблонЭлементаКоллекции = Новый_InvoiceCorrectionItem();
					ИначеЕсли Content.Type = "XmlAcceptanceCertificateContent" Тогда
						ШаблонЭлементаКоллекции = Новый_AcceptanceCertificateItem();
					ИначеЕсли Content.Type = "XmlTorg12Content" Тогда
						ШаблонЭлементаКоллекции = Новый_Torg12Item();
					КонецЕсли;
				КонецЕсли;
				
			ИначеЕсли Элемент.Ключ = "PaymentDocuments" Тогда
				
				ШаблонЭлементаКоллекции = Новый_PaymentDocument();
				
			ИначеЕсли Элемент.Ключ = "OriginalInvoices" Тогда
				
				ШаблонЭлементаКоллекции = Новый_OriginalInvoice();
				
			ИначеЕсли Элемент.Ключ = "Signers" Тогда
				
				ШаблонЭлементаКоллекции = Новый_ExtendedSigner();
				
			ИначеЕсли Элемент.Ключ = "StructedAdditionalInfos" Тогда
				
				ШаблонЭлементаКоллекции = Новый_StructedAdditionalInfo();
				
			ИначеЕсли Элемент.Ключ = "CustomDeclarations" Тогда
				
				ШаблонЭлементаКоллекции = Новый_CustomDeclaration();
				
			ИначеЕсли Элемент.Ключ = "Waybills" Тогда
				
				ШаблонЭлементаКоллекции = Новый_Waybill();
				
			ИначеЕсли Элемент.Ключ = "TransferBases" Тогда
				
				ШаблонЭлементаКоллекции = Новый_TransferBase();
				
			ИначеЕсли Элемент.Ключ = "Invoices" Тогда
				
				ШаблонЭлементаКоллекции = Новый_InvoiceForCorrectionInfo();
				
			ИначеЕсли Элемент.Ключ = "InvoiceRevisions" Тогда
				
				ШаблонЭлементаКоллекции = Новый_InvoiceRevisionInfo();
				
			ИначеЕсли Элемент.Ключ = "CorrectionBases" Тогда
				
				ШаблонЭлементаКоллекции = Новый_CorrectionBase();
				
			ИначеЕсли Элемент.Ключ = "Works" Тогда
				
				ШаблонЭлементаКоллекции = Новый_Act552WorkDescription();
				
			ИначеЕсли Элемент.Ключ = "Grounds" И ТипЗнч(Элемент.Значение) = Тип("Массив") Тогда
				
				ШаблонЭлементаКоллекции = Новый_GroundInfo();
				
			КонецЕсли;
			
			
			Если ШаблонЭлементаКоллекции <> Неопределено Тогда
				
				ВГраница = Content[Элемент.Ключ].Count - 1;
				
				Для Сч = 0 По ВГраница Цикл
					
					Попытка 
						Item = Content[Элемент.Ключ].GetItem(Сч);
					Исключение
						ОписаниеОшибки = ОписаниеОшибки();
					КонецПопытки;
					
					Если НЕ ЗначениеЗаполнено(ОписаниеОшибки) Тогда
						
						НовыйЭлемент = СкопироватьСтруктуру(ШаблонЭлементаКоллекции);
						
						ЗаполнитьСтруктуруПоКонтенту(Item, НовыйЭлемент, ОписаниеСтруктурыДанных + "." + Элемент.Ключ);
						
						СтруктураДанных[Элемент.Ключ].Добавить(НовыйЭлемент);
						
					КонецЕсли; 
					
				КонецЦикла;
				
			Иначе
				
				Попытка
					ЗначениеContent = Content[Элемент.Ключ];
				Исключение
					ОписаниеОшибки = ОписаниеОшибки();
				КонецПопытки;
				
				Если НЕ ЗначениеЗаполнено(ОписаниеОшибки) Тогда
					
					Если ТипЗнч(Элемент.Значение) = Тип("Структура") Тогда
						ЗаполнитьСтруктуруПоКонтенту(ЗначениеContent, Элемент.Значение, ОписаниеСтруктурыДанных + "." + Элемент.Ключ);
					Иначе
						СтруктураДанных[Элемент.Ключ] = ЗначениеContent;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
				ВызватьИсключение "Ошибка чтения контента " + ОписаниеСтруктурыДанных + "." + Элемент.Ключ + Символы.ПС + ОписаниеОшибки;
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция СкопироватьСтруктуру(СтруктураИсходник)
		
		СтруктураПриемник =	Новый Структура;
		Для каждого ЭлементИсходник Из СтруктураИсходник Цикл
			Если ТипЗнч(ЭлементИсходник.Значение) = Тип("Массив") Тогда
				СтруктураПриемник.Вставить(ЭлементИсходник.Ключ, Новый Массив);
			ИначеЕсли ТипЗнч(ЭлементИсходник.Значение) = Тип("Структура") Тогда
				СтруктураПриемник.Вставить(ЭлементИсходник.Ключ, СкопироватьСтруктуру(ЭлементИсходник.Значение));
			Иначе
				СтруктураПриемник.Вставить(ЭлементИсходник.Ключ, Неопределено);	
			КонецЕсли;
		КонецЦикла;
		
		Возврат СтруктураПриемник;
		
	КонецФункции	
	
	&НаКлиенте
	// функция убирает из ИНН 2 лидирующих нуля, если ИНН начинается с 00
	Функция УдалитьЛишниеНулиИНН(ИНН) Экспорт
		
		Если Лев(ИНН, 2) = "00" Тогда
			Возврат Сред(ИНН, 3);
		Иначе
			Возврат ИНН;
		КонецЕсли;
	
	КонецФункции
	
	&НаКлиенте
	Функция ПреобразоватьСтрокуВМассивПодстрок(Знач Строка, Разделитель = ",", ПропускатьПустыеСтроки = Ложь, ПрименятьСокрЛП = Ложь)
		
		МассивПодстрок = Новый Массив;
		
		ДлинаРазделителя = СтрДлина(Разделитель);
		
		ПозицияРазделителя = Найти(Строка, Разделитель);
		
		Пока ПозицияРазделителя > 0 Цикл
			
			Подстрока = Лев(Строка, ПозицияРазделителя - 1);
			
			Если НЕ ПропускатьПустыеСтроки ИЛИ ЗначениеЗаполнено(Подстрока) Тогда
				МассивПодстрок.Добавить(?(ПрименятьСокрЛП, СокрЛП(Подстрока), Подстрока));
			КонецЕсли;
			
			Строка = Сред(Строка, ПозицияРазделителя + ДлинаРазделителя);
			
			ПозицияРазделителя = Найти(Строка, Разделитель);
			
		КонецЦикла;
		
		Если НЕ ПустаяСтрока(Строка) Тогда
			МассивПодстрок.Добавить(?(ПрименятьСокрЛП, СокрЛП(Строка), Строка));
		КонецЕсли;
		
		Возврат МассивПодстрок;
		
	КонецФункции 
	
	&НаКлиенте
	Функция СтруктураФИО(ФИО) Экспорт
		
		Результат = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
		
		МассивПодстрок = ПреобразоватьСтрокуВМассивПодстрок(ФИО, " ", Ложь, Истина);
		
		РазмерМассива = МассивПодстрок.Количество();
		
		Если РазмерМассива > 0 Тогда
			Результат.Фамилия = МассивПодстрок[0];
		КонецЕсли;
		
		Если РазмерМассива > 1 Тогда
			Результат.Имя = МассивПодстрок[1];
		КонецЕсли;
		
		Если РазмерМассива > 2 Тогда
			Результат.Отчество = МассивПодстрок[2];
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьИдентификаторыЭДО() Экспорт
		
		ОбъектыБезИдентификаторовЭДО = ОбъектыБезИдентификаторовЭДО();
		
		Если ОбъектыБезИдентификаторовЭДО.Количество() > 0 Тогда
			
			КэшИдентификаторовЭДО = Новый Соответствие;
			
			Если ОбъектыБезИдентификаторовЭДО.Свойство("Организации") Тогда
				
				ТекстСостояния			 = НСтр("ru = 'Заполнение идентификаторов участников ЭДО для справочника Организации'");
				НачальныйРазмерМассива	 = ОбъектыБезИдентификаторовЭДО.Организации.Количество();
				ТекущийРазмерМассива	 = НачальныйРазмерМассива;
				
				Пока ТекущийРазмерМассива > 0 Цикл
					
					ПоказатьСостояниеОбработкиСписка(
					ТекстСостояния,
					НачальныйРазмерМассива - ТекущийРазмерМассива,
					НачальныйРазмерМассива);
					
					ОбработатьПорциюИдентификаторовЭДО(ОбъектыБезИдентификаторовЭДО.Организации, КэшИдентификаторовЭДО, "ДиадокИдентификаторЭДОДляОрганизации");
					
					ТекущийРазмерМассива = ОбъектыБезИдентификаторовЭДО.Организации.Количество();
					
				КонецЦикла;
				
			КонецЕсли;
			
			Если ОбъектыБезИдентификаторовЭДО.Свойство("Контрагенты") Тогда
				
				ТекстСостояния			 = НСтр("ru = 'Заполнение идентификаторов участников ЭДО для справочника Контрагенты'");
				НачальныйРазмерМассива	 = ОбъектыБезИдентификаторовЭДО.Контрагенты.Количество();
				ТекущийРазмерМассива	 = НачальныйРазмерМассива;
				
				Пока ТекущийРазмерМассива > 0 Цикл
					
					ПоказатьСостояниеОбработкиСписка(
					ТекстСостояния,
					НачальныйРазмерМассива - ТекущийРазмерМассива,
					НачальныйРазмерМассива);
					
					ОбработатьПорциюИдентификаторовЭДО(ОбъектыБезИдентификаторовЭДО.Контрагенты, КэшИдентификаторовЭДО, "ДиадокИдентификаторЭДО");
					
					ТекущийРазмерМассива = ОбъектыБезИдентификаторовЭДО.Контрагенты.Количество();
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработатьПорциюИдентификаторовЭДО(МассивОбъектов, КэшИдентификаторовЭДО, НаименованиеСвойства)
		
		РазмерПорции = 50;
		
		ПорцияОбъектов = Новый Массив;
		
		НачальныйИндекс = МассивОбъектов.ВГраница();
		
		Если НачальныйИндекс > РазмерПорции Тогда
			КонечныйИндекс = НачальныйИндекс - РазмерПорции;
		Иначе
			КонечныйИндекс = -1;
		КонецЕсли;
		
		Пока НачальныйИндекс > КонечныйИндекс Цикл
			
			ПорцияОбъектов.Добавить(МассивОбъектов[НачальныйИндекс]);
			МассивОбъектов.Удалить(НачальныйИндекс);
						
			НачальныйИндекс = НачальныйИндекс - 1;
			
		КонецЦикла;
		
		Для Каждого ТекущийОбъект Из ПорцияОбъектов Цикл
			
			ИдентификаторЭДО = КэшИдентификаторовЭДО[ТекущийОбъект.BoxID];
			Если ИдентификаторЭДО = Неопределено Тогда
				ИдентификаторЭДО = МетодКлиента("Модуль_РаботаССерверомДиадок", "ИдентификаторЭДО", ТекущийОбъект.BoxID);
				КэшИдентификаторовЭДО.Вставить(ТекущийОбъект.BoxID, ИдентификаторЭДО);
			КонецЕсли;
			
			ТекущийОбъект.Удалить("BoxID");
			ТекущийОбъект.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
			
		КонецЦикла;

		ЗаписатьВБазуИдентификаторыЭДО(ПорцияОбъектов, НаименованиеСвойства);
		
	КонецПроцедуры
	
	&НаСервере
	Функция ОбъектыБезИдентификаторовЭДО()
		
		Результат = Новый Структура;
		
		// Идентификаторы ЭДО для справочника организаций
		СвойствоИдентификаторЯщика = МетодСервера("", "ИдентификаторСвойстваЯщикОрганизации");
		СвойствоИдентификаторЭДО   = МетодСервера("", "ИдентификаторСвойстваИдентификаторЭДОДляОрганизации");
		
		РезультатЗапроса = ОбъектыБезИдентификаторовЭДО_РезультатЗапроса(СвойствоИдентификаторЯщика, СвойствоИдентификаторЭДО);
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Результат.Вставить("Организации", Новый Массив);
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				Результат.Организации.Добавить(Новый Структура("Объект, BoxID", Выборка.Объект, Выборка.BoxID));
			КонецЦикла;
			
		КонецЕсли;
		
		// Идентификаторы ЭДО для справочника контрагентов
		СвойствоИдентификаторЯщика = МетодСервера("", "ИдентификаторСвойстваЯщикКонтрагентаПрефикс");
		СвойствоИдентификаторЭДО   = МетодСервера("", "ИдентификаторСвойстваИдентификаторЭДОДляКонтрагента");
		
		РезультатЗапроса = ОбъектыБезИдентификаторовЭДО_РезультатЗапроса(СвойствоИдентификаторЯщика, СвойствоИдентификаторЭДО);
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Результат.Вставить("Контрагенты", Новый Массив);
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				Результат.Контрагенты.Добавить(Новый Структура("Объект, BoxID", Выборка.Объект, Выборка.BoxID));
			КонецЦикла;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаСервере
	Функция ОбъектыБезИдентификаторовЭДО_РезультатЗапроса(НаименованиеСвойстваИдентификаторЯщика, НаименованиеСвойстваИдентификаторЭДО)
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИдентификаторыЯщиков.Объект,
		|	ИдентификаторыЯщиков.Значение КАК BoxID
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ИдентификаторыЯщиков
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ИдентификаторыЭДО
		|		ПО ИдентификаторыЯщиков.Объект = ИдентификаторыЭДО.Объект
		|			И (ИдентификаторыЭДО.Свойство = &СвойствоИдентификаторЭДО)
		|ГДЕ
		|	ИдентификаторыЯщиков.Свойство = &СвойствоИдентификаторЯщика
		|	И ИдентификаторыЯщиков.Значение <> """"
		|	И ИдентификаторыЭДО.Свойство ЕСТЬ NULL");
		
		Запрос.УстановитьПараметр("СвойствоИдентификаторЯщика", МетодСервера("", "НайтиСвойство", НаименованиеСвойстваИдентификаторЯщика));
		Запрос.УстановитьПараметр("СвойствоИдентификаторЭДО"  , МетодСервера("", "НайтиСвойство", НаименованиеСвойстваИдентификаторЭДО));
		
		Возврат Запрос.Выполнить();
		
	КонецФункции
		
	&НаСервере
	Процедура ЗаписатьВБазуИдентификаторыЭДО(МассивОбъектов, НаименованиеСвойства)
		
		Свойство = МетодСервера("", "НайтиСвойство", НаименованиеСвойства);
		
		Для Каждого ТекущийОбъект Из МассивОбъектов Цикл
			
			Запись = РегистрыСведений.ДополнительныеСведения.СоздатьМенеджерЗаписи();
			Запись.Объект	= ТекущийОбъект.Объект;
			Запись.Свойство = Свойство;
			Запись.Значение = ТекущийОбъект.ИдентификаторЭДО;
			Запись.Записать();
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьProto(Document, КонтентОтветногоТитула = Ложь) Экспорт
		
		Результат = Новый Структура;
		
		ТипКонтента = ПолучитьТипКонтента(Document.Version, Document.Type);
		
		Если КонтентОтветногоТитула Тогда
			ТипКонтента	 = СтрЗаменить(ТипКонтента, "SellerContent", "BuyerContent");
			ПротоКонтент = ПротоКонтентОтветногоТитулаДокумента(Document);
		Иначе
			ПротоКонтент = ПротоКонтентДокумента(Document);
		КонецЕсли;
		
		Результат.Вставить("ТипКонтента", ТипКонтента);
		
		Если ЗначениеЗаполнено(ПротоКонтент) Тогда
			Результат.Вставить("Контент", ПротоКонтент);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПротоКонтентДокумента(Document) Экспорт
		
		КэшКонтента = КэшПротоКонтентаДокумента(Document.OrganizationId, Document.DocumentId);
		
		Если КэшКонтента.ПротоКонтент = Неопределено Тогда
			
			ФайлXML = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьФайлТитулаПродавца", Document);
			
			КэшКонтента.ПротоКонтент = МетодСервера("ГенерацияXML", "XML_В_ProtoСтруктура", ФайлXML);
			
		КонецЕсли;
		
		Возврат КэшКонтента.ПротоКонтент;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПротоКонтентОтветногоТитулаДокумента(Document) Экспорт
		
		КэшКонтента = КэшПротоКонтентаДокумента(Document.OrganizationId, Document.DocumentId);
		
		Если КэшКонтента.ПротоКонтентОтветногоТитула = Неопределено Тогда
			
			ФайлXML = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьФайлТитулаПокупателя", Document);
			
			Если ЗначениеЗаполнено(ФайлXML) Тогда
				КэшКонтента.ПротоКонтентОтветногоТитула = МетодСервера("ГенерацияXML", "XML_В_ProtoСтруктура", ФайлXML);
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат КэшКонтента.ПротоКонтентОтветногоТитула;
		
	КонецФункции
	
	&НаКлиенте
	Функция СформироватьОтветныйТитулДокумента(Document, ПротоКонтент)
		
		ТипКонтента = ПолучитьТипКонтента(Document.Version, Document.Type);
		ТипКонтента = СтрЗаменить(ТипКонтента, "SellerContent", "BuyerContent");
		
		ФайлТитулаПродавца	 = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьФайлТитулаПродавца"	, Document);
		ФайлПодписиПродавца	 = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьФайлПодписиПродавца"	, Document);
		
		Результат = СформироватьОтветныйТитулДокументаНаСервере(ТипКонтента, ПротоКонтент, ФайлТитулаПродавца, ФайлПодписиПродавца);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаСервере
	Функция СформироватьОтветныйТитулДокументаНаСервере(ТипКонтента, ПротоКонтент, ФайлТитулаПродавца, ФайлПодписиПродавца)
		
		ПротоКонтентXDTO			 = МетодСервера("ГенерацияXML", "ПротоКонтент_В_ПротоКонтентXDTO", ПротоКонтент, ТипКонтента);
		ПараметрыФормированияФайла	 = МетодСервера("ГенерацияXML", "XML_ПараметрыФормированияФайла");
		
		Если ТипКонтента = "Utd820BuyerContent" Тогда
			
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ОтветныйТитул_УПД_820"
				, ПротоКонтентXDTO, ФайлТитулаПродавца, ФайлПодписиПродавца, ПараметрыФормированияФайла);
			
		ИначеЕсли ТипКонтента = "UtdBuyerContent" Тогда
			
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ОтветныйТитул_УПД_155"
				, ПротоКонтентXDTO, ФайлТитулаПродавца, ФайлПодписиПродавца, ПараметрыФормированияФайла);
			
		ИначеЕсли ТипКонтента = "UcdBuyerContent" Тогда
			
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ОтветныйТитул_УКД"
			, ПротоКонтентXDTO, ФайлТитулаПродавца, ФайлПодписиПродавца, ПараметрыФормированияФайла);
			
		ИначеЕсли ТипКонтента = "TovTorgBuyerContent" Тогда
			
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ОтветныйТитул_Торг_551"
			, ПротоКонтентXDTO, ФайлТитулаПродавца, ФайлПодписиПродавца, ПараметрыФормированияФайла);
			
		ИначеЕсли ТипКонтента = "Act552BuyerContent" Тогда
			
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ОтветныйТитул_Акт_552"
			, ПротоКонтентXDTO, ФайлТитулаПродавца, ФайлПодписиПродавца, ПараметрыФормированияФайла);
			
		ИначеЕсли ТипКонтента = "Torg12BuyerContent" Тогда
			
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ОтветныйТитул_Торг_172"
			, ПротоКонтентXDTO, ФайлТитулаПродавца, ПараметрыФормированияФайла);
			
		ИначеЕсли ТипКонтента = "AcceptanceCertificateBuyerContent" Тогда
			
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ОтветныйТитул_Акт_172"
			, ПротоКонтентXDTO, ФайлТитулаПродавца, ПараметрыФормированияФайла);
			
		Иначе
			
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Ошибка заполнения титула покупателя, неизвестный тип контента %1'"), ТипКонтента);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		Результат = МетодСервера("ГенерацияXML", "ОбъектXDTO_В_ДвоичныеДанные", ОбъектXDTO);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция КэшПротоКонтентаДокумента(BoxID, DocumentId)
		
		МаксимальныйРазмерКэша = 100;
		
		Если КэшПротоКонтента = Неопределено Тогда
			
			КэшПротоКонтента = Новый Структура;
			КэшПротоКонтента.Вставить("МассивКлючей", Новый Массив);
			КэшПротоКонтента.Вставить("Соответствие", Новый Соответствие);
			
		КонецЕсли;
		
		Ключ = BoxID + DocumentId;
		Результат = КэшПротоКонтента.Соответствие[Ключ];
		
		Если Результат = Неопределено Тогда
			
			Результат = Новый Структура;
			Результат.Вставить("ПротоКонтент");
			Результат.Вставить("ПротоКонтентОтветногоТитула");
			
			Если КэшПротоКонтента.МассивКлючей.Количество() = МаксимальныйРазмерКэша Тогда
				КэшПротоКонтента.Соответствие.Удалить(КэшПротоКонтента.МассивКлючей[0]);
				КэшПротоКонтента.МассивКлючей.Удалить(0);
			КонецЕсли;
			
			КэшПротоКонтента.Соответствие.Вставить(Ключ, Результат);
			КэшПротоКонтента.МассивКлючей.Добавить(Ключ);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Определяет является ли организация тестовой
	//
	// Параметры:
	//	ДанныеОрганизации - СОМ-объект, Структура - может передаваться:
	//													- объект компоненты Organization;
	//													- объект компоненты Counteragent
	//													- структура (см. Новый_Organization())
	//
	// Возвращаемое значение:
	//	Булево - признак того, что организация тестовая
	&НаКлиенте
	Функция ЭтоТестоваяОрганизация(ДанныеОрганизации) Экспорт
		
		Результат = ДанныеОрганизации.IsTest ИЛИ ДанныеОрганизации.IsPilot;
		
		Возврат Результат;
		
	КонецФункции
	
//} ИНТЕГРАЦИЯ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ ИНИЦАЛИЗАЦИЯ content

// описанные функции вызываются на клиенте из форм,
// должны совпадать с описанием функций инициализации контентов на сервере ("Модуль_ИнтеграцияУниверсальный")

//{ документ

	&НаКлиенте
	Функция Новый_Torg12SellerContent() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Torg12SellerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Date");
		Результат.Вставить("Number");
		
		Результат.Вставить("SupplyDate");
		
		Результат.Вставить("WaybillDate");
		Результат.Вставить("WaybillNumber");
		
		Результат.Вставить("OperationCode");
		
		Результат.Вставить("GroundName");
		Результат.Вставить("GroundDate");
		Результат.Вставить("GroundNumber");
		
		Результат.Вставить("AttachmentSheetsQuantity");
		
		Результат.Вставить("ShipperDepartment");
		Результат.Вставить("ShipperOkdp");
		
		Результат.Вставить("AdditionalInfo");
		
		Результат.Вставить("Seller", Новый_OrganizationInfo());
		Результат.Вставить("Buyer" , Новый_OrganizationInfo());
		
		Результат.Вставить("Shipper"  , Новый_OrganizationInfo());
		Результат.Вставить("Consignee", Новый_OrganizationInfo());
		
		Результат.Вставить("Totals" , Новый_Torg12Totals());
		Результат.Вставить("Commons", Новый_Torg12Commons());
		
		Результат.Вставить("SupplyAllowedBy"  , Новый_Official());
		Результат.Вставить("ChiefAccountant"  , Новый_Official());
		Результат.Вставить("SupplyPerformedBy", Новый_Official());
		
		Результат.Вставить("Signer", Новый_Signer());
		
		Результат.Вставить("Items", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции	

	&НаКлиенте
	Функция Новый_Torg12BuyerContent() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Torg12BuyerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("ShipmentReceiptDate");
		Результат.Вставить("AdditionalInfo");
		
		Результат.Вставить("Receiver", Новый_Official());
		Результат.Вставить("Accepter", Новый_Official());
		Результат.Вставить("Attorney", Новый_Attorney());
		
		Результат.Вставить("Signer", Новый_Signer());
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_AcceptanceCertificateSellerContent() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/AcceptanceCertificateSellerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Date");
		Результат.Вставить("Number");
		
		Результат.Вставить("Title");
		Результат.Вставить("SignatureDate");
		
		Результат.Вставить("AdditionalInfo");
		
		Результат.Вставить("Seller", Новый_OrganizationInfo());
		
		Результат.Вставить("Official", Новый_Official());
		Результат.Вставить("Attorney", Новый_Attorney());
		
		Результат.Вставить("Signer", Новый_Signer());
		
		Результат.Вставить("Items", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции	

	&НаКлиенте
	Функция Новый_AcceptanceCertificateBuyerContent() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/AcceptanceCertificateBuyerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("SignatureDate");
		Результат.Вставить("Complaints");
		
		Результат.Вставить("AdditionalInfo");
		
		Результат.Вставить("Official", Новый_Official());
		Результат.Вставить("Attorney", Новый_Attorney());
		
		Результат.Вставить("Signer", Новый_Signer());
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_InvoiceContent() Экспорт
		
		Результат = Новый Структура;
		
		Результат.Вставить("Date");
		Результат.Вставить("Number");
		
		Результат.Вставить("InvoiceRevisionDate");
		Результат.Вставить("InvoiceRevisionNumber");
		
		Результат.Вставить("Currency");
		
		Результат.Вставить("Seller", Новый_OrganizationInfo());
		Результат.Вставить("Buyer" , Новый_OrganizationInfo());
		
		Результат.Вставить("Shipper"  , Новый_ShipperOrConsigneeInfo());
		Результат.Вставить("Consignee", Новый_ShipperOrConsigneeInfo());
		
		Результат.Вставить("Signer", Новый_Signer());
		
		Результат.Вставить("Totals", Новый_InvoiceTotals());
		
		Результат.Вставить("Items", Новый Массив);
		Результат.Вставить("PaymentDocuments", Новый Массив);
		
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Результат.Вставить("ВалютаСсылка");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_InvoiceCorrectionContent() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/InvoiceCorrectionContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("InvoiceCorrectionDate");
		Результат.Вставить("InvoiceCorrectionNumber");
		
		Результат.Вставить("InvoiceCorrectionRevisionDate");
		Результат.Вставить("InvoiceCorrectionRevisionNumber");
		
		Результат.Вставить("Currency");
		
		Результат.Вставить("Seller", Новый_OrganizationInfo());
		Результат.Вставить("Buyer" , Новый_OrganizationInfo());
		
		Результат.Вставить("TotalsInc", Новый_InvoiceTotals());
		Результат.Вставить("TotalsDec", Новый_InvoiceTotals());
		
		Результат.Вставить("Signer", Новый_Signer());
		
		Результат.Вставить("Items", Новый Массив);
		Результат.Вставить("OriginalInvoices", Новый Массив);
		
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Результат.Вставить("ВалютаСсылка");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_UtdSellerContent() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/UtdSellerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Date");
		Результат.Вставить("Number");
		Результат.Вставить("Function");
		
		Результат.Вставить("RevisionDate");
		Результат.Вставить("RevisionNumber");
		
		Результат.Вставить("Name");
		Результат.Вставить("Creator");
		Результат.Вставить("CreatorBase");
		
		Результат.Вставить("Currency");
		Результат.Вставить("CurrencyRate");
		
		Результат.Вставить("GovernmentContractInfo");
		
		Результат.Вставить("Seller", Новый_ExtendedOrganizationInfo());
		Результат.Вставить("Buyer" , Новый_ExtendedOrganizationInfo());
		
		Результат.Вставить("Shipper"  , Новый_Shipper());
		Результат.Вставить("Consignee", Новый_ExtendedOrganizationInfo());
		
		Результат.Вставить("InvoiceTable", Новый_InvoiceTable());
		Результат.Вставить("TransferInfo", Новый_TransferInfo());
		
		Результат.Вставить("AdditionalInfoId", Новый_AdditionalInfoId());
		
		Результат.Вставить("Signers", Новый Массив);
		Результат.Вставить("PaymentDocuments", Новый Массив);
		
		Результат.Вставить("СчетФактураСсылка");
		Результат.Вставить("ВалютаСсылка");
		
		Возврат Результат;
		
	КонецФункции	

	&НаКлиенте
	Функция Новый_UtdBuyerContent() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/UtdBuyerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("AcceptanceDate");
		
		Результат.Вставить("Creator");
		Результат.Вставить("CreatorBase");
		
		Результат.Вставить("OperationCode");
		Результат.Вставить("OperationContent");
		
		Результат.Вставить("Employee"	, Новый_Employee());
		Результат.Вставить("OtherIssuer", Новый_OtherIssuer());
		
		Результат.Вставить("AdditionalInfoId", Новый_AdditionalInfoId());
		
		Результат.Вставить("Signers", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_UcdSellerContent() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/UcdSellerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Date");
		Результат.Вставить("Number");
		Результат.Вставить("Function");
		
		Результат.Вставить("RevisionDate");
		Результат.Вставить("RevisionNumber");
		
		Результат.Вставить("Name");
		Результат.Вставить("Creator");
		Результат.Вставить("CreatorBase");
		
		Результат.Вставить("Currency");
		Результат.Вставить("CurrencyRate");
		
		Результат.Вставить("GovernmentContractInfo");
		
		Результат.Вставить("Seller", Новый_ExtendedOrganizationInfo());
		Результат.Вставить("Buyer" , Новый_ExtendedOrganizationInfo());
		
		Результат.Вставить("InvoiceCorrectionTable", Новый_InvoiceCorrectionTable());
		Результат.Вставить("EventContent", Новый_EventContent());
		
		Результат.Вставить("AdditionalInfoId", Новый_AdditionalInfoId());
		
		Результат.Вставить("Invoices", Новый Массив);
		Результат.Вставить("Signers" , Новый Массив);
		
		Результат.Вставить("СчетФактураСсылка");
		Результат.Вставить("ВалютаСсылка");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_UcdBuyerContent() Экспорт 
		
		Возврат Новый_UtdBuyerContent();
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_TovTorgSellerContent() Экспорт 
		
		// http://1c-docs.diadoc.ru/ru/latest/TovTorgSellerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentName");
		
		Результат.Вставить("DocumentDate");
		Результат.Вставить("DocumentNumber");
		
		Результат.Вставить("RevisionDate");
		Результат.Вставить("RevisionNumber");
		
		Результат.Вставить("Currency");
		Результат.Вставить("CurrencyRate");
		
		Результат.Вставить("DocumentCreator");
		Результат.Вставить("DocumentCreatorBase");
		
		Результат.Вставить("OperationType");
		
		Результат.Вставить("GovernmentContractInfo");
		
		Результат.Вставить("Table", Новый_TovTorgTable());
		
		Результат.Вставить("Seller", Новый_ExtendedOrganizationInfo());
		Результат.Вставить("Buyer" , Новый_ExtendedOrganizationInfo());
		
		Результат.Вставить("Shipper"  , Новый_ExtendedOrganizationInfo());
		Результат.Вставить("Carrier"  , Новый_ExtendedOrganizationInfo());
		Результат.Вставить("Consignee", Новый_ExtendedOrganizationInfo());
		
		Результат.Вставить("TransferInfo"	 , Новый_TovTorgTransferInfo());
		Результат.Вставить("AdditionalInfoId", Новый_AdditionalInfoId());
		
		Результат.Вставить("Signers", Новый Массив);
		Результат.Вставить("Grounds", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_TovTorgBuyerContent() Экспорт 
		
		// http://1c-docs.diadoc.ru/ru/latest/TovTorgBuyerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentCreator");
		Результат.Вставить("DocumentCreatorBase");
		
		Результат.Вставить("OperationCode");
		Результат.Вставить("OperationContent");
		
		Результат.Вставить("AcceptanceDate");
		
		Результат.Вставить("Employee"	, Новый_Employee());
		Результат.Вставить("OtherIssuer", Новый_OtherIssuer());
		
		Результат.Вставить("AdditionalInfo", Новый_AdditionalInfoId());
		
		Результат.Вставить("Signers", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_Act552SellerContent() Экспорт 
		
		// http://1c-docs.diadoc.ru/ru/latest/Act552SellerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentName");
		
		Результат.Вставить("DocumentDate");
		Результат.Вставить("DocumentNumber");
		
		Результат.Вставить("RevisionDate");
		Результат.Вставить("RevisionNumber");
		
		Результат.Вставить("Currency");
		Результат.Вставить("CurrencyRate");
		
		Результат.Вставить("DocumentCreator");
		Результат.Вставить("DocumentCreatorBase");
		
		Результат.Вставить("OperationType");
		Результат.Вставить("OperationTitle");
		
		Результат.Вставить("GovernmentContractInfo");
		
		Результат.Вставить("Seller", Новый_ExtendedOrganizationInfo());
		Результат.Вставить("Buyer" , Новый_ExtendedOrganizationInfo());
		
		Результат.Вставить("TransferInfo"  , Новый_Act552TransferInfo());
		Результат.Вставить("AdditionalInfo", Новый_AdditionalInfoId());
		
		Результат.Вставить("Works"	, Новый Массив);
		Результат.Вставить("Signers", Новый Массив);
		Результат.Вставить("Grounds", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_Act552BuyerContent() Экспорт 
		
		// http://1c-docs.diadoc.ru/ru/latest/Act552BuyerContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentCreator");
		Результат.Вставить("DocumentCreatorBase");
		
		Результат.Вставить("OperationType");
		Результат.Вставить("OperationContent");
		
		Результат.Вставить("AcceptanceDate");
		
		Результат.Вставить("CreatedThingInfo");
		Результат.Вставить("CreatedThingAcceptDate");
		
		Результат.Вставить("AdditionalInfoId", Новый_AdditionalInfoId());
		
		Результат.Вставить("Signers", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции
	
//} документ

//{ шапка
	
	&НаКлиенте
	Функция Новый_OrganizationInfo() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/OrganizationInfo.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Name");
		Результат.Вставить("Inn");
		Результат.Вставить("Kpp");
		Результат.Вставить("IsSoleProprietor");
		
		Результат.Вставить("FnsParticipantId");
		
		Результат.Вставить("Okopf");
		Результат.Вставить("Okpo");
		
		Результат.Вставить("Phone");
		Результат.Вставить("Fax");
		
		Результат.Вставить("BankAccountNumber");
		Результат.Вставить("BankName");
		Результат.Вставить("BankId");
		
		Результат.Вставить("Address", Новый_AddressInfo());
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_ExtendedOrganizationInfo() Экспорт 
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/ExtendedOrganizationInfo.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Name");
		Результат.Вставить("Type");
		Результат.Вставить("Inn");
		Результат.Вставить("Kpp");
		
		Результат.Вставить("Okopf");
		Результат.Вставить("Okpo");
		Результат.Вставить("Okdp");
		
		Результат.Вставить("FnsParticipantId");
		Результат.Вставить("BoxId");
		
		Результат.Вставить("IndividualEntityRegistrationCertificate");
		Результат.Вставить("OrganizationOrPersonInfo");
		
		Результат.Вставить("Phone");
		Результат.Вставить("Email");
		
		Результат.Вставить("BankAccountNumber");
		Результат.Вставить("CorrespondentAccount");
		Результат.Вставить("BankName");
		Результат.Вставить("BankId");
		
		Результат.Вставить("Department");
		
		Результат.Вставить("AdditionalInfo");
		
		Результат.Вставить("Address", Новый_AddressInfo());
		
		Результат.Вставить("Ссылка");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_Shipper() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Shipper.html
		
		Результат = Новый Структура;
		Результат.Вставить("SameAsSeller");
		Результат.Вставить("OrganizationInfo", Новый_ExtendedOrganizationInfo());
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_ShipperOrConsigneeInfo() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/ShipperOrConsigneeInfo.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Name");
		Результат.Вставить("IsSoleProprietor");
		Результат.Вставить("SameAsSellerOrBuyer");
		
		Результат.Вставить("Address", Новый_AddressInfo());
		
		Результат.Вставить("Ссылка");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_AddressInfo() Экспорт 
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/AddressInfo.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("IsForeign");
		
		Результат.Вставить("ZipCode");
		Результат.Вставить("RegionCode");
		
		Результат.Вставить("Territory");
		Результат.Вставить("City");
		Результат.Вставить("Locality");
		Результат.Вставить("Street");
		Результат.Вставить("Building");
		Результат.Вставить("Block");
		Результат.Вставить("Apartment");
		Результат.Вставить("CountryCode");
		
		Результат.Вставить("AddressText");
		Результат.Вставить("AddressCode");
		
		Возврат Результат;
		
	КонецФункции	

	&НаКлиенте
	Функция Новый_AdditionalInfoId() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/AdditionalInfoId.html
		
		Результат = Новый Структура;
		Результат.Вставить("InfoFileId");
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_StructedAdditionalInfo() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/StructedAdditionalInfo.html
		
		Результат = Новый Структура;
		Результат.Вставить("Key");
		Результат.Вставить("Value");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_InvoiceForCorrectionInfo() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/InvoiceForCorrectionInfo.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("InvoiceDate");
		Результат.Вставить("InvoiceNumber");
		
		Результат.Вставить("InvoiceRevisions", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_InvoiceRevisionInfo() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/InvoiceRevisionInfo.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("RevisionDate");
		Результат.Вставить("RevisionNumber");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_OriginalInvoice() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/OriginalInvoice.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Date");
		Результат.Вставить("Number");
		
		Результат.Вставить("InvoiceRevisionDate");
		Результат.Вставить("InvoiceRevisionNumber");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_PaymentDocument() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/PaymentDocument.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Date");
		Результат.Вставить("Number");
		
		Возврат Результат;
		
	КонецФункции

//} шапка

//{ ТЧ
	
	&НаКлиенте
	Функция Новый_Torg12Item() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Torg12Item.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Product");
		Результат.Вставить("Feature");
		Результат.Вставить("Sort");
		Результат.Вставить("Article");
		Результат.Вставить("ProductCode");
		Результат.Вставить("ExternalCode");
		
		Результат.Вставить("UnitCode");
		Результат.Вставить("UnitName");
		
		Результат.Вставить("ParcelsQuantity");
		Результат.Вставить("ParcelType");
		Результат.Вставить("ParcelCapacity");
		
		Результат.Вставить("GrossQuantity");
		Результат.Вставить("Quantity");
		
		Результат.Вставить("Price");
		Результат.Вставить("TotalWithVatExcluded");
		Результат.Вставить("TaxRate");
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		
		Результат.Вставить("AdditionalInfo");
		
		Результат.Вставить("СсылкаНаЕИ");
		
		Возврат Результат; 
		
	КонецФункции

	&НаКлиенте
	Функция Новый_AcceptanceCertificateItem() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/AcceptanceCertificateItem.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Name");
		Результат.Вставить("Description");
		
		Результат.Вставить("UnitCode");
		Результат.Вставить("UnitName");
		
		Результат.Вставить("Quantity");
		
		Результат.Вставить("Price");
		Результат.Вставить("TotalWithVatExcluded");
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		
		Результат.Вставить("AdditionalInfo");
		
		Результат.Вставить("СсылкаНаЕИ");
		
		Возврат Результат; 
		
	КонецФункции

	&НаКлиенте
	Функция Новый_InvoiceItem() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/InvoiceItem.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Product");
		Результат.Вставить("UnitCode");
		Результат.Вставить("Quantity");
		
		Результат.Вставить("Price");
		Результат.Вставить("TotalWithVatExcluded");
		Результат.Вставить("TaxRate");
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		Результат.Вставить("Excise");
		
		Результат.Вставить("CountriesOfOrigin");
		Результат.Вставить("CustomsDeclarationNumbers");
		
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Результат.Вставить("ЕдиницаИзмеренияСсылка");
		Результат.Вставить("СтранаПроисхожденияСсылка");
		Результат.Вставить("СсылкаНаЕИ");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_InvoiceCorrectionItem() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/InvoiceCorrectionItem.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Product");
		
		Результат.Вставить("OriginalValues" , Новый_InvoiceItemFields());
		Результат.Вставить("CorrectedValues", Новый_InvoiceItemFields());
		
		Результат.Вставить("AmountsInc", Новый_AmountsDiff());
		Результат.Вставить("AmountsDec", Новый_AmountsDiff());
		
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_InvoiceTable() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/InvoiceTable.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Items", Новый Массив);
		
		Результат.Вставить("TotalNet");
		
		Результат.Вставить("TotalWithVatExcluded");
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_ExtendedInvoiceItem() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/ExtendedInvoiceItem.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Product");
		Результат.Вставить("VendorCode");
		
		Результат.Вставить("ItemMark");
		Результат.Вставить("AdditionalProperty");
		
		Результат.Вставить("UnitCode");
		Результат.Вставить("UnitName");
		
		Результат.Вставить("ToRelease");
		Результат.Вставить("Quantity");
		
		Результат.Вставить("Price");
		Результат.Вставить("SubtotalWithVatExcluded");
		Результат.Вставить("TaxRate");
		Результат.Вставить("Vat");
		Результат.Вставить("Subtotal");
		Результат.Вставить("Excise");
		
		Результат.Вставить("AccountDebit");
		Результат.Вставить("AccountCredit");
		
		Результат.Вставить("CustomDeclarations"		, Новый Массив);
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Результат.Вставить("ЕдиницаИзмеренияСсылка");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_InvoiceCorrectionTable() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/InvoiceCorrectionTable.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Items", Новый Массив);
		
		Результат.Вставить("TotalsInc", Новый_InvoiceTotals());
		Результат.Вставить("TotalsDec", Новый_InvoiceTotals());
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_ExtendedInvoiceCorrectionItem() Экспорт
		
		// http://1c-docs.diadoc.ru/ru/latest/ExtendedInvoiceCorrectionItem.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Product");
		
		Результат.Вставить("ItemAccountDebit");
		Результат.Вставить("ItemAccountCredit");
		
		Результат.Вставить("OriginalValues" , Новый_InvoiceItemFields());
		Результат.Вставить("CorrectedValues", Новый_InvoiceItemFields());
		
		Результат.Вставить("AmountsInc", Новый_AmountsDiff());
		Результат.Вставить("AmountsDec", Новый_AmountsDiff());
		
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_TovTorgTable() Экспорт
		
		// http://1c-docs.diadoc.ru/ru/latest/TovTorgTable.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Items", Новый Массив);
		
		Результат.Вставить("TotalQuantity");
		Результат.Вставить("TotalGross");
		Результат.Вставить("TotalNet");
		
		Результат.Вставить("TotalWithVatExcluded");
		Результат.Вставить("TotalVat");
		Результат.Вставить("Total");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_TovTorgItem() Экспорт
		
		// http://1c-docs.diadoc.ru/ru/latest/TovTorgItem.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Product");
		Результат.Вставить("Feature");
		Результат.Вставить("Sort");
		Результат.Вставить("VendorCode");
		Результат.Вставить("ProductCode");
		
		Результат.Вставить("UnitName");
		Результат.Вставить("Unit");
		
		Результат.Вставить("PackageType");
		Результат.Вставить("QuantityInPack");
		
		Результат.Вставить("ItemToRelease");
		Результат.Вставить("Quantity");
		Результат.Вставить("Gross");
		Результат.Вставить("Net");
		
		Результат.Вставить("Price");
		Результат.Вставить("SubtotalWithVatExcluded");
		Результат.Вставить("TaxRate");
		Результат.Вставить("Vat");
		Результат.Вставить("Subtotal");
		
		Результат.Вставить("ItemAccountDebit");
		Результат.Вставить("ItemAccountCredit");
		
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Результат.Вставить("СсылкаНаЕИ");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_Act552WorkDescription() Экспорт
		
		// http://1c-docs.diadoc.ru/ru/latest/Act552WorkDescription.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Items", Новый Массив);
		
		Результат.Вставить("StartingDate");
		Результат.Вставить("CompletionDate");
		
		Результат.Вставить("TotalWithVatExcluded");
		Результат.Вставить("TotalVat");
		Результат.Вставить("Total");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_Act552WorkItem() Экспорт
		
		// http://1c-docs.diadoc.ru/ru/latest/Act552WorkItem.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Name");
		Результат.Вставить("Description");
		
		Результат.Вставить("UnitCode");
		Результат.Вставить("UnitName");
		
		Результат.Вставить("Quantity");
		
		Результат.Вставить("Price");
		Результат.Вставить("SubtotalWithVatExcluded");
		Результат.Вставить("TaxRate");
		Результат.Вставить("Vat");
		Результат.Вставить("Subtotal");
		
		Результат.Вставить("ItemAccountDebit");
		Результат.Вставить("ItemAccountCredit");
		
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Результат.Вставить("СсылкаНаЕИ");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_CustomDeclaration() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/CustomDeclaration.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("CountryCode");
		Результат.Вставить("DeclarationNumber");
		
		Результат.Вставить("СтранаПроисхожденияСсылка");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_InvoiceItemFields() Экспорт 
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/InvoiceItemFields.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("UnitCode");
		Результат.Вставить("Quantity");
		
		Результат.Вставить("Price");
		Результат.Вставить("TotalWithVatExcluded");
		Результат.Вставить("TaxRate");
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		Результат.Вставить("Excise");
		
		Результат.Вставить("ЕдиницаИзмеренияСсылка");
		
		Возврат Результат;
		
	КонецФункции	
	
//} ТЧ

//{ подвал

	&НаКлиенте
	Функция Новый_TransferInfo() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/TransferInfo.html
		
	 	Результат = Новый Структура;
		
		Результат.Вставить("OperationInfo");
		Результат.Вставить("OperationType");
		
		Результат.Вставить("TransferTextInfo");
		Результат.Вставить("TransferDate");
		
		Результат.Вставить("CreatedThingInfo");
		Результат.Вставить("CreatedThingTransferDate");
		
		Результат.Вставить("Waybills"	  , Новый Массив);
		Результат.Вставить("TransferBases", Новый Массив);
		
		Результат.Вставить("Carrier", Новый_ExtendedOrganizationInfo());
		
		Результат.Вставить("Employee"	, Новый_Employee());
		Результат.Вставить("OtherIssuer", Новый_OtherIssuer());
		
		Результат.Вставить("AdditionalInfoId", Новый_AdditionalInfoId());
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_TovTorgTransferInfo() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/TovTorgTransferInfo.html
		
	 	Результат = Новый Структура;
		
		Результат.Вставить("OperationInfo");
		Результат.Вставить("TransferDate");
		Результат.Вставить("Attachment");
		
		Результат.Вставить("Employee"	, Новый_Employee());
		Результат.Вставить("OtherIssuer", Новый_OtherIssuer());
		
		Результат.Вставить("Waybills", Новый Массив);
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_Act552TransferInfo() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Act552TransferInfo.html
		
	 	Результат = Новый Структура;
		
		Результат.Вставить("OperationInfo");
		Результат.Вставить("TransferDate");
		
		Результат.Вставить("CreatedThingInfo");
		Результат.Вставить("CreatedThingTransferDate");
		
		Результат.Вставить("StructedAdditionalInfos", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_TransferBase() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/TransferBase.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentName");
		Результат.Вставить("DocumentDate");
		Результат.Вставить("DocumentNumber");
		
		Результат.Вставить("DocumentInfo");
		
		Возврат Результат; 
		
	КонецФункции

	&НаКлиенте
	Функция Новый_Waybill() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Waybill.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentDate");
		Результат.Вставить("DocumentNumber");
		
		Возврат Результат; 
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_EventContent() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/EventContent.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("OperationContent");
		Результат.Вставить("NotificationDate");
		Результат.Вставить("TransferDocDetails");
		Результат.Вставить("CostChangeInfo");
		
		Результат.Вставить("CorrectionBases", Новый Массив);
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_CorrectionBase() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/CorrectionBase.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("BaseDocumentName");
		Результат.Вставить("BaseDocumentNumber");
		Результат.Вставить("BaseDocumentDate");
		
		Результат.Вставить("AdditionalInfo");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_GroundInfo() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/GroundInfo.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Name");
		Результат.Вставить("Number");
		Результат.Вставить("Date");
		Результат.Вставить("Info");
		
		Возврат Результат;
		
	КонецФункции
	
//} подвал

//{ ответственные, подписи

	&НаКлиенте
	Функция Новый_Organization() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Organization.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Id");
		Результат.Вставить("Name");
		Результат.Вставить("Inn");
		Результат.Вставить("Kpp");
		Результат.Вставить("AuthenticateType");
		Результат.Вставить("Login");
		Результат.Вставить("FnsParticipantId");
		Результат.Вставить("FnsRegistrationDate");
		Результат.Вставить("IsTest");
		Результат.Вставить("IsPilot");
		Результат.Вставить("EncryptedDocumentsAllowed");
		
		Результат.Вставить("Certificate", Новый_PersonalCertificate());
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_Signer() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Signer.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Surname");
		Результат.Вставить("FirstName");
		Результат.Вставить("Patronymic");
		
		Результат.Вставить("JobTitle");
		
		Результат.Вставить("Inn");
		
		Результат.Вставить("IsSoleProprietor");
		Результат.Вставить("SoleProprietorRegistrationCertificate");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_ExtendedSigner() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/ExtendedSigner.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("BoxId");
		Результат.Вставить("CertificateThumbprint");
		
		Результат.Вставить("SignerDetails", Новый_ExtendedSignerDetails());
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_ExtendedSignerDetails() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/ExtendedSignerDetails.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Status");
		Результат.Вставить("SignerType");
		Результат.Вставить("SignerInfo");
		
		Результат.Вставить("Surname");
		Результат.Вставить("FirstName");
		Результат.Вставить("Patronymic");
		
		Результат.Вставить("JobTitle");
		
		Результат.Вставить("Inn");
		Результат.Вставить("OrganizationName");
		Результат.Вставить("RegistrationCertificate");
		
		Результат.Вставить("Powers");
		Результат.Вставить("PowersBase");
		Результат.Вставить("OrganizationPowersBase");
		
		Возврат Результат; 
		
	КонецФункции

	&НаКлиенте
	Функция Новый_Signature() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Signature.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("SignDate");
		Результат.Вставить("SignatureAuthenticityDate");
		
		Результат.Вставить("Certificate", Новый_PersonalCertificate());
		
		Возврат Результат;
		
	КонецФункции
		
	&НаКлиенте
	Функция Новый_PersonalCertificate() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/PersonalCertificate.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Thumbprint");
		
		Результат.Вставить("BeginDate");
		Результат.Вставить("EndDate");
		
		Результат.Вставить("Name");
		Результат.Вставить("IssuerName");
		Результат.Вставить("OrganizationName");
		
		Результат.Вставить("Inn");
		Результат.Вставить("Kpp");
		
		Результат.Вставить("JobTitle");
		
		Результат.Вставить("IsQualifiedElectronicSignature");
		
		Возврат Результат;
		
	КонецФункции
		
	&НаКлиенте
	Функция Новый_ExtendedSignerDetailsToPost() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/ExtendedSignerDetailsToPost.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Status");
		Результат.Вставить("SignerType");
		Результат.Вставить("SignerInfo");
		
		Результат.Вставить("JobTitle");
		
		Результат.Вставить("Powers");
		Результат.Вставить("PowersBase");
		Результат.Вставить("OrganizationPowersBase");
		
		Результат.Вставить("RegistrationCertificate");
		
		Возврат Результат; 
		
	КонецФункции
		
	&НаКлиенте
	Функция Новый_Official() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Official.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Surname");
		Результат.Вставить("FirstName");
		Результат.Вставить("Patronymic");
		
		Результат.Вставить("JobTitle");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_Employee() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Employee.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Surname");
		Результат.Вставить("FirstName");
		Результат.Вставить("Patronymic");
		
		Результат.Вставить("EmployeePosition");
		Результат.Вставить("EmployeeBase");
		Результат.Вставить("EmployeeInfo");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_OtherIssuer() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/OtherIssuer.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Surname");
		Результат.Вставить("FirstName");
		Результат.Вставить("Patronymic");
		
		Результат.Вставить("EmployeePosition");
		Результат.Вставить("EmployeeBase");
		Результат.Вставить("EmployeeInfo");
		
		Результат.Вставить("OrganizationName");
		Результат.Вставить("OrganizationBase");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_Attorney() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Attorney.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("Date");
		Результат.Вставить("Number");
		
		Результат.Вставить("IssuerOrganizationName");
		Результат.Вставить("IssuerAdditionalInfo");
		
		Результат.Вставить("RecipientAdditionalInfo");
		
		Результат.Вставить("Issuer"	  , Новый_Official());
		Результат.Вставить("Recipient", Новый_Official());
		
		Возврат Результат;
		
	КонецФункции
	
//} ответственные, подписи

//{ итоги
	
	&НаКлиенте
	Функция Новый_Torg12Commons() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Torg12Commons.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("TotalParcelsQuantity");
		Результат.Вставить("TotalGrossQuantity");
		Результат.Вставить("TotalQuantity");
		
		Результат.Вставить("TotalParcelsQuantityInWords");
		Результат.Вставить("TotalGrossQuantityInWords");
		Результат.Вставить("TotalQuantityInWords");
		
		Результат.Вставить("TotalSum");
		Результат.Вставить("TotalSumInWords");
		
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_Torg12Totals() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Torg12Totals.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("ParcelsQuantity");
		Результат.Вставить("GrossQuantity");
		Результат.Вставить("Quantity");
		
		Результат.Вставить("TotalWithVatExcluded");
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция Новый_InvoiceTotals() Экспорт
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/Totals-InvoiceTotals.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("TotalWithVatExcluded");
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		
		Возврат Результат;
						
	КонецФункции

	&НаКлиенте
	Функция Новый_AmountsDiff() Экспорт 
		
		// http://diadocsdk-1c.readthedocs.io/ru/latest/AmountsDiff.html
		
		Результат = Новый Структура;
		
		Результат.Вставить("TotalWithVatExcluded");
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		Результат.Вставить("Excise");
		
		Возврат Результат;
		
	КонецФункции	

//} итоги

//{ НЕФОРМАЛИЗОВАННЫЕ

	&НаКлиенте
	Функция Новый_NonformalizedDocumentToSend() Экспорт
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentDate");
		Результат.Вставить("DocumentNumber");
		
		Результат.Вставить("Comment");
		
		Результат.Вставить("NeedRecipientSignature", Ложь);
		
		Возврат Результат;
	
	КонецФункции

	&НаКлиенте
	Функция Новый_ServiceDetails() Экспорт
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentDate");
		Результат.Вставить("DocumentNumber");
		
		Результат.Вставить("Comment");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_NonformalizedProforma() Экспорт
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentDate");
		Результат.Вставить("DocumentNumber");
		
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		
		Результат.Вставить("Grounds");
		Результат.Вставить("Comment");
		
		Результат.Вставить("БезНДС", Ложь);
	
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_PriceList() Экспорт
		
		Возврат Новый Структура;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_Torg12() Экспорт
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentDate");
		Результат.Вставить("DocumentNumber");
		
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		
		Результат.Вставить("Grounds");
		Результат.Вставить("Comment");
		
		Результат.Вставить("БезНДС", Ложь);
		Результат.Вставить("NeedRecipientSignature", Истина);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_AcceptanceCertificate() Экспорт
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentDate");
		Результат.Вставить("DocumentNumber");
		
		Результат.Вставить("Vat");
		Результат.Вставить("Total");
		
		Результат.Вставить("Grounds");
		Результат.Вставить("Comment");
		
		Результат.Вставить("БезНДС", Ложь);
		Результат.Вставить("NeedRecipientSignature", Истина);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Новый_Contract() Экспорт
		
		Результат = Новый Структура;
		
		Результат.Вставить("DocumentDate");
		Результат.Вставить("DocumentNumber");
		
		Результат.Вставить("ContractType");
		Результат.Вставить("ContractPrice");
		Результат.Вставить("Comment");
		
		Результат.Вставить("ЦенаНеУказана", Ложь);
		
		Возврат Результат;
		
	КонецФункции
	
//} НЕФОРМАЛИЗОВАННЫЕ

//} ИНИЦАЛИЗАЦИЯ content
////////////////////////////////////////////////////////////////////////////////

//{ ИНТЕРАКТИВНЫЕ ДЕЙСТВИЯ

	&НаКлиенте
	Функция ОтправитьНаОбработку(ЭДОбъект, ПараметрыОтправкиНаСогласование) Экспорт
		
		Если НЕ ПараметрыОтправкиНаСогласование = Неопределено Тогда
			
			Если ПустаяСтрока(ПараметрыОтправкиНаСогласование.TargetUserId) И ПустаяСтрока(ПараметрыОтправкиНаСогласование.TargetDepartmentId) Тогда
				
				ПараметрыФормы=	Новый Структура();
				ПараметрыФормы.Вставить("Заголовок", 		"Ошибка работы с контрагентом");
				ПараметрыФормы.Вставить("ОписаниеОшибки", 	"Не указан получатель документа.");
				ПараметрыФормы.Вставить("Подробности", 		"Не указан получатель документа.");
				
				МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы,);

				Возврат Ложь;
			ИначеЕсли ПараметрыОтправкиНаСогласование.ЭтоТекущийПользователь = истина Тогда
				//если пользователь передает на согласование самому себе - то просто оставляем документ в исходящих неотправленных 
			Иначе
				
				Попытка
					
					ЗаданиеНаСогласование=							ЭДОбъект.CreateResolutionRequestTask();
					ЗаданиеНаСогласование.ResolutionRequestType=	ПараметрыОтправкиНаСогласование.ResolutionRequestType;
					Если ЗначениеЗаполнено(ПараметрыОтправкиНаСогласование.TargetUserId) Тогда
						ЗаданиеНаСогласование.TargetUserId=			ПараметрыОтправкиНаСогласование.TargetUserId;
					ИначеЕсли ЗначениеЗаполнено(ПараметрыОтправкиНаСогласование.TargetDepartmentId) Тогда
						ЗаданиеНаСогласование.TargetDepartmentId=	ПараметрыОтправкиНаСогласование.TargetDepartmentId;
					КонецЕсли;
					ЗаданиеНаСогласование.Comment=					ПараметрыОтправкиНаСогласование.Комментарий;
					ЗаданиеНаСогласование.Send();
					
					Возврат Истина;
					
				Исключение
					
					ОписаниеОшибки=	ОписаниеОшибки();
					Если Найти(ОписаниеОшибки, "Resolution request author user id and target user id must differ")>0 Тогда
						ОписаниеОшибки=	"Нельзя отправить документ на согласование или подписание самому себе";
					ИначеЕсли Найти(ОписаниеОшибки, "User can not add signature request to document")>0
						ИЛИ Найти(ОписаниеОшибки, "User can not add approvement request to document")>0
						ИЛИ Найти(ОписаниеОшибки, "document that cannot be rejected")>0 Тогда
						ОписаниеОшибки=	"Документ уже отправлен на подписание или согласование";
					Иначе
						ОписаниеОшибки=	"Неизвестная ошибка обработки";
					КонецЕсли;
					
					ПараметрыФормы=	Новый Структура();
					ПараметрыФормы.Вставить("Заголовок", 		"Ошибка обработки");
					ПараметрыФормы.Вставить("ОписаниеОшибки", 	ОписаниеОшибки);
					ПараметрыФормы.Вставить("Подробности", 		ОписаниеОшибки());
					
					МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы,);
					
					Возврат Ложь;
					
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецФункции
	
	// Создает задание на подпись документа.
	//
	// Параметры:
	//  Document	 - COMОбъект - Объект, производный от Document;
	//  ПротоКонтент - Структура - (Необязательный) Данные для заполнения ответного титула.
	//  Асинхронно	 - Булево	 - Истина, если задание нужно запустить асинхронно.
	// 
	// Возвращаемое значение:
	//  AsyncResult - результат асинхронной операции, если флаг Асинхронно = Истина;
	//  Неопределено - если флаг Асинхронно = Ложь.
	//
	&НаКлиенте
	Функция ПодписатьВДиадоке(Document, ПротоКонтент = Неопределено, Асинхронно = Ложь) Экспорт
		
		Результат = Неопределено;
		
		ЭтоФормализованныйДокумент = ЭтоФормализованныйДокумент(Document.Type);
		ПроверитьСертификат(Document.OrganizationId, ЭтоФормализованныйДокумент);
	
		ReplySendTask = Document.CreateReplySendTask2("AcceptDocument");
		
		Если ПротоКонтент <> Неопределено Тогда
			
			ОтветныйТитул = СформироватьОтветныйТитулДокумента(Document, ПротоКонтент);
			
			ContentItem = ReplySendTask.ContentItems.GetItem(0);
			ContentItem.LoadContentFromBase64(Base64СтрокаИзДвоичныхДанных(ОтветныйТитул));
			
		КонецЕсли;
		
		Если Асинхронно Тогда
			Результат = ReplySendTask.SendAsync();
		Иначе
			ReplySendTask.Send();
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции

	// Создает и отправляет отказ в подписи документа.
	//
	// Параметры:
	//  Document	 - COMОбъект - Объект, производный от Document;
	//  ТекстОтказа	 - Строка - произвольный комментарий к отказу.
	//
	&НаКлиенте
	Процедура ОтказатьВПодписи(Document, ТекстОтказа = "") Экспорт
		
		ЭтоФормализованныйДокумент = ЭтоФормализованныйДокумент(Document.Type);
		ПроверитьСертификат(Document.OrganizationId, ЭтоФормализованныйДокумент);
		
		ДанныеСотрудника = МетодКлиента("Модуль_РаботаССерверомДиадок", "ДанныеКонтекстаДиадок", Document.OrganizationId, "ДанныеСотрудника");
		
		ReplySendTask = Document.CreateReplySendTask2("RejectDocument");
		
		ContentItem = ReplySendTask.ContentItems.GetItem(0);
		ContentItem.Content.Signer.Surname		 = ДанныеСотрудника.Фамилия;
		ContentItem.Content.Signer.FirstName	 = ДанныеСотрудника.Имя;
		ContentItem.Content.Signer.Patronymic	 = ДанныеСотрудника.Отчество;
		ContentItem.Content.Signer.JobTitle		 = ДанныеСотрудника.Должность;
		ContentItem.Content.Signer.Inn			 = Document.Organization.Inn;
		ContentItem.Content.Comment				 = ТекстОтказа;
		
		ReplySendTask.Send();
		
	КонецПроцедуры
	
	// Создает и отправляет запрос на уточнение докуемнта.
	//
	// Параметры:
	//  Document	 - COMОбъект - Объект, производный от Document;
	//  Комментарий	 - Строка - произвольный комментарий к запросу.
	//
	&НаКлиенте
	Процедура ЗапроситьУточнение(Document, Комментарий = "") Экспорт
		
		ЭтоФормализованныйДокумент = ЭтоФормализованныйДокумент(Document.Type);
		ПроверитьСертификат(Document.OrganizationId, ЭтоФормализованныйДокумент);
		
		ДанныеСотрудника = МетодКлиента("Модуль_РаботаССерверомДиадок", "ДанныеКонтекстаДиадок", Document.OrganizationId, "ДанныеСотрудника");
		
		ReplySendTask = Document.CreateReplySendTask2("CorrectionRequest");
		
		ContentItem = ReplySendTask.ContentItems.GetItem(0);
		ContentItem.Content.Signer.Surname		 = ДанныеСотрудника.Фамилия;
		ContentItem.Content.Signer.FirstName	 = ДанныеСотрудника.Имя;
		ContentItem.Content.Signer.Patronymic	 = ДанныеСотрудника.Отчество;
		ContentItem.Content.Signer.JobTitle		 = ДанныеСотрудника.Должность;
		ContentItem.Content.Signer.Inn			 = Document.Organization.Inn;
		ContentItem.Content.Comment				 = Комментарий;
		
		ReplySendTask.Send();
		
	КонецПроцедуры

	&НаКлиенте
	Функция ПолучитьКраткоеИмяФайла(ПолноеИмяФайла)
		
		Результат =  ПолноеИмяФайла;
		Пока Найти(Результат, "\")>0 цикл 
			Результат = Прав(Результат, СтрДлина(Результат) -  Найти(Результат, "\"));
		КонецЦикла;
		
		Возврат Результат;
		
		
	КонецФункции	
	
	&НаКлиенте
	Функция ОтправитьПринятьПриглашениеКонтрагенту(Organization, CounteragentId, ИНН, Комментарий, ИмяФайла = "", КонтрагентСсылка = Неопределено, FnsParticipantId = "") Экспорт
		
		Результат =	Ложь;
		
		Организация = BoxID_2_Организация_Форма(Organization.ID);
		
		Если ЗначениеЗаполнено(ИмяФайла) Тогда
			
			TrustConnectionRequest=					Organization.CreateAcquireCounteragentTask(ИмяФайла);
			
			TrustConnectionRequest.CounteragentBoxId= CounteragentId;
			TrustConnectionRequest.Inn  			= ИНН;
			TrustConnectionRequest.Message 			= Комментарий;
			TrustConnectionRequest.FileName 		= ПолучитьКраткоеИмяФайла(ИмяФайла);
			
			Попытка
				TrustConnectionRequest.SendAsync();
				Результат = Истина;
			Исключение
				СообщитьПользователю("Не удалось обработать контрагента. Описание ошибки: " + ОписаниеОшибки());
				Результат = Ложь;
			КонецПопытки;
			
			Если Результат = Истина Тогда
				Платформа.ПовторноеИспользованиеСброситьЗначение("Модуль_Клиент", "GetCounteragentListByStatus", Организация, "IsInvitedByMe");
			КонецЕсли;
			
		Иначе
			
			Counteragent = Organization.GetCounteragentById(CounteragentId);
			Попытка
				
				СтарыйСтатус = Counteragent.GetStatus();
				Counteragent.AcquireCounteragent(Комментарий);
				НовыйСтатус = Counteragent.GetStatus();
				Результат = Истина;
				
			Исключение
				СообщитьПользователю("Не удалось обработать контрагента. Описание ошибки: " + ОписаниеОшибки());
				Результат = Ложь;
			КонецПопытки;
			
			Если Результат = Истина Тогда
				Платформа.ПовторноеИспользованиеСброситьЗначение("Модуль_Клиент", "GetCounteragentListByStatus", Организация, СтарыйСтатус);
				Если НовыйСтатус <> СтарыйСтатус Тогда
					Платформа.ПовторноеИспользованиеСброситьЗначение("Модуль_Клиент", "GetCounteragentListByStatus", Организация, НовыйСтатус);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ КонтрагентСсылка = Неопределено Тогда
			Установить_CounteragentBoxID_для_Контрагент(КонтрагентСсылка, CounteragentId);
			МетодСервера(,"УстановитьИдентификаторЭДОДляКонтрагента", КонтрагентСсылка, FnsParticipantId);
		КонецЕсли;	
		
		Возврат Результат;
		
	КонецФункции
		
	&НаКлиенте
	Процедура СообщитьПользователю(ТекстСообщения) Экспорт
		
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ТекстСообщения;
		СообщениеПользователю.Сообщить();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ВыбратьПараметрыПолучателяПриОтправкеНаСогласование(ФормаВладелец, Организация, ResolutionRequestType, ПараметрыОбработчика = Неопределено) Экспорт
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("ResolutionRequestType", ResolutionRequestType);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыбораПолучателя", ПараметрыФормы, ФормаВладелец,
		"ОбработчикОткрытиеФормыВыбораПолучателя", ПараметрыОбработчика, ФормаВладелец);

	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПодтверждениеВыполненияОперации(ИмяОбработчика, ВладелецОбработчика, ПараметрыОбработчика= Неопределено) Экспорт
		
		ПоказатьВопрос(Новый ОписаниеОповещения(ИмяОбработчика, ВладелецОбработчика, ПараметрыОбработчика), "Подтвердите выполнение операции", РежимДиалогаВопрос.ОКОтмена, 120, КодВозвратаДиалога.Отмена);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПоказатьСостояниеОбработкиСписка(ТекстСостояния, ТекущийНомер, РазмерСписка, ОтображатьДеталиПрогресса = Истина, ПояснениеСостояния = Неопределено) Экспорт
		
		ПрогрессСостояния	 = ТекущийНомер / ?(ЗначениеЗаполнено(РазмерСписка), РазмерСписка, 1) * 100;
		ДеталиПрогресса		 = "";
		
		Если ОтображатьДеталиПрогресса Тогда
			ДеталиПрогресса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = ' (%1 из %2)'"), ТекущийНомер, РазмерСписка);
		КонецЕсли;
		
		ПоказатьСостояниеОбработки(ТекстСостояния + ДеталиПрогресса, ПрогрессСостояния, ПояснениеСостояния);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПоказатьСостояниеОбработки(ТекстСостояния, ПрогрессСостояния = Неопределено, ПояснениеСостояния = Неопределено) Экспорт
		
		КартинкаСостояния = МетодКлиента("Модуль_Клиент", "ЭДО_БиблиотекаКартинок").КартинкаЗаголовка32;
		
		Состояние(ТекстСостояния, ПрогрессСостояния, ПояснениеСостояния, КартинкаСостояния);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОповеститьПользователя(ТекстОповещения, КлючУникальностиОповещения = Неопределено) Экспорт
		
		ЗаголовокОповещения	 = Платформа.ПараметрыКлиент.СловарьWL.Заголовок;
		КартинкаОповещения	 = МетодКлиента("Модуль_Клиент", "ЭДО_БиблиотекаКартинок").КартинкаЗаголовка32;
		СистемнаяИнформация	 = Новый СистемнаяИнформация;
		
		Если СравнитьВерсии(СистемнаяИнформация.ВерсияПриложения, "8.3.10") < 0 Тогда
			Выполнить("ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения, КартинкаОповещения)");
		Иначе
			Выполнить("ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения, КартинкаОповещения, , КлючУникальностиОповещения)");
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПоказатьПростоеПредупреждение(ТекстПредупреждения) Экспорт
		
		ТаймаутПредупреждения	 = 120;
		ЗаголовокПредупреждения	 = Платформа.ПараметрыКлиент.СловарьWL.Заголовок;
		
		ПоказатьПредупреждение( , ТекстПредупреждения, ТаймаутПредупреждения, ЗаголовокПредупреждения);
		
	КонецПроцедуры
		
	// Открывает в браузере по умолчанию руководство пользователя на странице "Не появляется документ на отправку"
	&НаКлиенте
	Процедура ПоказатьИнструкцию_НетДокументовНаОтправку() Экспорт
		
		АдресаИнтернетРесурсов = АдресаИнтернетРесурсов();
				
		ПерейтиПоНавигационнойСсылке(АдресаИнтернетРесурсов.Инструкция_НетДокументовНаОтправку);
		
	КонецПроцедуры //ПоказатьИнструкцию_НетДокументовНаОтправку()
	
	// Открывает в браузере по умолчанию руководство пользователя на странице "Переотправка документов"
	&НаКлиенте
	Процедура ПоказатьИнструкцию_ПереотправкаДокументов() Экспорт
		
		АдресаИнтернетРесурсов = АдресаИнтернетРесурсов();
				
		ПерейтиПоНавигационнойСсылке(АдресаИнтернетРесурсов.Инструкция_ПереотправкаДокументов);
		
	КонецПроцедуры //ПоказатьИнструкцию_ПереотправкаДокументов()
	
//} ИНТЕРАКТИВНЫЕ ДЕЙСТВИЯ

//{ ПОЛУЧИТЬ...

	&НаСервере
	Функция ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадокСервер(МассивСсылок, OrganizationId)
		
		Результат=	Новый Массив;
		
		Для Каждого InitialDocumentId Из МассивСсылок Цикл
			СсылкаРНК = ПолучитьDocumentID_2_Документ(InitialDocumentId, OrganizationId);
			Если СсылкаРНК <> Неопределено И ТипЗнч(СсылкаРНК) <> Тип("ДокументСсылка.СчетФактураПолученный") Тогда
				Результат.Добавить(СсылкаРНК);
			КонецЕсли;
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадок(ОбъектДиадок) Экспорт
		
		InitialDocumentIds=	Новый Массив;
		Для Индекс = 0 По ОбъектДиадок.InitialDocumentIds.Count - 1 Цикл
			InitialDocumentIds.Добавить(ОбъектДиадок.InitialDocumentIds.GetItem(Индекс));
		КонецЦикла;
		
		OrganizationId=		ОбъектДиадок.Organization.Id;
		МассивСсылок=		ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадокСервер(InitialDocumentIds, OrganizationId);
		
		Возврат МассивСсылок;
		
	КонецФункции	
		
	&НаСервере
	Функция НайтиПодходящийСчетФактуруИзРНК(Знач МассивСсылокРНК, Знач ИдентификаторДокументаВДиадок, ЭтоВходящийДокумент)

		Запрос= Новый Запрос;

		ПредставлениеСФ = МетодСервера(, "ПредставлениеДокументов").СчетФактураВыданный;

		Если (ЭтоВходящийДокумент И НЕ Метаданные.Документы.СчетФактураПолученный.ТабличныеЧасти.Найти("ДокументыОснования") = Неопределено) 
		  ИЛИ (НЕ ЭтоВходящийДокумент И НЕ Метаданные.Документы[ПредставлениеСФ].ТабличныеЧасти.Найти("ДокументыОснования") = Неопределено) Тогда

		  Запрос.Текст=	
				"ВЫБРАТЬ
				|	СчетФактура.Ссылка,
				|	ВЫБОР
				|		КОГДА УжеСопоставленные.Значение ЕСТЬ NULL 
				|				ИЛИ УжеСопоставленные.Значение = """"
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ИСТИНА
				|	КОНЕЦ КАК УжеСопоставлен
				|ИЗ
				|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактура
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &Свойство) КАК УжеСопоставленные
				|		ПО СчетФактура.Ссылка = УжеСопоставленные.Объект
				|ГДЕ
				|	СчетФактура.ДокументОснование В (&МассивСсылокРНК)";
			
		Иначе
			
			Запрос.Текст=	
				"ВЫБРАТЬ
				|	СчетФактура.Ссылка,
				|	ВЫБОР
				|		КОГДА УжеСопоставленные.Значение ЕСТЬ NULL 
				|				ИЛИ УжеСопоставленные.Значение = """"
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ИСТИНА
				|	КОНЕЦ КАК УжеСопоставлен
				|ИЗ
				|	Документ.СчетФактураПолученный КАК СчетФактура
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &Свойство) КАК УжеСопоставленные
				|		ПО СчетФактура.Ссылка = УжеСопоставленные.Объект
				|ГДЕ
				|	СчетФактура.ДокументОснование В (&МассивСсылокРНК)";
			
		КонецЕсли;
		
		Запрос.УстановитьПараметр("МассивСсылокРНК", МассивСсылокРНК);
		Запрос.УстановитьПараметр("Свойство", ИдентификаторДокументаВДиадок);
		
		Если НЕ ЭтоВходящийДокумент Тогда
			Запрос.Текст= СтрЗаменить(Запрос.Текст, "СчетФактураПолученный", ПредставлениеСФ);
		КонецЕсли;

		РезультатЗапроса=	Запрос.Выполнить();
		Выборка=			РезультатЗапроса.Выбрать();
		
		Возврат Выборка;
		
	КонецФункции

	&НаСервере
	Функция ПолучитьПодходящуюСФ(МассивСсылокРНК, ЭтоВходящийДокумент = Истина) Экспорт

		ИдентификаторДокументаВДиадок=	ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(МетодСервера(,"ИдентификаторСвойстваИдентификаторДокументаВДиадок"));
		ВыборкаПодходящихСФ=	НайтиПодходящийСчетФактуруИзРНК(МассивСсылокРНК, ИдентификаторДокументаВДиадок, ЭтоВходящийДокумент);

		Если ВыборкаПодходящихСФ.Количество() > 0 Тогда
			ВыборкаПодходящихСФ.Следующий();
			Возврат ВыборкаПодходящихСФ.Ссылка;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	КонецФункции

	&НаКлиенте
	Функция ПолучитьСвязанныйInvoice(Document) Экспорт
		
		Результат = Неопределено;
		
		Organization		   = Document.Organization;
		InitialDocumentIds	   = Document.InitialDocumentIds;
		SubordinateDocumentIds = Document.SubordinateDocumentIds;
		
		TimeStamp = '00010101';
		
		ВГраница = InitialDocumentIds.Count - 1;
		Для Ц = 0 По ВГраница Цикл
			
			Попытка
				InitialDocument = Organization.GetDocumentById(InitialDocumentIds.GetItem(Ц));
				// берем последнюю по времени СФ
				Если InitialDocument.Type = "Invoice"
					И TimeStamp < InitialDocument.TimeStamp Тогда
					TimeStamp = InitialDocument.TimeStamp; 
					Результат = InitialDocument; 
				КонецЕсли;
			Исключение				
				//Не получить документ по ID можем по разным причинам: нет доступа в подразделение документа, либо документ еще на подписи/согласовании у отправителя.
				//Определить тип недоступного документа в таких ситуациях мы не можем. 
				//Поэтому вызывать исключение в таких ситуациях не всегда правильно.
				//Будем исходить допущения: если нет доступа к документу, то эти данные для нас не значимы в контексте получения ГТД из СФ. 				
			КонецПопытки;			
			
		КонецЦикла;
		
		ВГраница = SubordinateDocumentIds.Count - 1;
		Для Ц = 0 По ВГраница Цикл
			
			Попытка
				SubordinateDocument = Organization.GetDocumentById(SubordinateDocumentIds.GetItem(Ц));
				// берем последнюю по времени СФ
				Если SubordinateDocument.Type = "Invoice"
					И TimeStamp < SubordinateDocument.TimeStamp Тогда
					TimeStamp = SubordinateDocument.TimeStamp; 
					Результат = SubordinateDocument; 
				КонецЕсли;
			Исключение
				//Не получить документ по ID можем по разным причинам: нет доступа в подразделение документа, либо документ еще на подписи/согласовании у отправителя.
				//Определить тип недоступного документа в таких ситуациях мы не можем. 
				//Поэтому вызывать исключение в таких ситуациях не всегда правильно.
				//Будем исходить допущения: если нет доступа к документу, то эти данные для нас не значимы в контексте получения ГТД из СФ.
			КонецПопытки;
			
		КонецЦикла;
		
		Если Результат = Неопределено Тогда
			
			// Пробуем получить счет-фактуру из сообщения, при условии что он там единственный.
			
			Попытка
				PackageDocuments = Document.GetDocumentPackage().Documents;
			Исключение КонецПопытки;
			
			Если PackageDocuments <> Неопределено Тогда
				
				ВГраница = PackageDocuments.Count - 1;
				Для Ц = 0 По ВГраница Цикл
				
					DocPackage = PackageDocuments.GetItem(Ц);
					
					Если DocPackage.Type = "Invoice" Тогда
						
						Если Результат = Неопределено Тогда
							Результат = DocPackage;
						Иначе
							// Счетов-фактур в сообщении больше одного.
							Результат = Неопределено;
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла; 
			КонецЕсли; 
		КонецЕсли; 
		
		Возврат Результат;		
		
	КонецФункции

	&НаКлиенте
	Функция ПолучитьМассивТиповДокументов(ФильтрПоТипамДокументов, ВидТипаДокумента)
		
		//ВидТипаДокумента = 0  все виды 	
		//ВидТипаДокумента = 1  документооборот СФ
		//ВидТипаДокумента = 2  строго двусторонний документ
		//ВидТипаДокумента = 3  строго односторонний документ
		//ВидТипаДокумента = 4  односторонний или двусторонний документ
		
		МассивТиповДокументов = Новый Массив;
		
		Если ФильтрПоТипамДокументов= "" И ВидТипаДокумента=0 Тогда
			МассивТиповДокументов.Добавить("Any");
			Возврат МассивТиповДокументов;			
		КонецЕсли;
		
		Если ФильтрПотипамДокументов = "" ИЛИ ФильтрПотипамДокументов = "1" Тогда // Накладные
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
				МассивТиповДокументов.Добавить("NonformalizedTorg12");
				МассивТиповДокументов.Добавить("NonformalizedAcceptanceCertificate");
				МассивТиповДокументов.Добавить("XmlTorg12");
				МассивТиповДокументов.Добавить("XmlAcceptanceCertificate");
				МассивТиповДокументов.Добавить("UniversalTransferDocument");
				МассивТиповДокументов.Добавить("UniversalCorrectionDocument");
				МассивТиповДокументов.Добавить("UniversalTransferDocumentRevision");
			КонецЕсли;	
		ИначеЕсли ФильтрПотипамДокументов = "11" Тогда // только Торг-12
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
				МассивТиповДокументов.Добавить("NonformalizedTorg12");
				МассивТиповДокументов.Добавить("XmlTorg12");
				МассивТиповДокументов.Добавить("UniversalTransferDocument");
				МассивТиповДокументов.Добавить("UniversalTransferDocumentRevision");
			КонецЕсли;	
		ИначеЕсли ФильтрПотипамДокументов = "12" Тогда // только Акт
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
				МассивТиповДокументов.Добавить("NonformalizedAcceptanceCertificate");
				МассивТиповДокументов.Добавить("XmlAcceptanceCertificate");
			КонецЕсли;
		ИначеЕсли  ФильтрПотипамДокументов = "13" Тогда // УПД (ПД)
			Если ВидТипаДокумента = 0 или ВидТипаДокумента = 2 Тогда
				МассивТиповДокументов.Добавить("UniversalTransferDocument");
			КонецЕсли;
		ИначеЕсли  ФильтрПотипамДокументов = "14" Тогда // УКД (ДИС)
			Если ВидТипаДокумента = 0 или ВидТипаДокумента = 2 Тогда
				МассивТиповДокументов.Добавить("UniversalCorrectionDocument");
			КонецЕсли;
		ИначеЕсли  ФильтрПотипамДокументов = "15" Тогда // иУПД (ДОП)
			Если ВидТипаДокумента = 0 или ВидТипаДокумента = 2 Тогда
				МассивТиповДокументов.Добавить("UniversalTransferDocumentRevision");
			КонецЕсли;
		КонецЕсли;		
		
		Если ФильтрПотипамДокументов = "" ИЛИ ФильтрПотипамДокументов = "2" Тогда // Счет-фактуры
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=1 Тогда
				МассивТиповДокументов.Добавить("AnyInvoiceDocumentType");
				МассивТиповДокументов.Добавить("UniversalTransferDocument");
				МассивТиповДокументов.Добавить("UniversalCorrectionDocument");
				МассивТиповДокументов.Добавить("UniversalTransferDocumentRevision");
	   		КонецЕсли;	   
	   	ИначеЕсли ФильтрПотипамДокументов = "21" Тогда // только Счет-фактура
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=1 Тогда
	    		МассивТиповДокументов.Добавить("Invoice");
			КонецЕсли;	   
	   	ИначеЕсли ФильтрПотипамДокументов = "22" Тогда // только ИСФ
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=1 Тогда
				МассивТиповДокументов.Добавить("InvoiceRevision");
	       	КонецЕсли; 	   
	   	ИначеЕсли ФильтрПотипамДокументов = "23" Тогда // только КСФ и ИКСФ
	   		Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=1 Тогда
	    		МассивТиповДокументов.Добавить("InvoiceCorrection");
	    	   	МассивТиповДокументов.Добавить("InvoiceCorrectionRevision");
			КонецЕсли; 
	    ИначеЕсли ФильтрПотипамДокументов = "24" Тогда // УПД (СФ)
	   	   Если ВидТипаДокумента = 0 или ВидТипаДокумента = 1 Тогда
			   МассивТиповДокументов.Добавить("UniversalTransferDocument");
		   КонецЕсли;
	    ИначеЕсли ФильтрПотипамДокументов = "25" Тогда // УКД (КСЧФ)
	   	   Если ВидТипаДокумента = 0 или ВидТипаДокумента = 1 Тогда
			   МассивТиповДокументов.Добавить("UniversalCorrectionDocument");
		   КонецЕсли;
		ИначеЕсли ФильтрПотипамДокументов = "26" Тогда // иУПД (КСЧФ)
	   	   Если ВидТипаДокумента = 0 или ВидТипаДокумента = 1 Тогда
			   МассивТиповДокументов.Добавить("UniversalTransferDocumentRevision");
		   КонецЕсли;   
	   	КонецЕсли;
	   
	   	Если ФильтрПотипамДокументов = "" ИЛИ ФильтрПотипамДокументов = "3" Тогда // Счет на оплату
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=3 Тогда
	    		МассивТиповДокументов.Добавить("NonformalizedProforma");
	    	КонецЕсли;	
	   	КонецЕсли;
	   
	   	Если ФильтрПотипамДокументов = "" ИЛИ ФильтрПотипамДокументов = "4" Тогда 	// Неформализованные документы
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда 			//строго двусторонний док
	    		МассивТиповДокументов.Добавить("ReconciliationAct"); 		//акт сверки
	    		МассивТиповДокументов.Добавить("Contract"); 				//договор
	    		МассивТиповДокументов.Добавить("TrustConnectionRequest");	//приглашение к ЭДО
	    		МассивТиповДокументов.Добавить("PriceList"); 				//ценовой лист
	    	КонецЕсли;	
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=3 Тогда 		//строго односторонний док
	    		МассивТиповДокументов.Добавить("ServiceDetails"); 		//детализация
	    	КонецЕсли;	
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=4 Тогда 		//как односторонний так и двусторонний док
	    		МассивТиповДокументов.Добавить("PriceListAgreement"); 	//протокол согласования цены
	    		МассивТиповДокументов.Добавить("CertificateRegistry"); 	//реестр сертификатов
	    		МассивТиповДокументов.Добавить("Nonformalized");  		//прочее
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "41" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
	    		МассивТиповДокументов.Добавить("ReconciliationAct");
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "42" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=3 Тогда
	    		МассивТиповДокументов.Добавить("ServiceDetails");
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "43" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
	    		МассивТиповДокументов.Добавить("Contract"); 
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "44" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=4 Тогда
	    		МассивТиповДокументов.Добавить("PriceListAgreement");
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "45" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
	    		МассивТиповДокументов.Добавить("TrustConnectionRequest");
	    	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "46" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=4 Тогда
	    		МассивТиповДокументов.Добавить("CertificateRegistry");
	    	КонецЕсли;	
	   //ИначеЕсли ФильтрПотипамДокументов = "47" Тогда
	   // 	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=2 Тогда
	   // 		МассивТиповДокументов.Добавить("PriceList"); 
	   // 	КонецЕсли;	
	    ИначеЕсли ФильтрПотипамДокументов = "47" Тогда 
	    	Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=4 Тогда
	    		МассивТиповДокументов.Добавить("Nonformalized");
			КонецЕсли;
		ИначеЕсли ФильтрПотипамДокументов = "48" Тогда
			Если ВидТипаДокумента=0 ИЛИ ВидТипаДокумента=4 Тогда
				МассивТиповДокументов.Добавить("SupplementaryAgreement");
			КонецЕсли;	
		ИначеЕсли ФильтрПотипамДокументов = "5" Тогда
			Если ВидТипаДокумента = 0 ИЛИ ВидТипаДокумента = 1 Тогда
				МассивТиповДокументов.Добавить("UniversalTransferDocument");
			КонецЕсли;
		ИначеЕсли ФильтрПотипамДокументов = "6" Тогда
			Если ВидТипаДокумента = 0 ИЛИ ВидТипаДокумента = 1 Тогда
				МассивТиповДокументов.Добавить("UniversalCorrectionDocument");
			КонецЕсли;
		ИначеЕсли ФильтрПотипамДокументов = "7" Тогда
			Если ВидТипаДокумента = 0 ИЛИ ВидТипаДокумента = 1 Тогда
				МассивТиповДокументов.Добавить("UniversalTransferDocumentRevision");
			КонецЕсли;	
		КонецЕсли;

		Возврат МассивТиповДокументов;
		
	КонецФункции
	
	&НаСервере
	Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита)
		
		Возврат МетодСервера(, "ЗначениеРеквизитаОбъекта", Ссылка, ИмяРеквизита);
		
	КонецФункции
	
	&НаСервере
	Функция ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты)
		
		Возврат МетодСервера(, "ЗначенияРеквизитовОбъекта", Ссылка, Реквизиты);
		
	КонецФункции
	
	&НаСервере
	Функция ЗначениеРеквизитаОбъектов(МассивСсылок, ИмяРеквизита)
		
		Возврат МетодСервера(, "ЗначениеРеквизитаОбъектов", МассивСсылок, ИмяРеквизита);
		
	КонецФункции
	
	&НаСервере
	Функция ЗначенияРеквизитовОбъектов(МассивСсылок, ИменаРеквизитов)
		
		Возврат МетодСервера(, "ЗначенияРеквизитовОбъектов", МассивСсылок, ИменаРеквизитов);
		
	КонецФункции
	
	&НаСервереБезКонтекста
	Функция ПолучитьЗначениеРеквизитаОбъекта(Объект, Реквизит)
		
		Возврат Объект[Реквизит];
		
	КонецФункции
	
	&НаКлиенте
	Функция ДокументНеСогласованРанее(Document) Экспорт
		
		Если Document.Resolutions = Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
		
				
		Для Ц=0 по Document.Resolutions.Count-1 Цикл
			
			ItemResolution= Document.Resolutions.GetItem(Ц);
			
			Если ItemResolution.ResolutionType = "ResolutionApprove" Тогда
				Возврат Ложь;
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Истина;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПроверитьРазмерВыбранныхФайлов(МассивАдресовФайлов, МассивБольшиеФайлы) Экспорт
		
		Индекс= 0;
		
		Пока Индекс <= МассивАдресовФайлов.ВГраница() Цикл
			
			ЭлементМассива= МассивАдресовФайлов[Индекс];
						
			ФайлДанных = Новый Файл(ЭлементМассива);
			Если ФайлДанных.Размер() > (5*1024*1024) Тогда
				МассивБольшиеФайлы.Добавить(ФайлДанных);
				МассивАдресовФайлов.Удалить(Индекс);
			Иначе
				Индекс= Индекс + 1;
			КонецЕсли;
				
		КонецЦикла;
						
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработатьМассивБольшихФайлов(МассивБольшиеФайлы, ФормаИнициатор) Экспорт
		
		СтрПредупреждения= "Невозможно добавить следующие файлы:";
		Для каждого БольшойФайл Из МассивБольшиеФайлы Цикл
			СтрПредупреждения = СтрПредупреждения + "
			|- " + БольшойФайл.Имя;
		КонецЦикла;
				
		ПараметрыФормы= Новый Структура();
		ПараметрыФормы.Вставить("Заголовок", 		"Не добавленные файлы");
		ПараметрыФормы.Вставить("ОписаниеОшибки", 	"Размер отправляемого файла не должен превышать 5Мб. См. ""Подробно"".");
		ПараметрыФормы.Вставить("Подробности", 		СтрПредупреждения);
		МетодКлиента(, "ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ФормаИнициатор);
						
	КонецПроцедуры
	
	&НаКлиенте
	Процедура Таймаут(ДлительностьВМилиСекундах) Экспорт
		
		Попытка
			WshShell = Новый COMОбъект("WScript.Shell");
			WshShell.Run("ping -n 1 -w "+Формат(ДлительностьВМилиСекундах, "ЧГ=0")+" 127.255.255.255", 0, -1);
		Исключение КонецПопытки;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ДействиеПриОбновленииМодуляФайловыйВариант(СтруктураРасположенияМодуля) Экспорт
		
		ИмяФайлаМодуля		= СтруктураРасположенияМодуля.Путь;
		РасположениеМодуля	= СтруктураРасположенияМодуля.Место;
		
		Действие = "ОставитьЗаявку";
		
		Если ЗначениеЗаполнено(ИмяФайлаМодуля) Тогда
			
			Попытка
				ФайлРезультата = ЗапросНаПолучениеФайла("https://diadoc-api.kontur.ru/1c-addin/versionDD.xml");
				Если ФайлРезультата = Неопределено Тогда 
					Возврат Действие;
				КонецЕсли;	
			Исключение
				МетодСервера(,"ОбработатьОшибкуНаСервере", "ОшибкаПолученияФайла", ОписаниеОшибки());
				Возврат Действие;
			КонецПопытки;
			
			ФайлСтарыхВерсий = Новый ЧтениеXML;
			ФайлСтарыхВерсий.ОткрытьФайл(ФайлРезультата);
			
			ТекущаяВерсияМодуля = МетодСервера(,"ВерсияОбработкиБезНомераСборки");
			ТекущаяВерсияМодуля = СтрЗаменить(ТекущаяВерсияМодуля, ".", "_");
			
			ЭтоСтарыйРелиз	= Ложь;
			
			Пока ФайлСтарыхВерсий.Прочитать() Цикл
				
				Если ФайлСтарыхВерсий.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
					
					//обработка блока старых версий
					Если ФайлСтарыхВерсий.Имя = "Old" Тогда
						ЭтоСтарыйРелиз = Истина;
					ИначеЕсли ФайлСтарыхВерсий.Имя = "UF" И ЭтоСтарыйРелиз Тогда
						
						Если ФайлСтарыхВерсий.ПолучитьАтрибут("version") = ТекущаяВерсияМодуля Тогда
							
							ХэшИсходный	= ВРег(ФайлСтарыхВерсий.ПолучитьАтрибут("md5"));
							ХэшМодуля	= MD5Файл(ИмяФайлаМодуля);
							
							Если ХэшИсходный = ХэшМодуля Тогда
								Действие = "Скачать";
							КонецЕсли;
							
							Прервать;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;	
			
			ФайлСтарыхВерсий.Закрыть();
			УдалитьФайлы(ФайлРезультата);
			
			Если РасположениеМодуля = "ВСправочнике" Тогда
				УдалитьФайлы(ИмяФайлаМодуля);
			КонецЕсли;	
			
		КонецЕсли;
		
		Возврат Действие;
		
	КонецФункции
	
	&НаКлиенте
	//расчет хэша файла
	Функция MD5Файл(ИмяФайла)
		
		Попытка
			Компонента = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocInvoiceAPI;
			Хэш = Компонента.HashFile("MD5", ИмяФайла);
		Исключение
			Хэш = 0;
		КонецПопытки;
		
		Возврат ВРег(Хэш);
		
	КонецФункции
	
	&НаКлиенте
	Процедура СохранитьФайл(ИмяФайлаПоУмолчанию, ИмяВременногоФайла, ЗаголовокДиалога = "", ФильтрДиалога = "") Экспорт
		
		Каталог = "";
		Режим 	= РежимДиалогаВыбораФайла.Сохранение;
		
		Если НЕ ЗначениеЗаполнено(ЗаголовокДиалога) Тогда
			ЗаголовокДиалога = НСтр("ru = 'Сохранение файла'");
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ФильтрДиалога) Тогда
			ФильтрДиалога = НСтр("ru = 'Все файлы(*.*)|*.*'");
		КонецЕсли;
		
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
		
		ДиалогОткрытияФайла.ПолноеИмяФайла 		= ИмяФайлаПоУмолчанию;
		ДиалогОткрытияФайла.Фильтр 				= ФильтрДиалога;
		ДиалогОткрытияФайла.МножественныйВыбор 	= Ложь;
		ДиалогОткрытияФайла.Заголовок 			= ЗаголовокДиалога;
		
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			
			ИмяКаталога 	= ДиалогОткрытияФайла.Каталог;
			ПолноеИмяФайла 	= ДиалогОткрытияФайла.ПолноеИмяФайла;
			
			СоздатьКаталог(ИмяКаталога);
			
			//На случай, если пользователь при сохранении изменил имя файла, которое задаем по умолчанию 
			НовоеИмяФайла = Сред(ПолноеИмяФайла, СтрДлина(ИмяКаталога) + 1, СтрДлина(ПолноеИмяФайла) - СтрДлина(ИмяКаталога));
									
			ДвоичныеДанныеОбновления = Новый ДвоичныеДанные(ИмяВременногоФайла);
			УдалитьФайлы(ИмяВременногоФайла);
		
			ДвоичныеДанныеОбновления.Записать(ПолноеИмяФайла);
			
			ОповеститьПользователя("Файл """ + НовоеИмяФайла + """ сохранен");
			
		КонецЕсли;
		
	КонецПроцедуры
	
////////////////////////////////////////////////////////////////////////////////
//{ ТЕХНИЧЕСКАЯ ПОДДЕРЖКА

	// Открывает браузер IE со страницей Контур.Диагностика
	&НаКлиенте
	Процедура ПровестиКонтурДиагностику() Экспорт
		
		СтраницаДиагностики = АдресаИнтернетРесурсов().СтраницаДиагностики;
		
		ОткрытьХостВIE(СтраницаДиагностики);
		
	КонецПроцедуры //ПровестиКонтурДиагностику()
	
	&НаКлиенте
	Процедура ОткрытьХостВIE(Хост) Экспорт
		
		Попытка
			
			IE = Новый COMОбъект("InternetExplorer.Application"); 
						
		Исключение
						
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка при открытии приложения Internet Explorer
											| по причине:
											| %1'"),
										ОписаниеОшибки());
									
			СообщитьПользователю(ТекстСообщения);
			
			Возврат;
			
		КонецПопытки;
		
		IE.Visible = True;
		IE.Navigate2(Хост);
		
	КонецПроцедуры
	
	// Формирует email в адрес технической поддержки Диадок. Письмо обязательно содержит технические данные
	// см. метод "ВывестиТехническуюИнформациюВТекстовыйДокумент" и опционально данные об организации
	// см. метод "ВывестиИнформациюОбОрганизацииВТекстовыйДокумент".
	//
	// Параметры:
	//	Организация	- СправочникСсылка, Неопределено	- в случае Неопределено данные по организации необходимо заполнить самостоятельно
	&НаКлиенте
	Процедура НаписатьПисьмоВТехПоддержку(Организация = Неопределено) Экспорт
		
		ИнформацияДляТехПоддержки = Новый ТекстовыйДокумент;
		
		ВывестиШаблонОписаниеПроблемыВТекстовыйДокумент(ИнформацияДляТехПоддержки);
		
		Если Организация = Неопределено Тогда
			
			ВывестиШаблонНеобходимоЗаполнитьВТекстовыйДокумент(ИнформацияДляТехПоддержки);
			
		КонецЕсли;
		
		ДобавитьВТекстовыйДокументИнформациюДляТехПоддержки(ИнформацияДляТехПоддержки, Организация, Истина);
		
		ПереводСтроки 	= "%0A";
		КоличествоСтрок = ИнформацияДляТехПоддержки.КоличествоСтрок();
		
		Для НомерСтроки = 1 По КоличествоСтрок Цикл 
			
			ТекСтрока 	= ИнформацияДляТехПоддержки.ПолучитьСтроку(НомерСтроки);
			ТекСтрока 	= СтрЗаменить(ТекСтрока, "\"	, "%2F");
			ТекСтрока 	= СтрЗаменить(ТекСтрока, """"	, "%22");
			
			ТелоПисьма 	= ?(НомерСтроки = 1, ТекСтрока, ТелоПисьма + ПереводСтроки + ТекСтрока);
			
		КонецЦикла;
		
		Гиперссылка = "mailto:[кому]?subject=[тема]&body=[тело]";
		Гиперссылка = СтрЗаменить(Гиперссылка, "[кому]", "diadoc@skbkontur.ru");
		Гиперссылка = СтрЗаменить(Гиперссылка, "[тема]", "Диадок. Модуль 1С");
		Гиперссылка = СтрЗаменить(Гиперссылка, "[тело]", ТелоПисьма);
		
		ЗапуститьПриложение(Гиперссылка);
		
	КонецПроцедуры //НаписатьПисьмоВТехПоддержку()
	
	// Добавляет в текстовый документ первичную информацию, необходимую для технической поддержки
	//
	// Параметры:
	//	ТекстовыйДокумент	- ТекстовыйДокумент					- текстовый документ, в который необходимо добавить информацию
	//	Организация			- СправочникСсылка, Неопределено	- организация, данные которой необходимо добавить в текстовый документ
	//	ДобавитьРазделитель	- Булево							- позволяет добавить разделитель перед началом нового блока с информацией
	&НаКлиенте
	Процедура ДобавитьВТекстовыйДокументИнформациюДляТехПоддержки(ТекстовыйДокумент, Организация = Неопределено, ДобавитьРазделитель = Ложь) Экспорт
		
		СтрокаРазделитель = "----------------------------";
					
		Если ЗначениеЗаполнено(Организация) Тогда
			
			Если ДобавитьРазделитель Тогда 
				ТекстовыйДокумент.ДобавитьСтроку(СтрокаРазделитель);
			КонецЕсли;
			
			ВывестиИнформациюОбОрганизацииВТекстовыйДокумент(ТекстовыйДокумент, Организация);
					
		КонецЕсли;
		
		Если ДобавитьРазделитель Тогда 
			ТекстовыйДокумент.ДобавитьСтроку(СтрокаРазделитель);
		КонецЕсли;
		ВывестиТехническуюИнформациюВТекстовыйДокумент(ТекстовыйДокумент);
		
	КонецПроцедуры //ДобавитьВТекстовыйДокументИнформациюДляТехПоддержки()
		
	// Добавляет в текстовый документ сообщение о том, какие данные необходимо заполнить самостоятельно
	//
	// Параметры:
	//  ТекстовыйДокумент 	- ТекстовыйДокумент - заполняемый текстовый документ
	&НаКлиенте
	Процедура ВывестиШаблонНеобходимоЗаполнитьВТекстовыйДокумент(ТекстовыйДокумент)
		
		ТекстовыйДокумент.ДобавитьСтроку("Необходимо заполнить:");
		ТекстовыйДокумент.ДобавитьСтроку("");
		ТекстовыйДокумент.ДобавитьСтроку("1. Наименование организации:");
		ТекстовыйДокумент.ДобавитьСтроку("2. ИНН:");
		ТекстовыйДокумент.ДобавитьСтроку("3. КПП:");
		
	КонецПроцедуры //ВывестиШаблонНеобходимоЗаполнитьВТекстовыйДокумент()
	
	// Добавляет в текстовый документ заголовок "Описание проблемы:" для самостоятельного заполнения
	//
	// Параметры:
	//  ТекстовыйДокумент 	- ТекстовыйДокумент - заполняемый текстовый документ
	&НаКлиенте
	Процедура ВывестиШаблонОписаниеПроблемыВТекстовыйДокумент(ТекстовыйДокумент)
		
		ТекстовыйДокумент.ДобавитьСтроку("Описание проблемы:");
		ТекстовыйДокумент.ДобавитьСтроку("");
		ТекстовыйДокумент.ДобавитьСтроку("");
		
	КонецПроцедуры //ВывестиШаблонОписаниеПроблемыВТекстовыйДокумент()
	
	// Добавляет в текстовый документ сведения об организации:
	// Наименование, ИНН, КПП
	//
	// Параметры:
	//  ТекстовыйДокумент 	- ТекстовыйДокумент - заполняемый текстовый документ
	//	Организация			- СправочникСсылка	- организация, информацию по которой добавляем в файл 
	//
	&НаКлиенте
	Процедура ВывестиИнформациюОбОрганизацииВТекстовыйДокумент(ТекстовыйДокумент, Организация)
				
		СведенияОбОрганизации = ОписаниеОрганизации(Организация);
		
		ТекстовыйДокумент.ДобавитьСтроку("Организация: " 	+ СведенияОбОрганизации.Наименование);
		ТекстовыйДокумент.ДобавитьСтроку("ИНН: " 			+ СведенияОбОрганизации.ИНН);
		ТекстовыйДокумент.ДобавитьСтроку("КПП: " 			+ СведенияОбОрганизации.КПП);
		
	КонецПроцедуры //ВывестиИнформациюОбОрганизацииВТекстовыйДокумент()
	
	// Добавляет в текстовый документ техническую информацию:
	// Версия модуля, компоненты, ОС и др.
	//
	// Параметры:
	//  ТекстовыйДокумент - ТекстовыйДокумент - заполняемый текстовый документ
	//
	&НаКлиенте
	Процедура ВывестиТехническуюИнформациюВТекстовыйДокумент(ТекстовыйДокумент)
		
		ТехИнфо = ТехническаяИнформация();
		
		Если ТехИнфо.Свойство("СпособАутентификации") Тогда
			Если ТехИнфо.СпособАутентификации = "ПоСертификату" Тогда 
				ТекстовыйДокумент.ДобавитьСтроку("Отпечаток сертификата: " + ТехИнфо.ОтпечатокСертификата);
			Иначе 
				ТекстовыйДокумент.ДобавитьСтроку("Логин: " + ТехИнфо.Логин);
			КонецЕсли;
		КонецЕсли;
		
		ТекстовыйДокумент.ДобавитьСтроку("Версия модуля: " 			+ ТехИнфо.ВерсияМодуля);
		ТекстовыйДокумент.ДобавитьСтроку("Версия компоненты: " 		+ ТехИнфо.ВерсияКомпоненты);
		ТекстовыйДокумент.ДобавитьСтроку("Версия платформы: " 		+ ТехИнфо.ВерсияПлатформы);
		ТекстовыйДокумент.ДобавитьСтроку("Версия ОС: " 				+ ТехИнфо.ВерсияОС);
		ТекстовыйДокумент.ДобавитьСтроку("Тип платформы: " 			+ ТехИнфо.ТипПлатформы);
		ТекстовыйДокумент.ДобавитьСтроку("Вид клиента: " 			+ ТехИнфо.ТипКлиента);
		ТекстовыйДокумент.ДобавитьСтроку("Строка соединения: " 		+ ТехИнфо.СтрокаСоединенияИБ);
		ТекстовыйДокумент.ДобавитьСтроку("Подключаемый модуль: " 	+ ТехИнфо.ПодключаемыйМодуль);
		ТекстовыйДокумент.ДобавитьСтроку("Конфигурация: " 			+ ТехИнфо.ОписаниеКонфигурации);
		
	КонецПроцедуры //ВывестиТехническуюИнформациюВТекстовыйДокумент()
	
	// Возвращает техническую информацию об использовании модуля
	// 
	// Возвращаемое значение:
	//  Структура - содержит поля:
	//   * ВерсияОС 			- Строка 		- Версия операционной системы.
	//   * ТипПлатформы 		- ТипПлатформы 	- сведения о типе платформы.
	//   * ВерсияПлатформы 		- Строка 		- Версия приложения 1С:Предприятие в формате <основная версия>.<младшая версия>.<релиз>.<дополнительный номер релиза>. Например, 8.2.9.200.
	//   * ТипКлиента 			- Строка 		- Вид и режим работы приложения. Например, "Толстый клиент обычное приложение".
	//   * ОписаниеКонфигурации - Строка 		- представление конфигурации.
	//   * СтрокаСоединенияИБ 	- Строка 		- Строка соединения информационной базы.
	//   * ВерсияМодуля 		- Строка 		- версия модуля Диадок.
	//   * ВерсияКомпоненты 	- Строка 		- версия компоненты Диадок.
	//   * СпособАутентификации - Строка 		- "ПоСертификату" или "ПоПаролю"
	//   * ОтпечатокСертификата - Строка (Необязательный) - Отпечаток сертификата, заполняется, если пользователь аутентифицировался по сертификату
	//   * Логин 				- Строка (Необязательный) - Отпечаток сертификата, заполняется, если пользователь аутентифицировался по логину и паролю
	//
	&НаКлиенте
	Функция ТехническаяИнформация() Экспорт
		
		СистемнаяИнформация 	= Новый СистемнаяИнформация;
		СтрокаСоединения		= СтрокаСоединенияИнформационнойБазы();
		ОписаниеКонфигурации	= ОписаниеКонфигурации();
		ПодключаемыйМодуль		= ?(Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.ИспользоватьМодуль, "Используется", "Не используется");
		ВидКлиента				= ВидКлиента();
		МодульТиповой			= МодульТиповой();
		
		ВерсияОбработки			= МетодСервера(,"ВерсияОбработки");
		ВерсияКомпоненты		= ?(Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок <> Неопределено, Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocInvoiceAPI.GetVersion(), "");
		
		ТехИнфо = Новый Структура;
		
		// системная информация
		ТехИнфо.Вставить("ВерсияОС"			, СистемнаяИнформация.ВерсияОС);
		ТехИнфо.Вставить("ТипПлатформы"		, Строка(СистемнаяИнформация.ТипПлатформы));
		ТехИнфо.Вставить("ВерсияПлатформы"	, СистемнаяИнформация.ВерсияПриложения);
		ТехИнфо.Вставить("ТипКлиента"		, ВидКлиента);
		
		// информационная база
		ТехИнфо.Вставить("ОписаниеКонфигурации"	, ОписаниеКонфигурации);
		ТехИнфо.Вставить("СтрокаСоединенияИБ"	, СтрокаСоединения);
		
		// модуль
		ТехИнфо.Вставить("ВерсияМодуля"			, ВерсияОбработки);
		ТехИнфо.Вставить("ВерсияКомпоненты"		, ВерсияКомпоненты);
		ТехИнфо.Вставить("ПодключаемыйМодуль"	, Строка(ПодключаемыйМодуль));
		ТехИнфо.Вставить("МодульТиповой"		, МодульТиповой);
		
		// параметры аутентификации
		ДополнитьТехИнформациюПараметрамиАутентификации(ТехИнфо);
		
		Возврат ТехИнфо;
		
	КонецФункции //ТехническаяИнформация()

	// Дополняет техническую информацию сведениями о параметрах аутентификации в сервисе Диадок.
	//
	// Параметры:
	//  ТехИнфо - Структура - см. ТехническаяИнформация()
	//
	&НаКлиенте
	Процедура ДополнитьТехИнформациюПараметрамиАутентификации(ТехИнфо)
		
		Если Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок = Неопределено Тогда
			
			// Если автоматом не можем определить имя интеграционного модуля
			Возврат;
			
		ИначеЕсли Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.Свойство("DiadocConnection") Тогда
			
			СоединениеДиадок = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection;
			Если СоединениеДиадок = Неопределено Тогда 
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
						
		ТипАутентификации 	= СоединениеДиадок.AuthenticateType;
		
		Если ВРег(ТипАутентификации) = ВРег("Certificate") Тогда 
			
			ЛичныйСертификат = СоединениеДиадок.Certificate;
			
			ТехИнфо.Вставить("СпособАутентификации"	, "ПоСертификату");
			ТехИнфо.Вставить("ОтпечатокСертификата"	, ЛичныйСертификат.Thumbprint);
			
		ИначеЕсли ВРег(ТипАутентификации) = ВРег("Login") Тогда 
			
			ТехИнфо.Вставить("СпособАутентификации"	, "ПоПаролю");
			ТехИнфо.Вставить("Логин"				, СоединениеДиадок.Login);
			
		КонецЕсли;
		
	КонецПроцедуры //ДополнитьТехИнформациюПараметрамиАутентификации()

	// Запускает онлайн чат с технической поддержкой, автоматически передавая необходимую первичную информацию
	//
	// Параметры:
	//	Организация	- СправочникСсылка, Неопределено	- в случае неопределено данные по организации переданы не будут
	&НаКлиенте
	Процедура ОткрытьОнлайнЧат(Организация = Неопределено) Экспорт
		
		ТехническаяИнформация = ТехническаяИнформация();
		
		ДопПараметрыURI = Новый Соответствие; // additionalParameters
		
		Если ЗначениеЗаполнено(Организация) Тогда
			
			РеквизитыОрганизации = ОписаниеОрганизации(Организация);
			
			// сведения об организации
			ДопПараметрыURI.Вставить(СтрокаБазовогоПараметраОнлайнКонсультанта("orgname"	), РеквизитыОрганизации.Наименование);
			ДопПараметрыURI.Вставить(СтрокаБазовогоПараметраОнлайнКонсультанта("inn"		), РеквизитыОрганизации.ИНН);
			ДопПараметрыURI.Вставить(СтрокаБазовогоПараметраОнлайнКонсультанта("kpp"		), РеквизитыОрганизации.КПП);
			
		КонецЕсли;
		
		// параметры аутентификации
		Если ТехническаяИнформация.Свойство("СпособАутентификации") Тогда
			Если ТехническаяИнформация.СпособАутентификации = "ПоСертификату" Тогда 
				ДопПараметрыURI.Вставить(СтрокаПродуктовогоПараметраОнлайнКонсультанта("User_thumbprint")	, ТехническаяИнформация.ОтпечатокСертификата);
			Иначе 
				ДопПараметрыURI.Вставить(СтрокаПродуктовогоПараметраОнлайнКонсультанта("User_login")		, ТехническаяИнформация.Логин);
			КонецЕсли;
		КонецЕсли;
		
		// системная информация
		ДопПараметрыURI.Вставить(СтрокаПродуктовогоПараметраОнлайнКонсультанта("OSVersion"			), ТехническаяИнформация.ВерсияОС);
		ДопПараметрыURI.Вставить(СтрокаПродуктовогоПараметраОнлайнКонсультанта("platformType"		), ТехническаяИнформация.ТипПлатформы);
		ДопПараметрыURI.Вставить(СтрокаПродуктовогоПараметраОнлайнКонсультанта("platformVersion"	), ТехническаяИнформация.ВерсияПлатформы);
		ДопПараметрыURI.Вставить(СтрокаПродуктовогоПараметраОнлайнКонсультанта("clientType"			), ТехническаяИнформация.ТипКлиента);
		
		// информационная база
		ДопПараметрыURI.Вставить(СтрокаПродуктовогоПараметраОнлайнКонсультанта("informationConfiguration"	), ТехническаяИнформация.ОписаниеКонфигурации);
		ДопПараметрыURI.Вставить(СтрокаПродуктовогоПараметраОнлайнКонсультанта("connectionString"			), ТехническаяИнформация.СтрокаСоединенияИБ);
		
		// модуль
		ДопПараметрыURI.Вставить(СтрокаПродуктовогоПараметраОнлайнКонсультанта("moduleVersion"	), ТехническаяИнформация.ВерсияМодуля);
		ДопПараметрыURI.Вставить(СтрокаПродуктовогоПараметраОнлайнКонсультанта("componentVersion"	), ТехническаяИнформация.ВерсияКомпоненты);
		
		ПараметрыURI = Новый Структура;
		ПараметрыURI.Вставить("type"				, "window");
		ПараметрыURI.Вставить("topic"				, "Диадок");
		ПараметрыURI.Вставить("hideMinimizedVersion", "true");
		ПараметрыURI.Вставить("additionalParameters", СтруктуруВКоллекциюПараметровURI(ДопПараметрыURI));
		
		// Формат гиперссылки:
		// host_name?type=window&topic=Диадок&hideMinimizedVersion=true&additionalParameters={"base/inn":"ИНН организации","base/kpp":"КПП организации","base/orgname":"Наименование организации","scope/1CDiadoc/User_login":"Логин клиента"(или в случае авторизации по сертификату "scope/1CDiadoc/User_thumbprint":"Отпечаток сертификата клиента"),"scope/1CDiadoc/moduleVersion":"Версия модуля","scope/1CDiadoc/componentVersion":"Версия компоненты","scope/1CDiadoc/platformVersion":"Версия платформы","scope/1CDiadoc/OSVersion":"Версия операционный системы","scope/1CDiadoc/platformType":"Windows x86","scope/1CDiadoc/clientType":"Тип клиента","scope/1CDiadoc/connectionString":"Строка соединения","scope/1CDiadoc/informationConfiguration":"Информация о конфигурации"}
		АдресРесурса = АдресаИнтернетРесурсов().ОнлайнКонсультант;
		АдресРесурса = АдресРесурса + СтруктуруВСтрокуПараметровURI(ПараметрыURI);
		
		ОткрытьХостВIE(АдресРесурса);
		
	КонецПроцедуры //ОткрытьОнлайнЧат()
	
	// Открывает в браузере IE страницу с настойками удаленного подключения по VNC
	&НаКлиенте
	Процедура ЗапуститьУдаленноеПодключениеVNC() Экспорт
		
		АдресаИнтернетРесурсов = АдресаИнтернетРесурсов();
	
		ОткрытьХостВIE(АдресаИнтернетРесурсов.УдаленноеПодключение);
		
	КонецПроцедуры //ЗапуститьУдаленноеПодключениеVNC()
	
////////////////////////////////////////////////////////////////////////////////
//} ТЕХНИЧЕСКАЯ ПОДДЕРЖКА

////////////////////////////////////////////////////////////////////////////////
// НАСТРОЙКА ФОРМАТА ОТПРАВКИ ДОКУМЕНТОВ

#Область НАСТРОЙКА_ФОРМАТА_ОТПРАВКИ_ДОКУМЕНТОВ

	// Псевдоперечисление ключей настроек форматов отправки документов,
	// которые записываются в свойства "ФормироватьУПД" или "ФормироватьУПДКонтрагент";
	//
	// Возвращаемое значение:
	//  Структура
	//
	&НаКлиенте
	Функция НастройкиФорматаОтправкиДокументов()
		
		Результат = Новый Структура;
		
		Результат.Вставить("СЧФДОП_820"	, "СЧФДОП_820"); // УПД Счф Доп 820@, УКД Ксчф Дис 820@
		Результат.Вставить("СЧФДОП"		, "СЧФДОП");	 // УПД Счф Доп 155@, УКД Ксчф Дис 189@
		
		Результат.Вставить("СЧФ_ДОП_820", "СЧФ_ДОП_820"); // УПД Счф 820@, УКД КСчф 189@, УПД Доп 820@
		Результат.Вставить("СЧФ_ДОП"	, "СЧФ_ДОП");	  // УПД Счф 155@, УКД КСчф 189@, УПД Доп 155@
		
		Результат.Вставить("СФТОРГ12АКТ_820", "СФТОРГ12АКТ_820"); // СФ 820@, КСФ 189@, Торг/Акт 820@
		Результат.Вставить("СФТОРГ12АКТ"	, "СФТОРГ12АКТ");	  // СФ 155@, КСФ 189@, Торг/Акт 155@
		
		Результат.Вставить("СЧФ", "СЧФ"); // УПД Счф 155@, УКД КСчф 189@, Торг/Акт 172@
		Результат.Вставить("ДОП", "ДОП"); // СФ 93@, КСФ 93@, УПД Доп 155@
		
		Результат.Вставить("СФ"			, "СФ");		// СФ 155@, КСФ 189@, Торг/Акт 172@
		Результат.Вставить("ТОРГ12АКТ"	, "ТОРГ12АКТ");	// СФ 93@, КСФ 93@, Торг/Акт 155@
		Результат.Вставить("НЕТ"		, "НЕТ");		// СФ 93@, КСФ 93@, Торг/Акт 172@
		
		Возврат Результат;
		
	КонецФункции
	
	// Псевдоперечисление вариантов отправки счетов-фактур
	//
	// Возвращаемое значение:
	//  Структура
	//
	&НаКлиенте
	Функция ВариантыОтправкиСчетовФактур()
		
		Результат = Новый Структура;
		
		Результат.Вставить("УПД_УКД_ПОЛНЫЙ_820"	, "УПД_УКД_ПОЛНЫЙ_820"); // УПД Счф Доп 820@, УКД Ксчф Дис 820@
		Результат.Вставить("УПД_УКД_ПОЛНЫЙ"		, "УПД_УКД_ПОЛНЫЙ");	 // УПД Счф Доп 155@, УКД Ксчф Дис 189@
		
		Результат.Вставить("УПД_УКД_СФ_820"	, "УПД_УКД_СФ_820"); // УПД Счф 820@, УКД КСчф 189@
		Результат.Вставить("УПД_УКД_СФ"		, "УПД_УКД_СФ");	 // УПД Счф 155@, УКД КСчф 189@
		
		Результат.Вставить("СФ_820", "СФ_820"); // СФ 820@, КСФ 189@
		Результат.Вставить("СФ_155", "СФ_155"); // СФ 155@, КСФ 189@
		
		Результат.Вставить("СФ_93", "СФ_93"); // СФ 93@, КСФ 93@
		
		Возврат Результат;
		
	КонецФункции
	
	// Псевдоперечисление вариантов отправки накладных и актов
	//
	// Возвращаемое значение:
	//  Структура
	//
	&НаКлиенте
	Функция ВариантыОтправкиНакладных()
		
		Результат = Новый Структура;
		
		Результат.Вставить("УПД_УКД_ПОЛНЫЙ_820"		, "УПД_УКД_ПОЛНЫЙ_820"); // УПД Счф Доп 820@, УКД Ксчф Дис 820@
		Результат.Вставить("УПД_УКД_ПОЛНЫЙ"			, "УПД_УКД_ПОЛНЫЙ");	 // УПД Счф Доп 155@, УКД Ксчф Дис 189@
		
		Результат.Вставить("УПД_УКД_ТОРГ12АКТ_820"	, "УПД_УКД_ТОРГ12АКТ_820");	 // УПД Доп 820@
		Результат.Вставить("УПД_УКД_ТОРГ12АКТ"		, "УПД_УКД_ТОРГ12АКТ");		 // УПД Доп 155@
		
		Результат.Вставить("ТОРГ12АКТ_820", "ТОРГ12АКТ_820"); // Торг/Акт 820@
		Результат.Вставить("ТОРГ12АКТ_155", "ТОРГ12АКТ_155"); // Торг/Акт 155@
		Результат.Вставить("ТОРГ12АКТ_172", "ТОРГ12АКТ_172"); // Торг/Акт 172@
		
		Возврат Результат;
		
	КонецФункции
	
	// Возвращает массив всех возможных настроек форматов отправки документов
	//
	// Возвращаемое значение:
	//  Массив - элементом массива является структура. См. метод Новый_НастройкаФорматаОтправки
	&НаКлиенте
	Функция МассивНастроекФорматовОтправки()
						
		Результат = Новый Массив;
						
		Настройки = НастройкиФорматаОтправкиДокументов();
		ВариантыОтправкиСФ	 = ВариантыОтправкиСчетовФактур();
		ВариантыОтправкиНакл = ВариантыОтправкиНакладных();
		
		// Правила заполнения массива:
		//	1.	Элементом массива является структура (см. метод Новый_НастройкаФорматаОтправки).
		// 	2.	Параметр ОсновнаяНастройка со значением Истина должен обязательно быть и только у одного элемента массива.
		// 	3.	Для каждого уникального значения параметра ВариантОтправкиСФ должен быть обязательно определен
		//		только 1 параметр ОсновнойВариантОтправкиНакладных со значением Истина.
				
		// СФ 820@
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.СЧФДОП_820
		, ВариантыОтправкиСФ.УПД_УКД_ПОЛНЫЙ_820
		, ВариантыОтправкиНакл.УПД_УКД_ПОЛНЫЙ_820
		, Истина, Истина);
		
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.СЧФ_ДОП_820
		, ВариантыОтправкиСФ.УПД_УКД_СФ_820
		, ВариантыОтправкиНакл.УПД_УКД_ТОРГ12АКТ_820
		, Истина, Ложь);
		
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.СФТОРГ12АКТ_820
		, ВариантыОтправкиСФ.СФ_820
		, ВариантыОтправкиНакл.ТОРГ12АКТ_820
		, Истина, Ложь);
		
		// СФ 155@
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.СЧФДОП
		, ВариантыОтправкиСФ.УПД_УКД_ПОЛНЫЙ
		, ВариантыОтправкиНакл.УПД_УКД_ПОЛНЫЙ
		, Истина, Ложь);
		
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.СЧФ_ДОП
		, ВариантыОтправкиСФ.УПД_УКД_СФ
		, ВариантыОтправкиНакл.УПД_УКД_ТОРГ12АКТ
		, Истина, Ложь);
		
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.СЧФ
		, ВариантыОтправкиСФ.УПД_УКД_СФ
		, ВариантыОтправкиНакл.ТОРГ12АКТ_172
		, Ложь, Ложь);
		
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.СФТОРГ12АКТ
		, ВариантыОтправкиСФ.СФ_155
		, ВариантыОтправкиНакл.ТОРГ12АКТ_155
		, Истина, Ложь);
		
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.СФ
		, ВариантыОтправкиСФ.СФ_155
		, ВариантыОтправкиНакл.ТОРГ12АКТ_172
		, Ложь, Ложь);
		
		// СФ 93@
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.НЕТ
		, ВариантыОтправкиСФ.СФ_93
		, ВариантыОтправкиНакл.ТОРГ12АКТ_172
		, Истина, Ложь, Истина);
		
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.ДОП
		, ВариантыОтправкиСФ.СФ_93
		, ВариантыОтправкиНакл.УПД_УКД_ТОРГ12АКТ
		, Ложь, Ложь, Истина);
		
		ДобавитьНастройкуФорматаОтправки(Результат
		, Настройки.ТОРГ12АКТ
		, ВариантыОтправкиСФ.СФ_93
		, ВариантыОтправкиНакл.ТОРГ12АКТ_155
		, Ложь, Ложь, Истина);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ДобавитьНастройкуФорматаОтправки(МассивНастроек
		, КлючНастройки, ВариантОтправкиСФ, ВариантОтправкиНакладных
		, ОсновнойВариантОтправкиНакладных, ОсновнаяНастройка, УстаревшаяНастройка = Ложь)
		
		НоваяНастройка = Новый_НастройкаФорматаОтправки();
		
		НоваяНастройка.КлючНастройки					= КлючНастройки;
		НоваяНастройка.ВариантОтправкиСФ				= ВариантОтправкиСФ;
		НоваяНастройка.ВариантОтправкиНакладных		 	= ВариантОтправкиНакладных;
		НоваяНастройка.ОсновнойВариантОтправкиНакладных = ОсновнойВариантОтправкиНакладных;
		НоваяНастройка.ОсновнаяНастройка				= ОсновнаяНастройка;
		НоваяНастройка.УстаревшаяНастройка				= УстаревшаяНастройка;
		
		МассивНастроек.Добавить(НоваяНастройка);
		
	КонецПроцедуры
	
	// Заготовка для описания настройки формата отправки
	//
	// Возвращаемое значение:
	//	Структура со следующими ключами:
	//    * КлючНастройки                    - Строка - значение для записи в карточке организации или контрагента
	//                                                  в свойство "ФормироватьУПД" или "ФормироватьУПДКонтрагент";
	//    * ВариантОтправкиСФ                - Строка - см. функцию ВариантыОтправкиСчетовФактур;
	//    * ВариантОтправкиНакладных         - Строка - см. функцию НастройкиФорматаОтправкиНакладной;
	//    * ОсновнойВариантОтправкиНакладных - Булево - Истина, вариант отправки счета-фактуры,
	//                                                  автоматически заполняется формат отправки накладной;
	//    * ОсновнаяНастройка                - Булево - Истина, если настройка автоматически заполняется
	//                                                  в карточке новой сопоставленной организации;
	//    * УстаревшаяНастройка              - Булево - Истина, если настройка больше не отображается
	//                                                  в списке для выбора формата отправки
	//
	&НаКлиенте
	Функция Новый_НастройкаФорматаОтправки()
		
		Результат = Новый Структура;
		
		Результат.Вставить("КлючНастройки"						, "");
		Результат.Вставить("ВариантОтправкиСФ"					, "");
		Результат.Вставить("ВариантОтправкиНакладных"			, "");
		Результат.Вставить("ОсновнойВариантОтправкиНакладных"	, Ложь);
		Результат.Вставить("ОсновнаяНастройка"					, Ложь);
		Результат.Вставить("УстаревшаяНастройка"				, Ложь);
		
		Возврат Результат;
		
	КонецФункции
	
#Область ПРЕДСТАВЛЕНИЕ_НАСТРОЙКИ

	// Представление варианта отправки счетов-фактур, для списков выбора
	//
	// Параметры:
	//  КлючНастройки - Строка - см. функцию ВариантыОтправкиСчетовФактур;
	// 
	// Возвращаемое значение:
	//  Строка - представление настройки
	//
	&НаКлиенте
	Функция ПредставлениеВариантаОтправкиСчетовФактур(КлючНастройки) Экспорт
		
		Результат = Неопределено;
		
		Если Не ЗначениеЗаполнено(КлючНастройки) Тогда
			Возврат Результат;
		КонецЕсли;
		
		Представления = ВсеПредставленияВариантовОтправкиСчетовФактур();
						
		Результат = Представления[КлючНастройки];
		
		Если Не ЗначениеЗаполнено(Результат) Тогда
			
			ТекстОшибки = ПодставитьПараметрыВСтроку(НСтр(
			"ru = 'Не удалось получить представление формата отправки счетов-фактур %1!'"), КлючНастройки);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ВсеПредставленияВариантовОтправкиСчетовФактур()
		
		ВариантыОтправки = ВариантыОтправкиСчетовФактур();
		
		Представления = Новый Соответствие;
		
		Представления.Вставить(	ВариантыОтправки.УПД_УКД_ПОЛНЫЙ_820,
								НСтр("ru = 'УПД в формате 820 приказа, УКД для комплекта передаточных документов и счетов-фактур'"));
								
		Представления.Вставить(	ВариантыОтправки.УПД_УКД_СФ_820,
								НСтр("ru = 'УПД в формате 820 приказа, УКД для счетов-фактур'"));
								
		Представления.Вставить(	ВариантыОтправки.СФ_820,
								НСтр("ru = 'В формате 820 приказа (КСФ - в формате 189 приказа)'"));
		
		Представления.Вставить(	ВариантыОтправки.УПД_УКД_ПОЛНЫЙ,
								НСтр("ru = 'УПД в формате 155 приказа, УКД для комплекта передаточных документов и счетов-фактур'"));
								
		Представления.Вставить(	ВариантыОтправки.УПД_УКД_СФ,
								НСтр("ru = 'УПД в формате 155 приказа, УКД для счетов-фактур'"));
								
		Представления.Вставить(	ВариантыОтправки.СФ_155,
								НСтр("ru = 'В формате 155 приказа (КСФ - в формате 189 приказа)'"));
								
		Представления.Вставить(	ВариантыОтправки.СФ_93,
								НСтр("ru = 'В формате 93 приказа'"));
								
		Возврат Представления;
		
	КонецФункции
	
	// Представление варианта отправки накладных и актов, для списков выбора
	//
	// Параметры:
	//  КлючНастройки - Строка - см. функцию ВариантыОтправкиНакладных;
	// 
	// Возвращаемое значение:
	//  Строка - представление настройки
	//
	&НаКлиенте
	Функция ПредставлениеВариантаОтправкиНакладных(КлючНастройки) Экспорт
		
		Результат = Неопределено;
		
		Если Не ЗначениеЗаполнено(КлючНастройки) Тогда
			Возврат Результат;
		КонецЕсли;
		
		Представления = ВсеПредставленияВариантовОтправкиНакладных();
						
		Результат = Представления[КлючНастройки];
		
		Если Не ЗначениеЗаполнено(Результат) Тогда
			
			ТекстОшибки = ПодставитьПараметрыВСтроку(НСтр(
			"ru = 'Не удалось получить представление формата отправки накладных %1!'"), КлючНастройки);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ВсеПредставленияВариантовОтправкиНакладных()
		
		ВариантыОтправки = ВариантыОтправкиНакладных();
		
		Представления = Новый Соответствие;
		
		Представления.Вставить(	ВариантыОтправки.УПД_УКД_ПОЛНЫЙ_820, 
								НСтр("ru = 'УПД в формате 820 приказа, УКД для комплекта передаточных документов и счетов-фактур'"));
								
		Представления.Вставить(	ВариантыОтправки.УПД_УКД_ТОРГ12АКТ_820,
								НСтр("ru = 'УПД в формате 820 приказа, УКД для передаточных документов'"));
								
		Представления.Вставить(	ВариантыОтправки.ТОРГ12АКТ_820,
								НСтр("ru = 'В формате 820 приказа'"));
								
		Представления.Вставить(	ВариантыОтправки.УПД_УКД_ПОЛНЫЙ,
								НСтр("ru = 'УПД в формате 155 приказа, УКД для комплекта передаточных документов и счетов-фактур'"));
								
		Представления.Вставить(	ВариантыОтправки.УПД_УКД_ТОРГ12АКТ,
								НСтр("ru = 'УПД в формате 155 приказа, УКД для передаточных документов'"));
								
		Представления.Вставить(	ВариантыОтправки.ТОРГ12АКТ_155,
								НСтр("ru = 'В формате 155 приказа'"));
								
		Представления.Вставить(	ВариантыОтправки.ТОРГ12АКТ_172,
								НСтр("ru = 'В формате 172 приказа'"));
								
		Возврат Представления;
		
	КонецФункции

#КонецОбласти
	
#Область ПОИСК_НАСТРОЙКИ
	
	// Настройка формата отправки документов, для новой организации
	//
	// Возвращаемое значение:
	//  Строка - см. фукнцию НастройкиФорматаОтправкиДокументов
	//
	&НаКлиенте
	Функция НастройкаФорматаОтправкиДокументовПоУмолчанию() Экспорт
		
		МассивНастроек = МассивНастроекФорматовОтправки();
		
		ПараметрыПоиска = Новый Структура("ОсновнаяНастройка", Истина);
		МассивНастроек = НайтиСтрокиВМассивеСтруктур(МассивНастроек, ПараметрыПоиска);
		
		// При правильном заполнении массива настроек форматов отправки 
		// в массиве будет только один элемент
		Результат = МассивНастроек[0].КлючНастройки;
		
		Возврат Результат;
		
	КонецФункции
	
	// Вариант отправки счетов-фактур, в зависимости от настройки формата отправки документов
	//
	// Параметры:
	//  КлючНастройки - Строка - см. функцию НастройкиФорматаОтправкиДокументов;
	// 
	// Возвращаемое значение:
	//  Строка - см. фукнцию ВариантыОтправкиСчетовФактур
	//
	&НаКлиенте
	Функция ВариантОтправкиСчетовФактурПоКлючуНастройки(КлючНастройки) Экспорт
		
		Результат = Неопределено;
		
		Если Не ЗначениеЗаполнено(КлючНастройки) Тогда
			Возврат Результат;
		КонецЕсли;
						
		МассивНастроек	= МассивНастроекФорматовОтправки();
		ПараметрыПоиска = Новый Структура("КлючНастройки", КлючНастройки);
		
		МассивНайденных	= НайтиСтрокиВМассивеСтруктур(МассивНастроек, ПараметрыПоиска);
		
		Если НЕ ЗначениеЗаполнено(МассивНайденных) Тогда
			
			ТекстОшибки = ПодставитьПараметрыВСтроку(НСтр(
			"ru = 'Не удалось получить настройку формата отправки счета-фактуры!
			|Формат отправки документов: %1'"), КлючНастройки);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		// При правильном заполнении массива настроек форматов отправки 
		// в массиве будет только один элемент
		Результат = МассивНайденных[0].ВариантОтправкиСФ;
		
		Возврат Результат;
		
	КонецФункции
	
	// Вариант отправки накладных, в зависимости от настройки формата отправки документов
	//
	// Параметры:
	//  КлючНастройки - Строка - см. функцию НастройкиФорматаОтправкиДокументов;
	// 
	// Возвращаемое значение:
	//  Строка - см. фукнцию ВариантыОтправкиНакладных
	//
	&НаКлиенте
	Функция ВариантОтправкиНакладныхПоКлючуНастройки(КлючНастройки) Экспорт
		
		Результат = Неопределено;
		
		Если Не ЗначениеЗаполнено(КлючНастройки) Тогда
			Возврат Результат;
		КонецЕсли;
		
		МассивНастроек	= МассивНастроекФорматовОтправки();
		ПараметрыПоиска = Новый Структура("КлючНастройки", КлючНастройки);
		
		МассивНайденных	= НайтиСтрокиВМассивеСтруктур(МассивНастроек, ПараметрыПоиска);
		
		Если НЕ ЗначениеЗаполнено(МассивНайденных) Тогда
			
			ТекстОшибки = ПодставитьПараметрыВСтроку(НСтр(
			"ru = 'Не удалось получить настройку формата отправки счета-фактуры!
			|Формат отправки документов: %1'"), КлючНастройки);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		// При правильном заполнении массива настроек форматов отправки 
		// в массиве будет только один элемент
		Результат = МассивНайденных[0].ВариантОтправкиНакладных;
						
		Возврат Результат;
		
	КонецФункции
	
	// Вариант отправки накладных по умолчанию, в зависимости от варианта отправки счетов-фактур
	//
	// Параметры:
	//  ВариантОтправкиСФ - Строка - см. функцию ВариантыОтправкиСчетовФактур;
	//
	// Возвращаемое значение:
	//  Строка - см. фукнцию ВариантыОтправкиНакладных
	//
	&НаКлиенте
	Функция ВариантОтправкиНакладныхПоУмолчанию(ВариантОтправкиСФ) Экспорт
		
		Результат = Неопределено;
		
		Если Не ЗначениеЗаполнено(ВариантОтправкиСФ) Тогда
			Возврат Результат;
		КонецЕсли;
		
		МассивНастроек = МассивНастроекФорматовОтправки();
				
		ПараметрыПоиска = Новый Структура;
		ПараметрыПоиска.Вставить("ВариантОтправкиСФ", ВариантОтправкиСФ);
		ПараметрыПоиска.Вставить("ОсновнойВариантОтправкиНакладных", Истина);
		
		МассивНайденных	= НайтиСтрокиВМассивеСтруктур(МассивНастроек, ПараметрыПоиска);
						
		Если Не ЗначениеЗаполнено(МассивНайденных) Тогда
			
			ТекстОшибки = ПодставитьПараметрыВСтроку(НСтр(
			"ru = 'Не удалось получить настройку по умолчанию для формата отправки накладных %1!'"), ВариантОтправкиСФ);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		// При правильном заполнении массива настроек форматов отправки 
		// в массиве будет только один элемент
		Результат = МассивНайденных[0].ВариантОтправкиНакладных;
		
		Возврат Результат;
		
	КонецФункции
	
	// Настройка формата отправки документов, в зависимости от варианта отправки счетов-фактур и накладных
	//
	// Параметры:
	//  ВариантОтправкиСФ        - Строка - см. функцию ВариантыОтправкиСчетовФактур;
	//  ВариантОтправкиНакладных - Строка - см. функцию ВариантыОтправкиНакладных;
	// 
	// Возвращаемое значение:
	//  Строка - см. фукнцию НастройкиФорматаОтправкиДокументов
	//
	&НаКлиенте
	Функция НастройкаФорматаОтправкиДокументов(ВариантОтправкиСФ, ВариантОтправкиНакладных) Экспорт
		
		Результат = Неопределено;
		
		Если Не ЗначениеЗаполнено(ВариантОтправкиСФ)
			Или Не ЗначениеЗаполнено(ВариантОтправкиНакладных) Тогда
			Возврат Результат;
		КонецЕсли;
		
		МассивНастроек = МассивНастроекФорматовОтправки();
		
		ПараметрыПоиска = Новый Структура;
		ПараметрыПоиска.Вставить("ВариантОтправкиСФ"		, ВариантОтправкиСФ);
		ПараметрыПоиска.Вставить("ВариантОтправкиНакладных"	, ВариантОтправкиНакладных);
		
		МассивНайденных	= НайтиСтрокиВМассивеСтруктур(МассивНастроек, ПараметрыПоиска);
					
		Если Не ЗначениеЗаполнено(МассивНайденных) Тогда
			
			ТекстОшибки = ПодставитьПараметрыВСтроку(НСтр(
			"ru = 'Не удалось получить общую настройку формата отправки документов!
			|Формат отправки счетов-фактур: %1
			|Формат отправки накладных: %2'"), ВариантОтправкиСФ, ВариантОтправкиНакладных);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
		// При правильном заполнении массива настроек форматов отправки 
		// в массиве будет только один элемент
		Результат = МассивНайденных[0].КлючНастройки;
						
		Возврат Результат;
		
	КонецФункции
	
#КонецОбласти

#Область ПОСТРОЕНИЕ_СПИСКОВ

	// Список для выбора вариантов отправки счетов-фактур,
	// в карточках организации и контрагента
	//
	// Возвращаемое значение:
	//  СписокЗначений - элемент списка содержит:
	//    * Значение      - Строка - см. функцию ВариантыОтправкиСчетовФактур;
	//    * Представление - Строка - см. функцию ПредставлениеВариантаОтправкиСчетовФактур;
	//
	&НаКлиенте
	Функция СписокВыбораВариантовОтправкиСчетовФактур() Экспорт
		
		Результат = Новый СписокЗначений;
		
		МассивНастроек = МассивНастроекФорматовОтправки();
				
		Для Каждого Настройка Из МассивНастроек Цикл
			
			ВариантОтправкиСФ	 = Настройка.ВариантОтправкиСФ;
			УстаревшаяНастройка	 = Настройка.УстаревшаяНастройка;
			
			Если Не УстаревшаяНастройка И Результат.НайтиПоЗначению(ВариантОтправкиСФ) = Неопределено Тогда
				
				ПредставлениеВарианта = ПредставлениеВариантаОтправкиСчетовФактур(ВариантОтправкиСФ);
				
				Результат.Добавить(ВариантОтправкиСФ, ПредставлениеВарианта);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции
	
	// Список для выбора вариантов отправки накладных,
	// в зависимости от варианта отправки счетов-фактур
	//
	// Параметры:
	//  ВариантОтправкиСФ - Строка - см. функцию ВариантыОтправкиСчетовФактур;
	//
	// Возвращаемое значение:
	//  СписокЗначений - элемент списка содержит:
	//    * Значение      - Строка - см. функцию ВариантыОтправкиНакладных;
	//    * Представление - Строка - см. функцию ПредставлениеВариантаОтправкиНакладных;
	//
	&НаКлиенте
	Функция СписокВыбораВариантовОтправкиНакладных(ВариантОтправкиСФ) Экспорт
		
		Результат = Новый СписокЗначений;
						
		МассивНастроек = МассивНастроекФорматовОтправки();
		
		Для Каждого Настройка Из МассивНастроек Цикл
			
			ВариантОтправкиНакладных = Настройка.ВариантОтправкиНакладных;
			
			Если Не Настройка.УстаревшаяНастройка
				И Настройка.ВариантОтправкиСФ = ВариантОтправкиСФ
				И Результат.НайтиПоЗначению(ВариантОтправкиНакладных) = Неопределено Тогда
				
				ПредставлениеВарианта = ПредставлениеВариантаОтправкиНакладных(ВариантОтправкиНакладных);
				
				Результат.Добавить(ВариантОтправкиНакладных, ПредставлениеВарианта);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции
	
#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
//{ КОНТУР.МЕТРИКИ

#Область МЕТРИКИ

#Область Служебные_методы

	&НаКлиенте
	Функция ИспользоватьМетрики() Экспорт
					
		Результат = Объект.ПараметрыКлиентСервер.Метрики.ИспользоватьМетрики;
			
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОтключитьМетрики()
		
		Объект.ПараметрыКлиентСервер.Метрики.ИспользоватьМетрики = Ложь;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция Метрика_КлиентскиеСобытия()
		
		Результат = Новый Структура;
		
		Результат.Вставить("ОткрытаФорма"				, "Открыта форма");
		Результат.Вставить("ЗакрытаФорма"				, "Закрыта форма");
		Результат.Вставить("НажатаКнопка"				, "Нажать кнопку");
		Результат.Вставить("НажатаНадпись"				, "Нажать надпись");
		Результат.Вставить("УстановленФлаг"				, "Установить флаг");
		Результат.Вставить("СнятФлаг"					, "Снять флаг");
		Результат.Вставить("СменаСтраницы"				, "Сменить страницу");
		Результат.Вставить("ПолеВводаПриИзменении"		, "Измененить поле ввода");
		Результат.Вставить("ПолеВводаОчистка"			, "Очистить поле ввода");
		Результат.Вставить("ПолеВводаОткрытие"			, "Открытть из поля ввода");
		Результат.Вставить("ПолеВводаНачалоВыбора"		, "Начат выбор в поле ввода");
		Результат.Вставить("ПолеВводаОбработкаВыбора"	, "Обработать выбора в поле ввода");
		
		Возврат Результат;
		
	КонецФункции
	
	// Псевдо перечисление. Хранит наименование категорий событий метрики.
	//
	// Возвращаемое значение:
	//	Структура
	&НаКлиенте
	Функция Метрика_Категории() Экспорт
		
		Результат = Новый Структура;
		
		Результат.Вставить("ИнициализацияМодуля", "Инициализация модуля");
		Результат.Вставить("НастройкиМодуля"	, "Настройки модуля");
		Результат.Вставить("НастройкиОтправки"	, "Настройки отправки");
		Результат.Вставить("СозданиеПНК"		, "Создание приходной накладной");
				
		Возврат Результат;
		
	КонецФункции // Метрика_Категории()
	
	&НаКлиенте
	Функция МаксимальныйРазмерСтека() Экспорт
		
		Результат = 100;
				
		Возврат Результат;
		
	КонецФункции
			
	// Топики, которые поддерживает Контур.Метрика.
	//
	// Возвращаемое значение:
	//   Структура
	//
	&НаКлиенте
	Функция ТопикиМетрики() Экспорт
		
		Результат = Новый Структура;
		
		Результат.Вставить("Behavior"	, "Behavior");	// Поведение
		Результат.Вставить("Log"		, "Log");		// Журнал
		Результат.Вставить("Health"		, "Health");	// Здоровье
		Результат.Вставить("Feedback"	, "Feedback");	// ОбратнаяСвязь
		Результат.Вставить("Statistics"	, "Statistics");// Статистика
		
		Возврат Результат;
		
	КонецФункции // ТопикиМетрики()
	
	// Форматирует строку версии по правилам семантического версионирования
	// https://semver.org/lang/ru/
	//
	// Параметры:
	//  Версия - Строка - версия, которую необходимо форматировать
	//
	// Возвращаемое значение:
	//  Строка - форматированная версия
	//
	&НаКлиенте
	Функция СемантическийФорматВерсии(Версия)
		
		СоставВерсии = РазделитьСтроку(СокрЛП(Версия), "./");
		
		Дополнение	 = "";
		ПустаяВерсия = "0";
		
		МажорнаяВерсия	 = СокрЛП(ЗначениеИзМассива(СоставВерсии, 0, ПустаяВерсия));
		МинорнаяВерсия	 = СокрЛП(ЗначениеИзМассива(СоставВерсии, 1, ПустаяВерсия));
		Исправление		 = СокрЛП(ЗначениеИзМассива(СоставВерсии, 2, ПустаяВерсия));
		
		МажорнаяВерсия	 = ТолькоЦифрыВСтрокеВерсии(МажорнаяВерсия, ПустаяВерсия);
		МинорнаяВерсия	 = ТолькоЦифрыВСтрокеВерсии(МинорнаяВерсия, ПустаяВерсия);
		Исправление		 = ТолькоЦифрыВСтрокеВерсии(Исправление, ПустаяВерсия);
		
		Для Сч = 3 По СоставВерсии.ВГраница() Цикл
			Дополнение = Дополнение + СтрЗаменить(СоставВерсии[Сч], " ", "");
		КонецЦикла;
		
		Если ЗначениеЗаполнено(Дополнение) Тогда
			Результат = СтрШаблон("%1.%2.%3-%4", МажорнаяВерсия, МинорнаяВерсия, Исправление, Дополнение);
		Иначе
			Результат = СтрШаблон("%1.%2.%3", МажорнаяВерсия, МинорнаяВерсия, Исправление);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции // СемантическийФорматВерсии()
	
	// Проверяет что версия содержит только цифры
	//
	// Параметры:
	//  Версия              - Строка - версия, которую нужно проверить
	//  ЗначениеПоУмолчанию - Строка - значение, которое нужно вернуть,
	//                                 если версия содержит не только цифры
	// 
	// Возвращаемое значение:
	//  Строка - значение входящего параметра Версия. ЗначениеПоУмолчанию если версия содержит не только цифры
	//
	&НаКлиенте
	Функция ТолькоЦифрыВСтрокеВерсии(Версия, ЗначениеПоУмолчанию)
		
		Результат = ЗначениеПоУмолчанию;
		
		Если ТолькоЦифрыВСтроке(Версия) Тогда
			Результат = Версия;
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции // ТолькоЦифрыВСтрокеВерсии()
							
	// Преобразует дату по часовому поясу компьютера в универсальную
	// и затем приводит её к строке в формате ISO8601
	//
	// Параметры
	//  ДатаПоМестномуВремени - Дата - дата по часовому поясу компьютера
	//
	// Возвращаемое значение:
	//   Строка - дата строкой
	//
	&НаКлиенте
	Функция УниверсальнаяДатаВФормате_ISO8601(ДатаПоМестномуВремени)
		
		УниверсальнаяДата = УниверсальноеВремя(ДатаПоМестномуВремени);
		
		Результат = Формат(УниверсальнаяДата, "ДФ=yyyy-MM-ddTHH:mm:ss+00:00");
		
		Возврат Результат;
		
	КонецФункции
	
	// Мапит значение настроек в представление удобочитаемое в метриках
	//
	// Параметры:
	//	МассивОрганизаций	- Массив	- элементом массива является структура см. ОрганизацииДляЧтенияНастроек()
	//
	// Возвращаемое значение:
	//	Массив структур	- см. тело метода
	&НаСервере
	Функция НастройкиОрганизацийДляСтатистики(МассивОрганизаций)
		
		Результат = Новый Массив;
		
		Для каждого ЭлементМассива Из МассивОрганизаций Цикл
			
			ЗначенияНастроек = МетодСервера( , "НастройкиОрганизации", ЭлементМассива.Организация1С);
			
			Настройки = Новый Структура;
									
			Настройки.Вставить("ОтправлятьНеПроведенные"		, ЗначенияНастроек.ОтправлятьНеПроведенные = Истина);
			Настройки.Вставить("ПодставлятьКППГрузоотправителя"	, ЗначенияНастроек.ПодставлятьКППГрузоотправителя = Истина);
			
			Если ЗначениеЗаполнено(ЗначенияНастроек.ТекстКомментарияДиадок) Тогда
				Настройки.Вставить("ТекстКомментарияДиадок"		, ЗначенияНастроек.ТекстКомментарияДиадок);
			КонецЕсли;
			
			СчетНаОсновании_Значение 		= ЗначенияНастроек.ФормироватьСчетНаОсновании;
			СчетНаОсновании_Представление 	= ПредставлениеНастройки_ФормироватьСчетНаОсновании(СчетНаОсновании_Значение);
			Настройки.Вставить("ФормироватьСчетНаОсновании", СчетНаОсновании_Представление);
			
			ФормаСчетаНаОплату_Значение 		= ЗначенияНастроек.СтандартнаяФормаСчетаНаОплату;
			ФормаСчетаНаОплату_Представление 	= ПредставлениеНастройки_ФормаСчетаНаОплату(ФормаСчетаНаОплату_Значение);
			Настройки.Вставить("ФормаСчетаНаОплату", ФормаСчетаНаОплату_Представление);
									
			Настройки.Вставить("ФормироватьУПД", ЗначенияНастроек.ФормироватьУПД);
			
			Настройки.Вставить("УказыватьОтсутствиеОснованияУПД", ЗначенияНастроек.УказыватьОтсутствиеОснованияУПД);
			
			ЭлементРезультата = Новый Структура("BoxId, Настройки", ЭлементМассива.BoxId, Настройки);
			Результат.Добавить(ЭлементРезультата); 
			
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции // НастройкиОрганизацийДляСтатистики()
	
	// Возвращает представление настройки "ФормироватьСчетНаОсновании" по значению этой настройки
	//
	// Параметры: 
	//	ЗначениеНастройки	- Число, Неопределено	- значение настройки из БД
	//
	// Возвращаемое значение:
	//	Строка	
	&НаСервере
	Функция ПредставлениеНастройки_ФормироватьСчетНаОсновании(ЗначениеНастройки)
		
		КоллекцияПредставление = Новый Соответствие;
		
		КоллекцияПредставление.Вставить(0, "Счета на оплату");
		КоллекцияПредставление.Вставить(1, "Документов продажи");
		КоллекцияПредставление.Вставить(2, "Не формировать");
		КоллекцияПредставление.Вставить(4, "Заказа покупателя");
		
		Если НЕ ЗначениеЗаполнено(ЗначениеНастройки) Тогда
			
			Результат = КоллекцияПредставление.Получить(0);
			
		Иначе
		
			Результат = КоллекцияПредставление.Получить(ЗначениеНастройки);
			
		КонецЕсли;
		
		Если Результат = Неопределено Тогда
			
			Результат = "Неизвесный идентификатор";
			
		КонецЕсли;
				
		Возврат Результат;
		
	КонецФункции // ПредставлениеНастройки_ФормироватьСчетНаОсновании()
	
	// Возвращает представление настройки "ФормаСчетаНаОплату" по значению этой настройки
	//
	// Параметры: 
	//	ЗначениеНастройки	- Строка, Неопределено	- значение настройки из БД
	//
	// Возвращаемое значение:
	//	Строка	
	&НаСервере
	Функция ПредставлениеНастройки_ФормаСчетаНаОплату(ЗначениеНастройки)
		
		Если ЗначениеНастройки = Неопределено
			ИЛИ ЗначениеНастройки = Истина Тогда
			
			Результат = "Стандартная";
			
		ИначеЕсли НЕ ЗначениеНастройки Тогда
			
			Результат = "Внешняя";
			
		Иначе
			
			Результат = "Неизвесный идентификатор";	
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции // ПредставлениеНастройки_СтандартнаяФормаСчетаНаОплату()
	
	// Адрес, на который надо отправлять данные
	// для фиксации событий в сервисе Контур.Метрика.
	//
	//
	// Возвращаемое значение:
	//   Строка - адрес метода
	//
	&НаКлиенте
	Функция АдресМетодаМетрики(Топик)
		
		Результат = ПодставитьПараметрыВСтроку("/metrika-pipe/v1/sources/1CModule/topics/DD_%1/events", Топик);
		
		Возврат Результат;
		
	КонецФункции
		
	&НаКлиенте
	Функция АдресKonturAPI(ТестовыйСервер = Ложь)
	
		Возврат ?(ТестовыйСервер, "api.dev.kontur", "api.kontur.ru");
	
	КонецФункции
	
	// Возвращает из общего контекста
	// глобальный идентификатор группировки событий метрики
	//
	&НаКлиенте
	Функция TraceId()
		
		Результат = СвойствоСтруктуры(Объект.ПараметрыКлиентСервер.Метрики, "TraceId");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура TraceId_Обновить(Префикс)
		
		Если ИспользоватьМетрики() Тогда
			
			Результат = Префикс + "_" + Новый УникальныйИдентификатор;
									
			Объект.ПараметрыКлиентСервер.Метрики.TraceId = Результат;
						
		КонецЕсли;
					
	КонецПроцедуры
	
	&НаКлиенте
	Процедура TraceId_Сбросить()
		
		Объект.ПараметрыКлиентСервер.Метрики.TraceId = Неопределено;
					
	КонецПроцедуры
	
	&НаКлиенте
	Функция ОписаниеПеременыхДляМетрики(Переменные)
		
		Результат = Новый Соответствие;
		
		Для Каждого Элемент Из Переменные Цикл
			
			Если ЗначениеЗаполнено(Элемент.Значение) Тогда
				
				ОписаниеЗначения = ОписаниеЗначенияДляМетрики(Элемент.Значение);
				
				Результат.Вставить(Элемент.Ключ, ОписаниеЗначения);
				
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ОписаниеЗначенияДляМетрики(Значение)
		
		ТипЗначения = ТипЗнч(Значение);
		
		Результат = Новый Структура("value, type");
		
		Если ТипЗначения = Тип("Число") Тогда
			
			Результат.type	 = "number";
			Результат.value	 = Формат(Значение, "ЧН=; ЧГ=");
			
		ИначеЕсли ТипЗначения = Тип("Строка") Тогда
			
			Результат.type	 = "string";
			Результат.value	 = Значение;
			
		ИначеЕсли ТипЗначения = Тип("УникальныйИдентификатор") Тогда
			
			Результат.type	 = "uuid";
			Результат.value	 = Строка(Значение);
			
		ИначеЕсли ТипЗначения = Тип("ДвоичныеДанные") Тогда
			
			Результат.type	 = "base64";
			Результат.value	 = Base64Строка(Значение);
			
		ИначеЕсли ТипЗначения = Тип("Булево") Тогда
			
			Результат.type	 = "string";
			Результат.value	 = Формат(Значение, "БЛ=False; БИ=True");
			
		ИначеЕсли ТипЗначения = Тип("Дата") Тогда
			
			Результат.type	 = "string";
			Результат.value	 = Формат(Значение, "ДФ='dd.MM.yyyy H:mm:ss'; ДП=");
			
		Иначе
			
			Попытка
				Результат.value	 = ЗначениеВСтрокуJSON(Значение);
				Результат.type	 = "string";
			Исключение
				Результат.value	 = "convert_error";
				Результат.type	 = "string";
			КонецПопытки;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ЗначениеВСтрокуJSON(Значение)
	
		Запись = Новый ("ЗаписьJSON");
		Запись.УстановитьСтроку();
		
		ЗаписатьJSON(Запись, Значение);
		
		Результат = Запись.Закрыть();
		
		Возврат Результат;
		
	КонецФункции
	
#КонецОбласти

#Область Регистрация_поведения

	&НаКлиенте
	Процедура Метрика_ДобавитьПоведение_НажатиеКнопки(НазваниеФормы, НазваниеКатегории, НазваниеДействия) Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		Категория	 = НазваниеКатегории;						
		Действие	 = НазваниеДействия;						
		Место		 = НазваниеФормы;							
		Событие		 = Метрика_КлиентскиеСобытия().НажатаКнопка;
		
		TraceId_Обновить(Место);
		
		Метрика_ЗаписатьПоведение(Категория, Действие, Место, Событие);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура Метрика_ДобавитьПоведение_ПолеВводаПриИзменении(НазваниеФормы, НазваниеКатегории, НазваниеДействия) Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		Категория	= НазваниеКатегории;
		Действие	= НазваниеДействия;
		Место		= НазваниеФормы;
		Событие		= Метрика_КлиентскиеСобытия().ПолеВводаПриИзменении;
		
		TraceId_Обновить(Место);
		
		Метрика_ЗаписатьПоведение(Категория, Действие, Место, Событие);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура Метрика_ДобавитьПоведение_ПриИзмененииФлага(	НазваниеФормы,
															НазваниеКатегории, 
															НазваниеДействия, 
															ФлагУстановлен	) Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		Категория	= НазваниеКатегории;
		Действие	= НазваниеДействия;
		Место		= НазваниеФормы;
		
		Если ФлагУстановлен Тогда
			Событие = Метрика_КлиентскиеСобытия().УстановленФлаг;
		Иначе
			Событие = Метрика_КлиентскиеСобытия().СнятФлаг;
		КонецЕсли;
				
		TraceId_Обновить(Место);
		
		Метрика_ЗаписатьПоведение(Категория, Действие, Место, Событие);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура Метрика_ДобавитьПоведение_ДействиеСФормой(НазваниеФормы, НазваниеКатегории, НазваниеДействия) Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		Категория	 = НазваниеКатегории;
		Действие	 = НазваниеДействия;
		Место		 = НазваниеФормы;
		
		TraceId_Обновить(Место);
		
		Метрика_ЗаписатьПоведение(Категория, Действие, Место);
		
	КонецПроцедуры

#КонецОбласти

#Область Регистрация_статистики

	&НаКлиенте
	Процедура Метрика_ДобавитьСтатистику_Авторизации(ДопПеременные = Неопределено) Экспорт
	
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		Категория	 = Метрика_Категории().ИнициализацияМодуля;
		Действие	 = "Авторизация";
		
		КлючРаработчика = Платформа.ВладелецФормы.ПолучитьПараметрыПодключения().КлючРазработчика;
		
		МассивBoxId = Новый Массив;
			
		Для каждого ЭлементКонтекста Из Платформа.ПараметрыКлиент.КонтекстДиадока Цикл
			
			МассивBoxId.Добавить(ЭлементКонтекста.BoxId);
			
		КонецЦикла;
		
		Переменные = Новый Соответствие;
						
		Переменные.Вставить("BoxId"				, МассивBoxId);
		Переменные.Вставить("КоличествоBoxId"	, МассивBoxId.Количество());
		Переменные.Вставить("КлючРазработчика"	, КлючРаработчика);
		
		Если ЗначениеЗаполнено(ДопПеременные) Тогда
			
			Для каждого КлючЗначение Из ДопПеременные Цикл
				
				Переменные.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
				
			КонецЦикла;
			
		КонецЕсли;
			
		Метрика_ЗаписатьСтатистику(Категория, Действие, , Переменные, Истина);
				
	КонецПроцедуры
	
	&НаКлиенте
	Процедура Метрика_ДобавитьСтатистику_НастройкиОрганизации(НазваниеДействия) Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		МассивОрганизаций = Новый Массив;
		
		Для каждого ЭлементКонтекста Из Платформа.ПараметрыКлиент.КонтекстДиадока Цикл
			
			ЭлементМассива = Новый Структура;
			ЭлементМассива.Вставить("BoxId"			, ЭлементКонтекста.BoxId);
			ЭлементМассива.Вставить("Организация1С"	, ЭлементКонтекста.Организация);
			
			МассивОрганизаций.Добавить(ЭлементМассива);
			
		КонецЦикла;
		
		НастройкиОрганизации = НастройкиОрганизацийДляСтатистики(МассивОрганизаций);
		
		Категория	 = Метрика_Категории().НастройкиОтправки;
		Действие	 = НазваниеДействия;
					
		Для Каждого ЭлементКоллекции Из НастройкиОрганизации Цикл
			
			Переменные = Новый Соответствие;
			Переменные.Вставить("BoxId"		, ЭлементКоллекции.BoxId);
			Переменные.Вставить("Настройки"	, ЭлементКоллекции.Настройки);
						
			Метрика_ЗаписатьСтатистику(Категория, Действие, , Переменные, Истина);
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура Метрика_ДобавитьСтатистику_СоздатьПриходнуюНакладную(ДопПеременные) Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		Категория	 = Метрика_Категории().СозданиеПНК;
		Действие	 = "Создать документ";
		
		Метрика_ЗаписатьСтатистику(Категория, Действие, , ДопПеременные, Истина);
		
	КонецПроцедуры
	
#КонецОбласти
			
#Область Базовые_методы_регистрации

	// Записывает событие метрики в топик "Behavior"
	//
	// Параметры:
	//  Категория  - Строка - категория события;
	//  Действие   - Строка - выполняемое действие;
	//  Место      - Строка - место возникновения события;
	//  Событие    - Строка - описание события;
	//  Переменные - Структура, Соответствие - произвольная информация;
	//  TraceID    - Строка         - локальный идентификатор группы событий;
	//             - Неопределено   - использовать общий идентификатор группы событий,
	//                                см. функцию TraceId();
	//  ДобавитьСистемнуюИнформацию - Булево - Истина, если вместе с событием
	//                                         нужно отправить описание окружения;
	//
	&НаКлиенте
	Процедура Метрика_ЗаписатьПоведение(Категория, Действие
		, Место		 = Неопределено
		, Событие	 = Неопределено
		, Переменные = Неопределено
		, ДобавитьСистемнуюИнформацию = Истина
		, TraceID	 = Неопределено) Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		Если TraceID = Неопределено Тогда
			TraceID = TraceId();
		КонецЕсли;
		
		Behavior 		= ТопикиМетрики().Behavior;
		СобытиеМетрики 	= НовоеСобытиеМетрики(Категория, Действие, Событие, Место);
		
		ЗарегистрироватьСобытиеМетрики(Behavior, СобытиеМетрики, TraceID, Переменные, ДобавитьСистемнуюИнформацию);
				
	КонецПроцедуры
	
	// Записывает событие метрики в топик "Statistics"
	//
	// Параметры:
	//  Категория  - Строка - категория события;
	//  Действие   - Строка - выполняемое действие;
	//  Событие    - Строка - описание события;
	//  Переменные - Структура, Соответствие - произвольная информация;
	//  TraceID    - Строка         - локальный идентификатор группы событий;
	//             - Неопределено   - использовать общий идентификатор группы событий,
	//                                см. функцию TraceId();
	//  ДобавитьСистемнуюИнформацию - Булево - Истина, если вместе с событием
	//                                         нужно отправить описание окружения;
	//
	&НаКлиенте
	Процедура Метрика_ЗаписатьСтатистику(Категория, Действие
		, Событие	 = Неопределено
		, Переменные = Неопределено
		, ДобавитьСистемнуюИнформацию = Истина
		, TraceID	 = Неопределено) Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		Если TraceID = Неопределено Тогда
			TraceID = TraceId();
		КонецЕсли;
		
		Statistics 		= ТопикиМетрики().Statistics;
		СобытиеМетрики 	= НовоеСобытиеМетрики(Категория, Действие, Событие);
						
		ЗарегистрироватьСобытиеМетрики(Statistics, СобытиеМетрики, TraceID, Переменные, ДобавитьСистемнуюИнформацию);
		
	КонецПроцедуры
	
	// Записывает событие метрики в топик "Log"
	//
	// Параметры:
	//  Категория  - Строка - категория события;
	//  Действие   - Строка - выполняемое действие;
	//  Событие    - Строка - описание события;
	//  Переменные - Структура, Соответствие - произвольная информация;
	//  ДобавитьСистемнуюИнформацию - Булево - Истина, если вместе с событием
	//                                         нужно отправить описание окружения;
	//  TraceID    - Строка         - локальный идентификатор группы событий;
	//             - Неопределено   - использовать общий идентификатор группы событий,
	//                                см. функцию TraceId();
	//
	&НаКлиенте
	Процедура Метрика_ЗаписатьЛог(Категория, Действие
		, Событие	 = Неопределено
		, Переменные = Неопределено
		, TraceID	 = Неопределено
		, ДобавитьСистемнуюИнформацию = Истина) Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		Если TraceID = Неопределено Тогда
			TraceID = TraceId();
		КонецЕсли;
		
		Log 			= ТопикиМетрики().Log;
		СобытиеМетрики 	= НовоеСобытиеМетрики(Категория, Действие, Событие);
		
		ЗарегистрироватьСобытиеМетрики(Log, СобытиеМетрики, TraceID, Переменные, ДобавитьСистемнуюИнформацию);
		
	КонецПроцедуры
	
	// Записывает событие метрики в топик "Health"
	//
	// Параметры:
	//  Категория  - Строка - категория события;
	//  Действие   - Строка - выполняемое действие;
	//  Событие    - Строка - описание события;
	//  Переменные - Структура, Соответствие - произвольная информация;
	//  ДобавитьСистемнуюИнформацию - Булево - Истина, если вместе с событием
	//                                         нужно отправить описание окружения;
	//  TraceID    - Строка         - локальный идентификатор группы событий;
	//             - Неопределено   - использовать общий идентификатор группы событий,
	//                                см. функцию TraceId();
	//
	&НаКлиенте
	Процедура Метрика_ЗаписатьЗамер(Категория, Действие
		, Место		 = Неопределено
		, Событие	 = Неопределено
		, Переменные = Неопределено
		, ДобавитьСистемнуюИнформацию = Истина
		, TraceID	 = Неопределено) Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		Если TraceID = Неопределено Тогда
			TraceID = TraceId();
		КонецЕсли;
		
		Health 			= ТопикиМетрики().Health;
		СобытиеМетрики 	= НовоеСобытиеМетрики(Категория, Действие, Событие, Место);
		
		ЗарегистрироватьСобытиеМетрики(Health, СобытиеМетрики, TraceID, Переменные, ДобавитьСистемнуюИнформацию);
		
	КонецПроцедуры
	
	// Добавляет событие в стек событий.
	//
	// Параметры:
	//  Топик          - Строка    - топик, к которому относится событие.
	//  СобытиеМетрики - Структура - см. функцию Метрика_НовыйСобытие()
	//  TraceID        - Строка    - идентификатор трассировки
	//  Переменные     - Структура - произвольная информация
	//  ДобавитьСистемнуюИнформацию - Булево - Истина, если в метрику нужно
	//                                         добавить описание окружения.
	//
	&НаКлиенте
	Процедура ЗарегистрироватьСобытиеМетрики(Топик, СобытиеМетрики
		, TraceID	 = Неопределено
		, Переменные = Неопределено
		, ДобавитьСистемнуюИнформацию = Ложь)
		
		ОписаниеПеременных = Неопределено;
		ОписаниеПриложения = Неопределено;
		
		Если ДобавитьСистемнуюИнформацию Тогда
			ОписаниеПриложения = МетодКлиента("Модуль_Клиент", "ОписаниеПриложения");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Переменные) Тогда
			ОписаниеПеременных = ОписаниеПеременыхДляМетрики(Переменные);
		КонецЕсли;
		
		Метрика = НоваяМетрика(СобытиеМетрики, TraceID, ОписаниеПеременных, ОписаниеПриложения);
		
		РазмерСтека = ПоместитьСобытиеМетрикиВСтекСобытий(Топик, Метрика);
		
		Если РазмерСтека >= МаксимальныйРазмерСтека() Тогда
			Метрики_Отправить();
		КонецЕсли;
		
	КонецПроцедуры
	
#КонецОбласти

#Область Конструкторы_объектов_метрики

	&НаКлиенте
	Функция НовоеСобытиеМетрики(Категория, Действие
		, Метка			 = Неопределено
		, Представление	 = Неопределено)
		
		Результат = Новый Структура;
		
		Результат.Вставить("Category"	, Категория);
		Результат.Вставить("Action"		, Действие);
		
		Если ЗначениеЗаполнено(Метка) Тогда
			Результат.Вставить("Label", Метка);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Представление) Тогда
			Результат.Вставить("View", Представление);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НоваяМетрика(СобытиеМетрики
		, TraceID			 = Неопределено
		, ОписаниеПеременных = Неопределено
		, ОписаниеПриложения = Неопределено)
		
		МоментВремени = НовыйМоментВремениМетрики();
		
		Результат = Новый Структура;
		
		Результат.Вставить("Path", СобытиеМетрики);
		Результат.Вставить("Time", МоментВремени);
		
		Если ЗначениеЗаполнено(ОписаниеПриложения) Тогда
			Результат.Вставить("AppContext", ОписаниеПриложения);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеПеременных) Тогда
			Результат.Вставить("Variables", ОписаниеПеременных);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(TraceID) Тогда
			Результат.Вставить("TraceID", Строка(TraceID));
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НовыйСтекСобытийМетрики()
		
		Топики = Новый Структура;
						
		Для Каждого Элемент Из ТопикиМетрики() Цикл
			Топики.Вставить(Элемент.Ключ, Новый Массив);
		КонецЦикла;
		
		Результат = Новый Структура;
		Результат.Вставить("Топики", Топики);
		Результат.Вставить("РазмерСтека", 0);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НовыйМоментВремениМетрики()
		
		ТекущаяДатаКомпьютера	 = ТекущаяДата();
		УниверсальнаяДатаСтрокой = УниверсальнаяДатаВФормате_ISO8601(ТекущаяДатаКомпьютера);
		
		Результат = Новый Структура;
		Результат.Вставить("ClientTime"			, УниверсальнаяДатаСтрокой);
		Результат.Вставить("SendFromClientTime"	, УниверсальнаяДатаСтрокой);
		
		Возврат Результат;
		
	КонецФункции
	
	// Конструктор описания системной информации
	//
	// Возвращаемое значение:
	//	Структура
	&НаКлиенте
	Функция НовоеОписаниеПриложения(ВерсияМодуля
		, ОписаниеОС						 = Неопределено
		, ОписаниеБраузера					 = Неопределено
		, ОписаниеПрограммы					 = Неопределено
		, ОписаниеАппаратноогоОбеспечения	 = Неопределено)
		
		Результат = Новый Структура;
		
		Результат.Вставить("AppVersion", ВерсияМодуля);
		
		Если ЗначениеЗаполнено(ОписаниеОС) Тогда
			Результат.Вставить("OS", ОписаниеОС);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеБраузера) Тогда
			Результат.Вставить("Browser", ОписаниеБраузера);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеПрограммы) Тогда
			Результат.Вставить("Platform", ОписаниеПрограммы);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеАппаратноогоОбеспечения) Тогда
			Результат.Вставить("Hardware", ОписаниеАппаратноогоОбеспечения);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции // НовоеОписаниеПриложения()
	
	&НаКлиенте
	Функция НоваяСессияМетрики(CustomSessionId)
	
		Результат = Новый Структура;
		
		Результат.Вставить("CustomSessionId", CustomSessionId);
		
		Возврат Результат;
		
	КонецФункции
	
#КонецОбласти

#Область Описание_окружения

	// Формирует описание технических характеристик компьютера
	// Возвращаемое значение:
	//	Структура
	&НаКлиенте
	Функция ОписаниеСистемнойИнформации()
		
		Результат = Новый Структура;
		
		Результат.Вставить("ВерсияОС");
		Результат.Вставить("ВерсияПриложения");
		Результат.Вставить("ИдентификаторКлиента");
		Результат.Вставить("ИнформацияПрограммыПросмотра ");
		Результат.Вставить("ОперативнаяПамять");
		Результат.Вставить("Процессор");
		Результат.Вставить("ТипПлатформы");
		
		СистемнаяИнформация = Новый СистемнаяИнформация;
		
		ЗаполнитьЗначенияСвойств(Результат, СистемнаяИнформация);
		
		Возврат Результат;
		
	КонецФункции // ОписаниеСистемнойИнформации()

	&НаКлиенте
	Функция ОписаниеПриложения() Экспорт
		
		ВерсияМодуля		 = Метрики_ВерсияИнтеграционногоМодуля();
		ОписаниеОС			 = ОписаниеОС();
		ОписаниеБраузера	 = ОписаниеБраузера();
		ОписаниеПрограммы	 = ОписаниеПрограммы();
		ОписаниеАппаратноогоОбеспечения = ОписаниеАппаратноогоОбеспечения();
			
		Результат = НовоеОписаниеПриложения(ВерсияМодуля
					, ОписаниеОС
					, ОписаниеБраузера
					, ОписаниеПрограммы
					, ОписаниеАппаратноогоОбеспечения);
						
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Метрики_ВерсияИнтеграционногоМодуля()
		
		Результат = МетодКлиента("Модуль_Клиент", "ТехническаяИнформация").ВерсияМодуля;
		
		Результат = СемантическийФорматВерсии(Результат);
		
		Возврат Результат;
		
	КонецФункции
	
	// Формирует описание операционной системы
	//
	// Возвращаемое значение:
	//	Структура
	&НаКлиенте
	Функция ОписаниеОС()
		
		СтрокаТипПлатформы = НРег(ОписаниеСистемнойИнформации().ТипПлатформы);
		
		Если Найти(СтрокаТипПлатформы, "64") > 0 Тогда
			BitSet = "x64";
		Иначе
			BitSet = "x86";
		КонецЕсли;
		
		Если Найти(СтрокаТипПлатформы, НРег("Linux")) <> 0 Тогда
			Name = "Linux";
		ИначеЕсли Найти(СтрокаТипПлатформы, НРег("MacOS")) <> 0 Тогда
			Name = "MacOS";
		ИначеЕсли Найти(СтрокаТипПлатформы, НРег("Windows")) <> 0 Тогда
			Name = "Windows";
		Иначе
			Name = "NoName";
		КонецЕсли;
		
		Version = "0.0.0"; // С версией какие-то авантюры нужны
		
		Результат = Новый Структура;
		Результат.Вставить("BitSet"	, BitSet);
		Результат.Вставить("Name"	, Name);
		Результат.Вставить("Version", Version);
		
		Возврат Результат;
		
	КонецФункции // ОписаниеОС()
	
	// Формирует описание конфигурации и платформы 1С
	//
	// Возвращаемое значение:
	//	Структура
	&НаКлиенте
	Функция ОписаниеПрограммы()
		
		ОписаниеКофигурации = ОписаниеКофигурации();
		
		Name	 = "1С:Предприятие 8";
		RunMode	 = Строка(ТекущийРежимЗапуска());
		Version	 = СемантическийФорматВерсии(ОписаниеСистемнойИнформации().ВерсияПриложения);
		
		Configuration		 = ОписаниеКофигурации.Имя;
		ConfigurationVersion = СемантическийФорматВерсии(ОписаниеКофигурации.Версия);
		
		Результат = Новый Структура;
		Результат.Вставить("Name"	, Name);
		Результат.Вставить("RunMode", RunMode);
		Результат.Вставить("Version", Version);
		
		Результат.Вставить("Configuration"			, Configuration);
		Результат.Вставить("ConfigurationVersion"	, ConfigurationVersion);
		
		Возврат Результат;
		
	КонецФункции // ОписаниеПрограммы()
			
	&НаСервереБезКонтекста
	Функция ОписаниеКофигурации()
		
		Результат = Новый Структура;
		Результат.Вставить("Имя"	, Метаданные.Имя);
		Результат.Вставить("Версия"	, Метаданные.Версия);
		
		Возврат Результат;
		
	КонецФункции
	
	// Формирует описание веб-браузера
	//
	// Возвращаемое значение:
	//	Структура
	&НаКлиенте
	Функция ОписаниеБраузера()
		
		Результат = Новый Структура;
		
		UserAgent = ОписаниеСистемнойИнформации().ИнформацияПрограммыПросмотра;
		
		Если ЗначениеЗаполнено(UserAgent) Тогда
			Результат.Вставить("UserAgent", UserAgent);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции // ОписаниеБраузера()
	
	// Формирует описание характеристик оборудования
	//
	// Возвращаемое значение:
	//	Структура	
	&НаКлиенте
	Функция ОписаниеАппаратноогоОбеспечения()
		
		Делитель = 1000;
		
		TotalMemoryGb = Окр(ОписаниеСистемнойИнформации().ОперативнаяПамять / Делитель);
		
		Результат = Новый Структура;
		Результат.Вставить("TotalMemoryGb", TotalMemoryGb);
		
		Возврат Результат; 
		
	КонецФункции // ОписаниеАппаратноогоОбеспечения()
	
#КонецОбласти
	
#Область Отправка_накопленных_событий_метрики

	// Отправляет все накопленные события сервера в сервис Контур.Метрика
	// и очищает стек событий.
	//
	&НаКлиенте
	Процедура Метрики_Отправить() Экспорт
		
		Если Не ИспользоватьМетрики() Тогда
			Возврат;
		КонецЕсли;
		
		СтекСобытий = СтекСобытийМетрики();
						
		Если СтекСобытий.РазмерСтека = 0 Тогда
			Возврат;
		КонецЕсли;
		
		УдалосьОтправить = Метрика_ОтправитьСтекСобытий(СтекСобытий);
		Если Не УдалосьОтправить Тогда
			ОтключитьМетрики();
		КонецЕсли;
		
		ОчиститьСтекСобытийМетрики();
		
	КонецПроцедуры
	
	// Отправляет стек событий в сервис Контур.Метрика
	//
	// Параметры:
	//  СтекСобытий	- Структура - см. функцию НовыйСтекСобытийМетрики();
	//
	// Возвращаемое значение:
	//   Булево - Истина, если удалось отправить все события стека.
	//
	&НаКлиенте
	Функция Метрика_ОтправитьСтекСобытий(СтекСобытий) Экспорт
		
		Результат = Ложь;
		
		Если ИспользоватьМетрики() Тогда
			
			ДобавитьИдентификаторыВСобытияМетрики(СтекСобытий);
								
			Результат = ОтправитьСтекСобытийМетрики(СтекСобытий);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	// Добавляет в события стека необходимые идентификаторы сессии и клиента.
	//
	// Параметры:
	//  СтекСобытий - Структура - см. функцию НовыйСтекСобытийМетрики()
	//
	&НаКлиенте
	Процедура ДобавитьИдентификаторыВСобытияМетрики(СтекСобытий)
		
		ПараметрыСессии = ПараметрыСессии();
		
		Для каждого Элемент Из СтекСобытий.Топики Цикл
			
			МассивСобытий = Элемент.Значение;
			
			Для каждого СобытиеМетрики Из МассивСобытий Цикл
				
				Id = Строка(Новый УникальныйИдентификатор);
				
				СобытиеМетрики.Вставить("Id"				, Id);
				СобытиеМетрики.Вставить("Session"			, ПараметрыСессии.Session);
				СобытиеМетрики.Вставить("ClientInstanceId"	, ПараметрыСессии.ClientInstanceId);
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецПроцедуры
		
	// Отправляет стек событий в сервис Контур.Метрика
	//
	// Параметры:
	//  СтекСобытий - Структура - см. функцию НовыйСтекСобытийМетрики()
	//
	// Возвращаемое значение:
	//   Булево - Истина, если удалось отправить все события стека.
	//
	&НаКлиенте
	Функция ОтправитьСтекСобытийМетрики(СтекСобытий)
		
		Отказ = Ложь;
		
		Для каждого Элемент Из СтекСобытий.Топики Цикл
			
			Топик			 = Элемент.Ключ;
			МассивСобытий	 = Элемент.Значение;
			
			Если ЗначениеЗаполнено(МассивСобытий) Тогда
				
				Попытка
					
					ОтправитьМассивСобытийТопикаМетрики(МассивСобытий, Топик);
					
				Исключение
					
					ВидОперации	 = НСтр("ru = 'Отправка данных в сервис Контур.Метрика'");
					ТектОшибки	 = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					
					ЗаписатьВЛог(ВидОперации, ТектОшибки);
					
					Отказ = Истина;
					
					Прервать;
					
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Результат = Не Отказ;
		
		Возврат Результат;
		
	КонецФункции
	
	// Отправляет массив событий в сервис Контур.Метрика
	//
	// Параметры:
	//  МассивСобытий - Массив - содержит структуры, см. функцию НоваяМетрика();
	//  Топик         - Строка - топик, в который надо отправить событие;
	//
	&НаКлиенте
	Процедура ОтправитьМассивСобытийТопикаМетрики(МассивСобытий, Топик)
		
		Попытка
			
			ОтправитьСобытияМетрики(МассивСобытий, Топик);
						
		Исключение
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			
			Если МассивСобытий.Количество() > 1 Тогда
				
				// Одним пакетом события отправить не удалось,
				// поэтому попробуем их отправить по одному.
				
				Для Каждого СобытиеМетрики Из МассивСобытий Цикл
					
					ОтправитьСобытияМетрики(СобытиеМетрики, Топик);
					
				КонецЦикла;
				
			Иначе
				
				ВызватьИсключение;
				
			КонецЕсли;
			
		КонецПопытки;
		
	КонецПроцедуры
	
	// Отправляет одно или несколько событий в сервис Контур.Метрика.
	//
	// Параметры:
	//  События - Массив, Структура - см. функцию НоваяМетрика();
	//  Топик   - Строка - топик, в который надо отправить событие;
	//
	&НаКлиенте
	Процедура ОтправитьСобытияМетрики(События, Топик)
		
		ОтправляемыеСобытия = ЗначениеВМассив(События);
					
		АдресМетода		 = АдресМетодаМетрики(Топик);
		ПараметрыСервиса = ПараметрыСервиса_Контур();
		
		АдресМетода = "https://" + ПараметрыСервиса.АдресВебСервиса + АдресМетода;
										
		Заголовки = Новый Соответствие;
		
		Заголовки.Вставить("Accept"			, "application/json");
		Заголовки.Вставить("Content-Type"	, "application/json;charset=utf-8");
		
		Запись = Новый ("ЗаписьJSON");
		Запись.УстановитьСтроку();
		
		Выполнить("ЗаписатьJSON(Запись, ОтправляемыеСобытия)");
		
		ТелоЗапроса = Запись.Закрыть();
						
		Ответ = Компонента_HttpTransport(АдресМетода, Заголовки, , ТелоЗапроса, Истина);
						
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПараметрыСессии()
	
		Результат = КэшМетрики_Прочитать("ПараметрыСессииДляМетрики");
		
		Если Результат = Неопределено Тогда
			
			ИдентификаторКлиента = ПолучитьИдентификаторКлиентаНаСервере();
			ИдентификаторСессии	 = Строка(ЭтаФорма.УникальныйИдентификатор);
			
			Результат = Новый Структура;
			Результат.Вставить("Session"			, НоваяСессияМетрики(ИдентификаторСессии));
			Результат.Вставить("ClientInstanceId"	, ИдентификаторКлиента);
			
			КэшМетрики_Поместить("ПараметрыСессииДляМетрики", Результат);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
		
	&НаСервереБезКонтекста
	Функция ПолучитьИдентификаторКлиентаНаСервере()
		
		Возврат Строка(ПользователиИнформационнойБазы.ТекущийПользователь().УникальныйИдентификатор);
		
	КонецФункции
	
	&НаКлиенте
	Функция ПараметрыСервиса_Контур(BoxId = Неопределено, ИспользоватьСлужебнуюУчетнуюЗапись = Ложь)
	
		Результат = Новый Структура;
		
		АдресВебСервиса = АдресKonturAPI();
		
		Результат.Вставить("АдресВебСервиса", АдресВебСервиса);
		
		Возврат Результат;
		
	КонецФункции
	
#КонецОбласти			
		
#Область Стек_Метрики
				
	// Возвращает значение из специального кэша
	//
	// Параметры:
	//  Ключ - Строка - см. процедуру КэшМетрики_Поместить.
	//
	// Возвращаемое значение: 
	//   Произвольный - см. процедуру КэшМетрики_Поместить.
	//
	&НаКлиенте
	Функция КэшМетрики_Прочитать(Ключ) Экспорт
		
		Результат = Неопределено;
		
		Если ЗначениеЗаполнено(КэшМетрики_Клиент) Тогда
			Результат = КэшМетрики_Клиент.Получить(Ключ);
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции

	// Помещает значение в специальный кэш
	//
	// Параметры:
	//  Ключ     - Строка       - уникальный ключ помещаемого значения.
	//  Значение - Произвольный - помещаемое значение.
	//
	&НаКлиенте
	Процедура КэшМетрики_Поместить(Ключ, Значение) Экспорт
		
		Если КэшМетрики_Клиент = Неопределено Тогда
			КэшМетрики_Клиент = Новый Соответствие;
		КонецЕсли;
		
		КэшМетрики_Клиент.Вставить(Ключ, Значение);
		
	КонецПроцедуры
	
	// Помещает событие в стек для дальнейшей отправки
	//
	// Параметры:
	//  Топик   - Строка       - топик, к которому относится метрика.
	//  Метрика - Соответствие - метрика с событием.
	//
	// Возвращаемое значение:
	//   Число - количество событий, помещенных в стек.
	//
	&НаКлиенте
	Функция ПоместитьСобытиеМетрикиВСтекСобытий(Топик, Метрика)
		
		СтекСобытий		= СтекСобытийМетрики();
		МассивСобытий	= СтекСобытий.Топики[Топик];
		
		МассивСобытий.Добавить(Метрика);
		
		СтекСобытий.РазмерСтека = СтекСобытий.РазмерСтека + 1;
		
		Результат = СтекСобытий.РазмерСтека;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция СтекСобытийМетрики()
		
		Результат = КэшМетрики_Прочитать("СтекСобытийМетрикиНаКлиенте");
		
		Если Результат = Неопределено Тогда
			
			Результат = НовыйСтекСобытийМетрики();
			
			КэшМетрики_Поместить("СтекСобытийМетрикиНаКлиенте", Результат);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОчиститьСтекСобытийМетрики()
		
		СтекСобытий = СтекСобытийМетрики();
		
		Для Каждого Элемент Из СтекСобытий.Топики Цикл
			
			Топик			 = Элемент.Ключ;
			МассивСобытий	 = СтекСобытий.Топики[Топик];
			
			МассивСобытий.Очистить();
			
		КонецЦикла;
		
		СтекСобытий.РазмерСтека = 0;
					
	КонецПроцедуры
	
#КонецОбласти
	

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
//} КОНТУР.МЕТРИКИ


// Хранит ссылки вызываемые ресурсы
//
// Возвращаемое значение:
//	Структура	
&НаКлиенте
Функция АдресаИнтернетРесурсов() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("СтраницаСменыПароля"		, "https://auth.kontur.ru/recover");
	Результат.Вставить("СправкаУстановкаСертификата", "https://www.kontur-extern.ru/support/faq/34/62");   
	Результат.Вставить("ПортальнаяАвторизация"		, "https://auth.kontur.ru/");
	Результат.Вставить("СтраницаДиагностики"		, "https://help.kontur.ru/diadoc");
	Результат.Вставить("ОнлайнКонсультант"			, "https://api.kontur.ru/chat-widget");
	Результат.Вставить("УдаленноеПодключение"		, "https://help.kontur.ru/vnc");
	
	Результат.Вставить("Инструкция_ПереотправкаДокументов"	, "https://wiki.diadoc.ru/pages/viewpage.action?pageId=7668876");
	Результат.Вставить("Инструкция_НетДокументовНаОтправку"	, "https://wiki.diadoc.ru/pages/viewpage.action?pageId=12879083");
	
	Возврат Результат;
	
КонецФункции //АдресаИнтернетРесурсов()


	МаксимальноеКоличествоЗаданий = 10;

#КонецЕсли