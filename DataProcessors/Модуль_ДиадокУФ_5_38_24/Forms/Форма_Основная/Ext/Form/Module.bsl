#Если ВебКлиент Тогда
	
	ВызватьИсключение НСтр("ru = 'Недопустимый режим работы (Веб-клиент).'");
	
#Иначе
	
	#Область ПЕРЕМЕННЫЕ_МОДУЛЯ
	
	&НаКлиенте
	Перем СтруктураИнтервалов Экспорт;
	
	&НаКлиенте
	Перем ДатаПоследнегоЗаполненияСписокВыбораКонтрагентID;
	
	&НаКлиенте
	Перем АктуальнаяВерсияМодуля;
	
	#КонецОбласти
	
	#Область ПЕРЕМЕННЫЕ_ПЛАТФОРМЫ
	
	&НаКлиенте
	Перем Платформа Экспорт;
	
	&НаСервере
	Перем Кеш_ОбработкаОбъект;
	
	#КонецОбласти
	
	#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ПЛАТФОРМЫ
	
	&НаКлиенте
	Функция МетодКлиента(ИмяМодуля= "", ИмяМетода, 
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
		
		Если Платформа.ВладелецФормы = Неопределено Тогда
			Платформа.ВладелецФормы = ЭтаФорма;
		КонецЕсли;
		
		Возврат Платформа.МетодКлиента(ИмяМодуля, ИмяМетода,
		Параметр0, Параметр1, Параметр2, Параметр3, Параметр4, 
		Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
		
	КонецФункции
	
	&НаКлиенте
	Функция МетодСервераБезКонтекста(ИмяМодуля= "", ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
		
		Возврат Платформа.МетодСервераБезКонтекста(ИмяМодуля, ИмяМетода,
		Параметр0, Параметр1, Параметр2, Параметр3, Параметр4, 
		Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
		
	КонецФункции
	
	&НаСервере
	Функция МетодСервера(Знач ИмяМодуля= "", Знач ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
		
		Возврат ОбработкаОбъект().МетодСервера(ИмяМодуля, ИмяМетода, 
		Параметр0, Параметр1, Параметр2, Параметр3, Параметр4, 
		Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
		
	КонецФункции
	
	&НаСервере
	Функция ОбработкаОбъект()
		
		Результат = Кеш_ОбработкаОбъект;
		
		Если Результат = Неопределено Тогда
			
			Если ЗначениеЗаполнено(Объект.ПараметрыКлиентСервер) Тогда
				
				СтруктураОбработки = ПолучитьИзВременногоХранилища(Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
				
				Если СтруктураОбработки <> Неопределено Тогда
					Результат = СтруктураОбработки.ОбработкаОбъект;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Результат = Неопределено Тогда
				
				Результат = РеквизитФормыВЗначение("Объект");
				
				Попытка
					ПоместитьВоВременноеХранилище(Новый Структура("ОбработкаОбъект", Результат), Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
				Исключение КонецПопытки;
				
			Иначе
				Результат.ПараметрыКлиентСервер = Объект.ПараметрыКлиентСервер;
			КонецЕсли;
			
			Кеш_ОбработкаОбъект = Результат;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	
	&НаСервере
	Процедура ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ВызовИзРасширения"			, Параметры.Свойство("ВызовИзРасширения"));
		ДополнительныеПараметры.Вставить("КаталогМодулейСервера"		, КаталогМодулейСервера);
		ДополнительныеПараметры.Вставить("РежимОтладкиСервера"	 		, РежимОтладкиСервера);
		ДополнительныеПараметры.Вставить("ОтправкаФайловСтарыйИнтерфейс", ОтправкаФайловСтарыйИнтерфейс);
		
		Объект.ПараметрыКлиентСервер = ОбработкаОбъект().ИнициализироватьОбщийКонтекстКлиентСервер(УникальныйИдентификатор, ДополнительныеПараметры);
		
		// ПараметрыКлиент - это одноразовый транспорт для передачи значений в Платформа.ПараметрыКлиент.
		Объект.ПараметрыКлиентСервер.Вставить("ПараметрыКлиент", Новый Структура);
		Объект.ПараметрыКлиентСервер.ПараметрыКлиент.Вставить("ПутьКФормам", 		 ОбработкаОбъект().Метаданные().ПолноеИмя() + ".Форма.");
		Объект.ПараметрыКлиентСервер.ПараметрыКлиент.Вставить("СловарьWL",			 ОбработкаОбъект().ПолучитьСловарь());
		Объект.ПараметрыКлиентСервер.ПараметрыКлиент.Вставить("УстановкаРасширения", ОбработкаОбъект().ПроверитьНеобходимостьУстановкиРасширения());
		
		// Изменять ПараметрыКлиентСервер можно ТОЛЬКО на клиенте!
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ИнициализироватьПлатформу() Экспорт
		
		Платформа= ПолучитьФорму(Объект.ПараметрыКлиентСервер.ПараметрыКлиент.ПутьКФормам+"Платформа",,ЭтаФорма, Истина);
		Платформа.ИнициализироватьПлатформу();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПлатформаПриЗакрытии(ЗавершениеРаботы)
		
		Если НЕ Объект.ПараметрыКлиентСервер.ВызовИзРасширения Тогда
			
			Платформа.ПриЗакрытииФормыОбработки(ЭтаФорма, Истина, ЗавершениеРаботы);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	#КонецОбласти
	
	//{ УПРАВЛЕНИЕ ВЕРСИЯМИ
	
	&НаКлиенте
	Процедура ВыбратьВручнуюМодульИнтеграцииЕслиНеобходимо(Отказ)
		
		Если Не ЗначениеЗаполнено(Объект.ПараметрыКлиентСервер.ИмяФормыИнтеграции) Тогда
			
			Отказ = Истина;
			
			ТекстОшибки = СтрШаблон(НСтр(
			"ru = 'Текущая конфигурация программы %1 не поддерживается!
			|В окне техподдержки, можно принудительно выбрать модуль интеграции'")
			, Платформа.ПараметрыКлиент.СинонимКонфигурации);
			
			ПараметрыФормы = НовыеПараметрыФормыВыводаОшибки();
			ПараметрыФормы.ОписаниеОшибки = НСтр("ru = 'Модуль интеграции не определен!'");
			ПараметрыФормы.Подробности = ТекстОшибки;
			
			ОткрытьФормуВыводаОшибки(ПараметрыФормы, Ложь, "ОбработчикОткрытьФормуТехПоддержки");
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПроверитьНаличиеМодуляИнтеграции(Отказ)
		
		Если Не ЗначениеЗаполнено(Объект.ПараметрыКлиентСервер.ИмяФормыИнтеграции) Тогда
			
			Отказ = Истина;
			
			ТекстОшибки = СтрШаблон(НСтр(
			"ru = 'Текущая конфигурация %1 не поддерживается!
			|Обработка будет закрыта.
			|Список поддерживаемых конфигураций
			|%2'")
			, Платформа.ПараметрыКлиент.СинонимКонфигурации
			, ПолучитьТекстПоддерживаемыхКонфигураций());
			
			ПараметрыФормы = НовыеПараметрыФормыВыводаОшибки();
			ПараметрыФормы.ОписаниеОшибки = НСтр("ru = 'Ошибка поддержки конфигурации'");
			ПараметрыФормы.Подробности = ТекстОшибки;
			
			ОткрытьФормуВыводаОшибки(ПараметрыФормы, Истина);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьТекстПоддерживаемыхКонфигураций() Экспорт
		
		Возврат 
		"    - Бухгалтерия предприятия, ред 3.0
		|    - Бухгалтерия государственного учреждения, ред 2.0
		|    - Управление торговлей, ред 11.4
		|    - Комплексная автоматизация, ред 2.4
		|    - Управление предприятием (ERP), ред 2.4
		|    - Управление небольшой фирмой, ред 1.6";
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьТекущуюВерсиюПлатформы() Экспорт
		
		СистемнаяИнформация=	Новый СистемнаяИнформация;
		Возврат СистемнаяИнформация.ВерсияПриложения;
		
	КонецФункции
	
	//} УПРАВЛЕНИЕ ВЕРСИЯМИ
	
	//{ СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
	
	&НаКлиенте
	Процедура СообщитьПользователю(ТекстСообщения)
		
		МетодКлиента("Модуль_Клиент", "СообщитьПользователю", ТекстСообщения);
		
	КонецПроцедуры
	
	&НаКлиентеНаСервереБезКонтекста
	Функция НайтиСтрокиВМассивеСтруктур(МассивСтруктур, ПараметрыОтбора) //вместо НайтиСтроки таблицы значений
		Результат = Новый Массив;
		Для Каждого Стр из МассивСтруктур Цикл
			БылоНесоответствие = Ложь;
			Для Каждого Стр1 из ПараметрыОтбора Цикл
				Если СокрЛП(ПараметрыОтбора[Стр1.Ключ]) <>  СокрЛП(Стр[Стр1.Ключ]) Тогда
					БылоНесоответствие = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если БылоНесоответствие = Ложь Тогда
				Результат.Добавить(Стр);
			КонецЕсли;
			
		КонецЦикла;
		Возврат Результат;
	КонецФункции
	
	&НаСервере
	Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
		ЗаполнитьФормуИзСохраненныхНастроек();
		ОбновитьОпции();
		
		ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
		
		УстановитьОбязательныеСвойстваЭлементов();
		
		ЗаполнитьРасположениеПодключаемогоМодуля();
		ЗаполнитьРеквизитыЗначениямиНастроек();
		
		НастроитьЭлементыФормы();
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьПараметрыВыборки(Режим =  Неопределено)
		
		ПараметрыВыборки=	Новый Структура();
		
		ПараметрыВыборки.Вставить("Направление", ПолучитьЗначениеНаправления());
		ПараметрыВыборки.Вставить("СписокДокументооборота", ?(Режим = "Подписание", 2, ?(ПустаяСтрока(СостояниеДокументооборота), 0, Число(СостояниеДокументооборота))));
		ПараметрыВыборки.Вставить("ТипыДокументов", ТипыДокументов);
		ПараметрыВыборки.Вставить("ПодразделениеID", ПодразделениеID);
		ПараметрыВыборки.Вставить("ВключатьПодчиненныеПодазделения", ВключатьПодчиненныеПодазделения);
		ПараметрыВыборки.Вставить("ОтбиратьПоДатеДокумента", ?(ПараметрыВыборки.Направление = "Inbound", СтруктураИнтервалов.Полученные.ОтбиратьПоДатеДокумента, СтруктураИнтервалов.Отправленные.ОтбиратьПоДатеДокумента));
		
		Возврат ПараметрыВыборки;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПоказатьДокументВДиадоке(ДокументДиадока) Экспорт
		
		МетодКлиента("Модуль_Клиент","ПоказатьДокументВДиадоке", ДокументДиадока.OrganizationId, ДокументДиадока.DocumentId);
		
	КонецПроцедуры 
	
	&НаСервереБезКонтекста
	Функция МассивСтруктур_2_ТаблицаЗначений(МассивСтруктур)
		СтрЗаменТипов = Новый Структура("Справочник,Документ,Перечисление","СправочникСсылка","ДокументСсылка","ПеречислениеСсылка");
		Результат = Новый ТаблицаЗначений;
		Если МассивСтруктур = Неопределено Или МассивСтруктур.Количество() = 0 Тогда
			Возврат Результат;
		Иначе 
			Образец = МассивСтруктур[0];
			Для Каждого  Стр из Образец Цикл
				Результат.Колонки.Добавить(Стр.Ключ,ПолучитьОписаниеТипаЗначение(Стр.Значение) );
			КонецЦикла;
		КонецЕсли;
		
		Для Каждого Стр Из МассивСтруктур Цикл
			СтрТ = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(СтрТ, Стр);		
		КонецЦикла;
		Возврат Результат;
	КонецФункции
	
	&НаСервереБезКонтекста
	Функция ПолучитьОписаниеТипаЗначение(ЗначениеЭл)
		СтрЗаменТипов = Новый Структура("Справочник,Документ,Перечисление","СправочникСсылка","ДокументСсылка","ПеречислениеСсылка");
		
		ТипКолонки = ТипЗнч(ЗначениеЭл);
		МассивТ= Новый массив;
		МД = метаданные.найтипотипу(типзнч(ЗначениеЭл));
		Если МД = Неопределено Тогда
			ПолноеИмяТипа = Строка(ТипЗнч(ЗначениеЭл));
		Иначе
			ПолноеИмяТипа = МД.ПолноеИмя();
			Для Каждого стрхх из СтрЗаменТипов цикл
				Если Найти(ПолноеИмяТипа,стрхх.Ключ) <> 0 Тогда
					ПолноеИмяТипа = СтрЗаменить(ПолноеИмяТипа, стрхх.Ключ, стрхх.Значение);
					прервать;
				КонецЕсли;
			конеццикла;
		КонецЕсли;
		Если ПолноеИмяТипа  = "Не определено" ИЛИ ПолноеИмяТипа = "Форматированная строка" Тогда
			ПолноеИмяТипа = "Строка"; //проверить - заглушка пока!!!
		КонецЕсли;
		
		Если найти( ПолноеИмяТипа, "ДокументСсылка")>0 Тогда
			возврат Документы.ТипВсеСсылки();
		иначе;	
			возврат  новый ОписаниеТипов(ПолноеИмяТипа)
		КонецЕсли;	
	КонецФункции	
	
	&НаКлиенте
	Функция НовыеПараметрыФормыВыводаОшибки()
		
		Результат = Новый Структура;
		Результат.Вставить("Заголовок"		, "");
		Результат.Вставить("Подробности"	, "");
		Результат.Вставить("ОписаниеОшибки"	, "");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОткрытьФормуВыводаОшибки(Результат, ЗакрытьФорму = Ложь, ИмяОбработчика = Неопределено, ПараметрыОбработчика = Неопределено) Экспорт
		
		ПараметрыФормы = Новый Структура;
		
		Если Результат.Свойство("Заголовок") Тогда
			ПараметрыФормы.Вставить("Заголовок", Результат.Заголовок);
		Иначе
			ПараметрыФормы.Вставить("Заголовок", "Ошибка работы с модулем " + Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("ОписаниеОшибки", Результат.ОписаниеОшибки);
		ПараметрыФормы.Вставить("Подробности"	, Результат.Подробности);
		
		Если ИмяОбработчика = Неопределено Тогда
			ИмяОбработчика = "ОбработчикОткрытияФормыОшибки";
			ПараметрыОбработчика = ЗакрытьФорму;
		КонецЕсли;
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма, ИмяОбработчика, ПараметрыОбработчика);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура НачатьОбновлениеСпискаДокументов() Экспорт
		
		ТаблицаДокументовВходящих.Очистить();
		ТаблицаДокументовИсходящих.Очистить();
		ТаблицаДокументовВнутренних.Очистить();
		СписокАктивные.Очистить();
		ТаблицаЖурналаДокументов.Очистить();
		
		ПодключитьОбработчикОжидания("ОбработчикОбновлениеСпискаДокументов", 0.1, истина);
		
	КонецПроцедуры
	
	&НаСервере
	Процедура НастроитьЭлементыФормы()
		
		СловарьWL= МетодСервера(,"ПолучитьСловарь");
		
		Команды.ПолученныеДиадокКоманднаяПанельОткрытьКарточкуДокумента.Подсказка= "Открыть карточку документа, отправленного через " + СловарьWL.НаименованиеСистемы;
		
		Команды.ДобавщитьЯщикДиадок.Подсказка= 		"Добавщить ящик " + СловарьWL.НаименованиеСистемы;
		Команды.УдалитьЯщикДиадок.Подсказка= 		"Удалить ящик " + СловарьWL.НаименованиеСистемы;
		Команды.КонтрагентыНайтиВДиадоке.Подсказка= "Контрагенты найти в " + СловарьWL.КраткоеНаименованиеСистемыПредложныйПадеж;
		
		Элементы.ДекорацияТехподдержка.Заголовок= 	"Техподдержка (v " + МетодСервера(,"ВерсияОбработки") + ")";
		
		Элементы.УдалитьУстановитьРасширениеКонфигурации.Видимость= НЕ  Объект.ПараметрыКлиентСервер.ПараметрыКлиент.УстановкаРасширения.ОтсутствуютПраваАдминистратора
		И ( Объект.ПараметрыКлиентСервер.ПараметрыКлиент.УстановкаРасширения.НеобходимоУстановить
		ИЛИ Объект.ПараметрыКлиентСервер.ПараметрыКлиент.УстановкаРасширения.РасширениеУжеУстановлено
		ИЛИ Объект.ПараметрыКлиентСервер.ПараметрыКлиент.УстановкаРасширения.БылОтказОтУстановки);
		
		Элементы.ТаблицаДокументовВходящихКомплекснаяОбработкаТорговыеСети.Видимость = Ложь;
		
		Элементы.СтраницаЖурналыДокументов.Видимость = ЖурналыДокументовПоказать;
		
		УстановитьУсловноеОформлениеФорматДокументовНаОтправку(Элементы.СписокПочтовыхЯщиков, "СписокПочтовыхЯщиков");
		УстановитьУсловноеОформлениеФорматДокументовНаОтправку(Элементы.СписокАктивные, "СписокАктивные");
		УстановитьУсловноеОформлениеЗаблокирована(Истина);
		УстановитьУсловноеОформлениеЗаблокирована(Ложь);
		
		//УстановитьВидимостьКнопкиОбновленияМодуля(Ложь);		
		УстановитьВидимостьКнопкиОбновленияМодуля(Истина);
		
	КонецПроцедуры
	
	&НаСервере
	Процедура УстановитьУсловноеОформлениеФорматДокументовНаОтправку(ЭлементФормы, ИмяРеквизитаТаблицы)
		
		Для Каждого ВозможныйФормат Из МетодСервера(, "ФорматыДокументовНаОтправку") Цикл
			
			ЭлементУО = УсловноеОформление.Элементы.Добавить();
			
			ПолеЭлемента = ЭлементУО.Поля.Элементы.Добавить();
			ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.ПутьКДанным + "ФорматДокументовНаОтправку");
			
			ОтборЭлемента = ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных(ИмяРеквизитаТаблицы + ".ФорматДокументовНаОтправку");
			ОтборЭлемента.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
			ОтборЭлемента.ПравоеЗначение 	= ВозможныйФормат.Ключ;
			
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", ВозможныйФормат.Значение);
			
			Если МетодСервера(, "ФорматДокументовНаОтправкуУстаревшийФормат", ВозможныйФормат.Ключ) Тогда
				ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура УстановитьУсловноеОформлениеЗаблокирована(Заблокирована)
		
		//оформление колонки Заблокирована
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		
		ПолеЭлемента = ЭлементУО.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("СписокПочтовыхЯщиковЗаблокирована");
		
		ОтборЭлемента = ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("СписокПочтовыхЯщиков.Заблокирована");
		ОтборЭлемента.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение 	= Заблокирована;
		
		Если Заблокирована Тогда 
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "Оплатите модуль");
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);
		Иначе
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "Оплачено");
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Черный);
		КонецЕсли;
		
		//оформление строки
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		
		ПолеЭлемента = ЭлементУО.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("СписокПочтовыхЯщиков");
		
		ОтборЭлемента = ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("СписокПочтовыхЯщиков.Заблокирована");
		ОтборЭлемента.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение 	= Заблокирована;
		
		Если Заблокирована Тогда 
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Серый);
		Иначе
			ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Черный);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура УправлениеФормой()
		
		Если НЕ Объект.ПараметрыКлиентСервер.ВызовИзРасширения Тогда
			Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаПолученныеДиадок;
		КонецЕсли;
		
		ВключатьПодчиненныеПодазделения= истина;
		
		Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БГУ20" Тогда
			Элементы.СтраницаЖурналыДокументов.Доступность=	Ложь;
		КонецЕсли;
		
		НастроитьЭлементыПодключаемогоМодуля();
		
		НастроитьЭлементУдалитьУстановитьРасширениеКонфигурации();
		
	КонецПроцедуры
	
	&НаСервере
	Процедура ЗаполнитьРасположениеПодключаемогоМодуля()
		
		ИспользоватьПодключаемыйМодуль	 = Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.ИспользоватьМодуль;
		РасположениеПодключаемогоМодуля	 = Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.РасположениеМодуля;
		РежимСовместимостиФорматов155и820= Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.РежимСовместимостиФорматов155и820;
		
		Если РасположениеПодключаемогоМодуля = "НаДиске" Тогда
			
			ПутьКПодключаемомуМодулюДиск = Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.ПутьКМодулю;
			
		ИначеЕсли РасположениеПодключаемогоМодуля = "ВБазеДанных" Тогда
			
			ПутьКПодключаемомуМодулюСсылка = Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.ПутьКМодулю;
			
		ИначеЕсли РасположениеПодключаемогоМодуля = "ВСоставеКонфигурации" Тогда
			
			МетаданныеОбработки = Метаданные.Обработки.Найти(Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.ПутьКМодулю);
			
			Если МетаданныеОбработки <> Неопределено Тогда
				
				Элементы.ПутьКПодключаемомуМодулюИмяОбработки.СписокВыбора.Добавить(Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.ПутьКМодулю, МетаданныеОбработки.Представление());
				
				ПутьКПодключаемомуМодулюИмяОбработки = Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.ПутьКМодулю;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура ЗаполнитьРеквизитыЗначениямиНастроек()
		
		ПутьКМодулюВСправочнике1С	= Объект.ПараметрыКлиентСервер.ПутьКМодулюВСправочнике1С;
		ЗапретитьРаботуСМетриками	= НЕ Объект.ПараметрыКлиентСервер.Метрики.ИспользоватьМетрики;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура НастроитьЭлементыПодключаемогоМодуля()
		
		Если НЕ Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.ИспользоватьМодуль Тогда
			
			Элементы.СтраницыПутьКПодключаемомуМодулю.ТекущаяСтраница	 = Элементы.СтраницаПутьКПодключаемомуМодулюНеИспользуется;
			Элементы.СтраницыПодключитьПМ.ТекущаяСтраница				 = Элементы.СтраницаПодключитьПМОтключен;
			
		ИначеЕсли РасположениеПодключаемогоМодуля = "НаДиске" Тогда
			
			Элементы.СтраницыПутьКПодключаемомуМодулю.ТекущаяСтраница	 = Элементы.СтраницаПутьКПодключаемомуМодулюДиск;
			Элементы.СтраницыПодключитьПМ.ТекущаяСтраница				 = Элементы.СтраницаПодключитьПМ;
			
		ИначеЕсли РасположениеПодключаемогоМодуля = "ВБазеДанных" Тогда
			
			Элементы.СтраницыПутьКПодключаемомуМодулю.ТекущаяСтраница	 = Элементы.СтраницаПутьКПодключаемомуМодулюСсылка;
			Элементы.СтраницыПодключитьПМ.ТекущаяСтраница				 = Элементы.СтраницаПодключитьПМ;
			
		ИначеЕсли РасположениеПодключаемогоМодуля = "ВСоставеКонфигурации" Тогда
			
			Элементы.СтраницыПутьКПодключаемомуМодулю.ТекущаяСтраница	 = Элементы.СтраницаПутьКПодключаемомуМодулюИмяОбработки;
			Элементы.СтраницыПодключитьПМ.ТекущаяСтраница				 = Элементы.СтраницаПодключитьПМ;
			
		Иначе
			
			Элементы.СтраницыПутьКПодключаемомуМодулю.ТекущаяСтраница	 = Элементы.СтраницаПутьКПодключаемомуМодулюНеВыбрано;
			Элементы.СтраницыПодключитьПМ.ТекущаяСтраница				 = Элементы.СтраницаПодключитьПМОтключен;
			
		КонецЕсли;
		
		Элементы.ПереключательРасположенияПодключаемогоМодуля.Доступность	 = ИспользоватьПодключаемыйМодуль;
		Элементы.ГруппаРасположениеПодключениеПМ.Доступность				 = ИспользоватьПодключаемыйМодуль;
		
		Элементы.РежимСовместимостиФорматов155и820.Видимость = ИспользоватьПодключаемыйМодуль;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура НастроитьЭлементУдалитьУстановитьРасширениеКонфигурации()
		
		Если Элементы.УдалитьУстановитьРасширениеКонфигурации.Видимость Тогда
			
			Если Платформа.ПараметрыКлиент.УстановкаРасширения.РасширениеУжеУстановлено Тогда
				Элементы.УдалитьУстановитьРасширениеКонфигурации.Заголовок= "Удалить расширение конфигурации";
			Иначе	
				Элементы.УдалитьУстановитьРасширениеКонфигурации.Заголовок= "Установить расширение конфигурации";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура РасположенияПодключаемогоМодуляПриИзменении()
		
		Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.РасположениеМодуля	 = РасположениеПодключаемогоМодуля;
		Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.ПутьКМодулю			 = ПутьКМодулюПМ();
		
		МетодКлиента( , "СинхронизироватьПараметрыКлиентСервера", Объект.ПараметрыКлиентСервер);
		
		МетодСервераБезКонтекста( ,"СохранитьНастройкиПодключаемогоМодуля");
		МетодСервераБезКонтекста( ,"СброситьКэшМодулей");
		
	КонецПроцедуры
	
	Функция ПутьКМодулюПМ()
		
		Результат = Неопределено;
		
		Если РасположениеПодключаемогоМодуля = "НаДиске" Тогда
			
			Результат = ПутьКПодключаемомуМодулюДиск;
			
		ИначеЕсли РасположениеПодключаемогоМодуля = "ВБазеДанных" Тогда
			
			Результат = ПутьКПодключаемомуМодулюСсылка;
			
		ИначеЕсли РасположениеПодключаемогоМодуля = "ВСоставеКонфигурации" Тогда
			
			Результат = ПутьКПодключаемомуМодулюИмяОбработки;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьЗначениеНаправления()
		
		Если Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаВнутренниеДиадок Тогда
			Возврат "OutboundWaitingForSenderSignature";
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаОтправленныеДиадок Тогда
			Возврат "Outbound";
		Иначе
			Возврат "Inbound";
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПолучитьТекущийПериод()
		
		Направление=	ПолучитьЗначениеНаправления();
		
		Возврат Новый Структура("ДатаНачала, ДатаОкончания, ОтбиратьПоДатеДокумента", 
		?(Направление = "Inbound", СтруктураИнтервалов.Полученные.ДатаНачала   , СтруктураИнтервалов.Отправленные.ДатаНачала),
		?(Направление = "Inbound", СтруктураИнтервалов.Полученные.ДатаОкончания, СтруктураИнтервалов.Отправленные.ДатаОкончания),
		?(Направление = "Inbound", СтруктураИнтервалов.Полученные.ОтбиратьПоДатеДокумента, СтруктураИнтервалов.Отправленные.ОтбиратьПоДатеДокумента));
		
	КонецФункции
	
	&НаСервере
	Функция РасшифровкаТекущегоСостоянияВзаимоотношенийСервер(ТекущийСтатус)
		Возврат МетодСервера(,"РасшифровкаТекущегоСостоянияВзаимоотношений", ТекущийСтатус);
	КонецФункции
	
	&НаКлиенте
	Процедура ОтправитьПринятьПриглашениеКонтрагенту(ПараметрыПриглашения, ТочкаВызова = "") Экспорт
		
		Контрагент = ПараметрыПриглашения.ТекущиеДанные;
		
		Organization	= МетодКлиента("Модуль_Клиент","ПолучитьЯщикДиадокДляОрганизации", Организация);
		Результат		= МетодКлиента("Модуль_Клиент", "ОтправитьПринятьПриглашениеКонтрагенту", Organization, Контрагент.ID, Контрагент.ИНН, ПараметрыПриглашения.Комментарий, ПараметрыПриглашения.ПутьКФайлу);
		
		Если Результат = Истина Тогда
			
			Counteragent=	Organization.GetCounteragentById(Контрагент.ID);
			
			СтарыйСтатус 	= ПараметрыПриглашения.ТекущиеДанные.ТекущийСтатус;
			НовыйСтатус 	= Counteragent.GetStatus();
			
			Если 	 НовыйСтатус = СтарыйСтатус								// это тот случай, когда сервер ДД еще не успел изменить статус контрагента, поэтому нам вернулся старый
				И НЕ ТочкаВызова = "ГрупповаяОбработкаКонтрагентов" Тогда	// при закрытии формы групповой обработки статусы обновятся
				
				Если СтарыйСтатус = "InvitesMe" Тогда
					ТекстСообщения = "Приглашение от контрагента " + Контрагент.Контрагент + " принято.";
				Иначе
					ТекстСообщения = "Приглашение контрагенту " + Контрагент.Контрагент + " отправлено.";
				КонецЕсли;
				
				ТекстСообщения = ТекстСообщения + "  
				|Для обновления статуса контрагента нажмите кнопку ""Обновить список"".";
				
				ПоказатьПредупреждение(, ТекстСообщения, , Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
				
			КонецЕсли;
			
			ПараметрыПриглашения.ТекущиеДанные.ТекущийСтатус			= НовыйСтатус;
			ПараметрыПриглашения.ТекущиеДанные.ТекущийСтатусРасшифровка	= РасшифровкаТекущегоСостоянияВзаимоотношенийСервер(Контрагент.ТекущийСтатус);
			
			СписокАктивныеПриАктивизацииСтроки(Элементы);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура УстановитьНаименованиеСистемы()
		
		НовыйЗаголовок = Платформа.ПараметрыКлиент.СловарьWL.Заголовок + ?(РежимОтладкиСервера, " (Режим отладки)", "");
		
		ЭтаФорма.Заголовок = НовыйЗаголовок;
		
		Элементы.СписокАктивныеКонтрагентыНайтиВДиадоке.Заголовок = "Найти контрагентов в " + Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыПредложныйПадеж;
		Элементы.СписокЯщиковДиадока.Заголовок = "Список ящиков " + Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыРодительныйПадеж;
		Элементы.СписокПочтовыхЯщиков.ПодчиненныеЭлементы.СписокПочтовыхЯщиковнаименованиеЯщика.Заголовок = "Наименование ящика в " + Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемыПредложныйПадеж; 
		
		ОбновитьЗаголовокИнтервала("Полученные");
		ОбновитьЗаголовокИнтервала("Отправленные");
		ОбновитьЗаголовокИнтервалаЖурналаДокументов();
		
		Если Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы <> "Диадок" Тогда
			ЗаменитьКартинкуЖурналовДокументов();
		КонецЕсли;
		
	КонецПроцедуры // ЗаполнитьЗаголовкиФормы()
	
	&НаКлиенте
	Процедура ЗаменитьКартинкуЖурналовДокументов()
		
		ТаблицаДокументовЕстьВДиадоке = ЭтаФорма.Элементы.ТаблицаДокументовРеализацияТоваровУслуг.ПодчиненныеЭлементы.ТаблицаДокументовЕстьВДиадоке;
		ТаблицаДокументовЕстьВДиадоке.КартинкаЗначений = БиблиотекаКартинок.ЗеленаяГалка;
		
	КонецПроцедуры // ()
	
	&НаКлиенте
	Процедура УстановитьТекущуюСтраницуПанелейОтбораПоШагу(Элемент, Шаг)
		
		ПозицияРазделителя= Найти(Элемент.Родитель.Имя, "_");
		
		Если ПозицияРазделителя > 0 Тогда
			
			НовыйНомерТекущейСтраницы= Число(Сред(Элемент.Родитель.Имя, ПозицияРазделителя + 1)) + Шаг;
			
			МаскаИмениЭлемента= Лев(Элемент.Родитель.Имя, ПозицияРазделителя - 1);
			
			НайденнаяСтаница= Элементы.Найти(МаскаИмениЭлемента + "_" + НовыйНомерТекущейСтраницы);
			
			Если НайденнаяСтаница <> Неопределено И НайденнаяСтаница.Родитель = Элемент.Родитель.Родитель Тогда
				Элемент.Родитель.Родитель.ТекущаяСтраница= Элементы.Найти(МаскаИмениЭлемента + "_" + НовыйНомерТекущейСтраницы);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСписокВыбораКонтрагентID(Элемент)
		
		Если Элемент.СписокВыбора.Количество() > 1 И ТекущаяДата() - ДатаПоследнегоЗаполненияСписокВыбораКонтрагентID < 10 Тогда
			Возврат;
		КонецЕсли;
		
		Элемент.СписокВыбора.Очистить();
		ДобавленныеЗначения = Новый Соответствие;
		
		СписокОрганизаций = ОрганизацииТекущегоКонтекста();
		
		Для Каждого ТекОрганизация ИЗ СписокОрганизаций Цикл 
			
			CounteragentList = МетодКлиента("Модуль_Клиент", "GetCounteragentListByStatus", ТекОрганизация.Значение, "IsMyCounteragent");
			
			Если CounteragentList <> Неопределено Тогда
				
				ВГраница = CounteragentList.Count - 1;
				
				Для ИндексЦикла = 0 ПО ВГраница Цикл
					
					Counteragent = CounteragentList.GetItem(ИндексЦикла);
					
					Если ДобавленныеЗначения[Counteragent.id] <> Истина Тогда 
						Элемент.СписокВыбора.Добавить(Counteragent.id, Counteragent.Name);
						ДобавленныеЗначения.Вставить(Counteragent.id, Истина);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;	
		
		ДобавленныеЗначения = Неопределено;
		Элемент.СписокВыбора.СортироватьПоПредставлению();
		
		ДатаПоследнегоЗаполненияСписокВыбораКонтрагентID = ТекущаяДата();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСписокВыбораСтатусВзаимоотношений(Элемент)
		
		Если Элемент.СписокВыбора.Количество() > 0 Тогда
			Возврат;
		КонецЕсли;
		
		Элемент.СписокВыбора.Добавить("IsMyCounteragent", "Партнерские отношения");
		Элемент.СписокВыбора.Добавить("InvitesMe"		, "Получен запрос");
		Элемент.СписокВыбора.Добавить("IsInvitedByMe"	, "Ожидается ответ");
		Элемент.СписокВыбора.Добавить("Rejected"		, "Заблокированные");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСписокВыбораТипыДокументов(Элемент)
		
		Если Элемент.СписокВыбора.Количество() > 1 Тогда
			Возврат;
		КонецЕсли;
		
		ПустаяПиктограмма= МетодКлиента("Модуль_Клиент", "ЭДО_БиблиотекаКартинок").ПустаяПиктограмма;
		
		Элемент.СписокВыбора.Очистить();
		
		Элемент.СписокВыбора.Добавить("1",  "Накладные");
		Элемент.СписокВыбора.Добавить("11", "Торг-12"												, , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("12", "Акт"													, , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("13", МетодКлиента("Модуль_Клиент", "ТипДокументаУПД", "ДОП")	, , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("14", МетодКлиента("Модуль_Клиент", "ТипДокументаУКД", "ДИС")	, , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("15", МетодКлиента("Модуль_Клиент", "ТипДокументаИУПД","ДОП")	, , ПустаяПиктограмма);
		
		Элемент.СписокВыбора.Добавить("2",  "Счета-фактуры");
		Элемент.СписокВыбора.Добавить("21", "Оригинальные СФ"										, , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("22", "Исправленные СФ"										, , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("23", "Корректировочные СФ"									, ,	ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("24", МетодКлиента("Модуль_Клиент", "ТипДокументаУПД", "СЧФ")	, , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("25", МетодКлиента("Модуль_Клиент", "ТипДокументаУКД", "КСЧФ"), , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("26", МетодКлиента("Модуль_Клиент", "ТипДокументаИУПД","СЧФ")	, , ПустаяПиктограмма);
		
		Элемент.СписокВыбора.Добавить("5",  МетодКлиента("Модуль_Клиент", "ТипДокументаУПД", "СЧФДОП"));
		Элемент.СписокВыбора.Добавить("6",  МетодКлиента("Модуль_Клиент", "ТипДокументаУКД", "КСЧФДИС"));
		Элемент.СписокВыбора.Добавить("7",  МетодКлиента("Модуль_Клиент", "ТипДокументаИУПД","СЧФДОП"));
		
		Элемент.СписокВыбора.Добавить("3",  "Счета на оплату");
		
		Элемент.СписокВыбора.Добавить("4",  "Неформализованные");
		Элемент.СписокВыбора.Добавить("41", "Акт сверки"				, ,	ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("42", "Детализация"				, , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("43", "Договор"					, ,	ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("44", "Протокол согласования цены", , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("45", "Приглашение к ЭДО"			, ,	ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("46", "Реестр сертификатов"		, , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("48", "Дополнительное соглашение"	, , ПустаяПиктограмма);
		Элемент.СписокВыбора.Добавить("47", "Прочее"					, ,	ПустаяПиктограмма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСписокВыбораСостояниеДокументооборота(Элемент)
		
		ТолькоСогласование = (Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаВнутренниеДиадок);
		
		Если Элемент.СписокВыбора.Количество() > 1 И ТолькоСогласование Тогда
			Возврат;
		КонецЕсли;
		
		ПустаяПиктограмма= МетодКлиента("Модуль_Клиент", "ЭДО_БиблиотекаКартинок").ПустаяПиктограмма;
		
		Список = Элемент.СписокВыбора;
		
		Список.Очистить();
		
		Список.Добавить("0", "Все документы");
		
		Если НЕ ТолькоСогласование Тогда
			
			Список.Добавить("-1",	"Статус подписания");  // "-1" = группа
			Список.Добавить("1",	"Завершен"			, , ПустаяПиктограмма);
			Список.Добавить("2",	"Не завершен"		, , ПустаяПиктограмма);
			Список.Добавить("3",	"Прекращен"			, , ПустаяПиктограмма);
			
			Если Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаПолученныеДиадок Тогда  
				Список.Добавить("4", "Ожидается уточнение", , ПустаяПиктограмма);
			Иначе
				Список.Добавить("4", "Требуется уточнение", , ПустаяПиктограмма);
			КонецЕсли;
			
		КонецЕсли;
		
		Список.Добавить("-1",	"Статус согласования");  // "-1" = группа
		Список.Добавить("11",	"На согласовании"			, , ПустаяПиктограмма);
		Список.Добавить("12",	"Согласован"				, , ПустаяПиктограмма);
		Список.Добавить("13",	"Отказано в согласовании"	, , ПустаяПиктограмма);
		Список.Добавить("14",	"Отказано в подписании"		, , ПустаяПиктограмма);
		
		Если НЕ ТолькоСогласование Тогда
			
			Список.Добавить("-1",	"Статус аннулирования");  // "-1" = группа
			Список.Добавить("21",	"Ожидается аннулирование"	, , ПустаяПиктограмма);
			Список.Добавить("22",	"Требуется аннулирование"	, , ПустаяПиктограмма);
			Список.Добавить("23",	"Документ аннулирован"		, , ПустаяПиктограмма);
			Список.Добавить("24",	"Отказано в аннулировании"	, , ПустаяПиктограмма);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбновитьЗаголовок()
		
		Если НЕ Элементы.ДекорацияТекущаяСтраница.Видимость Тогда
			Возврат;
		КонецЕсли;
		
		Если Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаОтправленныеДиадок Тогда 
			Элементы.ДекорацияТекущаяСтраница.Заголовок= "Отправленные через " + Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы;
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаПолученныеДиадок Тогда 
			Элементы.ДекорацияТекущаяСтраница.Заголовок= "Полученные через " + Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы;
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаВнутренниеДиадок Тогда 
			Элементы.ДекорацияТекущаяСтраница.Заголовок= "Исходящие на согласование через " + Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы;
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаКонтрагенты Тогда
			Элементы.ДекорацияТекущаяСтраница.Заголовок= "Список контрагентов";
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаНастройка Тогда
			Элементы.ДекорацияТекущаяСтраница.Заголовок= "Настройка";
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаЖурналыДокументов Тогда
			Элементы.ДекорацияТекущаяСтраница.Заголовок= "Журналы документов (beta)";
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСписок(Команда) Экспорт
		
		Если Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаКонтрагенты тогда 
			
			//Сбрасываем Кэш со статусами контрагентов
			Платформа.ПовторноеИспользованиеСброситьЗначение("Модуль_Клиент", "GetCounteragentListByStatus", Организация, "IsMyCounteragent");
			Платформа.ПовторноеИспользованиеСброситьЗначение("Модуль_Клиент", "GetCounteragentListByStatus", Организация, "InvitesMe");
			Платформа.ПовторноеИспользованиеСброситьЗначение("Модуль_Клиент", "GetCounteragentListByStatus", Организация, "IsInvitedByMe");
			Платформа.ПовторноеИспользованиеСброситьЗначение("Модуль_Клиент", "GetCounteragentListByStatus", Организация, "Rejected");
			
			ПриОткрытииСтраницыКонтрагентов();
			
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаЖурналыДокументов тогда 
			
			СформироватьЖурналДокументов();
			
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаПолученныеДиадок тогда
			
			НачатьОбновлениеСпискаДокументов();
			
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаОтправленныеДиадок тогда
			
			НачатьОбновлениеСпискаДокументов();
			
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаВнутренниеДиадок тогда
			
			НачатьОбновлениеСпискаДокументов();
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьСтрокуДляФормыОплатыСервиса(СтрокаКонтекста)
		
		СтрокаСпискаОрганизаций = Новый Структура("Name, Inn, Kpp, ФИО");
		
		СтрокаСпискаОрганизаций.Name	= СтрокаКонтекста.ДанныеОрганизации.Name;
		СтрокаСпискаОрганизаций.Inn		= СтрокаКонтекста.ДанныеОрганизации.Inn;
		СтрокаСпискаОрганизаций.Kpp		= СтрокаКонтекста.ДанныеОрганизации.Kpp;
		СтрокаСпискаОрганизаций.ФИО		= СтрокаКонтекста.ДанныеСотрудника.ФИО;
		
		Возврат СтрокаСпискаОрганизаций;
		
	КонецФункции	
	
	&НаСервере
	Процедура ЗаполнитьФормуИзСохраненныхНастроек()
		
		ЗначенияРеквизитов = ЗначенияСохраненныхРеквизитовФормы();
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ЗначенияРеквизитов);
		
	КонецПроцедуры
	
	&НаСервере
	Функция ЗначенияСохраненныхРеквизитовФормы()
		
		Результат = СохраняемыеРеквизитыФормы();
		СохраненныеЗначения = ХранилищеСистемныхНастроек.Загрузить(ИмяФормы + "/ТекущиеДанные");
		
		Если ТипЗнч(СохраненныеЗначения) = Тип("Соответствие") Тогда
			
			Для каждого Элемент Из Результат Цикл
				
				ИмяРеквизита		 = Элемент.Ключ;
				ЗначениеРеквизита	 = СохраненныеЗначения[ИмяРеквизита];
				
				Результат[ИмяРеквизита] = ЗначениеРеквизита;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаСервере
	Функция СохраняемыеРеквизитыФормы()
		
		Результат = Новый Структура;
		Результат.Вставить("КаталогМодулейСервера");
		Результат.Вставить("РежимОтладкиСервера");
		Результат.Вставить("ОтправкаФайловСтарыйИнтерфейс");
		Результат.Вставить("ЖурналыДокументовПоказать");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаСервере
	Процедура УстановитьОбязательныеСвойстваЭлементов()
		
		// Устанавливаем свойства программно, на случай если обработку сохранят платформой младше 8.3.7
		
		СвойствоАвтоМаксимальнаяШирина = Новый Структура("АвтоМаксимальнаяШирина", Ложь);
		ЗаполнитьЗначенияСвойств(Элементы.ДекорацияТекущаяСтраница			  , СвойствоАвтоМаксимальнаяШирина);
		ЗаполнитьЗначенияСвойств(Элементы.ДекорацияТекущийПользовательВДиадоке, СвойствоАвтоМаксимальнаяШирина);
		
		ГоризонтальноеПоложениеЦентр = Новый Структура("ГоризонтальноеПоложениеПодчиненных", ГоризонтальноеПоложениеЭлемента.Центр);
		ЗаполнитьЗначенияСвойств(Элементы.ПанельОкончанияОплаченногоПериода, ГоризонтальноеПоложениеЦентр);
		
		// Устанавливаем свойства программно, на случай если обработку сохранят платформой младше 8.3.8
		
		ЗначениеСвойства = МетодСервера(,"СвойствоСистемногоПеречисления", ВидКнопкиФормы, "ГиперссылкаКоманднойПанели");
		Если ЗначениеСвойства <> Неопределено Тогда
			Элементы.ТаблицаДокументовИсходящихИнструкцияПоПереотправке.Вид = ЗначениеСвойства;
		КонецЕсли;
		
	КонецПроцедуры
	
	//}СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ	
	
	//{ ЗАПУСК
	
	//{ Инициализация
	
	&НаКлиенте
	Функция ОчиститьПеременныеМодуля()
		
		Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок= Неопределено;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПодключитьКомпонентуДиадока(Отказ)
		
		РезультатПодключения = МетодКлиента("Модуль_Клиент", "ПодключитьКомпонентуДиадока");
		
		ВерсияКомпоненты		 = РезультатПодключения.ВерсияКомпоненты;
		ТребуетсяПерезапуск		 = РезультатПодключения.ТребуетсяПерезапуск;
		ИспользуетсяРегистрация	 = РезультатПодключения.ИспользуетсяРегистрация;
		
		Если ТребуетсяПерезапуск Тогда
			
			ПараметрыФормы = НовыеПараметрыФормыВыводаОшибки();
			ПараметрыФормы.Заголовок		 = НСтр("ru = 'Обновление внешней компоненты'");
			ПараметрыФормы.ОписаниеОшибки	 = НСтр("ru = 'Для обновления внешней компоненты необходимо перезапустить программу'");
			ПараметрыФормы.Подробности		 = СтрШаблон(НСтр("ru = 'Версия новой компоненты %1'"), ВерсияКомпоненты);
			
			ОткрытьФормуВыводаОшибки(ПараметрыФормы, Истина);
			
			Отказ = Истина;
			
		КонецЕсли;
		
		Платформа.ПараметрыКлиент.ИспользуетсяРегистрацияКомпоненты = ИспользуетсяРегистрация;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПроверитьОбъектыБазыДанных(Отказ)
		
		РезультатПодготовки = МетодСервераБезКонтекста(, "ПодготовкаКонфигурацииКРаботе");
		
		Если РезультатПодготовки.Отказ Тогда
			
			Отказ = Истина;
			
			ПараметрыФормы = НовыеПараметрыФормыВыводаОшибки();
			ЗаполнитьЗначенияСвойств(ПараметрыФормы, РезультатПодготовки);
			ОткрытьФормуВыводаОшибки(ПараметрыФормы, Истина);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ИнициализироватьСтруктуруИнтервалов()
		
		Если СтруктураИнтервалов <> Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураИнтервалов = Новый Структура;
		СтруктураИнтервалов.Вставить("Полученные", НоваяСтруктураИнтервала());
		СтруктураИнтервалов.Вставить("Отправленные", НоваяСтруктураИнтервала());
		СтруктураИнтервалов.Вставить("ЖурналДокументов", НоваяСтруктураИнтервала());
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция НоваяСтруктураИнтервала()
		
		ДатаНачала		 = НачалоДня(ТекущаяДата());
		ДатаОкончания	 = КонецДня(ТекущаяДата());
		
		Результат = Новый Структура;
		Результат.Вставить("ДатаНачала", ДатаНачала);
		Результат.Вставить("ДатаОкончания", ДатаОкончания);
		Результат.Вставить("ОтбиратьПоДатеДокумента", Ложь);
		
		Возврат Результат;
		
	КонецФункции
	
	//} Инициализация
	
	&НаКлиенте
	Процедура НачатьИнициализациюМодуля()
		
		Если Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок <> Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("ИнициализироватьМодуль", 0.1, Истина);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ИнициализироватьМодуль() Экспорт
		
		Отказ = Ложь;
		
		ПроверитьОбъектыБазыДанных(Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ВыбратьВручнуюМодульИнтеграцииЕслиНеобходимо(Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ПодключитьКомпонентуДиадока(Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ИнициализироватьНовыйОбъектКомпонентыДиадока(Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ПроверитьНаличиеМодуляИнтеграции(Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		Если Не ПроверитьИнтернетПодключение() Тогда
			Возврат;
		КонецЕсли;
		
		ИнициализироватьСтруктуруИнтервалов();
		
		АвторизоватьсяИЗаполнитьКонтекст(Истина);
		
		НачатьСборСтатистики();
		
		НачатьПроверкуАктуальностиНастройкиФорматовОтправки();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура НачатьПроверкуАктуальностиНастройкиФорматовОтправки()
		
		ПодключитьОбработчикОжидания("ПроверитьАктуальностиНастройкиФорматовОтправки", 0.1, Истина);
		
	КонецПроцедуры
	
	// Проверяет настройки отправки документов по всем организациям контекста
	// и, если хотя бы у одной организации есть настройка, позволяющая отправлять
	// документы в устаревших форматах, показывает форму с текстом предупреждения
	// о необходимости изменить настройки отправки.
	//
	// Процедура вызывается при каждом открытии модуля.
	//
	&НаКлиенте
	Процедура ПроверитьАктуальностиНастройкиФорматовОтправки() Экспорт
		
		НастройкиОтправки = НастройкиОтправкиПоОрганизациям();
		
		ЕстьНеактуальныеНастройки = Ложь;
		
		Для Каждого Элемент Из НастройкиОтправки Цикл
			
			КлючНастройки = Элемент.Значение;
			
			Если ЭтоУстаревшаяНастройкаОтправки(КлючНастройки) Тогда
				ЕстьНеактуальныеНастройки = Истина;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЕстьНеактуальныеНастройки Тогда
			УведомитьОСтаромФорматеОтправкиДокументов();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗапуститьМодуль()
		
		УправлениеФормой();
		ПредложитьУстановкуРасширенияКонфигурации();
		
		ПанельРежимовПриСменеСтраницы("", Элементы.ПанельРежимов.ТекущаяСтраница);
		
		МетодКлиента("Модуль_Клиент", "ЗаполнитьИдентификаторыЭДО");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаИзменитьпараметрыПодключенияПрокси(Команда)
		
		МетодКлиента( , "ОткрытьФормуОбработкиМодально",
		"НастройкаПроксиСервера",
		,
		,
		"ОбработчикОткрытияФормыНастройкаПроксиСервера",
		,
		ЭтаФорма);
		
	КонецПроцедуры
	
	&НаСервере
	Функция ПолучитьОписаниеКонфигурацииДляДиадок(ОписаниеКонтекстаРасширения="") 
		
		ОписаниеКонфигурации = Метаданные.Синоним + " ("+Метаданные.Версия+")";
		ОписаниеКонфигурации = ОписаниеКонфигурации + " extention: "+ОписаниеКонтекстаРасширения;
		
		Возврат ОписаниеКонфигурации;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ИнициализироватьНовыйОбъектКомпонентыДиадока(Отказ)
		
		Попытка
			
			ОбъектКомпоненты = МетодКлиента("Модуль_Клиент", "НовыйОбъектКомпонентыДиадока");
			
		Исключение
			
			Отказ = Истина;
			
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			
			ПараметрыФормы = НовыеПараметрыФормыВыводаОшибки();
			ПараметрыФормы.ОписаниеОшибки	 = НСтр("ru = 'Ошибка создания объекта внешней компоненты'");
			ПараметрыФормы.Подробности		 = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			ОткрытьФормуВыводаОшибки(ПараметрыФормы, Истина);
			
			Возврат;
			
		КонецПопытки;
		
		КонтекстРасширения	 = Платформа.ПараметрыКлиент.УстановкаРасширения.КонтекстРасширения;
		ОписаниеКонфигурации = ПолучитьОписаниеКонфигурацииДляДиадок(КонтекстРасширения);
		
		ОбъектКомпоненты.OneSConfigaration = ОписаниеКонфигурации;
		
		КонтекстСервера = НовыйКонтекстСервераДиадок();
		КонтекстСервера.DiadocInvoiceAPI = ОбъектКомпоненты;
		Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок = КонтекстСервера;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция НовыйКонтекстСервераДиадок()
		
		Результат = Новый Структура;
		Результат.Вставить("DiadocInvoiceAPI");
		Результат.Вставить("DiadocConnection");
		Результат.Вставить("ПредставлениеПользователя");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПроверитьИнтернетПодключение()
		
		Отказ = Ложь;
		
		КонтекстСервера	 = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок;
		ОбъектКомпоненты = КонтекстСервера.DiadocInvoiceAPI;
		
		Если Не ЗначениеЗаполнено(ОбъектКомпоненты.ServerURL) Тогда
			ЗаполнитьНастройкиПодключения(ОбъектКомпоненты);
		КонецЕсли;
		
		ПодключениеУстановлено = КонтекстСервера.DiadocInvoiceAPI.TestConnection();
		
		Если Не ПодключениеУстановлено Тогда
			
			Отказ = Истина;
			
			МетодКлиента(, "ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибкиИнтернет");
			
		КонецЕсли;
		
		Возврат Не Отказ;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьНастройкиПодключения(ОбъектКомпоненты)
		
		НастройкиПодключения = МетодСервераБезКонтекста(, "НастройкиИнтернетПодключения");
		НастройкиПрокси		 = НастройкиПодключения.НастройкиПрокси;
		
		ОбъектКомпоненты.ServerURL	 = НастройкиПодключения.АдресАПИ;
		ОбъектКомпоненты.ApiClientId = НастройкиПодключения.КлючРазработчика;
		
		Если НастройкиПрокси.ВариантИспользования = "ИспользоватьНастройкиIE" Тогда
			
			ОбъектКомпоненты.ProxyMode = "UseDefaultProxy";
			
		ИначеЕсли НастройкиПрокси.ВариантИспользования = "ИспользоватьПроксиСервер" Тогда
			
			ОбъектКомпоненты.ProxyMode = "UseProxy";
			
			ОбъектКомпоненты.ProxySettings.URL		 = НастройкиПрокси.Адрес;
			ОбъектКомпоненты.ProxySettings.Login	 = НастройкиПрокси.Логин;
			ОбъектКомпоненты.ProxySettings.Password	 = НастройкиПрокси.Пароль;
			
		Иначе
			
			ОбъектКомпоненты.ProxyMode = "NoProxy";
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция АвторизоватьсяИЗаполнитьКонтекст(ВходПоПоследнемуСертификату = Ложь) Экспорт
		
		Результат=	Новый Структура("Отказ, ОписаниеОшибки, Подробности", Ложь, "", "");
		
		МассивНаименованийНастроек= Новый Массив;
		МассивНаименованийНастроек.Добавить("ДиадокПоследнийСертификатПользователя");
		МассивНаименованийНастроек.Добавить("ДиадокПоследнееПредставлениеПользователя");
		
		ПараметрыАвторизации= МетодСервераБезКонтекста(,"ПолучитьНастройкиПользователя", МассивНаименованийНастроек);
		
		Если Лев(ПараметрыАвторизации.ДиадокПоследнийСертификатПользователя, 6) = "login:" Тогда
			Логин = Сред(ПараметрыАвторизации.ДиадокПоследнийСертификатПользователя, 7);
			Режим = "АвторизацияПоЛогину";
		Иначе
			Режим = "АвторизацияПоСертификату";
		КонецЕсли;
		
		
		Если ВходПоПоследнемуСертификату = Истина
			И Режим = "АвторизацияПоСертификату" 
			И НЕ ПустаяСтрока(ПараметрыАвторизации.ДиадокПоследнийСертификатПользователя) Тогда
			
			РезультатАвторизации = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьDiadocConnection", ПараметрыАвторизации.ДиадокПоследнийСертификатПользователя, ПараметрыАвторизации.ДиадокПоследнееПредставлениеПользователя);
			
			Если РезультатАвторизации.DiadocConnection <> Неопределено Тогда
				
				Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection= 		   РезультатАвторизации.DiadocConnection;
				Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.ПредставлениеПользователя= РезультатАвторизации.ПредставлениеПользователя;
				
				ВыполнитьЛогикуПослеАвторизации();
				
			Иначе
				
				ПараметрыФормы=	Новый Структура;
				ПараметрыФормы.Вставить("Режим"						 , Режим);
				
				МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_Авторизация", ПараметрыФормы, ЭтаФорма, "ОбработчикАвторизации");
				
			КонецЕсли;
			
		Иначе
			
			ПараметрыФормы=	Новый Структура;
			ПараметрыФормы.Вставить("Режим"						 , Режим);
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_Авторизация", ПараметрыФормы, ЭтаФорма, "ОбработчикАвторизации");
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПредложитьУстановкуРасширенияКонфигурации()
		
		Если НЕ Объект.ПараметрыКлиентСервер.ВызовИзРасширения И Платформа.ПараметрыКлиент.УстановкаРасширения.НеобходимоУстановить Тогда
			
			ПараметрыФормы= Новый Структура("ТекущяяВерсияРасширенияДиадок", Платформа.ПараметрыКлиент.УстановкаРасширения.ТекущяяВерсияРасширенияДиадок);
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаУстановкиРасширенияКонфигурации", ПараметрыФормы, ЭтаФорма, "ОбработчикЗапросУстановкиРасширенияКонфигурации");
			
		КонецЕсли;
		
	КонецПроцедуры
	
	//{ ОБНОВЛЕНИЕ МОДУЛЯ
	
	&НаКлиенте
	Процедура ПроверитьНаАктуальностьВерсиюМодуля()
		
		ТребуетсяОбновление = Ложь;
		ЕстьНезаблокированныеОрганизации = ЗначениеЗаполнено(МетодКлиента("Модуль_Клиент", "ПолучитьОрганизацииНезаблокированныеПоAPI"));
		
		Если ЕстьНезаблокированныеОрганизации Тогда
			
			ТекущаяВерсияМодуля = МетодСервераБезКонтекста(, "ВерсияОбработки");
			
			// Если в версии обработки есть не только цифры, значит модуль кастомный
			МодульКастомный = МетодКлиента("Модуль_Клиент", "ТолькоЦифрыВСтроке", СтрЗаменить(ТекущаяВерсияМодуля, ".", "")) = Ложь;
			
			Если Не МодульКастомный Тогда
				
				ПрочитатьАктуальнуюВерсиюМодуля();
				
				Если ТипЗнч(АктуальнаяВерсияМодуля) = Тип("Строка") Тогда
					ТребуетсяОбновление = МетодКлиента("Модуль_Клиент", "СравнитьВерсии", АктуальнаяВерсияМодуля, ТекущаяВерсияМодуля) > 0;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		УстановитьВидимостьКнопкиОбновленияМодуля(ТребуетсяОбновление);
		
	КонецПроцедуры
	
	&НаКлиенте
	//читаем из файла на сервере актуальную версию модуля
	Процедура ПрочитатьАктуальнуюВерсиюМодуля()
		
		АктуальнаяВерсияМодуля = 0;
		
		Попытка
			ФайлРезультата = МетодКлиента("Модуль_Клиент", "ЗапросНаПолучениеФайла", "https://diadoc-api.kontur.ru/1c-addin/versionDD.xml");
			Если ФайлРезультата = Неопределено Тогда 
				Возврат;
			КонецЕсли;	
		Исключение
			МетодСервера(,"ОбработатьОшибкуНаСервере", "ОшибкаПолученияФайла", ОписаниеОшибки());
			Возврат;
		КонецПопытки;
		
		ФайлАктуальныхВерсий = Новый ЧтениеXML;
		ФайлАктуальныхВерсий.ОткрытьФайл(ФайлРезультата);
		
		ЭтоНовыйРелиз = Ложь;
		
		Пока ФайлАктуальныхВерсий.Прочитать() Цикл
			
			Если ФайлАктуальныхВерсий.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				
				//обработка блока новых релизов
				Если ФайлАктуальныхВерсий.Имя = "Active" Тогда
					ЭтоНовыйРелиз = Истина;
				ИначеЕсли ФайлАктуальныхВерсий.Имя = "UF" И ЭтоНовыйРелиз Тогда
					АктуальнаяВерсияМодуля = ФайлАктуальныхВерсий.ПолучитьАтрибут("version"); //формат "5_20_01"
					АктуальнаяВерсияМодуля = СтрЗаменить(АктуальнаяВерсияМодуля, "_", ".");
					Прервать;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ФайлАктуальныхВерсий.Закрыть();
		УдалитьФайлы(ФайлРезультата);
		
	КонецПроцедуры	
	
	&НаСервере
	Процедура УстановитьВидимостьКнопкиОбновленияМодуля(ВерсияИзменилась)
		
		Элементы.КнопкаСкачатьОбновление.Видимость		= ВерсияИзменилась;		
		Элементы.ДекорацияИсторияОбновлений.Видимость	= НЕ ВерсияИзменилась;
		Элементы.ДекорацияКартинкаИстории.Видимость		= НЕ ВерсияИзменилась;
		
	КонецПроцедуры
	
	&НаКлиенте
	//алгоритм действий для обновления модуля при различных вариантах расположения модуля
	Процедура СкачатьОбновление(Команда)
		
		ТекущаяВерсияМодуля = МетодСервераБезКонтекста(,"ВерсияОбработкиБезНомераСборки");
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("НоваяВерсия",	 АктуальнаяВерсияМодуля);
		ПараметрыФормы.Вставить("ТекущаяВерсия", ТекущаяВерсияМодуля);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаНовости", ПараметрыФормы, ЭтаФорма);	
		
	КонецПроцедуры
	
	//} ОБНОВЛЕНИЕ МОДУЛЯ
	
	//} ЗАПУСК
	
	//{ НАСТРОЙКИ
	
	&НаСервере
	Функция ПолучитьПараметрыПодключения() Экспорт
		
		ПараметрыПодключения = Новый Структура;
		
		ПараметрыПодключения.Вставить("КлючРазработчика", ОбработкаОбъект().КлючРазработчика());
		ПараметрыПодключения.Вставить("АдресАПИ"		, ОбработкаОбъект().АдресАпи());
		
		Возврат ПараметрыПодключения;
		
	КонецФункции	
	
	&НаКлиенте
	Процедура ОбновитьПараметрыПрокси()
		
		ПараметрыПрокси = МетодСервераБезКонтекста(, "ПараметрыПроксиСервера");
		
		ВариантыИспользованияПрокси = МетодКлиента("Модуль_Клиент", "ВариантыИспользованияПроксиСервера");
		ВариантЗначение 			= ПараметрыПрокси.ВариантИспользования;
		
		ВариантПредставление = ВариантыИспользованияПрокси.НайтиПоЗначению(ВариантЗначение).Представление;
		
		Элементы.ЗначениеВариантИспользованияПроксиСервера.Заголовок = ВариантПредставление;
		Элементы.ЗначениеАдресПроксиСервера.Заголовок				 = ПараметрыПрокси.Адрес;
		Элементы.ЗначениеПортПроксиСервера.Заголовок				 = ПараметрыПрокси.Порт;
		Элементы.ЗначениеЛогинПроксиСервера.Заголовок				 = ПараметрыПрокси.Логин;
		
		Если ЗначениеЗаполнено(ПараметрыПрокси.Пароль) Тогда
			Элементы.ЗначениеПарольПроксиСервера.Заголовок			 = "***";
		КонецЕсли;
		
	КонецПроцедуры 
	
	&НаСервере
	Процедура ОбновитьОпции()
		
		ТочностьЦеныСФ = ХранилищеОбщихНастроек.Загрузить("НастройкаОпций_Диадок", "ТочностьЦеныСФ_Диадок");
		
		Если НЕ ЗначениеЗаполнено(ТочностьЦеныСФ) Тогда
			ТочностьЦеныСФ = 2;
			СохранитьЗначенияОпцийНаСервере(ТочностьЦеныСФ);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СформироватьПредставленияФормированияПечатныхФорм(СтрокаТаблицы)
		
		Если СтрокаТаблицы.СпособФормированияАкта = 0 Тогда
			СтрокаТаблицы.СпособФормированияАктаПредставление = "Использовать форму рекомендованную ФНС";
		Иначе
			СтрокаТаблицы.СпособФормированияАктаПредставление = "Использовать внешнюю печатную форму";
		КонецЕсли;
		
		Если СтрокаТаблицы.ФормироватьСчетНаОсновании = Неопределено Тогда
			ФормироватьСчетНаОсновании=	0;
		Иначе
			ФормироватьСчетНаОсновании=	СтрокаТаблицы.ФормироватьСчетНаОсновании;
		КонецЕсли;	
		
		ЗначениеСпискаФормироватьСчетНаОсновании=	СписокФормироватьСчетНаОсновании.НайтиПоЗначению(ФормироватьСчетНаОсновании);
		
		Если ФормироватьСчетНаОсновании = 2 Тогда
			СтрокаТаблицы.СпособФормированияСчетаПредставление=	ЗначениеСпискаФормироватьСчетНаОсновании.Представление;
		Иначе
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ВнешняяПечатнаяФормаСчета) Тогда
				СтрокаТаблицы.СпособФормированияСчетаПредставление = "На основании: " + ЗначениеСпискаФормироватьСчетНаОсновании.Представление + ", использовать стандартную форму";
			Иначе
				СтрокаТаблицы.СпособФормированияСчетаПредставление = "На основании: " + ЗначениеСпискаФормироватьСчетНаОсновании.Представление + ", использовать внешнюю печатную форму";
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбновитьДанныеСтраницыНастроек()
		
		КонтекстДиадока = Платформа.ПараметрыКлиент.КонтекстДиадока;
		
		КэшФорматДокументовНаОтправку = НастройкиОтправкиПоОрганизациям();
		
		СписокПочтовыхЯщиков.Очистить();
		
		Для Каждого Стр Из КонтекстДиадока Цикл
			
			Стр2 = СписокПочтовыхЯщиков.Добавить();
			Стр2.Организация 		= Стр.Организация;
			Стр2.НаименованиеЯщика 	= Стр.Box.Name;
			Стр2.idBox 				= Стр.BoxId;
			Стр2.Заблокирована		= Стр.ЗаблокированаПоAPI;
			
			Стр2.ФорматДокументовНаОтправку = КэшФорматДокументовНаОтправку[Стр.Организация];
			
		КонецЦикла;
		
		ОбновитьОпции();
		
	КонецПроцедуры	
	
	// Возвращает настройки отправки указанных организаций.
	//
	// Параметры:
	//  СписокОрганизаций - Массив - содержит ссылки на элементы справочника "Организации".
	// 
	// Возвращаемое значение:
	//  Соответствие:
	//    * Ключ - СправочникСсылка - ссылка на организацию
	//    * Значение - Строка - значение настройки "ДиадокФормироватьУПД"
	//
	&НаСервере
	Функция ЗначениеСвойстваДиадокФормироватьУПД(СписокОрганизаций)
		
		КэшФорматДокументовНаОтправку = МетодСервера(, "ПолучитьЗначенияСвойстваОбъектов", СписокОрганизаций, "ДиадокФормироватьУПД");
		
		Возврат КэшФорматДокументовНаОтправку;
		
	КонецФункции
	
	// Получает настройки отправки документов по всем организациям контекста.
	// 
	// Возвращаемое значение:
	//  Соответствие:
	//    * Ключ - СправочникСсылка - ссылка на организацию
	//    * Значение - Строка - значение настройки "ДиадокФормироватьУПД"
	//
	&НаКлиенте
	Функция НастройкиОтправкиПоОрганизациям()
		
		КонтекстДиадока = Платформа.ПараметрыКлиент.КонтекстДиадока;
		
		ОрганизацииВ1С = Новый Массив;
		
		Для каждого СтрокаЯщик Из КонтекстДиадока Цикл
			ОрганизацииВ1С.Добавить(СтрокаЯщик.Организация);
		КонецЦикла;
		
		Результат = ЗначениеСвойстваДиадокФормироватьУПД(ОрганизацииВ1С);
		
		Возврат Результат;
		
	КонецФункции
	
	// Проверяет актуальность настройки отправки документов.
	//
	// Параметры:
	//  КлючНастройки - Строка - значение настройки "ДиадокФормироватьУПД"
	// 
	// Возвращаемое значение:
	//  Булево - Истина, если эта настройка позволяет отправлять документы в неактуальных форматах.
	//
	&НаКлиенте
	Функция ЭтоУстаревшаяНастройкаОтправки(КлючНастройки)
		
		Результат = Ложь;
		
		Если КлючНастройки = "НЕТ"
			Или КлючНастройки = "ТОРГ12АКТ"
			Или КлючНастройки = "ДОП" Тогда
			Результат = Истина;
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОчиститьФильтрОрганизация()
		ОрганизацияID = "";
		ПодразделениеID = "";
		ВключатьПодчиненныеПодазделения = истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОчиститьФильтрКонтрагент()
		КонтрагентID = "";
	КонецПроцедуры 
	
	&НаКлиенте
	Процедура ИзменитьВидТаблицыДокументов(Вид)
		
		Если Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаПолученныеДиадок Тогда
			Таблица= ТаблицаДокументовВходящих;
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаОтправленныеДиадок Тогда 
			Таблица= ТаблицаДокументовИсходящих;
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаВнутренниеДиадок Тогда 
			Таблица= ТаблицаДокументовВнутренних;
		КонецЕсли;
		
		Если Вид = "Классический" Тогда
			
			Для Каждого Строка ИЗ Таблица Цикл
				Строка.ИндексИконкиПакета= 0;
			КонецЦикла;
			
		ИначеЕсли Вид = "Пакеты" Тогда	
			
			Для Каждого Строка ИЗ Таблица Цикл
				Строка.ИндексИконкиПакета= Строка.ИндексИконкиПакетаКопия;
			КонецЦикла;
			
			Таблица.Сортировать("MessageId, ИндексИконкиПакета, ПозицияСортировки, ДатаУчета Убыв");
			
		КонецЕсли;
		
	КонецПроцедуры
	
	//} НАСТРОЙКИ
	
	//{ ОБРАБОТЧИК
	
	&НаКлиенте
	Процедура ОбработчикАвторизации(РезультатАвторизации, ДополнительныеПараметры) Экспорт
		
		Если РезультатАвторизации = Неопределено Тогда
			
			Если НЕ Объект.ПараметрыКлиентСервер.ВызовИзРасширения Тогда
				Закрыть();
			Иначе
				Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок= Неопределено;
				Платформа.ПараметрыКлиент.КонтекстДиадока.Очистить();	
			КонецЕсли;
			
			Возврат;
		Иначе 	
			Организация		 = Неопределено;
			ОрганизацияID	 = "";
			Платформа.ПараметрыКлиент.КонтекстДиадока.Очистить();
		КонецЕсли;
		
		Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection = РезультатАвторизации.DiadocConnection;
		Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.ПредставлениеПользователя = РезультатАвторизации.ПредставлениеПользователя;
		
		ВыполнитьЛогикуПослеАвторизации();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытияФормыОшибки(РезультатВыбора, ЗакрытьФорму) Экспорт
		
		Если ЗакрытьФорму = Истина Тогда
			Закрыть();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикВыборОрганизации(РезультатВыбора, Элемент) Экспорт
		
		Если РезультатВыбора <> Неопределено тогда 
			
			Организация= РезультатВыбора.Значение;
			
			ОрганизацияКонтрагентыПриИзменении(Элемент);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикБлокировкаКонтрагента(РезультатВопроса, ДополнительныеПараметры) Экспорт
		
		Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
			
			Organization = МетодКлиента("Модуль_Клиент","ПолучитьЯщикДиадокДляОрганизации", Организация);
			Если Organization = Неопределено Тогда 
				Возврат;
			КонецЕсли;
			
			Counteragent = Organization.GetCounteragentById(ДополнительныеПараметры.ID);
			
			Попытка
				
				СтарыйСтатус = Counteragent.GetStatus();
				Counteragent.BreakWithCounteragent("");
				
			Исключение 
				
				ПараметрыФормы=	Новый Структура();
				ПараметрыФормы.Вставить("Заголовок", 		"Ошибка работы с контрагентом");
				ПараметрыФормы.Вставить("ОписаниеОшибки", 	"Не удалось заблокировать контрагента");
				ПараметрыФормы.Вставить("Подробности", 		ОписаниеОшибки());
				
				МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма);
				
				Возврат;
				
			КонецПопытки;
			
			Counteragent = Organization.GetCounteragentById(ДополнительныеПараметры.ID);
			
			ДополнительныеПараметры.ТекущийСтатус				= Counteragent.GetStatus();
			ДополнительныеПараметры.ТекущийСтатусРасшифровка	= РасшифровкаТекущегоСостоянияВзаимоотношенийСервер(ДополнительныеПараметры.ТекущийСтатус);
			
			Платформа.ПовторноеИспользованиеСброситьЗначение("Модуль_Клиент", "GetCounteragentListByStatus", Организация, СтарыйСтатус);
			Если ДополнительныеПараметры.ТекущийСтатус <> СтарыйСтатус Тогда
				Платформа.ПовторноеИспользованиеСброситьЗначение("Модуль_Клиент", "GetCounteragentListByStatus", Организация, ДополнительныеПараметры.ТекущийСтатус);
			КонецЕсли;
			
			СписокАктивныеПриАктивизацииСтроки(Элементы);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	
	// SAA
	// Add diadok id in state registry
	//
	// Параметры:
	//  ТекущийDocumentId  - String - Current diadoc document id
	//                 
	//
	&НаСервере
	Процедура ЗаполнитьРегистрСостоянийДиадок(ТекущийDocumentId)
		
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	pcru_ex_ДокументыДиадок.ИдентификаторДиадок КАК ИдентификаторДиадок,
		|	pcru_ex_ДокументыДиадок.Состояние КАК Состояние
		|ИЗ
		|	РегистрСведений.pcru_ex_ДокументыДиадок КАК pcru_ex_ДокументыДиадок
		|ГДЕ
		|	pcru_ex_ДокументыДиадок.ИдентификаторДиадок = &ИдентификаторДиадок";
		Запрос.УстановитьПараметр("ИдентификаторДиадок",ТекущийDocumentId );
		Если  Запрос.Выполнить().Пустой() Тогда
			Мз  = РегистрыСведений.pcru_ex_ДокументыДиадок.СоздатьМенеджерЗаписи();
			Мз.ИдентификаторДиадок = ТекущийDocumentId;
			Мз.Записать(Истина);
		КонецЕсли;  
		
		
	КонецПроцедуры // ЗаполнитьРегистрСостоянийДиадок()
	
	// <Описание функции>
	//  SAA
	// Параметры:
	//  <Параметр1>  - <Тип.Вид> - <описание параметра>
	//                 <продолжение описания параметра>
	//  <Параметр2>  - <Тип.Вид> - <описание параметра>
	//                 <продолжение описания параметра>
	//
	// Возвращаемое значение:
	//   <Тип.Вид>   - <описание возвращаемого значения>
	//
	&НаСервереБезКонтекста
	Функция ПолучитьСостояние(ТекущийDocumentId)
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	pcru_ex_ДокументыДиадок.Состояние КАК Состояние
		|ИЗ
		|	РегистрСведений.pcru_ex_ДокументыДиадок КАК pcru_ex_ДокументыДиадок
		|ГДЕ
		|	pcru_ex_ДокументыДиадок.ИдентификаторДиадок = &ИдентификаторДиадок";
		Запрос.УстановитьПараметр("ИдентификаторДиадок",ТекущийDocumentId );
		Если  Запрос.Выполнить().Пустой() Тогда
			Возврат "Не найден"
			
		Иначе
			Выборка = Запрос.Выполнить().Выбрать();
			Выборка.Следующий();
			Возврат Выборка.Состояние;	
		КонецЕсли;  
		
		
		
	КонецФункции // ПолучитьСостояние()
	
	
	&НаКлиенте
	Процедура ОбработчикОбновлениеСпискаДокументов()
		
		Если Не ПроверитьИнтернетПодключение() Тогда
			Возврат;
		КонецЕсли;
		
		Направление   = ПолучитьЗначениеНаправления();
		ПериодВыборки = ПолучитьТекущийПериод();
		
		Если Направление = "OutboundWaitingForSenderSignature" Тогда
			ТЧ = ТаблицаДокументовВнутренних;
		ИначеЕсли Направление = "Outbound" Тогда
			ТЧ = ТаблицаДокументовИсходящих;
		Иначе
			ТЧ = ТаблицаДокументовВходящих;
		КонецЕсли;
		
		ТЧ.Очистить();
		
		МассивДокументов = МетодКлиента("Модуль_Клиент", "ВернутьВыборкуРНКИСчетовФактурДиадок",
		ПериодВыборки.ДатаНачала, ПериодВыборки.ДатаОкончания, 
		Организация, КонтрагентID, ПолучитьПараметрыВыборки());
		
		ПараметрыПМ = Новый Структура;
		ПараметрыПМ.Вставить("Направление", Направление);
		ПараметрыПМ.Вставить("МассивДокументов", МассивДокументов);
		МетодСервера(, "ОбработкаСобытияПодключаемогоМодуля", "ПриОбновленииСпискаДокументов", ПараметрыПМ);
		
		ДобавленныеДокументы = Новый Соответствие; // запоминаем, данные каких документов уже добавили
		
		Для Каждого СтруктураДокумента Из МассивДокументов Цикл
			
			ТекущийDocumentId= СтруктураДокумента.DocumentId;
			//SAA
			ЗаполнитьРегистрСостоянийДиадок(ТекущийDocumentId);
			ТекСостояние = ПолучитьСостояние(ТекущийDocumentId);
			СтруктураДокумента.Вставить("PCRU_Состояние",ТекСостояние);
			//SAA
			Если ДобавленныеДокументы[ТекущийDocumentId] = Истина Тогда
				// Дубли документов могут появиться, например, если документ отправлен в подразделение Диадока.
				// В этом случае документ будет отображаться дважды: для головной организации и организации, 
				// связанной с подразделением Диадока
				Продолжить;
			КонецЕсли;
			
			ДобавленныеДокументы.Вставить(ТекущийDocumentId, Истина);
			//SAA
			СтрокаТЧ = ТЧ.Добавить();
			//SAA
			ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтруктураДокумента);
    		//SAA
		//	СтрокаТЧ.PCRU_Состояние = ТекСостояние;
			//SAA

		КонецЦикла;
		
		МассивДокументов = Неопределено;
		
		ТЧ_Заполнить_ДатаУчета(ТЧ);
		
		ТЧ.Сортировать("ДатаУчета Убыв, MessageId, ПозицияСортировки");
		
		ТЧ_Заполнить_ИндексИконкиПакета(ТЧ);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТЧ_Заполнить_ДатаУчета(ТЧ)
		
		// Т.к. в качестве даты учета используется ConfirmationDate или Timestamp, для сообщения в целом берем максимальную дату.
		
		КэшДатыУчета = Новый Соответствие;
		
		Для Каждого СтрокаТЧ Из ТЧ Цикл
			
			ДатаУчета = КэшДатыУчета[СтрокаТЧ.MessageId];
			ДатаУчета = ?(ДатаУчета = Неопределено, '00010101', ДатаУчета);
			
			КэшДатыУчета.Вставить(СтрокаТЧ.MessageId, Макс(СтрокаТЧ.ДатаУчета, ДатаУчета));
			
		КонецЦикла;
		
		Для Каждого СтрокаТЧ Из ТЧ Цикл
			СтрокаТЧ.ДатаУчета = КэшДатыУчета[СтрокаТЧ.MessageId];
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТЧ_Заполнить_ИндексИконкиПакета(ТЧ)
		
		ВГраница = ТЧ.Количество() - 1;
		
		Для ИндексСтроки = 0 ПО ВГраница Цикл
			
			СтрокаТЧ = ТЧ[ИндексСтроки];
			
			MessageId = СтрокаТЧ.MessageId;
			
			MessageIdВыше = ?(ИндексСтроки - 1 > -1		  , ТЧ[ИндексСтроки-1].MessageId, Неопределено);
			MessageIdНиже = ?(ИндексСтроки + 1 <= ВГраница, ТЧ[ИндексСтроки+1].MessageId, Неопределено);
			
			Если MessageId <> MessageIdВыше И MessageId = MessageIdНиже Тогда
				
				СтрокаТЧ.ИндексИконкиПакета = 1; // Начало сообщения
				
			ИначеЕсли MessageId = MessageIdВыше И MessageId = MessageIdНиже Тогда
				
				СтрокаТЧ.ИндексИконкиПакета = 2; // Середина сообщения
				
			ИначеЕсли MessageId = MessageIdВыше И MessageId <> MessageIdНиже Тогда
				
				СтрокаТЧ.ИндексИконкиПакета = 3; // Окончание сообщения
				
			КонецЕсли;
			
			СтрокаТЧ.ИндексИконкиПакетаКопия = СтрокаТЧ.ИндексИконкиПакета;
			
		КонецЦикла;	
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВыбораИнтервала(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда 
			
			Если РезультатЗакрытия.ТекущаяСтраница = "СтраницаОтправленныеДиадок" Тогда
				
				Если 	РезультатЗакрытия.ДатаНачала <> СтруктураИнтервалов.Отправленные.ДатаНачала
					ИЛИ РезультатЗакрытия.ДатаОкончания <> СтруктураИнтервалов.Отправленные.ДатаОкончания
					ИЛИ РезультатЗакрытия.ОтбиратьПоДатеДокумента <> СтруктураИнтервалов.Отправленные.ОтбиратьПоДатеДокумента
					Тогда
					ЗаполнитьЗначенияСвойств(СтруктураИнтервалов.Отправленные, РезультатЗакрытия);
					ОбновитьЗаголовокИнтервала("Отправленные");
					НачатьОбновлениеСпискаДокументов();
				КонецЕсли;
				
			ИначеЕсли РезультатЗакрытия.ТекущаяСтраница = "СтраницаПолученныеДиадок" Тогда
				
				Если 	РезультатЗакрытия.ДатаНачала <> СтруктураИнтервалов.Полученные.ДатаНачала
					ИЛИ РезультатЗакрытия.ДатаОкончания <> СтруктураИнтервалов.Полученные.ДатаОкончания
					ИЛИ РезультатЗакрытия.ОтбиратьПоДатеДокумента <> СтруктураИнтервалов.Полученные.ОтбиратьПоДатеДокумента
					Тогда
					ЗаполнитьЗначенияСвойств(СтруктураИнтервалов.Полученные, РезультатЗакрытия);
					ОбновитьЗаголовокИнтервала("Полученные");
					НачатьОбновлениеСпискаДокументов();
				КонецЕсли;
				
			ИначеЕсли РезультатЗакрытия.ТекущаяСтраница = "СтраницаЖурналыДокументов" Тогда
				
				Если 	РезультатЗакрытия.ДатаНачала <> СтруктураИнтервалов.ЖурналДокументов.ДатаНачала
					ИЛИ РезультатЗакрытия.ДатаОкончания <> СтруктураИнтервалов.ЖурналДокументов.ДатаОкончания
					Тогда
					ЗаполнитьЗначенияСвойств(СтруктураИнтервалов.ЖурналДокументов, РезультатЗакрытия);
					ОбновитьЗаголовокИнтервалаЖурналаДокументов();
					СформироватьЖурналДокументов();
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыКарточкиКонтрагента(РезультатЗакрытия, ТекущиеДанные) Экспорт
		
		Если РезультатЗакрытия.Свойство("ВнесеныИзменения") <> Неопределено И РезультатЗакрытия.ВнесеныИзменения = Истина Тогда
			
			ОтборСтрок=	Новый Структура("ИНН", ТекущиеДанные.ИНН);
			
			ЗаполнитьСписокКонтрагентов();
			
			НайденныеСтроки= СписокАктивные.НайтиСтроки(ОтборСтрок);
			
			Если НайденныеСтроки.Количество() > 0 Тогда
				Элементы.СписокАктивные.ТекущаяСтрока=	НайденныеСтроки[0].ПолучитьИдентификатор();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыПараметрыОтправки(РезультатЗакрытия, ТекущиеДанные) Экспорт
		
		Если РезультатЗакрытия <> Неопределено Тогда
			
			ОтборСтрок = Новый Структура("Организация", РезультатЗакрытия.Организация);
			НайденныеСтроки = СписокПочтовыхЯщиков.НайтиСтроки(ОтборСтрок);
			
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				НайденнаяСтрока.ФорматДокументовНаОтправку = РезультатЗакрытия.ФормироватьУПД;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВводаКомментария(РезультатЗакрытия, ТекущиеДанные) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			
			ПараметрыПриглашения=	Новый Структура();
			ПараметрыПриглашения.Вставить("ТекущиеДанные", 	ТекущиеДанные);
			ПараметрыПриглашения.Вставить("Комментарий", 	РезультатЗакрытия.Комментарий);
			ПараметрыПриглашения.Вставить("ПутьКФайлу", 	РезультатЗакрытия.ПутьКФайлу);
			
			ОтправитьПринятьПриглашениеКонтрагенту(ПараметрыПриглашения, "ФормаОсновная");
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыДобавленияЯщиковПриСтарте(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если РезультатЗакрытия <> Неопределено Тогда
			ЗаполнитьИерархиюОрганизацийDiadoc();
			ОбновитьКонтекстДиадока();
			ОбновитьДанныеСтраницынастроек();
			ЗапуститьМодуль();
			
			Если РезультатЗакрытия.ЕстьСтарыйФорматОтправкиИсходящихДокументов Тогда
				УведомитьОСтаромФорматеОтправкиДокументов();
			КонецЕсли;
			
			ПоказатьПанельОкончанияОплаченногоПериода();
			
		Иначе
			СброситьПоследнийСертификатПользователя();
			Закрыть();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытьФормуТехПоддержки(РезультатВыбора, ДополнительныеПараметры) Экспорт
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТелефонТехподдержки"	, Платформа.ПараметрыКлиент.СловарьWL.ТелефонТехподдержки);
		ПараметрыФормы.Вставить("Организация"			, Организация);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаТехподдержка", ПараметрыФормы, ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыДобавленияЯщиков(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если РезультатЗакрытия <> Неопределено Тогда
			
			ЗаполнитьИерархиюОрганизацийDiadoc();
			ОбновитьКонтекстДиадока();
			ОбновитьДанныеСтраницынастроек();
			
			Если РезультатЗакрытия.ЕстьСтарыйФорматОтправкиИсходящихДокументов Тогда
				УведомитьОСтаромФорматеОтправкиДокументов();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикПутьКПодключаемомуМодулюДискЗавершениеВыбораФайла(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если РезультатЗакрытия = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПутьКПодключаемомуМодулюДиск = РезультатЗакрытия[0];
		
		РасположенияПодключаемогоМодуляПриИзменении();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикКомандаСохранитьШаблонПМЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
		
		Если РезультатВыбора = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		МетодКлиента(,"ВыгрузитьШаблонПодключаемогоМодуля", РезультатВыбора[0]);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикЗаполнитьСписок(РезультатВыбора, ДополнительныеПараметры) Экспорт
		
		Если РезультатВыбора <> Неопределено Тогда
			ЗаполнитьСписок(Неопределено);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыУведомленияОСтаромФормате(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если РезультатЗакрытия = "ПоказатьНастройки" Тогда
			
			ТекущийЭлемент = Элементы.СписокПочтовыхЯщиков;
			
			Элементы.СтраницыОтбор.ТекущаяСтраница = Элементы.СтраницаПустойОтбор;
			ОбновитьДанныеСтраницыНастроек();
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытияФормыНастройкаПроксиСервера(РезультатЗакрытия, ДопПараметры) Экспорт 
		
		Если РезультатЗакрытия = Истина Тогда
			
			ОбновитьПараметрыПрокси();
			
		КонецЕсли;
		
	КонецПроцедуры	
	
	&НаКлиенте
	Процедура ЖурналыДокументовПоказатьПриИзменении(Элемент)
		
		Элементы.СтраницаЖурналыДокументов.Видимость = ЖурналыДокументовПоказать;
		
		ДобавитьСтатистику_ИзменилиПоказатьЖурналДокументов();
		
	КонецПроцедуры
	
	// Вызывается из формы "ФормаВыводаHTMLДокумента"
	&НаКлиенте
	Процедура ОбработчикПровестиДиагностику(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
		
		МетодКлиента("Модуль_Клиент", "ПровестиКонтурДиагностику");
		
	КонецПроцедуры //ОбработчикПровестиДиагностику()
	
	&НаКлиенте
	Процедура ОбработчикОткрытьОнлайнЧат(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
		
		МетодКлиента("Модуль_Клиент", "ОткрытьОнлайнЧат");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОтправитьEmail(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
		
		МетодКлиента("Модуль_Клиент", "НаписатьПисьмоВТехПоддержку");
		
	КонецПроцедуры
	
	// Обработчик ожидания, который запускает отправку стека метрик
	&НаКлиенте
	Процедура ОбработчикОтправитьСтатистику()
		
		МетодКлиента("Модуль_Клиент", "Метрики_Отправить");
		
	КонецПроцедуры
	
	
	//} ОБРАБОТЧИК
	
	//{ КОМАНДЫ
	
	&НаКлиенте
	Процедура КомандаЗагрузитьИОтправить(Команда) Экспорт 
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("ОтправкаФайловСтарыйИнтерфейс", ОтправкаФайловСтарыйИнтерфейс);
		
		МетодКлиента(, "ОткрытьФормуОбработки", "Форма_Выгрузка", ПараметрыФормы, ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаТаблицаЖурналаДокументовСоздать(Команда)
		
		Если ТекущийЭлемент.Имя = "ТаблицаДокументовРеализацияТоваровУслуг" Тогда
			РеализацияТоваровУслугПредставление= МетодСервераБезКонтекста(, "ПредставлениеДокументов").РеализацияТоваровУслуг;
			ИмяНовойФормы= "Документ."+РеализацияТоваровУслугПредставление+".ФормаОбъекта";
		ИначеЕсли ТекущийЭлемент.Имя = "ТаблицаДокументовСчетФактураВыданный" Тогда
			СчетФактураВыданныйПредставление= МетодСервераБезКонтекста(, "ПредставлениеДокументов").СчетФактураВыданный;
			ИмяНовойФормы= "Документ."+СчетФактураВыданныйПредставление+".ФормаОбъекта";
		КонецЕсли;
		
		МетодКлиента(,"ОткрытьФормуОбъектаИБ",,ИмяНовойФормы);
		СформироватьЖурналДокументов();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаТаблицаЖурналаДокументовИзменить(Команда)
		
		ТекущиеДанные= Элементы[ТекущийЭлемент.Имя].ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			МетодКлиента(,"ОткрытьФормуОбъектаИБ", ТекущиеДанные.Ссылка);
			СформироватьЖурналДокументов();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаТаблицаЖурналаДокументовУдалить(Команда)
		
		Если НЕ Элементы[ТекущийЭлемент.Имя].ТекущиеДанные = Неопределено Тогда
			СсылкаНаДокумент=	Элементы[ТекущийЭлемент.Имя].ТекущиеДанные.Ссылка;
			ПометитьДокументНаУдаление(СсылкаНаДокумент);
			ОтобразитьИзменениеДанных(СсылкаНаДокумент, ВидИзмененияДанных.Изменение);
			СформироватьЖурналДокументов();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаТаблицаЖурналаДокументовУстановитьИнтервал(Команда)
		
		ПараметрыФормы=	Новый Структура;
		ПараметрыФормы.Вставить("ДатаНачала"			 	  , СтруктураИнтервалов.ЖурналДокументов.ДатаНачала);
		ПараметрыФормы.Вставить("ДатаОкончания"			 	  , СтруктураИнтервалов.ЖурналДокументов.ДатаОкончания);
		ПараметрыФормы.Вставить("ТекущаяСтраница"			  , Элементы.ПанельРежимов.ТекущаяСтраница.Имя);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыбораИнтервала", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВыбораИнтервала");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаТаблицаЖурналаДокументовПровести(Команда)
		
		Если НЕ Элементы[ТекущийЭлемент.Имя].ТекущиеДанные = Неопределено Тогда
			СсылкаНаДокумент=	Элементы[ТекущийЭлемент.Имя].ТекущиеДанные.Ссылка;
			Если ПровестиОтменитьДокумент(СсылкаНаДокумент, "Проведение") Тогда
				ОтобразитьИзменениеДанных(СсылкаНаДокумент, ВидИзмененияДанных.Изменение);
				СформироватьЖурналДокументов();
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаТаблицаЖурналаДокументовОтменаПроведения(Команда)
		
		Если НЕ Элементы[ТекущийЭлемент.Имя].ТекущиеДанные = Неопределено Тогда
			СсылкаНаДокумент=	Элементы[ТекущийЭлемент.Имя].ТекущиеДанные.Ссылка;
			Если ПровестиОтменитьДокумент(СсылкаНаДокумент, "ОтменаПроведения") Тогда
				ОтобразитьИзменениеДанных(СсылкаНаДокумент, ВидИзмененияДанных.Изменение);
				СформироватьЖурналДокументов();
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаПолученныеДиадокКоманднаяПанельУстановитьИнтервал(Команда)
		
		ПараметрыФормы=	Новый Структура;
		ПараметрыФормы.Вставить("ДатаНачала"			 	  , СтруктураИнтервалов.Полученные.ДатаНачала);
		ПараметрыФормы.Вставить("ДатаОкончания"			 	  , СтруктураИнтервалов.Полученные.ДатаОкончания);
		ПараметрыФормы.Вставить("ОтбиратьПоДатеДокумента"	  , СтруктураИнтервалов.Полученные.ОтбиратьПоДатеДокумента);
		ПараметрыФормы.Вставить("ТекущаяСтраница"			  , Элементы.ПанельРежимов.ТекущаяСтраница.Имя);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыбораИнтервала", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВыбораИнтервала");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаОтправленныеДиадокКоманднаяПанельУстановитьИнтервал(Команда)
		
		ПараметрыФормы=	Новый Структура;
		ПараметрыФормы.Вставить("ДатаНачала"			 	  , СтруктураИнтервалов.Отправленные.ДатаНачала);
		ПараметрыФормы.Вставить("ДатаОкончания"			 	  , СтруктураИнтервалов.Отправленные.ДатаОкончания);
		ПараметрыФормы.Вставить("ОтбиратьПоДатеДокумента"	  , СтруктураИнтервалов.Отправленные.ОтбиратьПоДатеДокумента);
		ПараметрыФормы.Вставить("ТекущаяСтраница"			  , Элементы.ПанельРежимов.ТекущаяСтраница.Имя);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыбораИнтервала", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВыбораИнтервала");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ГрупповаяОбработкаКонтрагентовОткрыть(Команда)
		ТекДанные = Элементы.СписокАктивные.ТекущиеДанные;
		Если ТекДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Organization = МетодКлиента("Модуль_Клиент","ПолучитьЯщикДиадокДляОрганизации", Организация);
		Если Organization = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("Контрагенты", СписокАктивные);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаГрупповаяОбработкаКонтрагентов", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыКарточкиКонтрагента", ТекДанные);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаОткрытьКарточкуКонтрагента(Команда)
		
		ТекДанные = Элементы.СписокАктивные.ТекущиеДанные;
		Если ТекДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Organization = МетодКлиента("Модуль_Клиент","ПолучитьЯщикДиадокДляОрганизации", Организация);
		Если Organization = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы=	Новый Структура;
		ПараметрыФормы.Вставить("СписокКонтрагентовВ1С", ТекДанные.СписокКонтрагентовВ1С);
		ПараметрыФормы.Вставить("CounteragentID"	   , ТекДанные.ID);
		ПараметрыФормы.Вставить("FnsParticipantId"	   , ТекДанные.FnsParticipantId);
		ПараметрыФормы.Вставить("НаименованиеДД"	   , ТекДанные.Контрагент + ?(ПустаяСтрока(ТекДанные.ИНН), "", " (" + ТекДанные.ИНН) + ?(ПустаяСтрока(ТекДанные.КПП), "", "/" + ТекДанные.КПП) + ")");
		ПараметрыФормы.Вставить("Организация"		   , Организация);
		ПараметрыФормы.Вставить("ИНН"				   , ТекДанные.ИНН);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаКарточкиКонтрагента", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыКарточкиКонтрагента", ТекДанные);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаКонтрагентыЗаблокироватьКонтрагента(Команда)
		
		ТекущиеДанные= Элементы.СписокАктивные.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Оповещение=	Новый ОписаниеОповещения("ОбработчикБлокировкаКонтрагента", ЭтаФорма, ТекущиеДанные);
		ПоказатьВопрос(Оповещение, "Вы действительно хотите заблокировать контрагента?", РежимДиалогаВопрос.ДаНет, 120, КодВозвратаДиалога.Нет, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы, КодВозвратаДиалога.Нет);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаКонтрагентыПринятьОтправитьПриглашение(Команда)
		
		ТекущиеДанные= Элементы.СписокАктивные.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ТекущиеДанные.ТекущийСтатус = "InvitesMe" Тогда
			
			ПараметрыПриглашения=	Новый Структура();
			ПараметрыПриглашения.Вставить("ТекущиеДанные", 	ТекущиеДанные);
			ПараметрыПриглашения.Вставить("Комментарий", 	"");
			ПараметрыПриглашения.Вставить("ПутьКФайлу", 	"");
			
			ОтправитьПринятьПриглашениеКонтрагенту(ПараметрыПриглашения, "ФормаОсновная");
			
		ИначеЕсли ТекущиеДанные.ТекущийСтатус = "RejectsMe" 
			ИЛИ ТекущиеДанные.ТекущийСтатус = "IsRejectedByMe" Тогда
			
			ПараметрыФормы=	Новый Структура();
			ПараметрыФормы.Вставить("Заголовок",	"Отправка приглашения");
			ПараметрыФормы.Вставить("Комментарий", 	"Предлагаем обмениваться электронными документами через систему " + Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы + ".");
			ПараметрыФормы.Вставить("Режим", 		"ОтправкаПриглашения");
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВводаКомментария", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВводаКомментария", ТекущиеДанные);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаКонтрагентыНайтиВДиадоке(Команда)
		
		ФормаПоискаКонтрагента=									МетодКлиента(,"ПолучитьФормуОбработки", "ФормаПоискаИПриглашенияКонтрагента");
		ФормаПоискаКонтрагента.Organization=					МетодКлиента("Модуль_Клиент","ПолучитьЯщикДиадокДляОрганизации", Организация);
		ФормаПоискаКонтрагента.Открыть();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаДобавщитьЯщикДиадок(Команда)
		
		СписокДобавленныхIdBox=	Новый СписокЗначений;
		Для каждого Ящик Из СписокПочтовыхЯщиков Цикл
			СписокДобавленныхIdBox.Добавить(Ящик.IdBox);
		КонецЦикла;
		
		ПараметрыФормы=	Новый Структура();
		ПараметрыФормы.Вставить("СписокДобавленныхIdBox", СписокДобавленныхIdBox);
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаДобавлениеЯщиков", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыДобавленияЯщиков");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаУдалитьЯщикДиадок(Команда)
		
		ТекСтрока=	Элементы.СписокПочтовыхЯщиков.ТекущиеДанные;
		Если ТекСтрока = Неопределено Тогда
			Возврат
		КонецЕсли;
		
		МетодСервераБезКонтекста(,"Установить_BoxID_для_Организация", ТекСтрока.Организация, "", "");
		МетодСервераБезКонтекста(,"УстановитьИдентификаторЭДОДляОрганизации", ТекСтрока.Организация, "");
		
		ц = 0;
		Пока ц < Платформа.ПараметрыКлиент.КонтекстДиадока.Количество() Цикл 
			Если Платформа.ПараметрыКлиент.КонтекстДиадока[ц].Организация = ТекСтрока.Организация Тогда 
				Платформа.ПараметрыКлиент.КонтекстДиадока.Удалить(ц);
			Иначе 
				ц =ц+1;
			КонецЕсли;
		КонецЦикла;
		
		ЗаполнитьИерархиюОрганизацийDiadoc();
		
		Если Текстрока.Организация = Организация Тогда
			Организация= Неопределено;
		КонецЕсли;
		
		ОбновитьДанныеСтраницыНастроек();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаПолученныеДиадокКоманднаяПанельОткрытьКарточкуДокумента(Команда)
		
		СформироватьПечатнуюФормуПоФайлуДиадок("ТаблицаДокументовВходящих");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаОтправленныеДиадокКоманднаяПанельОткрытьКарточкуДокумента(Команда)
		
		СформироватьПечатнуюФормуПоФайлуДиадок("ТаблицаДокументовИсходящих");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаВнутренниеДиадокКоманднаяПанельОткрытьКарточкуДокумента(Команда)
		
		СформироватьПечатнуюФормуПоФайлуДиадок("ТаблицаДокументовВнутренних");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаГрупповаяОбработкаДокументовПодписатьИСоздать(Команда)
		
		ОткрытьГрупповуюОбработкуВходящихДокументов("ПодписатьИСоздать");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаГрупповаяОбработкаДокументовПодписать(Команда)
		
		ОткрытьГрупповуюОбработкуВходящихДокументов("Подписать");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаГрупповаяОбработкаДокументовСопоставить(Команда)
		
		ОткрытьГрупповуюОбработкуВходящихДокументов("Сопоставить");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаГрупповаяОбработкаДокументовСохранитьНаДиск(Команда)
		
		ОткрытьГрупповуюОбработкуВходящихДокументов("СохранитьНаДиск");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаГрупповаяОбработкаДокументовСоздать(Команда)
		
		ОткрытьГрупповуюОбработкуВходящихДокументов("Создать");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаГрупповаяОбработкаДокументовКомплекснаяОбработкаТорговыеСети(Команда)
		
		ОткрытьГрупповуюОбработкуВходящихДокументов("КомплекснаяОбработкаТорговыеСети");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОткрытьГрупповуюОбработкуВходящихДокументов(ТекущийСценарий)
		
		Если Не ПроверитьИнтернетПодключение() Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Режим", 			"");
		ПараметрыФормы.Вставить("ТекущийСценарий",	ТекущийСценарий);
		ПараметрыФормы.Вставить("ТекущийПериод", 	ПолучитьТекущийПериод());
		ПараметрыФормы.Вставить("КонтрагентID", 	КонтрагентID);
		ПараметрыФормы.Вставить("ПараметрыВыборки", ПолучитьПараметрыВыборки());
		ПараметрыФормы.Вставить("Организация", 		Организация);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаГрупповаяОбработка", ПараметрыФормы, ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаНастроитьПараметрыОтправки(Команда)
		// Вставить содержимое обработчика.
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СохранитьЗначенияОпций(Команда)
		
		СохранитьЗначенияОпцийНаСервере(ТочностьЦеныСФ);
		
		ДобавитьСтатистику_ИзменениеТочностьЦеныСФ();
		
	КонецПроцедуры
	
	&НаСервере
	Процедура СохранитьЗначенияОпцийНаСервере(ТочностьЦеныСФ)
		ХранилищеОбщихНастроек.Сохранить("НастройкаОпций_Диадок", "ТочностьЦеныСФ_Диадок", ТочностьЦеныСФ);
	КонецПроцедуры
	
	&НаКлиенте
	Процедура УдалитьУстановитьРасширениеКонфигурации(Команда)
		
		МетодКлиента("Модуль_Клиент","ПодтверждениеВыполненияОперации", "ОбработчикУдалитьУстановитьРасширениеКонфигурации", ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикУдалитьУстановитьРасширениеКонфигурации(РезультатВопроса, ДополнительныеПараметры) Экспорт
		
		Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
			ОбработчикЗапросУстановкиРасширенияКонфигурации(Истина, Новый Структура("ТолькоУдалить", Платформа.ПараметрыКлиент.УстановкаРасширения.РасширениеУжеУстановлено));
			НастроитьЭлементУдалитьУстановитьРасширениеКонфигурации();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаПроверитьПодключениеПМ(Команда)
		
		ТекстСообщения = НСтр("ru = 'Подключаемый модуль успешно подключен'");
		
		Попытка
			
			МетодСервераБезКонтекста( ,"ПроверитьПодключениеПМ");
			
		Исключение
			
			Если РежимОтладкиСервера Тогда
				ВызватьИсключение;
			КонецЕсли;
			
			Ошибка = ИнформацияОбОшибке();
			ТекстСообщения = КраткоеПредставлениеОшибки(Ошибка);
			
		КонецПопытки;
		
		ПоказатьПредупреждение(
		, ТекстСообщения
		, 60
		, НСтр("ru = 'Инициализация подключаемого модуля'"));
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаВыгрузитьСерверныеМодули(Команда)
		
		Если Не ЗначениеЗаполнено(КаталогМодулейСервера) Тогда
			Возврат;
		КонецЕсли;
		
		МетодСервераБезКонтекста(, "ВыгрузитьВложенныеОбработки", КаталогМодулейСервера);
		
		ТекстОповещения = НСтр("ru = 'Выгрузка модулей завершена.'");
		ПоказатьОповещениеПользователя(ТекстОповещения);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КомандаСохранитьШаблонПМ(Команда)
		
		ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогВыбораФайла.МножественныйВыбор= Ложь;
		ДиалогВыбораФайла.Фильтр = "Внешняя обработка (*.epf)|*.epf";
		ДиалогВыбораФайла.ПолноеИмяФайла= "ПодключаемыйМодульДиадок.epf";
		ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ОбработчикКомандаСохранитьШаблонПМЗавершение", ЭтаФорма));
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовВидПакеты(Команда)
		
		ИзменитьВидТаблицыДокументов("Пакеты");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовСортироватьАЯ(Команда)
		
		ИзменитьВидТаблицыДокументов("Классический");
		СортироватьТаблицуДокументов("Возр");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовСортироватьЯА(Команда)
		
		ИзменитьВидТаблицыДокументов("Классический");
		СортироватьТаблицуДокументов("Убыв");
		
	КонецПроцедуры	
	
	&НаКлиенте
	Процедура ПоказатьИнструкцию_ПереотправкаДокументов(Команда)
		
		МетодКлиента("Модуль_Клиент", "ПоказатьИнструкцию_ПереотправкаДокументов");
		
	КонецПроцедуры //ПоказатьИнструкцию_ПереотправкаДокументов()
	
	//} КОМАНДЫ
	
	//{ СОБЫТИЯ
	
	&НаКлиенте
	Процедура СтраницыДокументовПриСменеСтраницы(Элемент)
		
		СформироватьЖурналДокументов();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПанельРежимовПриСменеСтраницы(Элемент, ТекущаяСтраница) Экспорт
		
		Элементы.ПолученныеДиадокКоманднаяПанельОткрытьКарточкуДокумента.Доступность= 							  Ложь;
		Элементы.ТаблицаДокументовИсходящихОтправленныеДиадокКоманднаяПанельОткрытьКарточкуДокумента.Доступность= Ложь;
		Элементы.ТаблицаДокументовВнутреннихВнутренниеДиадокКоманднаяПанельОткрытьКарточкуДокумента.Доступность=  Ложь;
		
		Элементы.СостояниеДокументооборота.Доступность= Истина;
		
		ОбновитьЗаголовок();
		
		Если ТекущаяСтраница = Элементы.СтраницаНастройка тогда 
			
			Элементы.СтраницыОтбор.ТекущаяСтраница= Элементы.СтраницаПустойОтбор;
			
			ОбновитьДанныеСтраницыНастроек();
			
		ИначеЕсли ТекущаяСтраница = Элементы.СтраницаКонтрагенты тогда 
			
			Если ЗначениеЗаполнено(Организация) Тогда
				Элементы.СтраницыКонтрагенты.ТекущаяСтраница= Элементы.СтраницаКонтрагентыСписок;
			Иначе
				Элементы.СтраницыКонтрагенты.ТекущаяСтраница= Элементы.СтраницаКонтрагентыПодсказкаВыбораОрганизации;
			КонецЕсли;
			
			Элементы.СтраницыОтбор.ТекущаяСтраница= Элементы.СтраницаОтборОрганизацияСтатус;
			
			Если НЕ ЗначениеЗаполнено(Организация) Тогда
				ПодключитьОбработчикОжидания("ВыборОрганизацииИзСписка", 0.3, Истина)
			Иначе
				ПриОткрытииСтраницыКонтрагентов();
			КонецЕсли;
			
		ИначеЕсли ТекущаяСтраница = Элементы.СтраницаЖурналыДокументов тогда 
			
			Элементы.СтраницыОтбор.ТекущаяСтраница= Элементы.СтраницаПустойОтбор;
			
			СформироватьЖурналДокументов();
			
		ИначеЕсли ТекущаяСтраница = Элементы.СтраницаПолученныеДиадок тогда
			
			Элементы.СтраницыОтбор.ТекущаяСтраница= Элементы.СтраницаОтбор;
			
			НачатьОбновлениеСпискаДокументов();
			
		ИначеЕсли ТекущаяСтраница = Элементы.СтраницаОтправленныеДиадок тогда
			
			Элементы.СтраницыОтбор.ТекущаяСтраница= Элементы.СтраницаОтбор;
			
			НачатьОбновлениеСпискаДокументов();
			
		ИначеЕсли ТекущаяСтраница = Элементы.СтраницаВнутренниеДиадок тогда
			
			Элементы.СтраницыОтбор.ТекущаяСтраница= Элементы.СтраницаОтбор;
			
			Элементы.СостояниеДокументооборота.Доступность= Ложь;
			СостояниеДокументооборота= "0";
			
			НачатьОбновлениеСпискаДокументов();
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаСервереБезКонтекста
	Функция ЗначениеНастройкиХранилищаОбщихНастроек(КлючОбъекта, КлючНастроек)
		
		Возврат ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, КлючНастроек);	
		
	КонецФункции
	
	&НаКлиенте
	Процедура СтраницыНастроекПриСменеСтраницы(Элемент, ТекущаяСтраница)
		
		Если ТекущаяСтраница = Элементы.Прокси
			И ПустаяСтрока(Элементы.ЗначениеВариантИспользованияПроксиСервера.Заголовок) Тогда
			
			ОбновитьПараметрыПрокси();
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПриОткрытииСтраницыКонтрагентов()
		
		Если ЗначениеЗаполнено(Организация) Тогда
			Элементы.СтраницыКонтрагенты.ТекущаяСтраница= Элементы.СтраницаКонтрагентыСписок;
		Иначе
			Элементы.СтраницыКонтрагенты.ТекущаяСтраница= Элементы.СтраницаКонтрагентыПодсказкаВыбораОрганизации;
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("ЗаполнитьСписокКонтрагентов", 0.1, Истина);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
		Элемент.СписокВыбора.Очистить();
		Элемент.СписокВыбора.ЗагрузитьЗначения(МетодКлиента("Модуль_Клиент","ПолучитьОрганизацииНезаблокированныеПоAPI"));
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПриОткрытии(Отказ)
		
		Если Платформа = Неопределено Тогда
			ИнициализироватьПлатформу();
		КонецЕсли;
		
		ИнициализироватьСтруктуруИнтервалов();
		
		УстановитьНаименованиеСистемы();
		
		НачатьИнициализациюМодуля();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
		
		Если  НЕ Отказ 
			И НЕ ЗавершениеРаботы Тогда // при завершении сеанса серверные вызовы запрещены
			
			МетодКлиента("Модуль_Клиент", "Метрики_Отправить");
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПриЗакрытии(ЗавершениеРаботы = Ложь)
		
		ПлатформаПриЗакрытии(ЗавершениеРаботы);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаЖурналаДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
		ТекущиеДанные= Элементы[ТекущийЭлемент.Имя].ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			МетодКлиента(,"ОткрытьФормуОбъектаИБ", ТекущиеДанные.Ссылка);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовПриАктивизацииСтроки(Элемент)
		
		Если НЕ Элементы.СтраницыДокументов.ПодчиненныеЭлементы.Найти(ТекущийЭлемент.Имя) = Неопределено Тогда
			Если НЕ Элементы[ТекущийЭлемент.Имя].ТекущиеДанные = Неопределено Тогда
				
				КнопкаПроведения=		Элементы[Элемент.Имя].КоманднаяПанель.ПодчиненныеЭлементы.Найти(Элемент.Имя + "Провести");
				КнопкаОтменыПроведения=	Элементы[Элемент.Имя].КоманднаяПанель.ПодчиненныеЭлементы.Найти(Элемент.Имя + "ОтменаПроведения");
				
				Если Элементы[Элемент.Имя].ТекущиеДанные.СостояниеДокумента = 0 Тогда
					КнопкаПроведения.Доступность=		Ложь;
					КнопкаОтменыПроведения.Доступность=	Истина;
				ИначеЕсли Элементы[Элемент.Имя].ТекущиеДанные.СостояниеДокумента = 1 Тогда
					КнопкаПроведения.Доступность=		Истина;
					КнопкаОтменыПроведения.Доступность=	Ложь;
				Иначе
					КнопкаПроведения.Доступность=		Ложь;
					КнопкаОтменыПроведения.Доступность=	Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовВходящихВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
		СформироватьПечатнуюФормуПоФайлуДиадок("ТаблицаДокументовВходящих");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовИсходящихВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
		Если Поле.Имя = "ТаблицаДокументовИсходящихСостояниеПередачиЧерезРоуминг" Тогда
			
			ТекущиеДанные= Элемент.ТекущиеДанные;
			
			//ТекущаяСтрока= ТаблицаДокументовИсходящих.Получить(ВыбраннаяСтрока);
			
			Если НРег(Лев(ТекущиеДанные.СостояниеПередачиЧерезРоуминг, 6)) = "ошибка" Тогда
				
				ЭДОбъект= Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(ТекущиеДанные.BoxID).GetDocumentById(ТекущиеДанные.DocumentID);
				
				ТекущиеДанные.СостояниеПередачиЧерезРоуминг= МетодКлиента("Модуль_Клиент","ПредставлениеСтатусаРоуминг", ЭДОбъект);
				
				Если НРег(Лев(ТекущиеДанные.СостояниеПередачиЧерезРоуминг, 6)) = "ошибка" Тогда
					
					ПараметрыФормы= Новый Структура("Заголовок, ОписаниеОшибки, Подробности",
					ТекущиеДанные.СостояниеПередачиЧерезРоуминг,
					ТекущиеДанные.СостояниеПередачиЧерезРоуминг,
					ТекущиеДанные.СостояниеПередачиЧерезРоумингДетали);
					
					МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма);
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			СформироватьПечатнуюФормуПоФайлуДиадок("ТаблицаДокументовИсходящих");
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовВнутреннихВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
		СформироватьПечатнуюФормуПоФайлуДиадок("ТаблицаДокументовВнутренних");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КонтрагентIDНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
		ЗаполнитьСписокВыбораКонтрагентID(Элемент);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КонтрагентIDПриИзменении(Элемент)
		
		МетодКлиента("Модуль_Клиент","ЭлементВРежимеВыбораИзСпискаПриИзменении", ЭтаФорма, Элемент, КонтрагентID);
		
		НачатьОбновлениеСпискаДокументов()
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КонтрагентIDАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
		
		ЗаполнитьСписокВыбораКонтрагентID(Элемент);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовВходящихПередНачаломИзменения(Элемент, Отказ)
		Отказ = истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаПередУдалением(Элемент, Отказ)
		Отказ = истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовИсходящихПриАктивизацииСтроки(Элемент)
		
		Элементы.ТаблицаДокументовИсходящихОтправленныеДиадокКоманднаяПанельОткрытьКарточкуДокумента.Доступность= Элемент.ТекущаяСтрока <> Неопределено;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовВходящихПриАктивизацииСтроки(Элемент)
		
		Элементы.ПолученныеДиадокКоманднаяПанельОткрытьКарточкуДокумента.Доступность= Элемент.ТекущаяСтрока <> Неопределено;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовВнутреннихПриАктивизацииСтроки(Элемент)
		
		Элементы.ТаблицаДокументовВнутреннихВнутренниеДиадокКоманднаяПанельОткрытьКарточкуДокумента.Доступность= Элемент.ТекущаяСтрока <> Неопределено;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработкаОповещения(ИмяСобытия, ПараметрыОповещения, Источник) Экспорт
		
		Если ИмяСобытия = "ИзменениеСтатусаДокументаДиадок"
			И Тип(Источник) = Тип("УправляемаяФорма") 
			И (Источник.ИмяФормы = Платформа.ПараметрыКлиент.ПутьКФормам+"ФормаДокумента"
			ИЛИ Источник.ИмяФормы = Платформа.ПараметрыКлиент.ПутьКФормам+"ФормаДокументаБезВизуализации" 
			ИЛИ Источник.ИмяФормы = Платформа.ПараметрыКлиент.ПутьКФормам+"ФормаГрупповаяОбработка")
			И ЗначениеЗаполнено(ПараметрыОповещения.BoxID)
			И ЗначениеЗаполнено(ПараметрыОповещения.DocumentID) Тогда
			
			Попытка
				
				Док = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(ПараметрыОповещения.BoxID).GetDocumentById(ПараметрыОповещения.DocumentID);
				
				НовыйСтатус				= МетодКлиента("Модуль_Клиент", "ПредставлениеСтатуса", Док);
				НовыйСтатусСогласования = МетодКлиента("Модуль_Клиент", "ПредставлениеСтатусаСогласования", Док);
				
			Исключение 
			КонецПопытки;
			
			Если НовыйСтатус <> Неопределено Тогда
				
				ОтборСтрок = Новый Структура;
				ОтборСтрок.Вставить("BoxID", 		ПараметрыОповещения.BoxID);
				ОтборСтрок.Вставить("DocumentID", 	ПараметрыОповещения.DocumentID);
				
				НайденныеСтроки = ТаблицаДокументовВходящих.НайтиСтроки(ОтборСтрок);
				Для Каждого СтрокаДокумента Из НайденныеСтроки Цикл
					СтрокаДокумента.СостояниеДокументооборота	= НовыйСтатус;
					СтрокаДокумента.СостояниеСогласования		= НовыйСтатусСогласования;
					СтрокаДокумента.Status						= Док.Status;
				КонецЦикла;
				
				НайденныеСтроки = ТаблицаДокументовВнутренних.НайтиСтроки(ОтборСтрок);
				Для Каждого СтрокаДокумента Из НайденныеСтроки Цикл
					СтрокаДокумента.СостояниеДокументооборота	= НовыйСтатус;
					СтрокаДокумента.СостояниеСогласования		= НовыйСтатусСогласования;
					СтрокаДокумента.Status						= Док.Status;
				КонецЦикла;
				
			КонецЕсли;
			
		ИначеЕсли ИмяСобытия = "ИзменениеСвязиДД1С"
			И ЗначениеЗаполнено(ПараметрыОповещения.BoxID)
			И ЗначениеЗаполнено(ПараметрыОповещения.DocumentID) Тогда
			
			Если ПараметрыОповещения.ТипСущности = "Документ"
				И ПараметрыОповещения.Свойство("Документ1С") Тогда
				
				ОтборСтрок=	Новый Структура;
				ОтборСтрок.Вставить("BoxID", ПараметрыОповещения.BoxID);
				ОтборСтрок.Вставить("DocumentID", ПараметрыОповещения.DocumentID);
				
				НайденныеСтроки=	ТаблицаДокументовВходящих.НайтиСтроки(ОтборСтрок);
				Если НайденныеСтроки.Количество() > 0 Тогда
					СтрокаДокумента=					НайденныеСтроки[0];
					СтрокаДокумента.ПервичныйДокумент=	ПараметрыОповещения.Документ1С;
				КонецЕсли;
				
				НайденныеСтроки =	ТаблицаДокументовИсходящих.НайтиСтроки(ОтборСтрок);
				Если НайденныеСтроки.Количество() > 0 Тогда
					СтрокаДокумента						= НайденныеСтроки[0];
					СтрокаДокумента.ПервичныйДокумент	= ПараметрыОповещения.Документ1С;
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли ИмяСобытия = "УдалениеДокументов" Тогда
			
			Для каждого ПараметрыДокумента Из ПараметрыОповещения Цикл
				
				ОтборСтрок=	Новый Структура;
				ОтборСтрок.Вставить("BoxID", 		ПараметрыДокумента.BoxID);
				ОтборСтрок.Вставить("DocumentID", 	ПараметрыДокумента.DocumentID);
				
				ТаблицаДокументов=	Неопределено;
				Если Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаПолученныеДиадок Тогда
					ТаблицаДокументов=	ТаблицаДокументовВходящих;
				ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаОтправленныеДиадок Тогда
					ТаблицаДокументов=	ТаблицаДокументовИсходящих;
				ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаВнутренниеДиадок Тогда
					ТаблицаДокументов=	ТаблицаДокументовВнутренних;
				КонецЕсли;
				
				Если НЕ ТаблицаДокументов = Неопределено Тогда
					НайденныеСтроки=	ТаблицаДокументов.НайтиСтроки(ОтборСтрок);
					Если НайденныеСтроки.Количество() > 0 Тогда
						СтрокаДокумента=	НайденныеСтроки[0];
						ИндексСтроки=		ТаблицаДокументов.Индекс(СтрокаДокумента);
						ТаблицаДокументов.Удалить(ИндексСтроки);
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли ИмяСобытия = "ИзменениеПодразделения" Тогда
			
			ОтборСтрок=	Новый Структура;
			
			ОтборСтрок.Вставить("BoxID", 		ПараметрыОповещения.BoxID);
			ОтборСтрок.Вставить("DocumentID", 	ПараметрыОповещения.DocumentID);
			
			ТаблицаДокументов=	Неопределено;
			Если Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаПолученныеДиадок Тогда
				ТаблицаДокументов=	ТаблицаДокументовВходящих;
			ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаОтправленныеДиадок Тогда
				ТаблицаДокументов=	ТаблицаДокументовИсходящих;
			ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаВнутренниеДиадок Тогда
				ТаблицаДокументов=	ТаблицаДокументовВнутренних;
			КонецЕсли;
			
			Если НЕ ТаблицаДокументов = Неопределено Тогда
				НайденныеСтроки=	ТаблицаДокументов.НайтиСтроки(ОтборСтрок);
				Если НайденныеСтроки.Количество() > 0 Тогда
					СтрокаДокумента=	НайденныеСтроки[0];
					СтрокаДокумента.Подразделение=	ПараметрыОповещения.DepartmentName;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СформироватьЖурналДокументов()
		
		ПараметрыЖурнала= Новый Структура;
		ПараметрыЖурнала.Вставить("НачалоПериода", СтруктураИнтервалов.ЖурналДокументов.ДатаНачала);
		ПараметрыЖурнала.Вставить("КонецПериода", СтруктураИнтервалов.ЖурналДокументов.ДатаОкончания);
		
		МетодКлиента("Модуль_Клиент", "ПоказатьСостояниеОбработки", НСтр("ru = 'Заполнение списка документов'"));
		
		ЗаполнитьТаблицуДокументов(ПараметрыЖурнала);
		
		ВсегоСтрок		 = ТаблицаЖурналаДокументов.Количество();
		ТекстСостояния	 = НСтр("ru = 'Обновление статуса документов'");
		
		Для Сч = 1 По ВсегоСтрок Цикл
			
			МетодКлиента("Модуль_Клиент", "ПоказатьСостояниеОбработкиСписка", ТекстСостояния, Сч, ВсегоСтрок);
			
			СтрокаТЧ = ТаблицаЖурналаДокументов[Сч-1];
			
			Если ЗначениеЗаполнено(СтрокаТЧ.ИдЯщикОрганизации) И ЗначениеЗаполнено(СтрокаТЧ.ИдДокументДиадок) Тогда
				
				Попытка
					ОбновитьДанныеСтроки(СтрокаТЧ);
				Исключение КонецПопытки;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СписокАктивныеПередНачаломИзменения(Элемент, Отказ)
		Отказ = истина;
		КомандаОткрытьКарточкуКонтрагента("")
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовВходящихПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
		Отказ=	Истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовИсходящихПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
		Отказ=	Истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовВнутреннихПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
		Отказ=	Истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовВнутреннихПередНачаломИзменения(Элемент, Отказ)
		Отказ=	Истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТаблицаДокументовВнутреннихПередУдалением(Элемент, Отказ)
		Отказ=	Истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СписокАктивныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
		Отказ=	Истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СписокПочтовыхЯщиковПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
		Отказ=	Истина;
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ДекорацияВыходНажатие(Элемент)
		
		Элементы.ДекорацияТекущийПользовательВДиадоке.Заголовок = "< Авторизация... >";
		
		ДобавитьСтатистику_НажалиСменитьПользователя();
		
		СброситьПоследнийСертификатПользователя();
		ОчиститьПеременныеМодуля();
		
		ИнициализироватьМодуль();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ДекорацияТехподдержкаНажатие(Элемент)
		
		ОбработчикОткрытьФормуТехПоддержки(Неопределено, Неопределено);
		
	КонецПроцедуры                   
	
	&НаКлиенте
	Процедура ДекорацияИсторияОбновленийНажатие(Элемент)
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаНовости",,ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СтатусВзаимоотношенийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
		ЗаполнитьСписокВыбораСтатусВзаимоотношений(Элемент);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СтатусВзаимоотношенийПриИзменении(Элемент)
		
		ЗаполнитьСписокКонтрагентов();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СписокАктивныеПриАктивизацииСтроки(Элемент)
		
		ТекущиеДанные= Элементы.СписокАктивные.ТекущиеДанные;
		
		Если НЕ ТекущиеДанные = Неопределено Тогда
			
			Если 	ТекущиеДанные.ТекущийСтатус = "InvitesMe"
				ИЛИ ТекущиеДанные.ТекущийСтатус = "RejectsMe"
				ИЛИ ТекущиеДанные.ТекущийСтатус = "IsRejectedByMe" Тогда
				
				Элементы.СписокАктивные.КоманднаяПанель.ПодчиненныеЭлементы.СписокАктивныеКонтрагентыПринятьОтправитьПриглашение.Доступность= Истина;
				Элементы.СписокАктивные.КонтекстноеМеню.ПодчиненныеЭлементы.СписокАктивныеКонтекстноеМенюГруппа1.ПодчиненныеЭлементы.
				СписокАктивныеКонтекстноеМенюКонтрагентыПринятьОтправитьПриглашение.Доступность= Истина;
				
			Иначе
				
				Элементы.СписокАктивные.КоманднаяПанель.ПодчиненныеЭлементы.СписокАктивныеКонтрагентыПринятьОтправитьПриглашение.Доступность= Ложь;
				Элементы.СписокАктивные.КонтекстноеМеню.ПодчиненныеЭлементы.СписокАктивныеКонтекстноеМенюГруппа1.ПодчиненныеЭлементы.
				СписокАктивныеКонтекстноеМенюКонтрагентыПринятьОтправитьПриглашение.Доступность= Ложь;
				
			КонецЕсли;
			
			Если ТекущиеДанные.ТекущийСтатус = "InvitesMe" Тогда
				
				Элементы.СписокАктивные.КоманднаяПанель.ПодчиненныеЭлементы.СписокАктивныеКонтрагентыПринятьОтправитьПриглашение.Заголовок= "Принять приглашение";
				Элементы.СписокАктивные.КонтекстноеМеню.ПодчиненныеЭлементы.СписокАктивныеКонтекстноеМенюГруппа1.ПодчиненныеЭлементы.
				СписокАктивныеКонтекстноеМенюКонтрагентыПринятьОтправитьПриглашение.Заголовок= "Принять приглашение";
				
			ИначеЕсли 	ТекущиеДанные.ТекущийСтатус = "RejectsMe"
				ИЛИ ТекущиеДанные.ТекущийСтатус = "IsRejectedByMe" Тогда
				
				Элементы.СписокАктивные.КоманднаяПанель.ПодчиненныеЭлементы.СписокАктивныеКонтрагентыПринятьОтправитьПриглашение.Заголовок= "Отправить новое приглашение";
				Элементы.СписокАктивные.КонтекстноеМеню.ПодчиненныеЭлементы.СписокАктивныеКонтекстноеМенюГруппа1.ПодчиненныеЭлементы.
				СписокАктивныеКонтекстноеМенюКонтрагентыПринятьОтправитьПриглашение.Заголовок= "Отправить новое приглашение";
				
			КонецЕсли;
			
			Если 	ТекущиеДанные.ТекущийСтатус = "IsMyCounteragent"
				ИЛИ ТекущиеДанные.ТекущийСтатус = "InvitesMe"
				ИЛИ ТекущиеДанные.ТекущийСтатус = "IsInvitedByMe" Тогда
				
				Элементы.СписокАктивные.КоманднаяПанель.ПодчиненныеЭлементы.СписокАктивныеКонтрагентыЗаблокироватьКонтрагента.Доступность= Истина;
				Элементы.СписокАктивные.КонтекстноеМеню.ПодчиненныеЭлементы.СписокАктивныеКонтекстноеМенюГруппа1.ПодчиненныеЭлементы.
				СписокАктивныеКонтекстноеМенюКонтрагентыЗаблокироватьКонтрагента.Доступность= Истина;
				
			Иначе
				
				Элементы.СписокАктивные.КоманднаяПанель.ПодчиненныеЭлементы.СписокАктивныеКонтрагентыЗаблокироватьКонтрагента.Доступность= Ложь;
				Элементы.СписокАктивные.КонтекстноеМеню.ПодчиненныеЭлементы.СписокАктивныеКонтекстноеМенюГруппа1.ПодчиненныеЭлементы.
				СписокАктивныеКонтекстноеМенюКонтрагентыЗаблокироватьКонтрагента.Доступность= Ложь;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТочностьЦеныСФПриИзменении(Элемент)
		
		Если ТочностьЦеныСФ < 2 ИЛИ ТочностьЦеныСФ > 11 Тогда
			
			ТочностьЦеныСФ = 2;
			
			ТекстСообщения = НСтр("ru = 'Точность цены счета-фактуры должна иметь значение от 2 до 11 знаков.'");
			СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ИспользоватьПодключаемыйМодульПриИзменении(Элемент)
		
		Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.ИспользоватьМодуль = ИспользоватьПодключаемыйМодуль;
		
		Если Не ИспользоватьПодключаемыйМодуль Тогда
			РежимСовместимостиФорматов155и820 = Ложь;
			Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.РежимСовместимостиФорматов155и820 = Ложь;
		КонецЕсли;
		
		МетодКлиента( , "СинхронизироватьПараметрыКлиентСервера", Объект.ПараметрыКлиентСервер);
		
		МетодСервераБезКонтекста( ,"СохранитьНастройкиПодключаемогоМодуля");
		МетодСервераБезКонтекста( ,"СброситьКэшМодулей");
		
		НастроитьЭлементыПодключаемогоМодуля();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура РежимСовместимостиФорматов155и820ПриИзменении(Элемент)
		
		Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.РежимСовместимостиФорматов155и820 = РежимСовместимостиФорматов155и820;
		
		МетодКлиента( , "СинхронизироватьПараметрыКлиентСервера", Объект.ПараметрыКлиентСервер);
		
		МетодСервераБезКонтекста( ,"СохранитьНастройкиПодключаемогоМодуля");
		МетодСервераБезКонтекста( ,"СброситьКэшМодулей");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПереключательРасположенияПодключаемогоМодуляПриИзменении(Элемент)
		
		РасположенияПодключаемогоМодуляПриИзменении();
		
		НастроитьЭлементыПодключаемогоМодуля();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПутьКПодключаемомуМодулюПриИзменении(Элемент)
		
		РасположенияПодключаемогоМодуляПриИзменении();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПутьКПодключаемомуМодулюСсылкаПриИзменении(Элемент)
		
		РасположенияПодключаемогоМодуляПриИзменении();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПутьКПодключаемомуМодулюИмяОбработкиПриИзменении(Элемент)
		
		РасположенияПодключаемогоМодуляПриИзменении();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПутьКПодключаемомуМодулюНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
		ДиалогВыбораФайла= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогВыбораФайла.МножественныйВыбор= Ложь;
		ДиалогВыбораФайла.ПолноеИмяФайла= ПутьКПодключаемомуМодулюДиск;
		ДиалогВыбораФайла.Фильтр = "Внешняя обработка (*.epf)|*.epf";
		ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ОбработчикПутьКПодключаемомуМодулюДискЗавершениеВыбораФайла", ЭтаФорма));
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПутьКПодключаемомуМодулюИмяОбработкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
		Элемент.СписокВыбора.Очистить();
		
		СписокОбработокДереваКонфигурации= МетодКлиента("Модуль_Клиент","СписокОбработокДереваКонфигурации");
		
		Для Каждого ЭлементСписка ИЗ СписокОбработокДереваКонфигурации Цикл
			Элемент.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПутьКПодключаемомуМодулюИмяОбработкиИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
		
		Элемент.СписокВыбора.Очистить();
		
		СписокОбработокДереваКонфигурации= МетодКлиента("Модуль_Клиент","СписокОбработокДереваКонфигурации");
		
		Для Каждого ЭлементСписка ИЗ СписокОбработокДереваКонфигурации Цикл
			Элемент.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПутьКПодключаемомуМодулюИмяОбработкиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
		ОбратныйИндекс= Элемент.СписокВыбора.Количество()-1;
		Пока ОбратныйИндекс > -1 Цикл
			
			Если Элемент.СписокВыбора[ОбратныйИндекс].Значение <> ВыбранноеЗначение Тогда
				Элемент.СписокВыбора.Удалить(ОбратныйИндекс);
			КонецЕсли;
			
			ОбратныйИндекс= ОбратныйИндекс - 1;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТипыДокументовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
		ЗаполнитьСписокВыбораТипыДокументов(Элемент);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТипыДокументовАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
		
		ЗаполнитьСписокВыбораТипыДокументов(Элемент);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ТипыДокументовПриИзменении(Элемент)
		
		МетодКлиента("Модуль_Клиент","ЭлементВРежимеВыбораИзСпискаПриИзменении", ЭтаФорма, Элемент, ТипыДокументов);
		
		НачатьОбновлениеСпискаДокументов()
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СостояниеДокументооборотаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
		ЗаполнитьСписокВыбораСостояниеДокументооборота(Элемент);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СостояниеДокументооборотаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
		
		ЗаполнитьСписокВыбораСостояниеДокументооборота(Элемент);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СостояниеДокументооборотаПриИзменении(Элемент)
		
		Если СостояниеДокументооборота = "-1" Тогда
			СостояниеДокументооборота = "0";  // Запретим выбирать группы
		КонецЕсли;
		
		НачатьОбновлениеСпискаДокументов();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОрганизацияПриИзменении(Элемент)
		
		МетодКлиента("Модуль_Клиент","ЭлементВРежимеВыбораИзСпискаПриИзменении", ЭтаФорма, Элемент, Организация);
		
		НачатьОбновлениеСпискаДокументов()
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОрганизацияКонтрагентыПриИзменении(Элемент)
		
		МетодКлиента("Модуль_Клиент","ЭлементВРежимеВыбораИзСпискаПриИзменении", ЭтаФорма, Элемент, Организация);
		
		Если ЗначениеЗаполнено(Организация) Тогда
			
			Элементы.СтраницыКонтрагенты.ТекущаяСтраница= Элементы.СтраницаКонтрагентыСписок;
			
			ЗаполнитьСписокКонтрагентов();
			
		Иначе
			Элементы.СтраницыКонтрагенты.ТекущаяСтраница= Элементы.СтраницаКонтрагентыПодсказкаВыбораОрганизации;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПутьКМодулюВСправочнике1СПриИзменении(Элемент)
		
		Объект.ПараметрыКлиентСервер.ПутьКМодулюВСправочнике1С = ПутьКМодулюВСправочнике1С;
		
		МетодКлиента( , "СинхронизироватьПараметрыКлиентСервера", Объект.ПараметрыКлиентСервер);
		
		МетодСервера( , "УстановитьЗначениеКонстанты", "ПутьКМодулюВСправочнике1С", ПутьКМодулюВСправочнике1С);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОтправкаФайловСтарыйИнтерфейсПриИзменении(Элемент)
		
		Объект.ПараметрыКлиентСервер.ОтправкаФайловСтарыйИнтерфейс = ОтправкаФайловСтарыйИнтерфейс;
		
		МетодКлиента( , "СинхронизироватьПараметрыКлиентСервера", Объект.ПараметрыКлиентСервер);
		
		МетодКлиента( ,"ОповеститьФормы", "ИзмененПараметр_ОтправкаФайловСтарыйИнтерфейс",,,"Форма_Выгрузка");
		
		ДобавитьСтатистику_ИзменилиОтправкаВСтаромИнтерфейсе();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗапретитьРаботуСМетрикамиПриИзменении(Элемент)
		
		Объект.ПараметрыКлиентСервер.Метрики.ИспользоватьМетрики = НЕ ЗапретитьРаботуСМетриками;
		
		МетодКлиента( , "СинхронизироватьПараметрыКлиентСервера", Объект.ПараметрыКлиентСервер);
		
		МетодСервераБезКонтекста( , "СохранитьНастройкуСбораМетрик");
		
		ВключитьОтключитьПериодическуюОтправкуСтатистики(Объект.ПараметрыКлиентСервер.Метрики.ИспользоватьМетрики);
		
	КонецПроцедуры
	
	//} СОБЫТИЯ
	
	//{ ТЕЛО МОДУЛЯ
	
	&НаСервере
	функция ПровестиОтменитьДокумент(СсылкаНаДокумент, Действие)
		
		ДокИзменен=	Ложь;
		
		Док=	СсылкаНаДокумент.ПолучитьОбъект();
		Попытка
			Док.Записать(РежимЗаписиДокумента[Действие]);
			ДокИзменен=	Истина;
		Исключение
			СообщениеПользователю=	Новый СообщениеПользователю;
			СообщениеПользователю.Текст=	ОписаниеОшибки();
			СообщениеПользователю.Сообщить();
		КонецПопытки;
		
		Возврат ДокИзменен;
		
	КонецФункции
	
	&НаСервере
	Процедура ПометитьДокументНаУдаление(СсылкаНаДокумент)
		
		ДокументОбъект=	СсылкаНаДокумент.ПолучитьОбъект();
		ДокументОбъект.УстановитьПометкуУдаления(НЕ ДокументОбъект.ПометкаУдаления);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбновитьКонтекстДиадока()
		
		Платформа.ПараметрыКлиент.КонтекстДиадока.Очистить();
		
		//получаем массив всех организаций и их подразделений 
		МассивOrganizationDepartment = Новый Массив;
		
		OrganizationList = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationList(); 
		
		Для Индекс = 0 По OrganizationList.Count - 1 Цикл
			
			Organization = OrganizationList.GetItem(Индекс);
			
			СтруктураОрганизации = Новый Структура;
			СтруктураОрганизации.Вставить("OrganizationId", Organization.Id);
			СтруктураОрганизации.Вставить("DepartmentKpp",	Неопределено);
			СтруктураОрганизации.Вставить("Организация1С",	Неопределено);
			
			МассивOrganizationDepartment.Добавить(СтруктураОрганизации);
			
			Для ИндексПодразделения = 0 По Organization.Departments.Count - 1 Цикл
				
				department = Organization.Departments.GetItem(ИндексПодразделения);
				
				Если НЕ ПустаяСтрока(department.Kpp) И department.Kpp <> Organization.Kpp Тогда
					
					СтруктураПодразделенияОрганизации =	Новый Структура;
					СтруктураПодразделенияОрганизации.Вставить("OrganizationId",	Organization.Id);
					СтруктураПодразделенияОрганизации.Вставить("DepartmentKpp",		department.Kpp);
					СтруктураПодразделенияОрганизации.Вставить("Организация1С",		Неопределено);
					
					МассивOrganizationDepartment.Добавить(СтруктураПодразделенияОрганизации);
					
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		КонтекстДиадока = Платформа.ПараметрыКлиент.КонтекстДиадока;
		//проверяем ранее заполненный массив на наличие сопоставленной организации в 1С и добавляем в контекст
		МассивOrganizationDepartment = МетодСервера(,"ПолучитьСопоставленныеОрганизациив1С", МассивOrganizationDepartment);
		Для Каждого ТекОрганизация Из МассивOrganizationDepartment Цикл
			
			Если ТекОрганизация.Организация1С <> Неопределено Тогда 
				
				НайденныеСтрокиСОрганизацией = НайтиСтрокиВМассивеСтруктур(КонтекстДиадока, Новый Структура("Организация", ТекОрганизация.Организация1С));
				Если НайденныеСтрокиСОрганизацией.Количество() = 0 Тогда
					МетодКлиента("Модуль_Клиент","ПолучитьЯщикДиадокДляОрганизации", ТекОрганизация.Организация1С);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры	
	
	&НаКлиенте
	Процедура СброситьПоследнийСертификатПользователя()
		
		СтруктураНастроек = Новый Структура;
		СтруктураНастроек.Вставить("ДиадокПоследнийСертификатПользователя",Неопределено);
		СтруктураНастроек.Вставить("ДиадокПоследнееПредставлениеПользователя", 	"");
		
		МетодСервераБезКонтекста(,"УстановитьНастройкиПользователей", СтруктураНастроек);
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПолучитьРасширениеФайлаДиадок(ИмяФайла)
		КолСим = СтрДлина(ИмяФайла);
		Для ИндЦикла = 1 По КолСим Цикл
			Инд = КолСим + 1 - ИндЦикла;
			Если Сред(ИмяФайла, Инд, 1) = "." Тогда
				Возврат ?(КолСим = Инд, 0, Сред(ИмяФайла, Инд + 1, КолСим - Инд));
			КонецЕсли;
		КонецЦикла;
	КонецФункции
	
	&НаКлиенте
	Функция НовоеОписаниеДокументаПакета()
		
		Результат = МетодКлиента("Модуль_Клиент", "НовоеОписаниеДокументаПакета");
		
		Возврат Результат;
		
	КонецФункции
	
	// Заполняет описание документа по данным строки таблицы формы
	// со списком входящих или исходящих документов.
	//
	// Параметры:
	//  ОписаниеДокумента	 - Структура - см. функцию НовоеОписаниеДокументаПакета;
	//  СтрокаТаблицы		 - ДанныеФормыЭлементКоллекции - строка таблицы формы;
	//
	&НаКлиенте
	Процедура ЗаполнитьОписаниеДокументаПоСтрокеТаблицы(ОписаниеДокумента, СтрокаТаблицы)
		
		ЗаполнитьЗначенияСвойств(ОписаниеДокумента, СтрокаТаблицы);
		
		Буфер = Новый Структура;
		Буфер.Вставить("CounteragentId");
		Буфер.Вставить("Номер");
		Буфер.Вставить("Дата");
		Буфер.Вставить("ПервичныйДокумент");
		Буфер.Вставить("DocumentFunction");
		Буфер.Вставить("ContentType");
		
		ЗаполнитьЗначенияСвойств(Буфер, СтрокаТаблицы);
		
		ОписаниеДокумента.CounteragentBoxID		 = Буфер.CounteragentId;
		ОписаниеДокумента.НомерДокумента		 = Буфер.Номер;
		ОписаниеДокумента.ДатаДокумента			 = Буфер.Дата;
		ОписаниеДокумента.Документ1С			 = Буфер.ПервичныйДокумент;
		ОписаниеДокумента.ФункцияУПД			 = Буфер.DocumentFunction;
		ОписаниеДокумента.ТипКонтента			 = Буфер.ContentType;
		ОписаниеДокумента.РасширениеФайлаДиадок	 = ПолучитьРасширениеФайлаДиадок(СтрокаТаблицы.FileName);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СформироватьПечатнуюФормуПоФайлуДиадок(ИмяТаблицыДокументов)
		
		ТекущиеДанные = Элементы[ИмяТаблицыДокументов].ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			ПоказатьПредупреждение(, "Не выбран документ", 120, Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
			Возврат;
		КонецЕсли;
		
		ПараметрМассивДокументовПакета = Новый Массив;
		
		Если ИмяТаблицыДокументов = "ТаблицаДокументовВходящих" Тогда
			
			МассивДокументовПакета = ТаблицаДокументовВходящих.НайтиСтроки(Новый Структура("MessageID", ТекущиеДанные.MessageID));
			
			Для каждого ДокументПакета из МассивДокументовПакета Цикл
				
				ЭтоТекущаяСтрока = (ДокументПакета = ТекущиеДанные);
				
				ЭлементМассива = НовоеОписаниеДокументаПакета();
				ЗаполнитьОписаниеДокументаПоСтрокеТаблицы(ЭлементМассива, ДокументПакета);
				ЭлементМассива.ЭтоТекущиеДанные = ЭтоТекущаяСтрока;
				ЭлементМассива.Вкл = ЭлементМассива.Вкл Или ЭтоТекущаяСтрока;
				
				ПараметрМассивДокументовПакета.Добавить(ЭлементМассива);
				
			КонецЦикла;
			
		Иначе
			
			ЭлементМассива = НовоеОписаниеДокументаПакета();
			ЗаполнитьОписаниеДокументаПоСтрокеТаблицы(ЭлементМассива, ТекущиеДанные);
			ЭлементМассива.ЭтоТекущиеДанные = Истина;
			ЭлементМассива.Вкл = Истина;
			
			ПараметрМассивДокументовПакета.Добавить(ЭлементМассива);
			
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("DepartmentKpp"			, ТекущиеДанные.DepartmentKpp);
		ПараметрыФормы.Вставить("DepartmentId"			, ТекущиеДанные.DepartmentId);
		ПараметрыФормы.Вставить("BoxID"					, ТекущиеДанные.BoxID);
		ПараметрыФормы.Вставить("CounteragentBoxID"		, ТекущиеДанные.CounteragentId);
		ПараметрыФормы.Вставить("МассивДокументовПакета", ПараметрМассивДокументовПакета);
		ПараметрыФормы.Вставить("ТочкаВызова"			, ИмяТаблицыДокументов);
		ПараметрыФормы.Вставить("Операция"				, "ОткрытьПакет");
		
		Попытка
			Document= Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(ТекущиеДанные.BoxID).GetDocumentById(ТекущиеДанные.DocumentID);
		Исключение
			
			Результат=	Новый Структура();
			Результат.Вставить("ОписаниеОшибки", 	"Ошибка получения документа с сервера " + Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
			Результат.Вставить("Подробности", 		ТекущиеДанные.DocumentID);
			ОткрытьФормуВыводаОшибки(Результат, Истина);
			
			Возврат;
			
		КонецПопытки;
		
		ИмяФормыПросмотра= 	МетодКлиента("Модуль_Клиент", "ПолучитьИмяФормыДокумента", Document);
		
		ФормаПросмотра= МетодКлиента(, "ПолучитьФормуОбработки", ИмяФормыПросмотра, ПараметрыФормы, ЭтаФорма, СокрЛП(ТекущиеДанные.DocumentID) + "/" + СокрЛП(ТекущиеДанные.BoxID));
		ФормаПросмотра.ЭДОбъект = Document;
		ФормаПросмотра.Открыть();
		
	КонецПроцедуры
	
	&НаСервере
	процедура ЗаполнитьСписокКонтрагентовСервер(МассивЗагрузки) 
		
		ТаблицаКонтрагентовСервер = Новый ТаблицаЗначений;
		ТаблицаКонтрагентовСервер.Колонки.Добавить("ID", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(50)));
		ТаблицаКонтрагентовСервер.Колонки.Добавить("Контрагент");
		ТаблицаКонтрагентовСервер.Колонки.Добавить("ИНН");
		ТаблицаКонтрагентовСервер.Колонки.Добавить("КПП");
		ТаблицаКонтрагентовСервер.Колонки.Добавить("FnsParticipantId");
		ТаблицаКонтрагентовСервер.Колонки.Добавить("ТекущийСтатус");
		
		Для каждого Элемент Из МассивЗагрузки Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаКонтрагентовСервер.Добавить(), Элемент);
		КонецЦикла; 
		
		ТабКонтагентовДляМаршрутизации = МетодСервера(,"ВернутьТаблицуМаршрутизацииПоТаблицеКонтрагентов", ТаблицаКонтрагентовСервер.Скопировать(,"ID"));
		
		Для Каждого СтрокаТаблицыКонтрагентов Из ТаблицаКонтрагентовСервер Цикл
			
			СтрокаСписка = СписокАктивные.Добавить();
			
			СтрокаСписка.ID					= СтрокаТаблицыКонтрагентов.ID;
			СтрокаСписка.FnsParticipantId	= СтрокаТаблицыКонтрагентов.FnsParticipantId;
			СтрокаСписка.ИНН				= СтрокаТаблицыКонтрагентов.ИНН;
			СтрокаСписка.КПП				= СтрокаТаблицыКонтрагентов.КПП;
			СтрокаСписка.Контрагент			= СтрокаТаблицыКонтрагентов.Контрагент;
			СтрокаСписка.ТекущийСтатус		= СтрокаТаблицыКонтрагентов.ТекущийСтатус;
			
			СтрокаСписка.ТекущийСтатусРасшифровка =	МетодСервера(,"РасшифровкаТекущегоСостоянияВзаимоотношений", СтрокаСписка.ТекущийСтатус);
			
			ПараметрыОтбора 	= Новый Структура("Id", СтрокаТаблицыКонтрагентов.Id);
			ТабМаршрутизации 	= ТабКонтагентовДляМаршрутизации.НайтиСтроки(ПараметрыОтбора);
			
			Для каждого СтрокаТабМаршрутизации Из ТабМаршрутизации Цикл
				
				СтрокаПоКонтрагенту = СтрокаСписка.СписокКонтрагентовВ1С.ПолучитьЭлементы().Добавить();
				СтрокаПоКонтрагенту.КонтрагентВ1С				= СтрокаТабМаршрутизации.Контрагент;
				СтрокаПоКонтрагенту.ПодразделениеКонтрагента	= СтрокаТабМаршрутизации.ПодразделениеКонтрагента;
				СтрокаПоКонтрагенту.ToDepartmentID				= СтрокаТабМаршрутизации.ToDepartmentID;
				
				ТабМаршрутизацииПоДоговорам = МетодСервера(,"ВернутьТаблицуМаршрутизацииПоДоговорам", Организация, СтрокаТабМаршрутизации.Контрагент);
				Для каждого СтрокаТабМаршрутизацииПоДоговорам Из ТабМаршрутизацииПоДоговорам Цикл
					
					СтрокаПоДоговорам = СтрокаПоКонтрагенту.ПолучитьЭлементы().Добавить();
					СтрокаПоДоговорам.Договор					= СтрокаТабМаршрутизацииПоДоговорам.Договор;
					СтрокаПоДоговорам.ПодразделениеКонтрагента	= СтрокаТабМаршрутизацииПоДоговорам.ПодразделениеДоговора;
					СтрокаПоДоговорам.ToDepartmentID			= СтрокаТабМаршрутизацииПоДоговорам.ToDepartmentID;
					СтрокаПоДоговорам.КонтрагентВ1С				= СтрокаПоКонтрагенту.КонтрагентВ1С;
					
				КонецЦикла;
				
				СтрокаСписка.ПредставлениеСпискаКонтрагентов1С = СтрокаСписка.ПредставлениеСпискаКонтрагентов1С + ?(ПустаяСтрока(СтрокаСписка.ПредставлениеСпискаКонтрагентов1С), "", "; ") + СтрокаТабМаршрутизации.КонтрагентНаименование;
				
				СтрокаСписка.ФорматДокументовНаОтправку = СтрокаТабМаршрутизации.ФорматДокументовНаОтправку;
				
			КонецЦикла;
			
			Если СтрокаСписка.СписокКонтрагентовВ1С.ПолучитьЭлементы().Количество() > 1 Тогда
				СтрокаСписка.ПредставлениеСпискаКонтрагентов1С=	СтрокаСписка.ПредставлениеСпискаКонтрагентов1С + ";";
			КонецЕсли;
			
		КонецЦикла;	
		
		СписокАктивные.Сортировать("Контрагент Возр");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСписокКонтрагентовПоСтатусу(МассивЗагрузки, КоллекцияКонтрагентов)
		
		Если КоллекцияКонтрагентов <> Неопределено Тогда
			
			Для Индекс = 0 По КоллекцияКонтрагентов.Count - 1 Цикл
				
				ЭлементКоллекции = КоллекцияКонтрагентов.GetItem(Индекс);
				
				ЗаполненнаяСтруктура = Новый Структура;
				ТекстДополнения = "";
				Если МетодКлиента("Модуль_Клиент", "ЭтоТестоваяОрганизация", ЭлементКоллекции) Тогда
					ТекстДополнения = "тестовый";	
				КонецЕсли;
				Если ЭлементКоллекции.IsRoaming Тогда
					ТекстДополнения = ТекстДополнения + ?(ЗначениеЗаполнено(ТекстДополнения), ", ", "") + "роуминговый";	
				КонецЕсли;
				ЗаполненнаяСтруктура.Вставить("Контрагент", 				ЭлементКоллекции.Name + ?(ЗначениеЗаполнено(ТекстДополнения), " (" + ТекстДополнения + ")", ""));
				
				ЗаполненнаяСтруктура.Вставить("ИНН", 						ЭлементКоллекции.Inn);
				ЗаполненнаяСтруктура.Вставить("КПП", 						ЭлементКоллекции.Kpp);
				ЗаполненнаяСтруктура.Вставить("Id",							ЭлементКоллекции.ID);
				ЗаполненнаяСтруктура.Вставить("FnsParticipantId",			ЭлементКоллекции.FnsParticipantId);
				ЗаполненнаяСтруктура.Вставить("ТекущийСтатус",				ЭлементКоллекции.GetStatus());
				ЗаполненнаяСтруктура.Вставить("ТекущийСтатусРасшифровка",	Неопределено);
				
				МассивЗагрузки.Добавить(ЗаполненнаяСтруктура);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСписокКонтрагентов()
		
		СписокАктивные.Очистить();
		
		МассивЗагрузки = Новый Массив;
		
		Если ПустаяСтрока(СтатусВзаимоотношений) Тогда
			
			КоллекцияКонтрагентовActive = МетодКлиента("Модуль_Клиент", "GetCounteragentListByStatus", Организация, "IsMyCounteragent");
			ЗаполнитьСписокКонтрагентовПоСтатусу(МассивЗагрузки, КоллекцияКонтрагентовActive);
			
			КоллекцияКонтрагентовInviting =	МетодКлиента("Модуль_Клиент", "GetCounteragentListByStatus", Организация, "InvitesMe");
			ЗаполнитьСписокКонтрагентовПоСтатусу(МассивЗагрузки, КоллекцияКонтрагентовInviting);
			
			КоллекцияКонтрагентовIsInvited = МетодКлиента("Модуль_Клиент", "GetCounteragentListByStatus", Организация, "IsInvitedByMe");
			ЗаполнитьСписокКонтрагентовПоСтатусу(МассивЗагрузки, КоллекцияКонтрагентовIsInvited);
			
			КоллекцияКонтрагентовDenied = МетодКлиента("Модуль_Клиент", "GetCounteragentListByStatus", Организация, "Rejected");
			ЗаполнитьСписокКонтрагентовПоСтатусу(МассивЗагрузки, КоллекцияКонтрагентовDenied);
			
		ИначеЕсли СтатусВзаимоотношений = "RejectsMe" ИЛИ СтатусВзаимоотношений = "IsRejectedByMe" Тогда
			
			КоллекцияКонтрагентовDenied = МетодКлиента("Модуль_Клиент", "GetCounteragentListByStatus", Организация, "Rejected");
			ЗаполнитьСписокКонтрагентовПоСтатусу(МассивЗагрузки, КоллекцияКонтрагентовDenied);
			
		Иначе
			
			КоллекцияКонтрагентовFilter = МетодКлиента("Модуль_Клиент", "GetCounteragentListByStatus", Организация, СтатусВзаимоотношений);
			ЗаполнитьСписокКонтрагентовПоСтатусу(МассивЗагрузки, КоллекцияКонтрагентовFilter);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(МассивЗагрузки) Тогда
			ЗаполнитьСписокКонтрагентовСервер(МассивЗагрузки);
		КонецЕсли; 
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ВыполнитьЛогикуПослеАвторизации()
		
		ИспользуетсяСертификатПоГОСТ2001 = МетодКлиента("Модуль_Клиент", "ИспользуетсяСертификатПоГОСТ2001");
		
		Если ИспользуетсяСертификатПоГОСТ2001 Тогда
			МетодКлиента("Модуль_Клиент", "ПоказатьПредупреждениеОбИспользованииСертификатаГОСТ2001");
		КонецЕсли;
		
		ЗаполнитьИерархиюОрганизацийDiadoc();
		ОбновитьКонтекстДиадока();
		
		Элементы.ДекорацияТекущийПользовательВДиадоке.Заголовок= Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.ПредставлениеПользователя;
		
		ОчиститьФильтрОрганизация();
		ОчиститьФильтрКонтрагент();
		
		Если Платформа.ПараметрыКлиент.КонтекстДиадока.Количество()=0 Тогда
			
			СписокДобавленныхIdBox=	Новый СписокЗначений;
			Для каждого Ящик Из СписокПочтовыхЯщиков Цикл
				СписокДобавленныхIdBox.Добавить(Ящик.IdBox);
			КонецЦикла;
			
			ПараметрыФормы=	Новый Структура();
			ПараметрыФормы.Вставить("СписокДобавленныхIdBox", СписокДобавленныхIdBox);
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаДобавлениеЯщиков", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыДобавленияЯщиковПриСтарте");
			
		Иначе
			ЗапуститьМодуль();
			
			//если есть заблокированные по API организации, то выводим информационное окно
			ОтборЗаблокированныхОрганизаций = Новый Структура;
			ОтборЗаблокированныхОрганизаций.Вставить("ЗаблокированаПоAPI", Истина);
			ЗаблокированныеОрганизации = МетодКлиента("Модуль_Клиент","НайтиСтрокиКонтекстДиадокаПоОтбору", ОтборЗаблокированныхОрганизаций);
			
			Если ЗаблокированныеОрганизации.Количество() > 0 Тогда 
				
				ПараметрыФормы = Новый Структура;
				СписокОрганизаций = Новый СписокЗначений();
				Для Каждого ТекСтрока Из ЗаблокированныеОрганизации Цикл
					СписокОрганизаций.Добавить(ПолучитьСтрокуДляФормыОплатыСервиса(ТекСтрока));
				КонецЦикла;
				ПараметрыФормы.Вставить("СписокОрганизаций", СписокОрганизаций);
				
				МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОплатаСервиса", ПараметрыФормы, ЭтаФорма);
				
			КонецЕсли;
			
			ПроверитьНаАктуальностьВерсиюМодуля();
			ПоказатьПанельОкончанияОплаченногоПериода();
			
		КонецЕсли;	
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ДобавитьПодразделенияВИерархиюОрганизацийDiadoc(Organization, Parent, Departments)
		
		ВГраница = Departments.Count-1;
		
		Для ИндексЦикла = 0 По ВГраница Цикл
			
			Item= Departments.GetItem(ИндексЦикла);
			
			Платформа.ПараметрыКлиент.ИерархияОрганизацийDiadoc.Вставить(Item.Id, 
			Новый Структура("Name, Id, Inn, Kpp, DepartmentKpp, ParentId", 
			Item.Name, Item.Id, Organization.Inn, Item.Kpp, Item.Kpp, Parent.Id));
			
			ДобавитьПодразделенияВИерархиюОрганизацийDiadoc(Organization, Item, Item.Subdepartments);
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьИерархиюОрганизацийDiadoc()
		
		OrganizationList= Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationList();
		
		ВГраница= OrganizationList.Count-1;
		
		Для ИндексЦикла = 0 По ВГраница Цикл
			
			Item= OrganizationList.GetItem(ИндексЦикла);
			
			//проверяем на блокировку по API
			Попытка
				Item.GetUserPermissions();
			Исключение
				Продолжить;
			КонецПопытки;
			
			Платформа.ПараметрыКлиент.ИерархияОрганизацийDiadoc.Вставить(Item.Id, 
			Новый Структура("Name, Id, Inn, Kpp, DepartmentKpp, ParentId", Item.Name, Item.Id, Item.Inn, Item.Kpp));
			
			ДобавитьПодразделенияВИерархиюОрганизацийDiadoc(Item, Item, Item.Departments);
			
		КонецЦикла;
		
	КонецПроцедуры
	
	// Возвращает список организаций 1С, для которых настроено сопоставление с ящиками ДД.
	// Если в отборе для документов выбрана организация, то в список добавляется только она.
	// 
	// Возвращаемое значение:
	//  СписокЗначений – список значений типа СправочникСсылка.Организации.
	//
	&НаКлиенте
	Функция ОрганизацииТекущегоКонтекста()
		
		Результат = Новый СписокЗначений;
		
		Для Каждого Стр ИЗ Платформа.ПараметрыКлиент.КонтекстДиадока Цикл
			
			Если Стр.ЗаблокированаПоAPI = Истина Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Организация) ИЛИ Стр.Организация = Организация Тогда 
				Результат.Добавить(Стр.Организация);
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции //ОрганизацииТекущегоКонтекста()	
	
	&НаСервере
	Функция ПолучитьСписокДокументов(ТекущаяСтраницаИмя, ПараметрыСписка)
		
		Запрос=	Новый Запрос;
		
		Если ТекущаяСтраницаИмя = "СтраницаРеализацияТоваровУслуг" Тогда
			
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
				
				Запрос.Текст=
				"ВЫБРАТЬ
				|	РасходнаяНакладная.Номер,
				|	РасходнаяНакладная.Дата КАК Дата,
				|	РасходнаяНакладная.Организация,
				|	РасходнаяНакладная.Контрагент,
				|	РасходнаяНакладная.СтруктурнаяЕдиница КАК Склад,
				|	ИдЯщикОрганизации.Значение КАК ИдЯщикОрганизации,
				|	ИдДокументДиадок.Значение КАК ИдДокументДиадок,
				|	РасходнаяНакладная.Ссылка,
				|	"""" КАК СостояниеДокументооборота,
				|	ВЫБОР
				|		КОГДА ИдДокументДиадок.Значение ЕСТЬ NULL 
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ИСТИНА
				|	КОНЕЦ КАК ЕстьВДиадоке,
				|	ВЫБОР
				|		КОГДА РасходнаяНакладная.Проведен
				|			ТОГДА 0
				|		ИНАЧЕ ВЫБОР
				|				КОГДА НЕ РасходнаяНакладная.ПометкаУдаления
				|					ТОГДА 1
				|				ИНАЧЕ 2
				|			КОНЕЦ
				|	КОНЕЦ КАК СостояниеДокумента,
				|	РасходнаяНакладная.СуммаДокумента
				|ИЗ
				|	Документ.РасходнаяНакладная КАК РасходнаяНакладная
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &ИдентификаторСвойстваЯщикОрганизации) КАК ИдЯщикОрганизации
				|		ПО РасходнаяНакладная.Организация = ИдЯщикОрганизации.Объект
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &ИдентификаторСвойстваИдентификаторДокументаВДиадок) КАК ИдДокументДиадок
				|		ПО РасходнаяНакладная.Ссылка = ИдДокументДиадок.Объект
				|ГДЕ
				|	ВЫБОР
				|			КОГДА &Ссылка = НЕОПРЕДЕЛЕНО
				|				ТОГДА ИСТИНА
				|			ИНАЧЕ РасходнаяНакладная.Ссылка = &Ссылка
				|		КОНЕЦ
				|	И РасходнаяНакладная.Дата МЕЖДУ &НачалоПериода И &КонецПериода
				|
				|УПОРЯДОЧИТЬ ПО
				|	Дата";
				
			Иначе
				
				Запрос.Текст=
				"ВЫБРАТЬ
				|	РеализацияТоваровУслуг.Номер,
				|	РеализацияТоваровУслуг.Дата КАК Дата,
				|	РеализацияТоваровУслуг.Организация,
				|	РеализацияТоваровУслуг.Контрагент,
				|	РеализацияТоваровУслуг.Склад,
				|	ИдЯщикОрганизации.Значение КАК ИдЯщикОрганизации,
				|	ИдДокументДиадок.Значение КАК ИдДокументДиадок,
				|	РеализацияТоваровУслуг.Ссылка,
				|	"""" КАК СостояниеДокументооборота,
				|	ВЫБОР
				|		КОГДА ИдДокументДиадок.Значение ЕСТЬ NULL 
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ИСТИНА
				|	КОНЕЦ КАК ЕстьВДиадоке,
				|	ВЫБОР
				|		КОГДА РеализацияТоваровУслуг.Проведен
				|			ТОГДА 0
				|		ИНАЧЕ ВЫБОР
				|				КОГДА НЕ РеализацияТоваровУслуг.ПометкаУдаления
				|					ТОГДА 1
				|				ИНАЧЕ 2
				|			КОНЕЦ
				|	КОНЕЦ КАК СостояниеДокумента,
				|	РеализацияТоваровУслуг.СуммаДокумента
				|ИЗ
				|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &ИдентификаторСвойстваЯщикОрганизации) КАК ИдЯщикОрганизации
				|		ПО РеализацияТоваровУслуг.Организация = ИдЯщикОрганизации.Объект
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &ИдентификаторСвойстваИдентификаторДокументаВДиадок) КАК ИдДокументДиадок
				|		ПО РеализацияТоваровУслуг.Ссылка = ИдДокументДиадок.Объект
				|ГДЕ
				|	ВЫБОР
				|			КОГДА &Ссылка = НЕОПРЕДЕЛЕНО
				|				ТОГДА ИСТИНА
				|			ИНАЧЕ РеализацияТоваровУслуг.Ссылка = &Ссылка
				|		КОНЕЦ
				|	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
				|
				|УПОРЯДОЧИТЬ ПО
				|	Дата";		
				
			КонецЕсли;
			
		ИначеЕсли ТекущаяСтраницаИмя = "СтраницаСчетФактураВыданный" Тогда
			
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
				
				Запрос.Текст=
				"ВЫБРАТЬ
				|	СчетФактураВыданный.Номер,
				|	СчетФактураВыданный.Дата КАК Дата,
				|	СчетФактураВыданный.Организация,
				|	СчетФактураВыданный.ДокументОснование.Контрагент КАК Контрагент,
				|	"""" КАК Склад,
				|	ИдЯщикОрганизации.Значение КАК ИдЯщикОрганизации,
				|	ИдДокументДиадок.Значение КАК ИдДокументДиадок,
				|	СчетФактураВыданный.Ссылка,
				|	"""" КАК СостояниеДокументооборота,
				|	ВЫБОР
				|		КОГДА ИдДокументДиадок.Значение ЕСТЬ NULL 
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ИСТИНА
				|	КОНЕЦ КАК ЕстьВДиадоке,
				|	ВЫБОР
				|		КОГДА СчетФактураВыданный.ДокументОснование.Проведен
				|			ТОГДА 0
				|		ИНАЧЕ ВЫБОР
				|				КОГДА НЕ СчетФактураВыданный.ПометкаУдаления
				|					ТОГДА 1
				|				ИНАЧЕ 2
				|			КОНЕЦ
				|	КОНЕЦ КАК СостояниеДокумента,
				|	СчетФактураВыданный.ДокументОснование.СуммаДокумента КАК СуммаДокумента
				|ИЗ
				|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &ИдентификаторСвойстваЯщикОрганизации) КАК ИдЯщикОрганизации
				|		ПО СчетФактураВыданный.Организация = ИдЯщикОрганизации.Объект
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &ИдентификаторСвойстваИдентификаторДокументаВДиадок) КАК ИдДокументДиадок
				|		ПО СчетФактураВыданный.Ссылка = ИдДокументДиадок.Объект
				|ГДЕ
				|	ВЫБОР
				|			КОГДА &Ссылка = НЕОПРЕДЕЛЕНО
				|				ТОГДА ИСТИНА
				|			ИНАЧЕ СчетФактураВыданный.Ссылка = &Ссылка
				|		КОНЕЦ
				|	И СчетФактураВыданный.Дата МЕЖДУ &НачалоПериода И &КонецПериода
				|
				|УПОРЯДОЧИТЬ ПО
				|	Дата";
				
			ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "БП30" Тогда
				
				Запрос.Текст=
				"ВЫБРАТЬ
				|	СчетФактураВыданный.Номер,
				|	СчетФактураВыданный.Дата КАК Дата,
				|	СчетФактураВыданный.Организация,
				|	СчетФактураВыданный.ДокументОснование.Контрагент КАК Контрагент,
				|	"""" КАК Склад,
				|	ИдЯщикОрганизации.Значение КАК ИдЯщикОрганизации,
				|	ИдДокументДиадок.Значение КАК ИдДокументДиадок,
				|	СчетФактураВыданный.Ссылка,
				|	"""" КАК СостояниеДокументооборота,
				|	ВЫБОР
				|		КОГДА ИдДокументДиадок.Значение ЕСТЬ NULL 
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ИСТИНА
				|	КОНЕЦ КАК ЕстьВДиадоке,
				|	ВЫБОР
				|		КОГДА СчетФактураВыданный.Проведен
				|			ТОГДА 0
				|		ИНАЧЕ ВЫБОР
				|				КОГДА НЕ СчетФактураВыданный.ПометкаУдаления
				|					ТОГДА 1
				|				ИНАЧЕ 2
				|			КОНЕЦ
				|	КОНЕЦ КАК СостояниеДокумента,
				|	ТаблицаДокОснований.СуммаДокумента КАК СуммаДокумента
				|ИЗ
				|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &ИдентификаторСвойстваЯщикОрганизации) КАК ИдЯщикОрганизации
				|		ПО СчетФактураВыданный.Организация = ИдЯщикОрганизации.Объект
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &ИдентификаторСвойстваИдентификаторДокументаВДиадок) КАК ИдДокументДиадок
				|		ПО СчетФактураВыданный.Ссылка = ИдДокументДиадок.Объект
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			СУММА(СчетФактураВыданныйДокументыОснования.ДокументОснование.СуммаДокумента) КАК СуммаДокумента,
				|			СчетФактураВыданныйДокументыОснования.Ссылка КАК СсылкаНаСФ
				|		ИЗ
				|			Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
				|		
				|		СГРУППИРОВАТЬ ПО
				|			СчетФактураВыданныйДокументыОснования.Ссылка) КАК ТаблицаДокОснований
				|		ПО СчетФактураВыданный.Ссылка = ТаблицаДокОснований.СсылкаНаСФ
				|ГДЕ
				|	ВЫБОР
				|			КОГДА &Ссылка = НЕОПРЕДЕЛЕНО
				|				ТОГДА ИСТИНА
				|			ИНАЧЕ СчетФактураВыданный.Ссылка = &Ссылка
				|		КОНЕЦ
				|	И СчетФактураВыданный.Дата МЕЖДУ &НачалоПериода И &КонецПериода
				|
				|УПОРЯДОЧИТЬ ПО
				|	Дата";
				
			ИначеЕсли Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УНФ16" Тогда
				
				Запрос.Текст=
				"ВЫБРАТЬ
				|	СчетФактураВыданный.Номер,
				|	СчетФактураВыданный.Дата КАК Дата,
				|	СчетФактураВыданный.Организация,
				|	СчетФактураВыданный.ДокументОснование.Контрагент КАК Контрагент,
				|	"""" КАК Склад,
				|	ИдЯщикОрганизации.Значение КАК ИдЯщикОрганизации,
				|	ИдДокументДиадок.Значение КАК ИдДокументДиадок,
				|	СчетФактураВыданный.Ссылка,
				|	"""" КАК СостояниеДокументооборота,
				|	ВЫБОР
				|		КОГДА ИдДокументДиадок.Значение ЕСТЬ NULL 
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ИСТИНА
				|	КОНЕЦ КАК ЕстьВДиадоке,
				|	ВЫБОР
				|		КОГДА СчетФактураВыданный.Проведен
				|			ТОГДА 0
				|		ИНАЧЕ ВЫБОР
				|				КОГДА НЕ СчетФактураВыданный.ПометкаУдаления
				|					ТОГДА 1
				|				ИНАЧЕ 2
				|			КОНЕЦ
				|	КОНЕЦ КАК СостояниеДокумента,
				|	ТаблицаДокОснований.СуммаДокумента КАК СуммаДокумента
				|ИЗ
				|	Документ.СчетФактура КАК СчетФактураВыданный
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &ИдентификаторСвойстваЯщикОрганизации) КАК ИдЯщикОрганизации
				|		ПО СчетФактураВыданный.Организация = ИдЯщикОрганизации.Объект
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			ДополнительныеСведения.Объект КАК Объект,
				|			ДополнительныеСведения.Свойство КАК Свойство,
				|			ДополнительныеСведения.Значение КАК Значение
				|		ИЗ
				|			РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
				|		ГДЕ
				|			ДополнительныеСведения.Свойство = &ИдентификаторСвойстваИдентификаторДокументаВДиадок) КАК ИдДокументДиадок
				|		ПО СчетФактураВыданный.Ссылка = ИдДокументДиадок.Объект
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			СУММА(СчетФактураВыданныйДокументыОснования.ДокументОснование.СуммаДокумента) КАК СуммаДокумента,
				|			СчетФактураВыданныйДокументыОснования.Ссылка КАК СсылкаНаСФ
				|		ИЗ
				|			Документ.СчетФактура.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
				|		
				|		СГРУППИРОВАТЬ ПО
				|			СчетФактураВыданныйДокументыОснования.Ссылка) КАК ТаблицаДокОснований
				|		ПО СчетФактураВыданный.Ссылка = ТаблицаДокОснований.СсылкаНаСФ
				|ГДЕ
				|	ВЫБОР
				|			КОГДА &Ссылка = НЕОПРЕДЕЛЕНО
				|				ТОГДА ИСТИНА
				|			ИНАЧЕ СчетФактураВыданный.Ссылка = &Ссылка
				|		КОНЕЦ
				|	И СчетФактураВыданный.Дата МЕЖДУ &НачалоПериода И &КонецПериода
				|
				|УПОРЯДОЧИТЬ ПО
				|	Дата";				
				
			КонецЕсли;
			
		КонецЕсли;
		
		
		Если Параметры.Свойство("Ссылка") Тогда
			Запрос.УстановитьПараметр("Ссылка", ПараметрыСписка.Ссылка);
		Иначе
			Запрос.УстановитьПараметр("Ссылка", Неопределено);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("НачалоПериода", ПараметрыСписка.НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", ПараметрыСписка.КонецПериода);
		
		ИдентификаторСвойстваЯщикОрганизации= ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(МетодСервера(,"ИдентификаторСвойстваЯщикОрганизации"));
		Запрос.УстановитьПараметр("ИдентификаторСвойстваЯщикОрганизации", ИдентификаторСвойстваЯщикОрганизации);
		
		ИдентификаторСвойстваИдентификаторДокументаВДиадок=	ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(МетодСервера(,"ИдентификаторСвойстваИдентификаторДокументаВДиадок"));
		Запрос.УстановитьПараметр("ИдентификаторСвойстваИдентификаторДокументаВДиадок", ИдентификаторСвойстваИдентификаторДокументаВДиадок);
		
		ТабДанных= Запрос.Выполнить().Выгрузить();
		
		Для Каждого СтрокаТабДанных Из ТабДанных Цикл
			СтрокаТабДанных.ИдДокументДиадок= МетодСервера(,"Документ_2_DocumentID", СтрокаТабДанных.Ссылка);
		КонецЦикла;
		
		Возврат ТабДанных;
		
	КонецФункции
	
	&НаСервере
	Процедура ЗаполнитьТаблицуДокументов(ПараметрыЖурнала)
		
		ИмяТекущейСтраницы=	Элементы.СтраницыДокументов.ТекущаяСтраница.Имя;
		ТабДанных=			ПолучитьСписокДокументов(ИмяТекущейСтраницы, ПараметрыЖурнала);
		ЗначениеВДанныеФормы(ТабДанных, ТаблицаЖурналаДокументов);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ГрупповаяОбработкаДокументов(Режим)
		
		Если Не ПроверитьИнтернетПодключение() Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы=	Новый Структура();
		ПараметрыФормы.Вставить("Режим"			  , Режим);
		ПараметрыФормы.Вставить("ТекущийПериод"	  , ПолучитьТекущийПериод());
		ПараметрыФормы.Вставить("КонтрагентID"	  , КонтрагентID);
		ПараметрыФормы.Вставить("ПараметрыВыборки", ПолучитьПараметрыВыборки(Режим));
		ПараметрыФормы.Вставить("Организация"	  , Организация);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаГрупповаяОбработка", ПараметрыФормы, ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ВыборОрганизацииИзСписка() Экспорт
		
		Элементы.Организация_2.СписокВыбора.Очистить();
		Элементы.Организация_2.СписокВыбора.ЗагрузитьЗначения(МетодКлиента("Модуль_Клиент","ПолучитьОрганизацииНезаблокированныеПоAPI"));
		Элементы.Организация_2.СписокВыбора.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ОбработчикВыборОрганизации", ЭтаФорма, Элементы.Организация_2));
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбновитьЗаголовокИнтервала(ИмяИнтервала)
		
		Элементы["Интервал"+ИмяИнтервала].Заголовок= МетодКлиента("Модуль_Клиент","ПредставлениеПериодаДД", СтруктураИнтервалов[ИмяИнтервала].ДатаНачала, СтруктураИнтервалов[ИмяИнтервала].ДатаОкончания) + " " + ?(СтруктураИнтервалов[ИмяИнтервала].ОтбиратьПоДатеДокумента, "(по дате документа)", "(по дате учета)");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбновитьЗаголовокИнтервалаЖурналаДокументов()
		
		ПредставлениеПериодаДД= МетодКлиента("Модуль_Клиент","ПредставлениеПериодаДД", СтруктураИнтервалов.ЖурналДокументов.ДатаНачала, СтруктураИнтервалов.ЖурналДокументов.ДатаОкончания);
		
		Элементы.ИнтервалЖурналДокументов_1.Заголовок= ПредставлениеПериодаДД;
		Элементы.ИнтервалЖурналДокументов_2.Заголовок= ПредставлениеПериодаДД;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбновитьДанныеСтроки(СтрокаТЧ)
		
		Document = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьДокументДиадок", СтрокаТЧ.ИдДокументДиадок, СтрокаТЧ.ИдЯщикОрганизации);
		
		СтрокаТЧ.СостояниеДокументооборота= МетодКлиента("Модуль_Клиент","ПредставлениеСтатуса", Document);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СписокПочтовыхЯщиковПараметрыОтправки(Команда)
		
		ТекущиеДанные = Элементы.СписокПочтовыхЯщиков.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			ПоказатьПредупреждение(,"Не выбрана организация!", , Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
			//если организация заблокирована по API, то выводим информационное окно вместо формы настройки параметров	
		ИначеЕсли ТекущиеДанные.Заблокирована Тогда 
			
			ПараметрыФормы = Новый Структура;
			СписокОрганизаций = Новый СписокЗначений();
			
			ДанныеОрганизации = МетодКлиента("Модуль_Клиент","НайтиСтрокиКонтекстДиадокаПоОтбору", Новый Структура("Организация", ТекущиеДанные.Организация));
			
			Если ДанныеОрганизации.Количество() > 0 Тогда 
				СписокОрганизаций.Добавить(ПолучитьСтрокуДляФормыОплатыСервиса(ДанныеОрганизации[0]));
			КонецЕсли;	
			
			ПараметрыФормы.Вставить("СписокОрганизаций", СписокОрганизаций);
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОплатаСервиса", ПараметрыФормы, ЭтаФорма);
			
		Иначе
			
			ПараметрыФормы = Новый Структура;
			
			ПараметрыФормы.Вставить("BoxId"		 , ТекущиеДанные.IdBox);
			ПараметрыФормы.Вставить("Организация", ТекущиеДанные.Организация);
			ПараметрыФормы.Вставить("ПредставлениеОрганизации", ТекущиеДанные.НаименованиеЯщика);
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаНастройкаПараметровОтправки", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыПараметрыОтправки");
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СписокПочтовыхЯщиковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
		
		СписокПочтовыхЯщиковПараметрыОтправки("");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СортироватьТаблицуДокументов(НаправлениеСортировки)
		
		Если Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаПолученныеДиадок Тогда
			
			ПараметрКолонки= ПараметрыСортировки(Элементы.ТаблицаДокументовВходящих.ТекущийЭлемент.Имя, "ТаблицаДокументовВходящих", НаправлениеСортировки);
			ТаблицаДокументовВходящих.Сортировать(ПараметрКолонки);
			
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаОтправленныеДиадок Тогда 
			
			ПараметрКолонки= ПараметрыСортировки(Элементы.ТаблицаДокументовИсходящих.ТекущийЭлемент.Имя, "ТаблицаДокументовИсходящих", НаправлениеСортировки);
			ТаблицаДокументовИсходящих.Сортировать(ПараметрКолонки);
			
		ИначеЕсли Элементы.ПанельРежимов.ТекущаяСтраница = Элементы.СтраницаВнутренниеДиадок Тогда 
			
			ПараметрКолонки= ПараметрыСортировки(Элементы.ТаблицаДокументовВнутренних.ТекущийЭлемент.Имя, "ТаблицаДокументовВнутренних", НаправлениеСортировки);
			ТаблицаДокументовВнутренних.Сортировать(ПараметрКолонки);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПараметрыСортировки(ИмяТекущегоЭлемента, ИмяТаблицыДокументов, НаправлениеСортировки)
		
		ИмяКолонкиСортировки= 	СтрЗаменить(ИмяТекущегоЭлемента, ИмяТаблицыДокументов, "");
		ИмяКолонкиСортировки= 	СтрЗаменить(ИмяКолонкиСортировки, "ИконкаПакета", "ИндексИконкиПакета");
		ПараметрКолонки= 		ИмяКолонкиСортировки + ?(ЗначениеЗаполнено(НаправлениеСортировки), " " + НаправлениеСортировки, "");
		
		Возврат ПараметрКолонки;
		
	КонецФункции
	
	&НаКлиенте
	Процедура УведомитьОСтаромФорматеОтправкиДокументов()
		
		HTMLТекст =
		"<HTML><HEAD>
		|<META content=""text/html; charset=utf-8"" http-equiv=Content-Type>
		|<STYLE type=""text/css"">h3{margin-top:0.5em; margin-bottom:1em;} p{margin-top:0.2em; margin-bottom:0em;}</STYLE></HEAD>
		|<BODY>
		|<H4>Счета-фактуры в формате 93 приказа не поддерживаются с 1-го июля 2017 г.
		|Актуальный формат утвержден приказом ФНС РФ от 24.03.2016 № <a href=""https://normativ.kontur.ru/document?moduleId=1&documentId=271958&rangeId=235782"">ММВ-7-15/155</a>.</H4>
		|<P>С 1-го октября 2019 г. модули Диадока 1С не будут отправлять счета-фактуры в старом формате. Рекомендуем использовать формат 155 приказа ФНС.
		|<a href=""https://wiki.diadoc.ru/pages/viewpage.action?pageId=7668836"">Инструкция</a> по настройке форматов документов на отправку.</P>
		|</BODY></HTML>";
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗаголовокФормы", "Прекращаем поддержку устаревшего формата.");
		ПараметрыФормы.Вставить("HTMLДокумент", HTMLТекст);
		ПараметрыФормы.Вставить("ЭтоУведомлениеОСменеФормата", Истина);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыводаHTMLДокумента", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыУведомленияОСтаромФормате");
		
	КонецПроцедуры
	
	//} ТЕЛО МОДУЛЯ
	
	//{ РАСШИРЕНИЕ КОНФИГУРАЦИИ
	
	&НаКлиенте
	Функция РазобратьВерсиюПриложенияПоРазрядам(Знач ВерсияПриложения)
		
		Платформа1С = Новый Структура("ПервыеДвеЦифры, ТретьяЦифра, ЧетвертаяЦифра");
		
		ВерсияПриложения 	= СокрЛП(ВерсияПриложения);
		
		ПерваяЦифра = Сред(ВерсияПриложения, 1, Найти(ВерсияПриложения, ".")-1);
		ПромежуточнаяСтрока = Сред(ВерсияПриложения, Найти(ВерсияПриложения, ".")+1, СтрДлина(ВерсияПриложения));
		
		ВтораяЦифра = Сред(ПромежуточнаяСтрока, 1, Найти(ПромежуточнаяСтрока, ".")-1);
		ПромежуточнаяСтрока = Сред(ПромежуточнаяСтрока, Найти(ПромежуточнаяСтрока, ".")+1, СтрДлина(ПромежуточнаяСтрока));
		
		ПервыеДвеЦифры = ПерваяЦифра + "." + ВтораяЦифра;
		
		ТретьяЦифра = Сред(ПромежуточнаяСтрока, 1, Найти(ПромежуточнаяСтрока, ".")-1);
		ПромежуточнаяСтрока = Сред(ПромежуточнаяСтрока, Найти(ПромежуточнаяСтрока, ".")+1, СтрДлина(ПромежуточнаяСтрока));
		
		ЧетвертаяЦифра = СокрЛП(ПромежуточнаяСтрока);
		
		Платформа1С.Вставить("ПервыеДвеЦифры", ПервыеДвеЦифры);
		Платформа1С.Вставить("ТретьяЦифра", ТретьяЦифра);
		Платформа1С.Вставить("ЧетвертаяЦифра", ЧетвертаяЦифра);
		
		Возврат Платформа1С;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОбработчикЗапросУстановкиРасширенияКонфигурации(РезультатЗапроса, ДополнительныеПараметры) Экспорт
		
		ТолькоУдалить= Ложь;
		
		Если ДополнительныеПараметры <> Неопределено Тогда
			ДополнительныеПараметры.Свойство("ТолькоУдалить", ТолькоУдалить);
		КонецЕсли;
		
		Если РезультатЗапроса <> Истина Тогда
			Возврат;
		КонецЕсли;
		
		ТекстСообщения= 			"";
		РазделительТекстаСообщения= "";
		ПерезапуститьПрограмму= 	Ложь;
		
		Результат= МетодСервераБезКонтекста(,"УдалитьРасширениеКонфигурации");
		Если НЕ Результат.Отказ Тогда
			Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
				ТекстСообщения= ТекстСообщения + РазделительТекстаСообщения + Результат.ОписаниеОшибки;
				РазделительТекстаСообщения = Символы.ПС + Символы.ПС;
			КонецЕсли;
			ПерезапуститьПрограмму= МАКС(ПерезапуститьПрограмму, Результат.ПерезапуститьПрограмму);
		Иначе
			ОткрытьФормуВыводаОшибки(Результат);
			Возврат;
		КонецЕсли;
		
		Если НЕ ТолькоУдалить Тогда
			
			Результат= МетодСервераБезКонтекста(,"УстановитьРасширениеКонфигурации");
			Если НЕ Результат.Отказ Тогда
				Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
					ТекстСообщения= ТекстСообщения + РазделительТекстаСообщения + Результат.ОписаниеОшибки;
					РазделительТекстаСообщения = Символы.ПС + Символы.ПС;
				КонецЕсли;
				ПерезапуститьПрограмму= МАКС(ПерезапуститьПрограмму, Результат.ПерезапуститьПрограмму);
			Иначе
				ОткрытьФормуВыводаОшибки(Результат);
				Возврат;
			КонецЕсли;
			
			СведенияОМодуле = МетодСервераБезКонтекста(, "ЗаполнитьДанныеОПомещаемомМодуле");
			Ф = Новый Файл(СведенияОМодуле.АдресОбработки);
			
			Если НЕ Ф.Существует() Тогда
				
				Результат.Отказ = Истина;
				Результат.ОписаниеОшибки = "Не обнаружен файл модуля на диске: " + Символы.ПС + "СведенияОМодуле.АдресОбработки";
				ОткрытьФормуВыводаОшибки(Результат);
				Возврат;
				
			КонецЕсли;
			
			ДвоичныеДанныеОбработки = Новый ДвоичныеДанные(СведенияОМодуле.АдресОбработки);
			АдресДвоичныеДанныеОбработки = ПоместитьВоВременноеХранилище(ДвоичныеДанныеОбработки, УникальныйИдентификатор);
			Результат= МетодСервераБезКонтекста(,"ПоместитьИнтеграционныйМодульВДополнительныеОбработки", АдресДвоичныеДанныеОбработки);
			УдалитьИзВременногоХранилища(АдресДвоичныеДанныеОбработки);
			
			Если НЕ Результат.Отказ Тогда
				Если НЕ ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
					ТекстСообщения= ТекстСообщения + РазделительТекстаСообщения + Результат.ОписаниеОшибки;
					РазделительТекстаСообщения = Символы.ПС + Символы.ПС;
				КонецЕсли;
			Иначе
				ОткрытьФормуВыводаОшибки(Результат);
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		КнопкиВопроса= Новый СписокЗначений;
		Если ПерезапуститьПрограмму Тогда
			ТекстСообщения= ТекстСообщения + РазделительТекстаСообщения + "Программу необходимо перезапустить!";
			КнопкиВопроса.Добавить("ПерезапуститьПрограмму", "Перезапустить программу");
		КонецЕсли;
		КнопкиВопроса.Добавить("ПродолжитьРаботу", "Продолжить работу");
		
		Платформа.ПараметрыКлиент.УстановкаРасширения= МетодСервераБезКонтекста(,"ПроверитьНеобходимостьУстановкиРасширения");
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработчикЗапросУстановкиРасширенияКонфигурацииЗавершение", ЭтаФорма), ТекстСообщения, КнопкиВопроса, 120, "ПродолжитьРаботу");
		
		НастроитьЭлементУдалитьУстановитьРасширениеКонфигурации();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикЗапросУстановкиРасширенияКонфигурацииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
		
		Если РезультатВопроса = "ПерезапуститьПрограмму" Тогда
			ПрекратитьРаботуСистемы(Истина);
		КонецЕсли;
		
	КонецПроцедуры
	
	
	//{ РАБОТА С ОРГАНИЗАЦИЕЙ
	&НаКлиенте
	Процедура ВыполнитьСопоставлениеОрганизации() Экспорт
		
		СписокДобавленныхIdBox = ПолучитьСписокСписокДобавленныхIdBox(МетодКлиента("Модуль_Клиент","ПолучитьОрганизацииНезаблокированныеПоAPI"));
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("СписокДобавленныхIdBox", СписокДобавленныхIdBox);
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаДобавлениеЯщиков", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыДобавленияЯщиковРасширение");
		
	КонецПроцедуры
	
	&НаСервере
	Функция ПолучитьСписокСписокДобавленныхIdBox(ОрганизацииФилиалыСопоставленныеСДиадоком)
		
		ТаблицаЯщиков= МетодСервера(,"ПолучитьТаблицуЯщиковДиадокОрганизации", ОрганизацииФилиалыСопоставленныеСДиадоком);
		
		СписокДобавленныхIdBox = Новый СписокЗначений;
		
		Для каждого СтрокаТаблицы Из ТаблицаЯщиков Цикл
			СписокДобавленныхIdBox.Добавить(СтрокаТаблицы.BoxId);
		КонецЦикла;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыДобавленияЯщиковРасширение(РезультатЗакрытия, ТекущиеДанные) Экспорт
		
		Если РезультатЗакрытия <> Неопределено Тогда
			Оповестить("РасширениеДиадокСопосталениеОрганизации");
		КонецЕсли;
		
	КонецПроцедуры
	
	//} РАБОТА С ОРГАНИЗАЦИЕЙ
	
	//{ РАБОТА С КОНТРАГЕНТОМ
	
	&НаКлиенте
	Процедура ЗаполнитьПараметрыПриглашенияИзДД(ПараметрыПриглашенияКА) Экспорт
		
		Organization = МетодКлиента("Модуль_Клиент", "ПолучитьЯщикДиадокДляОрганизации", ПараметрыПриглашенияКА.Организация);
		
		Если Organization <> Неопределено Тогда 
			
			КоллекцияКонтрагентов = Organization.GetCounteragentListByInnKpp(ПараметрыПриглашенияКА.ИннКонтрагента);
			
			Если КоллекцияКонтрагентов.Count = 0 Тогда
				
				ТекстСообщения = НСтр("ru = 'Контрагент по ИНН и КПП не найден!'"); //!! необходимо обработать ситуацию
				СообщитьПользователю(ТекстСообщения);
				
			Иначе 
				
				Counteragent = КоллекцияКонтрагентов.GetItem(0);
				ПараметрыПриглашенияКА.Organization			= Organization;
				ПараметрыПриглашенияКА.CounteragentId		= Counteragent.Id;
				ПараметрыПриглашенияКА.FnsParticipantId		= Counteragent.FnsParticipantId;
				ПараметрыПриглашенияКА.CounteragentStatus	= Counteragent.GetStatus();
				
			КонецЕсли;
			
		Иначе
			ТекстСообщения = НСтр("ru = 'Не найдена Организация!'");
			СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПригласитьКАРасширение(ПараметрыПриглашенияКА) Экспорт
		
		CounteragentStatus 	= ПараметрыПриглашенияКА.CounteragentStatus;
		
		Organization		= ПараметрыПриглашенияКА.Organization;
		CounteragentId		= ПараметрыПриглашенияКА.CounteragentId;
		FnsParticipantId 	= ПараметрыПриглашенияКА.FnsParticipantId;
		
		Если CounteragentStatus = "InvitesMe" Тогда
			
			Результат = МетодКлиента("Модуль_Клиент","ОтправитьПринятьПриглашениеКонтрагенту", Organization, CounteragentId, ПараметрыПриглашенияКА.ИннКонтрагента, "", "");
			Если Результат = Истина Тогда
				УстановитьCounteragentBoxID(CounteragentId, ПараметрыПриглашенияКА.Контрагент);
				МетодСервераБезКонтекста(,"УстановитьИдентификаторЭДОДляКонтрагента", ПараметрыПриглашенияКА.Контрагент, FnsParticipantId);
				ПоказатьПредупреждение(,"Контрагенту отправлено приглашение");
				Оповестить("РасширениеДиадокРаботаСКонтрагентом");
			КонецЕсли;
			
		ИначеЕсли CounteragentStatus = "NotInCounteragentList" или CounteragentStatus = "RejectsMe" или CounteragentStatus = "IsRejectedByMe" Тогда
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("Заголовок", 	"Отправка приглашения");
			ПараметрыФормы.Вставить("Комментарий", 	"Предлагаем обмениваться электронными документами через систему Диадок");
			ПараметрыФормы.Вставить("Режим", 		"ОтправкаПриглашения");
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВводаКомментария", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВводаКомментарияРасширение", ПараметрыПриглашенияКА);
			
		ИначеЕсли CounteragentStatus = "IsMyCounteragent" ИЛИ CounteragentStatus = "IsInvitedByMe" Тогда
			
			УстановитьCounteragentBoxID(CounteragentId, ПараметрыПриглашенияКА.КонтрагентСсылка);
			МетодСервераБезКонтекста(,"УстановитьИдентификаторЭДОДляКонтрагента", ПараметрыПриглашенияКА.КонтрагентСсылка, FnsParticipantId);
			ПоказатьПредупреждение(,"Контрагент из Диадок сопоставлен контрагенту из 1С");
			Оповестить("РасширениеДиадокРаботаСКонтрагентом");
			
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВводаКомментарияРасширение(РезультатЗакрытия, ТекущиеДанные) Экспорт
		
		Organization 		= ТекущиеДанные.Organization;
		CounteragentID 		= ТекущиеДанные.CounteragentID;
		FnsParticipantId 	= ТекущиеДанные.FnsParticipantId;
		
		Результат =	МетодКлиента("Модуль_Клиент","ОтправитьПринятьПриглашениеКонтрагенту", Organization, CounteragentID, ТекущиеДанные.ИннКонтрагента, РезультатЗакрытия.Комментарий, РезультатЗакрытия.ПутьКФайлу);
		
		Если Результат = Истина Тогда
			УстановитьCounteragentBoxID(CounteragentID, ТекущиеДанные.КонтрагентСсылка);
			МетодСервераБезКонтекста(,"УстановитьИдентификаторЭДОДляКонтрагента", ТекущиеДанные.КонтрагентСсылка, FnsParticipantId);
			ПоказатьПредупреждение(,"Контрагенту отправлено приглашение");
			Оповестить("РасширениеДиадокРаботаСКонтрагентом");
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура УстановитьCounteragentBoxID(Id, ВыбКонтрагент) Экспорт
		
		МетодСервера(,"Установить_CounteragentBoxID_для_Контрагент", ВыбКонтрагент, id);
		
	КонецПроцедуры
	
	//} РАБОТА С КОНТРАГЕНТОМ
	
	
	//{ ОТПРАВИТЬ ДОКУМЕНТ
	
	&НаКлиенте
	Процедура ОтправитьЧерезДиадокРасширение(ПараметрыОтправкиДокумента) Экспорт
		
		ТекущаяОрганизация=	ПараметрыОтправкиДокумента.Организация;
		ДанныеКонтрагента= 	ПараметрыОтправкиДокумента.ДанныеКонтрагента;
		
		Organization= МетодКлиента("Модуль_Клиент","ПолучитьЯщикДиадокДляОрганизации", ТекущаяОрганизация);
		Если Organization = Неопределено Тогда 
			ПараметрыФормы= Новый Структура();
			ПараметрыФормы.Вставить("Заголовок", 		"Ошибка отправки документа");
			ПараметрыФормы.Вставить("ОписаниеОшибки", 	"Не удалось отправить документ");
			ПараметрыФормы.Вставить("Подробности", 		"Не могу получить текущую организацию на сервере Диадок.");
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма);
			Возврат;
		КонецЕсли;
		
		ЗаголовокОшибки= 	"";
		ОписаниеОшибки= 	"";
		ПодробностиОшибки= 	"";
		
		CounteragentList= Organization.GetCounteragentListByInnKpp(ДанныеКонтрагента.Инн, ДанныеКонтрагента.Кпп);
		Если CounteragentList.Count > 0 Тогда
			CounteragentItem= CounteragentList.GetItem(0);
			Status= CounteragentItem.GetStatus();
			Если НЕ Status = "IsMyCounteragent" Тогда
				ЗаголовокОшибки= 	"Ошибка отправки документа";
				ОписаниеОшибки= 	"Не удалось отправить документ";
				ПодробностиОшибки= 	"Нельзя отправить документы контрагенту, с которым не установлены партнерские отношения.";
			КонецЕсли;
		Иначе
			ЗаголовокОшибки= 	"Ошибка отправки документа";
			ОписаниеОшибки= 	"Не удалось отправить документ";
			ПодробностиОшибки= 	"Не могу найти контрагента с ИНН: " + ДанныеКонтрагента.Инн + " КПП: " + ДанныеКонтрагента.Кпп + " на сервере Диадок.";		
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
			ПараметрыФормы= Новый Структура();
			ПараметрыФормы.Вставить("Заголовок", 		ЗаголовокОшибки);
			ПараметрыФормы.Вставить("ОписаниеОшибки", 	ОписаниеОшибки);
			ПараметрыФормы.Вставить("Подробности", 		ПодробностиОшибки);
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма);
			Возврат;	
		КонецЕсли;
		
		//ЗаполнитьСписокКонтрагентов();
		
		Форма_Выгрузка= МетодКлиента(,"ПолучитьФормуОбработки", "Форма_Выгрузка",,ЭтаФорма);
		
		Форма_Выгрузка.СтруктураПараметрыДокументаРасширение= ПараметрыОтправкиДокумента;
		
		Форма_Выгрузка.НачатьВыгрузку(Новый Структура("Организация", Организация));
		
	КонецПроцедуры
	
	//} ОТПРАВИТЬ ДОКУМЕНТ
	
	
	//{ ПОСМОТРЕТЬ ДОКУМЕНТ
	
	&НаКлиенте
	Процедура ПоказатьОтправленныйДокумент(СтруктураИсходныеДанные) Экспорт
		
		СтруктураID= ПолучитьСтруктуруId(СтруктураИсходныеДанные);
		
		Document= Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(СтруктураID.BoxID).GetDocumentById(СтруктураID.DocumentID);
		
		ПараметрыФормы= Новый Структура;
		
		Если НЕ Document.Department = Неопределено Тогда
			ПараметрыФормы.Вставить("DepartmentKpp", 		Document.Department.Kpp);
			ПараметрыФормы.Вставить("DepartmentId", 		Document.Department.Id);
		КонецЕсли;
		
		ЭлементМассива= НовоеОписаниеДокументаПакета();
		ЭлементМассива.DocumentType = Document.Type;
		ЭлементМассива.Документ1С = СтруктураИсходныеДанные.ДокументСсылка;
		ЭлементМассива.CounteragentBoxID = Document.Counteragent.ID;
		ЭлементМассива.РасширениеФайлаДиадок = ПолучитьРасширениеФайлаДиадок(Document.FileName);
		
		МассивДокументовПакета= Новый Массив;
		МассивДокументовПакета.Добавить(ЭлементМассива);
		
		ПараметрыФормы.Вставить("BoxID", 					Document.OrganizationID);
		ПараметрыФормы.Вставить("CounteragentBoxID", 		Document.Counteragent.ID);
		ПараметрыФормы.Вставить("МассивДокументовПакета", 	МассивДокументовПакета);
		ПараметрыФормы.Вставить("ТочкаВызова", 				"ТаблицаДокументовИсходящих");
		
		ИмяФормыПросмотра= 	МетодКлиента("Модуль_Клиент","ПолучитьИмяФормыДокумента", Document);
		ФормаПросмотра= 	МетодКлиента(,"ПолучитьФормуОбработки", ИмяФормыПросмотра, ПараметрыФормы, ЭтаФорма, СокрЛП(Document.DocumentID) + "/" + СокрЛП(Document.OrganizationID));
		
		ФормаПросмотра.ЭДОбъект= Document;
		ФормаПросмотра.Открыть();
		
	КонецПроцедуры
	
	&НаСервере
	Функция ПолучитьСтруктуруId(Знач СтруктураИсходныеДанные)
		
		СтруктураID = Новый Структура("BoxID, DocumentID");
		
		НаименованиеСвойстваИдентификаторЯщикДокументаВДиадок = МетодСервера(,"ИдентификаторСвойстваИдентификаторЯщикДокументаВДиадок");
		ПВХИдентификаторЯщикДокументаВДиадок = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(НаименованиеСвойстваИдентификаторЯщикДокументаВДиадок);
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДополнительныеСведения.Значение
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Объект = &Объект
		|	И ДополнительныеСведения.Свойство = &Свойство";
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Объект", СтруктураИсходныеДанные.ДокументСсылка);
		Запрос.УстановитьПараметр("Свойство", ПВХИдентификаторЯщикДокументаВДиадок);
		
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		
		Если РезультатЗапроса.Количество()>0 Тогда
			СтруктураID.BoxID = РезультатЗапроса[0].Значение;				
		КонецЕсли;
		
		СтруктураID.DocumentID = МетодСервера(,"Документ_2_DocumentID", СтруктураИсходныеДанные.ДокументСсылка);
		
		Возврат СтруктураID;			
		
	КонецФункции
	
	//} ПОСМОТРЕТЬ ДОКУМЕНТ
	
	//{ ОТМЕНИТЬ СОПОСТАВЛЕНИЕ
	
	&НаКлиенте
	Процедура ОтменитьСопоставлениеРасширение(ПараметрыОтмены) Экспорт
		
		НаименованиеСистемы= Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы;
		КоличествоДокументовОтмены= ПараметрыОтмены.МассивДокументов.Количество();
		Если КоличествоДокументовОтмены > 1 Тогда
			ТекстНачалоВопроса= "Вы действительно хотите отменить сопоставление для " + КоличествоДокументовОтмены + " документов";
		Иначе
			ТекстНачалоВопроса= "Вы действительно хотите отменить сопоставление для документа";
		КонецЕсли;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработчикОтменитьСопоставление", ЭтаФорма, ПараметрыОтмены), 
		ТекстНачалоВопроса + " " + НаименованиеСистемы + "?", 
		РежимДиалогаВопрос.ДаНет, 
		120, 
		КодВозвратаДиалога.Нет, 
		НаименованиеСистемы, 
		КодВозвратаДиалога.Нет);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОтменитьСопоставление(РезультатВопроса, ПараметрыОбработчика) Экспорт
		
		Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
			Для Каждого ТекДок Из ПараметрыОбработчика.МассивДокументов Цикл
				МетодСервера(,"Установить_DocumentID_Для_Документ", ТекДок, "", "");
				МетодСервера(,"ОчиститьCustomDocumentId", ТекДок);
			КонецЦикла;
			Если ПараметрыОбработчика.ФормаВызова <> Неопределено Тогда
				ПараметрыОбработчика.ФормаВызова.РасширениеДиадок_НастроитьФормуДокумента();
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	//} ОТМЕНИТЬ СОПОСТАВЛЕНИЕ
	
	//} РАСШИРЕНИЕ КОНФИГУРАЦИИ	
	
	//} ОТЛАДКА
	
	&НаКлиенте
	Процедура РежимОтладкиСервераПриИзменении(Элемент)
		
		Объект.ПараметрыКлиентСервер.РежимОтладкиСервера = РежимОтладкиСервера;
		
		ЗаполнитьПутьКаталогаОбработок();
		
		МетодСервераБезКонтекста( , "СброситьКэшМодулей");
		
		МетодКлиента( , "СинхронизироватьПараметрыКлиентСервера", Объект.ПараметрыКлиентСервер);
		
		УстановитьНаименованиеСистемы();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КаталогМодулейСервераНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
		ДиалогВыбораФайла= Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		ДиалогВыбораФайла.МножественныйВыбор= Ложь;
		ДиалогВыбораФайла.Каталог= КаталогМодулейСервера;
		ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ОбработчикКаталогМодулейСервераЗавершениеВыбораКаталога", ЭтаФорма));
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура КаталогМодулейСервераПриИзменении(Элемент)
		
		Объект.ПараметрыКлиентСервер.КаталогМодулейСервера = КаталогМодулейСервера;
		
		МетодКлиента( , "СинхронизироватьПараметрыКлиентСервера", Объект.ПараметрыКлиентСервер);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьПутьКаталогаОбработок()
		
		Если Не РежимОтладкиСервера Или ЗначениеЗаполнено(КаталогМодулейСервера) Тогда
			Возврат;
		КонецЕсли;
		
		ИмяФайла = Объект.ПараметрыКлиентСервер.ИспользуемоеИмяФайла;
		
		Если ЗначениеЗаполнено(ИмяФайла) Тогда
			Файл = Новый Файл(ИмяФайла);
			КаталогМодулейСервера = Файл.Путь;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикКаталогМодулейСервераЗавершениеВыбораКаталога(РезультатВыбора, ДополнительныеПараметры) Экспорт
		
		Если РезультатВыбора = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		КаталогМодулейСервера= РезультатВыбора[0];
		
		КаталогМодулейСервераПриИзменении(Неопределено);
		
	КонецПроцедуры
	
	//{ ОТЛАДКА
	
	//{ ОНЛАЙН СЧЕТ
	
	&НаКлиенте
	Процедура НадписьОкончанияОплаченногоПериодаНажатие(Элемент)
		
		Попытка
			
			МассивСчетов 	= МассивОнлайнСчетов();
			HTMLТекст 		= HTMLСписокОнлайнСчетов(МассивСчетов);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗаголовокФормы"			, НСтр("ru = 'Продление и обслуживание сервиса Диадок'"));
			ПараметрыФормы.Вставить("HTMLДокумент"				, HTMLТекст);
			ПараметрыФормы.Вставить("ДополнительныеПараметры"	, МассивСчетов);
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыводаHTMLДокумента", ПараметрыФормы, ЭтаФорма);	
			
		Исключение
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Заголовок"		, "Ошибка");
			ПараметрыФормы.Вставить("ОписаниеОшибки", "Не удалось получить данные по счетам, выставленным за использование сервиса Контур.Диадок");
			ПараметрыФормы.Вставить("Подробности"	, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОткрытьФормуВыводаОшибки(ПараметрыФормы);
			
		КонецПопытки;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПоказатьПанельОкончанияОплаченногоПериода()
		
		Элементы.ПанельОкончанияОплаченногоПериода.Видимость = ЕстьОнлайнСчета();
		
	КонецПроцедуры //ПоказатьПанельОкончанияОплаченногоПериода()
	
	// Возвращает HTML текст со списком выставленных счетов 
	// 
	// Возвращаемое значение:
	//   Строка
	//
	&НаКлиенте
	Функция HTMLСписокОнлайнСчетов(МассивСчетов)
		
		HTMLШаблоны	 = HTMLШаблоныСпискаОнлайнСчетов();	
		Результат	 = HTMLШаблоны.Начало;
		
		Для каждого ИнформацияПоСчету Из МассивСчетов Цикл
			
			Если ЗначениеЗаполнено(ИнформацияПоСчету.КодОшибки) Или ЗначениеЗаполнено(ИнформацияПоСчету.ТекстОшибки) Тогда
				
				Если ЗначениеЗаполнено(ИнформацияПоСчету.КодОшибки) Тогда
					ТекстОшибки = Строка(ИнформацияПоСчету.КодОшибки) + " Error: " + ИнформацияПоСчету.ТекстОшибки;
				Иначе
					ТекстОшибки = "Error: " + ИнформацияПоСчету.ТекстОшибки;
				КонецЕсли;
				
				НоваяСтрока = HTMLШаблоны.Ошибка;
				НоваяСтрока = СтрЗаменить(НоваяСтрока, "[ТекстОшибки]"	, ТекстОшибки);
				НоваяСтрока = СтрЗаменить(НоваяСтрока, "[Организация]"	, ИнформацияПоСчету.Организация);
				
				Результат = Результат + НоваяСтрока;
				
			Иначе
				
				Сумма		 		= Формат(ИнформацияПоСчету.Сумма, "ЧДЦ=2") + " руб.";
				НомерСчета	 		= "Счет №" + ИнформацияПоСчету.Номер;
				ОрганизацияВСчет 	= СокрЛП(ИнформацияПоСчету.Организация);
				ТарифныйПлан	 	= ИнформацияПоСчету.ТарифныйПлан;
				ОплаченныйПериод 	= "Оплаченный период заканчивается " + Формат(ИнформацияПоСчету.ДатаОкончанияПодписки, "ДФ='д ММММ'");
				СтрокаОповещения 	= МетодКлиента("Модуль_Клиент", "ПараметрыОповещенияВСтрокуHTMLСообщения", "СохранитьФайлОнлайнСчетаНаДиск", ИнформацияПоСчету.СсылкаНаФайл);
				
				НоваяСтрока = HTMLШаблоны.Счет;
				НоваяСтрока = СтрЗаменить(НоваяСтрока, "[Сумма]"				, Сумма);
				НоваяСтрока = СтрЗаменить(НоваяСтрока, "[НомерСчета]"			, НомерСчета);
				НоваяСтрока = СтрЗаменить(НоваяСтрока, "[КомандаСохранитьФайл]"	, СтрокаОповещения);
				НоваяСтрока = СтрЗаменить(НоваяСтрока, "[Организация]"			, ОрганизацияВСчет);
				
				Результат = Результат + НоваяСтрока;
				
				НоваяСтрока = HTMLШаблоны.ТарифныйПлан;
				НоваяСтрока = СтрЗаменить(НоваяСтрока, "[ОплаченныйПериод]"	, ОплаченныйПериод);
				НоваяСтрока = СтрЗаменить(НоваяСтрока, "[ТарифныйПлан]"		, ТарифныйПлан);
				
				Результат = Результат + НоваяСтрока;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Результат = Результат + HTMLШаблоны.Окончание;
		
		Возврат Результат;
		
	КонецФункции // HTMLСписокОнлайнСчетов()
	
	// Возвращает структуру, содержащую шаблоны разметки HTML для вывода информации по онлайн счетам
	// 
	// Возвращаемое значение:
	//   Структура с ключами "Начало, Счет, ТарифныйПлан, Ошибка, Окончание"
	//
	&НаКлиенте
	Функция HTMLШаблоныСпискаОнлайнСчетов()
		
		КартинкаСкачатьBase64 = "iVBORw0KGgoAAAANSUhEUgAAAAwAAAARCAYAAADpPU2iAAAAI0lEQVR42mNgGI4g7cp/FDyqAZciXJgkTSTZRF1PI2saEAAAvER4B6ldUPIAAAAASUVORK5CYII=";
		
		Начало = 
		"<HTML>
		|<HEAD><META content=""text/html; charset=utf-8"" http-equiv=Content-Type></HEAD>
		|<BODY style=""FONT-FAMILY: Segoe UI; COLOR: #808080"">
		|<P>Мы подготовили документы. Скачайте и оплатите счет</P>
		|<TABLE style=""OVERFLOW: visible"" cellSpacing=0>
		|<TR style=""FONT-SIZE: 11pt"">
		|	<TD width=270 style=""PADDING-BOTTOM: 5px; BORDER-BOTTOM: #959595 1px solid"">Организация</TD>
		|	<TD width=370 style=""PADDING-BOTTOM: 5px; BORDER-BOTTOM: #959595 1px solid"">Счет и тарифный план</TD>
		|	<TD width=130 style=""PADDING-BOTTOM: 5px; BORDER-BOTTOM: #959595 1px solid; TEXT-ALIGN: center"">Сумма</TD>
		|</TR>";
		
		Счет = "
		|<TR style=""FONT-SIZE: 11pt; VERTICAL-ALIGN: top; COLOR: #000000"">
		|	<TD style=""PADDING-TOP: 10px; "">[Организация]</TD>
		|	<TD style=""PADDING-TOP: 10px; PADDING-LEFT: 20px"">[НомерСчета]</TD>
		|	<TD style=""PADDING-TOP: 10px; TEXT-ALIGN: center"">[Сумма]</TD>
		|	<TD style=""PADDING-TOP: 10px; TEXT-ALIGN: left""><A href=""[КомандаСохранитьФайл]""><IMG src=""data:image/png;base64," + КартинкаСкачатьBase64 + """ align=top border=0 alt=""Скачать"">Скачать</A></TD>
		|</TR>";
		
		ТарифныйПлан = "
		|<TR style=""FONT-SIZE: 11pt; VERTICAL-ALIGN: top"">
		|	<TD style=""PADDING-TOP: 10px;"">[ОплаченныйПериод]</TD>
		|	<TD style=""PADDING-TOP: 10px; PADDING-LEFT: 20px"">[ТарифныйПлан]</TD>
		|</TR>";
		
		Ошибка = "
		|<TR style=""FONT-SIZE: 11pt; VERTICAL-ALIGN: top; COLOR: #000000"">
		|	<TD style=""PADDING-TOP: 10px;"">[Организация]</TD>
		|	<TD style=""PADDING-TOP: 10px;"">[ТекстОшибки]</TD>
		|</TR>";
		
		Окончание = "</TABLE></BODY></HTML>";
		
		Результат = Новый Структура;
		
		Результат.Вставить("Начало"			, Начало);
		Результат.Вставить("Счет"			, Счет);
		Результат.Вставить("ТарифныйПлан"	, ТарифныйПлан);
		Результат.Вставить("Ошибка"			, Ошибка);
		Результат.Вставить("Окончание"		, Окончание);
		
		Возврат Результат;
		
	КонецФункции //HTMLШаблоныСпискаОнлайнСчетов()
	
	// Проверяет наличие выставленных на портале СКБ Контура онлайн-счетов
	// за использование сервиса Контур-Диадок.
	//
	// Возвращаемое значение:
	//  Булево - Истина, если онлайн-счета выставлены.
	//
	&НаКлиенте
	Функция ЕстьОнлайнСчета()
		
		Результат = Ложь;
		
		Попытка
			
			МассивСчетов = МассивОнлайнСчетов();
			
			Для каждого ИнформацияПоСчету Из МассивСчетов Цикл
				
				Если ЗначениеЗаполнено(ИнформацияПоСчету.СсылкаНаФайл) Тогда
					Результат = Истина;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		Исключение
			
			ТекстСообщения = "Не удалось проверить наличие счетов за использование сервиса Контур.Диадок" 
			+ Символы.ПС +  "по причине:" + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			Сообщить(ТекстСообщения, СтатусСообщения.Внимание);
			
		КонецПопытки;
		
		Возврат Результат;
		
	КонецФункции // ЕстьОнлайнСчета()
	
	// Формирует массив с данными о выставленных счетах на продление
	// 
	// Возвращаемое значение:
	//   Массив
	//
	&НаКлиенте
	Функция МассивОнлайнСчетов()
		
		Результат = Новый Массив;
		
		КонтекстДиадока 	= Платформа.ПараметрыКлиент.КонтекстДиадока;
		
		Для каждого ЭлементКонтекста из КонтекстДиадока Цикл
			
			BoxId	= ЭлементКонтекста.BoxId;
			ИНН 	= ЭлементКонтекста.ДанныеОрганизации.Inn;
			КПП 	= ЭлементКонтекста.ДанныеОрганизации.kpp;
			АвторизационныйТокен 	= ЭлементКонтекста.Connection.Token;
			ОрганизацияДиадок 		= ЭлементКонтекста.ДанныеОрганизации.Name;
			
			Ответ = МетодКлиента("Модуль_Клиент", "Kontur_API_PortalBills_Metas", ИНН, КПП, АвторизационныйТокен);
			
			МассивСчетов 	= МетодКлиента("Модуль_Клиент", "СвойствоСтруктуры", Ответ, "metas");
			КодОшибки 		= МетодКлиента("Модуль_Клиент", "СвойствоСтруктуры", Ответ, "statuscode");
			ТекстОшибки 	= МетодКлиента("Модуль_Клиент", "СвойствоСтруктуры", Ответ, "message");
			
			Если ЗначениеЗаполнено(КодОшибки) Или ЗначениеЗаполнено(ТекстОшибки) Тогда
				
				ИнформацияПоСчету = НоваяИнформацияПоСчету();
				
				ИнформацияПоСчету.КодОшибки		 = КодОшибки;
				ИнформацияПоСчету.ТекстОшибки	 = ТекстОшибки;
				ИнформацияПоСчету.Организация	 = ОрганизацияДиадок;
				
				Результат.Добавить(ИнформацияПоСчету);
				
			ИначеЕсли ЗначениеЗаполнено(МассивСчетов) Тогда
				
				Для каждого Счет Из МассивСчетов Цикл
					
					ИнформацияПоСчету = НоваяИнформацияПоСчету();
					
					ИнформацияПоСчету.BoxId					 = BoxId;
					ИнформацияПоСчету.Номер					 = Счет.number;
					ИнформацияПоСчету.Сумма					 = Число(Счет.price);
					ИнформацияПоСчету.ТарифныйПлан			 = СтрЗаменить(Счет.plan, "'", "");
					ИнформацияПоСчету.СсылкаНаФайл			 = Счет.downloadLink;
					ИнформацияПоСчету.ДатаОкончанияПодписки	 = XMLЗначение(Тип("Дата"), Счет.subscriptionEndDate);
					ИнформацияПоСчету.Организация			 = ОрганизацияДиадок;
					ИнформацияПоСчету.ИДЛицевогоСчета		 = Ответ.accountId;
					ИнформацияПоСчету.ИДТарифа				 = Счет.billId;
					ИнформацияПоСчету.ТекстОшибки			 = ТекстОшибки;
					
					Результат.Добавить(ИнформацияПоСчету);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат Результат;
		
	КонецФункции //МассивОнлайнСчетов()
	
	&НаКлиенте
	Функция НоваяИнформацияПоСчету()
		
		Результат = Новый Структура;
		
		Результат.Вставить("BoxId");
		Результат.Вставить("Номер");
		Результат.Вставить("Сумма");
		Результат.Вставить("КодОшибки");
		Результат.Вставить("ТекстОшибки");
		Результат.Вставить("Организация");
		Результат.Вставить("ТарифныйПлан");
		Результат.Вставить("СсылкаНаФайл");
		Результат.Вставить("ДатаОкончанияПодписки");
		Результат.Вставить("ИДЛицевогоСчета");
		Результат.Вставить("ИДТарифа");
		
		Возврат Результат;
		
	КонецФункции
	
	//Метод получает pdf файл счета из сервиса Билли и вызывает диалог сохранения файла
	//
	//	Параметры:
	//		СсылкаНаФайл	- Строка	- ссылка на скачиваемый файл
	//		МассивСчетов	- Массив	- массив, выставленных счетов на оплату
	//
	&НаКлиенте
	Процедура СохранитьФайлОнлайнСчетаНаДиск(СсылкаНаФайл, МассивСчетов) Экспорт
		
		ОписаниеСчета	 = МетодКлиента("Модуль_Клиент", "МассивСтруктур_Найти_НаКлиенте", МассивСчетов, СсылкаНаФайл, "СсылкаНаФайл");
		ИДТарифа		 = ОписаниеСчета.ИДТарифа;
		ИДЛицевогоСчета	 = ОписаниеСчета.ИДЛицевогоСчета;
		
		Попытка
			
			АвторизационныйТокен = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.Token;
			
			ФайлСчета = МетодКлиента("Модуль_Клиент", "Kontur_API_PortalBills_pdf", ИДТарифа, ИДЛицевогоСчета, АвторизационныйТокен);
			
			ИмяФайла 			= "Счет № " + ОписаниеСчета.Номер + ".pdf";
			ЗаголовокДиалога 	= НСтр("ru = 'Сохранение счета'");
			ФильтрДиалога		= НСтр("ru = 'Документ PDF (*.pdf)|*.pdf'");
			
			МетодКлиента("Модуль_Клиент", "СохранитьФайл", ИмяФайла, ФайлСчета, ЗаголовокДиалога, ФильтрДиалога);
			
		Исключение
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Заголовок"		, "Ошибка");
			ПараметрыФормы.Вставить("ОписаниеОшибки", НСтр("ru = 'Не удалось сохранить файл счета'"));
			ПараметрыФормы.Вставить("Подробности"	, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОткрытьФормуВыводаОшибки(ПараметрыФормы);
			
		КонецПопытки;
		
	КонецПроцедуры //СохранитьФайлОнлайнСчетаНаДиск()
	
	//} ОНЛАЙН СЧЕТ
	
	//{ ОБЕРТКИ
	
	&НаКлиенте
	Процедура ДобавитьСтатистику_ОткрытиеФормы()
		
		Категории = МетодКлиента("Модуль_Клиент", "Метрика_Категории");
		
		НазваниеФормы 		= "Форма основная";
		НазваниеКатегории 	= Категории.ИнициализацияМодуля;
		НазваниеДействия 	= "Открыть форму";						
		
		МетодКлиента(	"Модуль_Клиент",
		"Метрика_ДобавитьПоведение_ДействиеСФормой",
		НазваниеФормы,
		НазваниеКатегории,
		НазваниеДействия	);
		
	КонецПроцедуры // ДобавитьСтатистику_ОткрытиеФормы()
	
	&НаКлиенте
	Процедура ДобавитьСтатистику_НажалиСменитьПользователя()
		
		Категории = МетодКлиента("Модуль_Клиент", "Метрика_Категории");
		
		НазваниеФормы 		= "Форма основная";
		НазваниеКатегории 	= Категории.ИнициализацияМодуля;
		НазваниеДействия 	= "Сменить пользователя";
		
		МетодКлиента(	"Модуль_Клиент",
		"Метрика_ДобавитьПоведение_НажатиеКнопки",
		НазваниеФормы,
		НазваниеКатегории,
		НазваниеДействия	);
		
	КонецПроцедуры // ДобавитьСтатистику_НажалиСменитьПользователя()
	
	&НаКлиенте
	Процедура ДобавитьСтатистику_ИзменилиПоказатьЖурналДокументов()
		
		Категории = МетодКлиента("Модуль_Клиент", "Метрика_Категории");
		
		НазваниеФормы 		= "Форма основная";
		НазваниеКатегории 	= Категории.НастройкиМодуля;
		НазваниеДействия 	= "Изменить показать журнал документов";
		ФлагУстановлен		= ЖурналыДокументовПоказать;
		
		МетодКлиента(	"Модуль_Клиент",
		"Метрика_ДобавитьПоведение_ПриИзмененииФлага",
		НазваниеФормы,
		НазваниеКатегории,
		НазваниеДействия,
		ФлагУстановлен	);
		
	КонецПроцедуры // ДобавитьСтатистику_ИзменилиПоказатьЖурналДокументов()
	
	&НаКлиенте
	Процедура ДобавитьСтатистику_ИзменениеТочностьЦеныСФ()
		
		Категории = МетодКлиента("Модуль_Клиент", "Метрика_Категории");
		
		НазваниеФормы 		= "Форма основная";
		НазваниеКатегории 	= Категории.НастройкиМодуля;
		НазваниеДействия 	= "Изменить точность цены в СФ";
		
		МетодКлиента(	"Модуль_Клиент",
		"Метрика_ДобавитьПоведение_НажатиеКнопки",
		НазваниеФормы,
		НазваниеКатегории,
		НазваниеДействия	);
		
	КонецПроцедуры // ДобавитьСтатистику_ИзменениеТочностьЦеныСФ()
	
	&НаКлиенте
	Процедура ДобавитьСтатистику_ИзменилиОтправкаВСтаромИнтерфейсе()
		
		Категории = МетодКлиента("Модуль_Клиент", "Метрика_Категории");
		
		НазваниеФормы 		= "Форма основная";
		НазваниеКатегории 	= Категории.НастройкиМодуля;
		НазваниеДействия 	= "Изменить отправка файлов в старом интерфейсе";
		ФлагУстановлен		= ОтправкаФайловСтарыйИнтерфейс;
		
		МетодКлиента(	"Модуль_Клиент",
		"Метрика_ДобавитьПоведение_ПриИзмененииФлага",
		НазваниеФормы,
		НазваниеКатегории,
		НазваниеДействия,
		ФлагУстановлен	);
		
	КонецПроцедуры // ДобавитьСтатистику_ИзменилиОтправкаВСтаромИнтерфейсе()
	
	&НаКлиенте
	Процедура ДобавитьСтатистику_Авторизация()
		
		Переменные = Новый Структура;
		
		Переменные.Вставить("ОтправкаФайловСтарыйИнтерфейс"	, ОтправкаФайловСтарыйИнтерфейс);
		Переменные.Вставить("ЖурналыДокументовПоказать"		, ЖурналыДокументовПоказать);
		Переменные.Вставить("ТочностьЦеныСФ"				, ТочностьЦеныСФ);
		
		РегистрацияВК = Платформа.ПараметрыКлиент.ИспользуетсяРегистрацияКомпоненты;
		Переменные.Вставить("РегистрацияВК", РегистрацияВК);
		
		МетодКлиента("Модуль_Клиент", "Метрика_ДобавитьСтатистику_Авторизации", Переменные);
		
	КонецПроцедуры // ДобавитьСтатистику_Авторизация()
	
	&НаКлиенте
	Процедура ДобавитьСтатистику_НастройкиОрганизаций()
		
		Категории = МетодКлиента("Модуль_Клиент", "Метрика_Категории");
		
		МетодКлиента(	"Модуль_Клиент", 
		"Метрика_ДобавитьСтатистику_НастройкиОрганизации",
		Категории.ИнициализацияМодуля	);
		
	КонецПроцедуры // ДобавитьСтатистику_НастройкиОрганизаций()
	
	// Управляет запуском(отключением) периодической отправки статистики
	//
	// Параметры:
	//	ВключитьОтправку	- Булево	- признак включаем или отключаем периодическую отправку
	&НаКлиенте
	Процедура ВключитьОтключитьПериодическуюОтправкуСтатистики(ВключитьОтправку)
		
		ИмяОбработчикаОтправки		= "ОбработчикОтправитьСтатистику"; 
		ИнтервалОтправкиВСекундах	= 120;
		
		Если ВключитьОтправку Тогда
			ПодключитьОбработчикОжидания(ИмяОбработчикаОтправки, ИнтервалОтправкиВСекундах);
		Иначе
			ОтключитьОбработчикОжидания(ИмяОбработчикаОтправки);
		КонецЕсли;
		
	КонецПроцедуры // ВключитьОтключитьПериодическуюОтправкуСтатистики()
	
	// Собирает первые события статистики
	// и запускает обработчик её отправки
	//
	&НаКлиенте
	Процедура НачатьСборСтатистики()
		
		ИспользоватьМетрики = МетодКлиента("Модуль_Клиент", "ИспользоватьМетрики");
		
		Если Не ИспользоватьМетрики Тогда
			Возврат;
		КонецЕсли;
		
		ДобавитьСтатистику_ОткрытиеФормы();
		ДобавитьСтатистику_Авторизация();
		ДобавитьСтатистику_НастройкиОрганизаций();
		
		ВключитьОтключитьПериодическуюОтправкуСтатистики(Истина);
		
	КонецПроцедуры
	
	//} ОБЕРТКИ
	
#КонецЕсли

&НаКлиенте
Процедура ОтправитьВДО(Команда)
	СписокВыбора = pcru_ex_WSWORKS.ПолучитьСписокПодразделенийДО();
	Если СписокВыбора.Количество() > 0  Тогда
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ВидДокумента","Заявка на оплату");
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаОповещенияВыбораТипа",ЭтаФорма,ДопПараметры);
		ПоказатьВыборИзСписка(ОписаниеОповещения,СписокВыбора);
	КонецЕсли; 	
	
КонецПроцедуры



&НаКлиенте
Процедура ОбработкаОповещенияВыбораТипа(ЗначениеВыбора, ДопПараметры) Экспорт
	// Процедура  обработки оповещений должна быть экспортной, должна иметь как минимум 2 параметра
	Если ЗначениеВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	
	Если Элементы.ТаблицаДокументовВходящих.ТекущиеДанные<>Неопределено Тогда
		ТекущиеДанные =Элементы.ТаблицаДокументовВходящих.ТекущиеДанные;		
		Document= Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(ТекущиеДанные.BoxID).GetDocumentById(ТекущиеДанные.DocumentID);
		Если Document.type <> "Nonformalized" и Document.type <> "NonformalizedProforma"  Тогда
			РасширениеФайлаДиадок = "XLSX";
			ТД =   СформироватьПечатнуюФормуПоДокументуДиадока(Document);
			ИмяВременногоФайла  = ПолучитьИмяВременногоФайла(РасширениеФайлаДиадок);
			ТД.Записать(ИмяВременногоФайла,ТипФайлаТабличногоДокумента.XLSX) ;
			ДД = новый ДвоичныеДанные(ИмяВременногоФайла);
			Строка64 = 	Base64Строка(ДД);
			УдалитьФайлы(ИмяВременногоФайла);
		Иначе
			РасширениеФайлаДиадок = СтрПолучитьСтроку(СтрЗаменить(Document.FileName,".",Символы.ПС),СтрЧислоСтрок(СтрЗаменить(Document.FileName,".",Символы.ПС))); 
			Если Document.HasCustomPrintForm = Ложь Тогда
				ИмяВременногоФайла=	ПолучитьИмяВременногоФайла(РасширениеФайлаДиадок);
				Document.SaveSenderContent(ИмяВременногоФайла);
			Иначе
				РасширениеФайлаДиадок = "pdf";
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла(РасширениеФайлаДиадок);
				Document.GetPrintForm(ИмяВременногоФайла, 30);
			КонецЕсли;
			ДД = новый ДвоичныеДанные(ИмяВременногоФайла);
			Строка64 = 	Base64Строка(ДД);			
			УдалитьФайлы(ИмяВременногоФайла);
		КонецЕсли; 
		Список =  pcru_ex_WSWORKS.ПолучитьСписокПодразделенийДО();
		ОтправитьВДОНаСервере(Элементы.ТаблицаДокументовВходящих.ТекущиеДанные.DocumentId,Строка64,РасширениеФайлаДиадок,ЗначениеВыбора.Значение,ДопПараметры.ВидДокумента);
	КонецЕсли; 
	
КонецПроцедуры



&НаСервере
Процедура ОтправитьВДОНаСервере(DocumentId,Строка64,РасширениеФайлаДиадок,Подр,ВидДокумента)
	Отбор = Новый Структура();
	Отбор.Вставить("DocumentId",DocumentId);
	Строки = ТаблицаДокументовВходящих.НайтиСтроки(Отбор);
	Если Строки.Количество() > 0 Тогда
		
		СтрокаJSON = СтруктураДокументДиадок(Строки[0], Строка64, РасширениеФайлаДиадок, Подр, ВидДокумента);
		ргОбменДанными.ОтправитьВОбмен("ДокументИзДиадок", Справочники.ОбменДаннымиКлиенты.НайтиПоКоду("UMFO"), Справочники.ОбменДаннымиКлиенты.НайтиПоКоду("DOCMNG"), Неопределено, СтрокаJSON);
		
		//Стр = Новый Структура;
		//Стр.Вставить("AttachmentVersion",Строки[0].AttachmentVersion);
		//Запрос = Новый Запрос;
		//Запрос.Текст = "ВЫБРАТЬ
		//|	ДополнительныеСведения.Объект КАК Объект
		//|ИЗ
		//|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		//|ГДЕ
		//|	ДополнительныеСведения.Значение = &Значение";
		//Запрос.УстановитьПараметр("Значение",Строки[0].CounteragentId);
		//Выборка = Запрос.Выполнить().Выбрать();
		//Пока Выборка.Следующий() Цикл
		//	Стр.Вставить("ИНН",Выборка.Объект.ИНН);
		//КонецЦикла;
		//Стр.Вставить("BoxID",Строки[0].BoxID);
		//Стр.Вставить("ContentType",Строки[0].ContentType);
		//Стр.Вставить("CounteragentId",Строки[0].CounteragentId);
		//Стр.Вставить("DepartmentId",Строки[0].DepartmentId);
		//Стр.Вставить("DepartmentKpp",Строки[0].DepartmentKpp);
		//Стр.Вставить("DocumentDirection",Строки[0].DocumentDirection);
		//Стр.Вставить("DocumentFunction",Строки[0].DocumentFunction);
		//Стр.Вставить("DocumentId",Строки[0].DocumentId);
		//Стр.Вставить("DocumentType",Строки[0].DocumentType);
		//Стр.Вставить("FileGUID",Строки[0].FileGUID);
		//Стр.Вставить("FileName",Строки[0].FileName);
		//Стр.Вставить("MessageID",Строки[0].MessageID);
		//Стр.Вставить("Resolutions",Строки[0].Resolutions);
		//Стр.Вставить("RevocationStatus",Строки[0].RevocationStatus);
		//Стр.Вставить("Status",Строки[0].Status);
		//Стр.Вставить("Валюта",Строки[0].Валюта);
		//Стр.Вставить("Дата",Строки[0].Дата);
		//Стр.Вставить("ДатаУчета",Строки[0].ДатаУчета);
		//Стр.Вставить("ЕстьInitialDocuments",Строки[0].ЕстьInitialDocuments);
		//Стр.Вставить("ИндексИконкиПакета",Строки[0].ИндексИконкиПакета);
		//Стр.Вставить("ИндексИконкиПакетаКопия",Строки[0].ИндексИконкиПакетаКопия);
		//Стр.Вставить("Номер",Строки[0].Номер);
		////	Стр.Вставить("ПервичныйДокумент",Строки[0].ПервичныйДокумент);
		//Стр.Вставить("Подразделение",Подр);
		//Стр.Вставить("ПозицияСортировки",Строки[0].ПозицияСортировки);
		//Стр.Вставить("Покупатель",Строки[0].Покупатель);
		//Стр.Вставить("Контрагент",Строки[0].Продавец);
		//Стр.Вставить("СостояниеДокументооборота",Строки[0].СостояниеДокументооборота);
		//Стр.Вставить("СостояниеСогласования",Строки[0].СостояниеСогласования);
		//Стр.Вставить("Сумма",Строки[0].Сумма);
		//Стр.Вставить("СуммаДокументаЗначение",Строки[0].СуммаДокументаЗначение);
		//Стр.Вставить("СуммаНДС",Строки[0].СуммаНДС);
		//Стр.Вставить("ТекстОшибки",Строки[0].ТекстОшибки);
		//Стр.Вставить("НаименованиеДокумента",Строки[0].ТипДокумента +" "+Строки[0].Номер );
		//Стр.Вставить("ЭтоТестовыйДок",Строки[0].ЭтоТестовыйДок);
		//Стр.Вставить("РасширениеФайлаДиадок",РасширениеФайлаДиадок);	
		//Стр.Вставить("Данные",Строка64);
		//Стр.Вставить("ВидДокумента",ВидДокумента);
		////
		//ЗаписьJSON = Новый ЗаписьJSON;
		//ЗаписьJSON.УстановитьСтроку();
		//ЗаписатьJSON(ЗаписьJSON, Стр ); 
		//СтрJSON = ЗаписьJSON.Закрыть();
		
		////
		////Если Тест Тогда
		////	Определение = Новый WSОпределения("http://ruspbpacc01/DOCMNG_TEST/ws/DocumentWorkflow.1cws?wsdl","WS","123");
		////	Прокси = Новый WSПрокси(Определение, "http://ruspbpacc01/DocumentWorkflow","Pcru_DocumentWorkflow" ,"Pcru_DocumentWorkflowSoap" );       
		////	Прокси.Пользователь = "WS";      Прокси.Пароль = "123";
		////Иначе
		
			//Определение = Новый WSОпределения("http://ruspbpacc01/DOCMNG/ws/DocumentWorkflow.1cws?wsdl","WS","WS159753");
			//Прокси = Новый WSПрокси(Определение, "http://ruspbpacc01/DocumentWorkflow","Pcru_DocumentWorkflow" ,"Pcru_DocumentWorkflowSoap" );       
			//Прокси.Пользователь = "WS";      Прокси.Пароль = "WS159753";
		////КонецЕсли; 
		//Результат = Прокси.NewDocument(СтрJSON);
		//Сообщить(Результат);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Функция СтруктураДокументДиадок(СтрокаДокумента, Строка64, РасширениеФайлаДиадок, Подр, ВидДокумента) Экспорт
	
	Стр = Новый Структура;
	Стр.Вставить("AttachmentVersion",СтрокаДокумента.AttachmentVersion);
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДополнительныеСведения.Объект КАК Объект
	|ИЗ
	|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|ГДЕ
	|	ДополнительныеСведения.Значение = &Значение";
	Запрос.УстановитьПараметр("Значение",СтрокаДокумента.CounteragentId);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Стр.Вставить("ИНН",Выборка.Объект.ИНН);
	КонецЦикла;
	Стр.Вставить("BoxID",СтрокаДокумента.BoxID);
	Стр.Вставить("ContentType",СтрокаДокумента.ContentType);
	Стр.Вставить("CounteragentId",СтрокаДокумента.CounteragentId);
	Стр.Вставить("DepartmentId",СтрокаДокумента.DepartmentId);
	Стр.Вставить("DepartmentKpp",СтрокаДокумента.DepartmentKpp);
	Стр.Вставить("DocumentDirection",СтрокаДокумента.DocumentDirection);
	Стр.Вставить("DocumentFunction",СтрокаДокумента.DocumentFunction);
	Стр.Вставить("DocumentId",СтрокаДокумента.DocumentId);
	Стр.Вставить("DocumentType",СтрокаДокумента.DocumentType);
	Стр.Вставить("FileGUID",СтрокаДокумента.FileGUID);
	Стр.Вставить("FileName",СтрокаДокумента.FileName);
	Стр.Вставить("MessageID",СтрокаДокумента.MessageID);
	Стр.Вставить("Resolutions",СтрокаДокумента.Resolutions);
	Стр.Вставить("RevocationStatus",СтрокаДокумента.RevocationStatus);
	Стр.Вставить("Status",СтрокаДокумента.Status);
	Стр.Вставить("Валюта",СтрокаДокумента.Валюта);
	Стр.Вставить("Дата",СтрокаДокумента.Дата);
	Стр.Вставить("ДатаУчета",СтрокаДокумента.ДатаУчета);
	Стр.Вставить("ЕстьInitialDocuments",СтрокаДокумента.ЕстьInitialDocuments);
	Стр.Вставить("ИндексИконкиПакета",СтрокаДокумента.ИндексИконкиПакета);
	Стр.Вставить("ИндексИконкиПакетаКопия",СтрокаДокумента.ИндексИконкиПакетаКопия);
	Стр.Вставить("Номер",СтрокаДокумента.Номер);
	//	Стр.Вставить("ПервичныйДокумент",СтрокаДокумента.ПервичныйДокумент);
	Стр.Вставить("Подразделение",Подр);
	Стр.Вставить("ПозицияСортировки",СтрокаДокумента.ПозицияСортировки);
	Стр.Вставить("Покупатель",СтрокаДокумента.Покупатель);
	Стр.Вставить("Контрагент",СтрокаДокумента.Продавец);
	Стр.Вставить("СостояниеДокументооборота",СтрокаДокумента.СостояниеДокументооборота);
	Стр.Вставить("СостояниеСогласования",СтрокаДокумента.СостояниеСогласования);
	Стр.Вставить("Сумма",СтрокаДокумента.Сумма);
	Стр.Вставить("СуммаДокументаЗначение",СтрокаДокумента.СуммаДокументаЗначение);
	Стр.Вставить("СуммаНДС",СтрокаДокумента.СуммаНДС);
	Стр.Вставить("ТекстОшибки",СтрокаДокумента.ТекстОшибки);
	Стр.Вставить("НаименованиеДокумента",СтрокаДокумента.ТипДокумента +" "+СтрокаДокумента.Номер );
	Стр.Вставить("ЭтоТестовыйДок",СтрокаДокумента.ЭтоТестовыйДок);
	Стр.Вставить("РасширениеФайлаДиадок",РасширениеФайлаДиадок);	
	Стр.Вставить("Данные",Строка64);
	Стр.Вставить("ВидДокумента",ВидДокумента);
    //
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Стр ); 
	СтрJSON = ЗаписьJSON.Закрыть();

	Возврат СтрJSON;
	
КонецФункции

&НаСервере
Процедура ПОказатьТабдокНаСервере()
	
	
КонецПроцедуры

&НаКлиенте
//Весь функционал взят из форм Диадока
//позволяет открывать не карточку файла а сам документ, что быстрее и удобнее.
Процедура ПОказатьТабдок(Команда)
	Если Элементы.ТаблицаДокументовВходящих.ТекущиеДанные<>Неопределено Тогда
		ТекущиеДанные = Элементы.ТаблицаДокументовВходящих.ТекущиеДанные;
		Попытка
			Document= Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(ТекущиеДанные.BoxID).GetDocumentById(ТекущиеДанные.DocumentID);
		Исключение
			Результат=	Новый Структура();
			Результат.Вставить("ОписаниеОшибки", 	"Ошибка получения документа с сервера " + Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы);
			Результат.Вставить("Подробности", 		ТекущиеДанные.DocumentID);
			ОткрытьФормуВыводаОшибки(Результат, Истина);
			Возврат;
		КонецПопытки;
		//  эти 2 типа относятся к неформализованным документам , которые передаются бинарными данными
		//  Если в дальнейшем выяснятся еще такие типы - стоит добавить их сюда.
		Если Document.type <> "Nonformalized" и Document.type <> "NonformalizedProforma"  Тогда
			ТД =   СформироватьПечатнуюФормуПоДокументуДиадока(Document);
			ТД.Показать();
		Иначе
			РасширениеФайлаДиадок = СтрПолучитьСтроку(СтрЗаменить(Document.FileName,".",Символы.ПС),СтрЧислоСтрок(СтрЗаменить(Document.FileName,".",Символы.ПС)));    
			Если Document.HasCustomPrintForm = Ложь Тогда
				ИмяВременногоФайла=	ПолучитьИмяВременногоФайла(РасширениеФайлаДиадок);
				Document.SaveSenderContent(ИмяВременногоФайла);
			Иначе
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
				Document.GetPrintForm(ИмяВременногоФайла, 30);
			КонецЕсли;
			ЗапуститьПриложение(ИмяВременногоФайла);
		КонецЕсли; 
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
// Параметры:
//	Document				- СОМ Объект	- обрабатываемый электронный документ
//	СтруктураSellerContent	- Структура 	- контент отправителя, соответствующий SellerContent, обрабатываемого электронного документа
//	СтруктураBuyerContent 	- Структура 	- контент получателя, соответствующий BuyerContent, обрабатываемого электронного документа 
Функция СформироватьДанныеОПодписях(Document, Content, BuyerContent)
	
	Результат = Новый Структура;
	
	Результат.Вставить("SellerSigner", МетодКлиента("Модуль_Клиент", "Новый_Signer"));
	Результат.Вставить("BuyerSigner" , МетодКлиента("Модуль_Клиент", "Новый_Signer"));
	
	Результат.Вставить("SenderSignature"   , МетодКлиента("Модуль_Клиент", "Новый_Signature"));
	Результат.Вставить("RecipientSignature", МетодКлиента("Модуль_Клиент", "Новый_Signature"));
	
	Результат.Вставить("DocumentMetaData", Новый Структура("Timestamp, DocumentID, SenderSignatureStatus, SenderName, RecipientName"));
	
	Результат.DocumentMetaData.DocumentID			 = Document.DocumentID;
	Результат.DocumentMetaData.TimeStamp			 = Document.TimeStamp;
	Результат.DocumentMetaData.SenderSignatureStatus = Document.SenderSignatureStatus;
	
	SenderSignature = Document.GetSenderSignature();
	Если SenderSignature <> Неопределено Тогда
		Результат.DocumentMetaData.SenderName = SenderSignature.Certificate.OrganizationName;
	КонецЕсли;
	
	RecipientSignature = Document.GetRecipientSignature();
	Если RecipientSignature <> Неопределено Тогда
		Результат.DocumentMetaData.RecipientName = RecipientSignature.Certificate.OrganizationName;
	КонецЕсли;
	
	Если Content.Свойство("Signer") Тогда
		
		Результат.SellerSigner = Content.Signer;
		
	ИначеЕсли Content.Свойство("Signers") И ЗначениеЗаполнено(Content.Signers) Тогда
		
		Результат.SellerSigner = Content.Signers[0].SignerDetails;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(BuyerContent) Тогда
		
		Если BuyerContent.Свойство("Signer") Тогда
			
			Результат.BuyerSigner = BuyerContent.Signer;
			
		ИначеЕсли BuyerContent.Свойство("Signers") И ЗначениеЗаполнено(BuyerContent.Signers) Тогда
			
			Результат.BuyerSigner = BuyerContent.Signers[0].SignerDetails;
			
		КонецЕсли;
		
	КонецЕсли;
	
	МетодКлиента("Модуль_Клиент", "ЗаполнитьСтруктуруПоКонтенту", Document.GetSenderSignature()	  , Результат.SenderSignature);
	МетодКлиента("Модуль_Клиент", "ЗаполнитьСтруктуруПоКонтенту", Document.GetRecipientSignature(), Результат.RecipientSignature);
	
	Возврат Результат;
	
КонецФункции	

&НаКлиенте
Функция СформироватьПечатнуюФормуПоДокументуДиадока(Document)
	
	Результат = Неопределено;
	
	ПротоСтруктура		 = МетодКлиента("Модуль_Клиент", "ПолучитьProto", Document, Ложь);
	ПротоСтруктураОтвета = МетодКлиента("Модуль_Клиент", "ПолучитьProto", Document, Истина);
	
	Если ПротоСтруктура.Свойство("Контент") Тогда
		
		
		ПротоКонтентОтвета = Неопределено;
		ПротоСтруктураОтвета.Свойство("Контент", ПротоКонтентОтвета);
		
		ПротоКонтент		 = ПротоСтруктура.Контент;
		ТипКонтента			 = ПротоСтруктура.ТипКонтента;
		ТипДокумента		 = Document.Type;
		ДанныеШтампа		 = СформироватьДанныеОПодписях(Document, ПротоКонтент, ПротоКонтентОтвета);
		
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ПротоКонтентОтвета"	, ПротоКонтентОтвета);
		ДопПараметры.Вставить("ДанныеШтампа"		, ДанныеШтампа);
		ДопПараметры.Вставить("ПоказатьДопСведения"	, Истина);
		
		Результат = МетодСервераБезКонтекста("ПечатныеФормы", "ПечатнаяФормаПротоКонтента", ПротоКонтент, ТипКонтента, ТипДокумента, ДопПараметры);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьВДОТест(Команда)
	СписокВыбора = pcru_ex_WSWORKS.ПолучитьСписокПодразделенийДО();
	Если СписокВыбора.Количество() > 0  Тогда
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ВидДокумента","Входящий");
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаОповещенияВыбораТипа",ЭтаФорма,ДопПараметры);
		ПоказатьВыборИзСписка(ОписаниеОповещения,СписокВыбора);
	КонецЕсли; 	
	//Если Элементы.ТаблицаДокументовВходящих.ТекущиеДанные<>Неопределено Тогда
	//	ТекущиеДанные =Элементы.ТаблицаДокументовВходящих.ТекущиеДанные;		
	//	Document= Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(ТекущиеДанные.BoxID).GetDocumentById(ТекущиеДанные.DocumentID);
	//	Если Document.type <> "Nonformalized" и Document.type <> "NonformalizedProforma"  Тогда
	//		РасширениеФайлаДиадок = "XLSX";
	//		ТД =   СформироватьПечатнуюФормуПоДокументуДиадока(Document);
	//		ИмяВременногоФайла  = ПолучитьИмяВременногоФайла(РасширениеФайлаДиадок);
	//		ТД.Записать(ИмяВременногоФайла,ТипФайлаТабличногоДокумента.XLSX) ;
	//		ДД = новый ДвоичныеДанные(ИмяВременногоФайла);
	//		Строка64 = 	Base64Строка(ДД);
	//		УдалитьФайлы(ИмяВременногоФайла);
	//	Иначе
	//		РасширениеФайлаДиадок = СтрПолучитьСтроку(СтрЗаменить(Document.FileName,".",Символы.ПС),СтрЧислоСтрок(СтрЗаменить(Document.FileName,".",Символы.ПС)));    
	//		Если Document.HasCustomPrintForm = Ложь Тогда
	//			ИмяВременногоФайла=	ПолучитьИмяВременногоФайла(РасширениеФайлаДиадок);
	//			Document.SaveSenderContent(ИмяВременногоФайла);
	//		Иначе
	//			ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
	//			Document.GetPrintForm(ИмяВременногоФайла, 30);
	//		КонецЕсли;
	//		ДД = новый ДвоичныеДанные(ИмяВременногоФайла);
	//		Строка64 = 	Base64Строка(ДД);
	//		УдалитьФайлы(ИмяВременногоФайла);
	//	КонецЕсли; 
	//	ОтправитьВДОНаСервере(Элементы.ТаблицаДокументовВходящих.ТекущиеДанные.DocumentId,Строка64,РасширениеФайлаДиадок,true);
	//КонецЕсли; 
	
КонецПроцедуры

