#Если ВебКлиент Тогда

ВызватьИсключение НСтр("ru = 'Недопустимый режим работы (Веб-клиент).'");

#Иначе

////////////////////////////////////////////////////////////////////////////////
//{ ПЕРЕМЕННЫЕ МОДУЛЯ

&НаКлиенте
Перем UserPermissions;
&НаКлиенте
Перем Organization Экспорт; 
&НаКлиенте
Перем ЭДОбъект Экспорт; 

&НаКлиенте
Перем IDТекущейСтроки;

&НаКлиенте
Перем МассивСсылокРНК;
&НаКлиенте
Перем ПодходящаяСФ;

&НаКлиенте
Перем НомерЗаказа;

&НаКлиенте
Перем ПредставлениеСтатусаРоуминг, ПредставлениеСтатусаРоумингДетали;

&НаКлиенте
Перем КэшТабличныеДокументы;

//} ПЕРЕМЕННЫЕ МОДУЛЯ
////////////////////////////////////////////////////////////////////////////////

#Область ПЕРМЕННЫЕ_ПЛАТФОРМЫ

&НаКлиенте
Перем Платформа Экспорт;

&НаСервере
Перем ОбработкаОбъект;

#КонецОбласти

#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ПЛАТФОРМЫ

&НаКлиенте
Функция МетодКлиента(ИмяМодуля= "", ИмяМетода, 
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL,
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат  Платформа.МетодКлиента(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаКлиенте
Функция МетодСервераБезКонтекста(ИмяМодуля= "", ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат Платформа.МетодСервераБезКонтекста(ИмяМодуля, ИмяМетода,
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция МетодСервера(Знач ИмяМодуля= "", Знач ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат ОбработкаОбъект().МетодСервера(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция ОбработкаОбъект() Экспорт
	
	Если ОбработкаОбъект = Неопределено Тогда
		
		СтруктураОбработки= ПолучитьИзВременногоХранилища(Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
		
		Если СтруктураОбработки <> Неопределено Тогда
			ОбработкаОбъект= СтруктураОбработки.ОбработкаОбъект;
		КонецЕсли;
		
		Если ОбработкаОбъект = Неопределено Тогда
			
			ОбработкаОбъект= РеквизитФормыВЗначение("Объект");
			
			Попытка
				ПоместитьВоВременноеХранилище(Новый Структура("ОбработкаОбъект", ОбработкаОбъект), Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
			Исключение КонецПопытки;
		
		Иначе
			ОбработкаОбъект.ПараметрыКлиентСервер= Объект.ПараметрыКлиентСервер;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма(ТекущийВладелецФормы)
	
	Если ТекущийВладелецФормы = Неопределено Тогда
		Возврат Неопределено
	ИначеЕсли Прав(ТекущийВладелецФормы.ИмяФормы, 14) = "Форма_Основная" Тогда
		Возврат ТекущийВладелецФормы;
	Иначе
		Возврат ОсновнаяФорма(ТекущийВладелецФормы.ВладелецФормы);
	КонецЕсли;
	
КонецФункции


&НаСервере
Процедура ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", Объект.ПараметрыКлиентСервер);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриОткрытии(Отказ)
	
	ОсновнаяФорма= ОсновнаяФорма(ВладелецФормы);
	
	Если ОсновнаяФорма <> Неопределено Тогда
		Платформа= ОсновнаяФорма.Платформа;
	КонецЕсли;
		
	Платформа.ПриОткрытииФормыОбработки(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриЗакрытии()
	
	Платформа.ПриЗакрытииФормыОбработки(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция СуществуетСвязьСДокументомУчета()
	
	Результат = ЗначениеЗаполнено(Документ1С);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ИспользоватьНовуюМеханикуОтраженияВУчете()
	
	Результат = Не СоздаватьДокументыУчетаСПредварительнымПросмотром();
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция СоздаватьДокументыУчетаСПредварительнымПросмотром()
	
	Результат = Ложь;
	
	Если ЭДОбъект <> Неопределено Тогда
		
		ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
		Результат = МетодКлиента("Модуль_Клиент", "ИспользоватьФормуВводаНакладной", ФорматЭД);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НаправлениеИсходящее()
	Возврат "Outbound";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НаправлениеВходящее()
	Возврат "Inbound";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НаправлениеВнутреннее()
	Возврат "Internal";
КонецФункции

&НаКлиенте
Функция ЭтоВходящийДокумент()
	
	Результат = Ложь;
	
	Если ЭДОбъект <> Неопределено
		И ЭДОбъект.Direction = НаправлениеВходящее() Тогда
		
		Результат = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИспользоватьПартнеровКакКонтрагентов()
	
	Результат = Ложь;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЭтоСемействоУТКАERP()
	
	Результат = Ложь;
	
	Если ОбработкаОбъект().ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция БазовыеФорматы()
	
	Результат = МетодКлиента("Модуль_Клиент", "БазовыеФорматы");
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ТипыКонтента()
	
	Результат = МетодКлиента("Модуль_Клиент", "ТипыКонтента");
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ФорматЭлектронногоДокумента(Document)
	
	Результат = МетодКлиента("Модуль_Клиент", "ФорматЭлектронногоДокумента", Document);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПользовательАвторизованПоСертификату()
	
	ТипАутентификации = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.AuthenticateType;
	
	Результат = (ТипАутентификации = "Certificate");
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПоддерживаетсяОтправкаФормата(ФорматЭД)
	
	БазовыеФорматы = БазовыеФорматы();
	
	ПоддерживаемыеФорматы = Новый Структура;
	ПоддерживаемыеФорматы.Вставить(БазовыеФорматы.utd820);
	ПоддерживаемыеФорматы.Вставить(БазовыеФорматы.utd);
	ПоддерживаемыеФорматы.Вставить(БазовыеФорматы.ucd);
	ПоддерживаемыеФорматы.Вставить(БазовыеФорматы.invoice);
	ПоддерживаемыеФорматы.Вставить(БазовыеФорматы.invoicecor);
	ПоддерживаемыеФорматы.Вставить(БазовыеФорматы.torg12);
	ПоддерживаемыеФорматы.Вставить(БазовыеФорматы.act);
	ПоддерживаемыеФорматы.Вставить(БазовыеФорматы.proformainvoice);
	
	Результат = ПоддерживаемыеФорматы.Свойство(ФорматЭД.БазовыйФормат);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ФИОПодписанта()
	
	Результат = МетодКлиента("Модуль_РаботаССерверомДиадок", "ПолучитьФИОПодписанта", Organization.Id);
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПредставлениеПодписи()
	
	ФИОПодписанта = ФИОПодписанта();
	
	Подстроки = Новый Массив;
	Подстроки.Добавить(Organization.Name);
	
	Если Не ПустаяСтрока(ФИОПодписанта) Тогда
		Подстроки.Добавить(ФИОПодписанта);
	КонецЕсли;
	
	Результат = СтрСоединить(Подстроки, ", ");
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПодготовитьПараметрыДляСозданияСчетаФактуры()
	
	Если ЭДОбъект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
	
	ЭтоСчетФактура = (ФорматЭД.ЕстьФункцияСФ И Не ФорматЭД.ЕстьФункцияПД);
	
	Если Не ЭтоСчетФактура Тогда
		Возврат;
	КонецЕсли;
	
	МассивСсылокРНК	 = МетодКлиента("Модуль_Клиент", "ПолучитьМассивСсылокРНКПоСчетуФактуреПолученномуДиадок", ЭДОбъект);
	ПодходящаяСФ	 = МетодКлиента("Модуль_Клиент", "ПолучитьПодходящуюСФ", МассивСсылокРНК);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоУПД(ИмяТипа)
	
	ТипыУПД = Новый Структура;
	ТипыУПД.Вставить("UniversalTransferDocument");
	ТипыУПД.Вставить("UniversalTransferDocumentRevision");
	
	Результат = ТипыУПД.Свойство(ИмяТипа);
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоУКД(ИмяТипа)
	
	ТипыУКД = Новый Структура;
	ТипыУКД.Вставить("UniversalCorrectionDocument");
	ТипыУКД.Вставить("UniversalCorrectionDocumentRevision");
	
	Результат = ТипыУКД.Свойство(ИмяТипа);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьПредупреждениеОбУстаревшемФормате()
	
	МетодКлиента("Модуль_Клиент", "ПоказатьПредупреждениеОбУстаревшемФормате"
		, ЭтаФорма
		, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредупреждениеОбУстаревшихНастройкахОтправки()
	
	МетодКлиента("Модуль_Клиент", "ПоказатьПредупреждениеОбУстаревшихНастройкахОтправки"
		, ЭтаФорма
		, "ОбработчикОткрытиеФормыУведомленияОСтаромФормате");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИнструкциюКакЗаполнитьОснованиеПередачи()
	
	АдресаРесурсов = МетодКлиента("Модуль_Клиент", "АдресаИнтернетРесурсов");
	ЗапуститьПриложение(АдресаРесурсов.Инструкция_ЗаполнитьОснованиеПередачи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИнструкциюКакЗаполнитьГТД()
	
	АдресаРесурсов = МетодКлиента("Модуль_Клиент", "АдресаИнтернетРесурсов");
	ЗапуститьПриложение(АдресаРесурсов.Инструкция_ЗаполнитьГТД);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОткрытиеФормыУведомленияОСтаромФормате(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> "ИзмененыНастройкиОтправки" Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Функция МодульКлиент_ФормироватьОтветныйТитул(ВидДокументооборота)
	
	Результат = МетодКлиента("Модуль_Клиент", "ФормироватьОтветныйТитул", ВидДокументооборота);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульКлиент_ПараметрыФормыПодписанияДокумента()
	
	Результат = МетодКлиента("Модуль_Клиент", "ПараметрыФормыПодписанияДокумента");
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция МодульКлиент_ТекстОшибкиДиадок(ТекстОшибки)
	
	Результат = МетодКлиента("Модуль_Клиент", "СформироватьТекстОшибкиДиадок", ТекстОшибки);
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПредставлениеСервиса()
	
	Результат = Платформа.ПараметрыКлиент.СловарьWL.НаименованиеСистемы;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
//{ ОБРАБОТЧИКИ ФОРМЫ

	&НаСервере
	Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
		ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
		
		ПрочитатьПараметрыФормы();
		
		Если Не ЗначениеЗаполнено(Организация) Тогда
			Организация = МетодСервера(, "BoxID_2_Организация", BoxID, DepartmentKpp);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Контрагент) 
			И ЗначениеЗаполнено(CounteragentBoxID) Тогда
			Контрагент = МетодСервера(, "CounteragentBoxID_2_Контрагент", CounteragentBoxID);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(CounteragentBoxID)
			И ЗначениеЗаполнено(Контрагент) Тогда
			CounteragentBoxID = МетодСервера(, "Контрагент_2_CounteragentBoxID", Контрагент);
		КонецЕсли;
		
		Если ТочкаВызова = "ТаблицаДокументовНаОтправку" 
			И (Операция = "СоздатьНовыйПакет" ИЛИ Операция = "ДобавитьВВыделенныеПакеты") Тогда
			
			ЗаполнитьТаблицуДокументовПоДаннымВыбранныхФайлов(Параметры.МассивАдресовФайлов);
			
		Иначе
			
			МассивДокументовПакета = Параметры.МассивДокументовПакета;
			
			ТекущиеДанные = Неопределено;
			ЗаполнитьТаблицуДокументов(ТочкаВызова, МассивДокументовПакета, ТекущиеДанные);
			
			Если ТекущиеДанные = Неопределено Тогда
				ТекущиеДанные = Параметры.МассивДокументовПакета[0]; 
			КонецЕсли;
			
			CounteragentBoxID	= ТекущиеДанные.CounteragentBoxID;
			DocumentType		= ТекущиеДанные.DocumentType;
			Документ1С			= ТекущиеДанные.Документ1С;
			ТипДокумента		= ТекущиеДанные.ТипДокумента;
			ДопСведения			= ТекущиеДанные.ДопСведения;
			ФункцияУПД			= ТекущиеДанные.ФункцияУПД;
			
			ТекущиеДанные.Свойство("DocumentID", 			DocumentID);
			ТекущиеДанные.Свойство("isTest", 				isTest);
			ТекущиеДанные.Свойство("DepartmentId", 			DepartmentId);
			ТекущиеДанные.Свойство("ВнешняяПечатнаяФорма", 	ВнешняяПечатнаяФорма);
			
			Параметры.Свойство("ОшибкаОтправкиПакета", ОшибкиОтправки);
			
			Если ТекущиеДанные.ЭтоФайл Тогда
				Элементы.ГруппаВизуализацияДокумента.ТекущаяСтраница = Элементы.СтраницаБезВизуализации;
			Иначе
				Элементы.ГруппаВизуализацияДокумента.ТекущаяСтраница = Элементы.СтраницаТабличноеПоле;
			КонецЕсли;
			
		КонецЕсли;
						
		НастроитьКнопкиКоманднойПанелиНаСервере();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПриОткрытии(Отказ)
		
		ПлатформаПриОткрытии(Отказ);
		
		ИнициализироватьКэшТабличныеДокументы();
		
		ЗаполнитьСписокВыбора_ТипДокумента();
		
		Если ТочкаВызова = "ТаблицаДокументовНаОтправку" И (Операция = "СоздатьНовыйПакет" ИЛИ Операция = "ДобавитьВВыделенныеПакеты") Тогда
											
			ЗаполнитьТаблицуДокументовДаннымиПоУмолчанию();
		
			Если Операция = "СоздатьНовыйПакет" Тогда
				ЗаполнитьСписокВыбора_Организация();
				Если ЗначениеЗаполнено(Организация) Тогда
					ОбработатьИзменениеОрганизации();
				КонецЕсли;
			КонецЕсли;
			
			УстановитьВидимостьПоТипуДокумента();
																				
		Иначе
			
			Если НЕ ЗначениеЗаполнено(Организация) Тогда
				Организация = МетодКлиента("Модуль_Клиент", "НайтиОрганизациюВИерархииОрганизацийDiadoc", BoxID, DepartmentId);
			КонецЕсли;
			
			СтрокаПозиционирования = СтрокаПозиционированияВТаблицеДокументов();
			
			ЗаполнитьПеременныеФормыПриОткрытии(СтрокаПозиционирования.IdСтроки);
			ПодготовитьПараметрыДляСозданияСчетаФактуры();
			
			Если ТочкаВызова = "ТаблицаДокументовНаОтправку" И ТаблицаДокументов.Количество() > 0 Тогда
				// позиционируем на выбранную строку
				Элементы.ТаблицаДокументов.ТекущаяСтрока = СтрокаПозиционирования.ПолучитьИдентификатор(); 
								
				ПровестиВалидациюДокументовПакета();
			КонецЕсли;
									
			Если СтрокаПозиционирования.ЭтоФайл Тогда
				УстановитьВидимостьПоТипуДокумента();
				ДанныеФайлаНаФорму(СтрокаПозиционирования);
			Иначе
				ВизуализацияПечатнойФормыПоСтруктуре();
			КонецЕсли;
			
		КонецЕсли;
						
		УправлениеФормой();
				
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник) Экспорт
		
		Если ИмяСобытия = "ДиадокЗакрытьФормуДокумента" Тогда
			
			ЭтоНовыйДокумент = (ЭДОбъект = Неопределено);
			
			Если ЭтоНовыйДокумент Тогда
				Закрыть(ТаблицаДокументов);
			Иначе
				Если ЭДОбъект <> Неопределено И Параметр = ЭДОбъект.DocumentID Тогда
					Закрыть();
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
			
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПриЗакрытии()
		
		ПлатформаПриЗакрытии();
		
	КонецПроцедуры
	
//} ОБРАБОТЧИКИ ФОРМЫ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ ФОРМИРОВАНИЕ ПЕЧАТНЫХ ФОРМ

	&НаКлиенте
	Процедура СформироватьПечатнуюФормуПоДокументу(ФИОПодписанта, ВизуализироватьДопПоля)
		
		ТекСтрока = ТекущаяСтрокаТаблицыДокументов();
		ФорматОтправки	 = ТекСтрока.ФорматОтправки;
		ТипКонтента		 = ФорматОтправки.ТипКонтента;
		ИмяТипаДокумента = ФорматОтправки.ИмяТипа;
		
		Если ЗначениеЗаполнено(ВнешняяПечатнаяФорма) Тогда
			
			ТабличныйДокумент 	= МетодСервера("Модуль_РаботаСВнешнимиПечатнымиФормами","СформироватьВнешнююПечатнуюФормуПоСсылкеВПФ", Документ1С, ВнешняяПечатнаяФорма, Организация).Таблица;
			Ошибки				= "";
			
		ИначеЕсли ЭтоФормализованныйФормат(ФорматОтправки) Тогда
			
			ПротоКонтент = ПротоконтентПоДокументу(Документ1С, ФорматОтправки, ФИОПодписанта, ДопСведения, НомерЗаказа);
			
			ВыводитьНаименованиеТовараСКодами = МетодКлиента("Модуль_РаботаССерверомДиадок", "ТребуетсяВыводитьНаименованиеТовараСКодами", Организация);
			
			ДопПараметрыПФ = Новый Структура;
			
			ДопПараметрыПФ.Вставить("ПоказатьДопСведения", ВизуализироватьДопПоля);
			
			ДопПараметрыПФ.Вставить("ВыводитьНаименованиеТовараСКодами", ВыводитьНаименованиеТовараСКодами);
			
			ТабличныйДокумент = МетодСервера("ПечатныеФормы", "ПечатнаяФормаПротоКонтента"
									, ПротоКонтент
									, ТипКонтента
									, ИмяТипаДокумента
									, ДопПараметрыПФ);
			
			Ошибки = ВалидироватьКонтентФормализованного(ПротоКонтент, ФорматОтправки, Документ1С, Истина);
			
		Иначе
			
			Попытка
				РезультатВизуализации = РезультатВизуализацииНеформализованного(ИмяТипаДокумента, Документ1С, Неопределено, ФИОПодписанта);
			Исключение
				Ошибки = МетодКлиента("Модуль_Клиент", "ОформитьОшибкиВHTML", ОписаниеОшибки(), "Ошибка формирования печатной формы");
			КонецПопытки;
			
			Если ЗначениеЗаполнено(РезультатВизуализации) Тогда
				ТабличныйДокумент	= РезультатВизуализации.Таблица;
				Ошибки 				= РезультатВизуализации.СписокОшибок;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			ЗакэшироватьРезультатВизуализации(ТабличныйДокумент, Ошибки, ОшибкиФормата);
			ТабПоле = ТабличныйДокумент;
		КонецЕсли;
		
		Ошибки = ТекстПолнойОшибки();
		ОшибкиФормата = "";
				
	КонецПроцедуры
	
	&НаКлиенте
	Функция РезультатВизуализацииНеформализованного(Парам_DocumentType, Парам_Документ1С, Парам_ПараметрыСогласования = Неопределено, Парам_ФИОПодписанта)
		
		Результат = Неопределено;
		
		Если 	Парам_DocumentType = "XmlAcceptanceCertificate"
			ИЛИ Парам_DocumentType = "AcceptanceCertificate" Тогда
			
			ВнешняяПечатнаяФорма = МетодСервераБезКонтекста(, "ПолучитьВнешнююПечатнуюФормуАкта", Парам_Документ1С);
			
		ИначеЕсли Парам_DocumentType = "ProformaInvoice" Тогда
			
			ВнешняяПечатнаяФорма = МетодСервераБезКонтекста(, "ПолучитьВнешнююПечатнуюФормуСчета", Парам_Документ1С);
			
			Если НЕ ЗначениеЗаполнено(ВнешняяПечатнаяФорма) Тогда
				
				Результат = МетодСервераБезКонтекста("Модуль_ИнтеграцияУниверсальный", "РезультатВизуализацииСчетаНаОплату"
								, Парам_Документ1С
								, Парам_ПараметрыСогласования
								, Парам_ФИОПодписанта);
				
			КонецЕсли;
			
		ИначеЕсли Парам_DocumentType = "ReconciliationAct" Тогда
			
			ВнешняяПечатнаяФорма = МетодСервераБезКонтекста(, "ПолучитьВнешнююПечатнуюФормуАктаСверки", Парам_Документ1С);
			
			Если НЕ ЗначениеЗаполнено(ВнешняяПечатнаяФорма) Тогда
				
				Результат = МетодСервераБезКонтекста("Модуль_ИнтеграцияУниверсальный", "РезультатВизуализацииАктаСверки"
								, Парам_Документ1С
								, Парам_ПараметрыСогласования
								, Парам_ФИОПодписанта);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Результат = Неопределено И ЗначениеЗаполнено(ВнешняяПечатнаяФорма) Тогда
			
			Результат = МетодСервераБезКонтекста("Модуль_РаботаСВнешнимиПечатнымиФормами"
							, "СформироватьВнешнююПечатнуюФормуПоСсылкеВПФ"
							, Парам_Документ1С
							, ВнешняяПечатнаяФорма);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	// Параметры:
	//	Document				- СОМ Объект	- обрабатываемый электронный документ
	//	СтруктураSellerContent	- Структура 	- контент отправителя, соответствующий SellerContent, обрабатываемого электронного документа
	//	СтруктураBuyerContent 	- Структура 	- контент получателя, соответствующий BuyerContent, обрабатываемого электронного документа 
	Функция СформироватьДанныеОПодписях(Document, Content, BuyerContent)
		
		Результат = Новый Структура;
		
		Результат.Вставить("SellerSigner", МетодКлиента("Модуль_Клиент", "Новый_Signer"));
		Результат.Вставить("BuyerSigner" , МетодКлиента("Модуль_Клиент", "Новый_Signer"));
		
		Результат.Вставить("SenderSignature"   , МетодКлиента("Модуль_Клиент", "Новый_Signature"));
		Результат.Вставить("RecipientSignature", МетодКлиента("Модуль_Клиент", "Новый_Signature"));
		
		Результат.Вставить("DocumentMetaData", Новый Структура("Timestamp, DocumentID, SenderSignatureStatus, SenderName, RecipientName"));
		
		Результат.DocumentMetaData.DocumentID			 = Document.DocumentID;
		Результат.DocumentMetaData.TimeStamp			 = Document.TimeStamp;
		Результат.DocumentMetaData.SenderSignatureStatus = Document.SenderSignatureStatus;
		
		SenderSignature = Document.GetSenderSignature();
		Если SenderSignature <> Неопределено Тогда
			Результат.DocumentMetaData.SenderName = SenderSignature.Certificate.OrganizationName;
		КонецЕсли;
		
		RecipientSignature = Document.GetRecipientSignature();
		Если RecipientSignature <> Неопределено Тогда
			Результат.DocumentMetaData.RecipientName = RecipientSignature.Certificate.OrganizationName;
		КонецЕсли;
		
		Если Content.Свойство("Signer") Тогда
			
			Результат.SellerSigner = Content.Signer;
			
		ИначеЕсли Content.Свойство("Signers") И ЗначениеЗаполнено(Content.Signers) Тогда
			
			Результат.SellerSigner = Content.Signers[0].SignerDetails;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(BuyerContent) Тогда
			
			Если BuyerContent.Свойство("Signer") Тогда
				
				Результат.BuyerSigner = BuyerContent.Signer;
				
			ИначеЕсли BuyerContent.Свойство("Signers") И ЗначениеЗаполнено(BuyerContent.Signers) Тогда
				
				Результат.BuyerSigner = BuyerContent.Signers[0].SignerDetails;
				
			КонецЕсли;
			
		КонецЕсли;
		
		МетодКлиента("Модуль_Клиент", "ЗаполнитьСтруктуруПоКонтенту", Document.GetSenderSignature()	  , Результат.SenderSignature);
		МетодКлиента("Модуль_Клиент", "ЗаполнитьСтруктуруПоКонтенту", Document.GetRecipientSignature(), Результат.RecipientSignature);
		
		Возврат Результат;
		
	КонецФункции	
	
	&НаКлиенте
	Функция СформироватьПечатнуюФормуПоДокументуДиадока(Document)
		
		Результат = Неопределено;
		
		ПротоСтруктура		 = МетодКлиента("Модуль_Клиент", "ПолучитьProto", Document, Ложь);
		ПротоСтруктураОтвета = МетодКлиента("Модуль_Клиент", "ПолучитьProto", Document, Истина);
		
		Если ПротоСтруктура.Свойство("Контент") Тогда
			
			ПроверитьПоказатьОшибкиВСтруктуреДокумента(ПротоСтруктура);
			
			ПротоКонтентОтвета = Неопределено;
			ПротоСтруктураОтвета.Свойство("Контент", ПротоКонтентОтвета);
			
			ИмяТипа = Document.TypeNamedId;
			
			ПротоКонтент		 = ПротоСтруктура.Контент;
			ТипКонтента			 = ПротоСтруктура.ТипКонтента;
			ТипДокумента		 = ИмяТипа;
			ДанныеШтампа		 = СформироватьДанныеОПодписях(Document, ПротоКонтент, ПротоКонтентОтвета);
			ПоказатьДопСведения	 = ВизуализироватьДопПоля(Document);
			
			ВыводитьНаименованиеТовараСКодами = МетодКлиента("Модуль_РаботаССерверомДиадок", "ТребуетсяВыводитьНаименованиеТовараСКодами", BoxID);
			
			ДопПараметры = Новый Структура;
			
			ДопПараметры.Вставить("ПротоКонтентОтвета"	, ПротоКонтентОтвета);
			ДопПараметры.Вставить("ДанныеШтампа"		, ДанныеШтампа);
			ДопПараметры.Вставить("ПоказатьДопСведения"	, ПоказатьДопСведения);
			
			ДопПараметры.Вставить("ВыводитьНаименованиеТовараСКодами", ВыводитьНаименованиеТовараСКодами);
			
			Результат = МетодСервераБезКонтекста("ПечатныеФормы", "ПечатнаяФормаПротоКонтента", ПротоКонтент, ТипКонтента, ТипДокумента, ДопПараметры);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
//} ФОРМИРОВАНИЕ ПЕЧАТНЫХ ФОРМ
////////////////////////////////////////////////////////////////////////////////
	
////////////////////////////////////////////////////////////////////////////////
//{ СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Функция ЭтоФормализованныйФормат(ФорматЭД)
	
	Результат = МетодКлиента("Модуль_Клиент", "ЭтоФормализованныйФормат", ФорматЭД);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция УказыватьОтсутствиеОснованияУПД()
	
	Результат = МетодСервераБезКонтекста(, "ПолучитьЗначениеСвойства", Организация, "ДиадокУказыватьОтсутствиеОснованияУПД",, Ложь);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ВключенРежимСовместимостиФорматов155и820()
	
	Результат = Объект.ПараметрыКлиентСервер.ПодключаемыйМодуль.РежимСовместимостиФорматов155и820;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ИспользоватьКонтурМаркировку()
	
	Результат = Объект.ПараметрыКлиентСервер.ИспользоватьСервисКонтурМаркировка;
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПротоконтентПоДокументу(СсылкаНаОбъект, ФорматОтправки
			, ФИОПодписанта = Неопределено
			, ДополнительныеСведения = Неопределено
			, НомерЗаказа = Неопределено)
	
	ДанныеСотрудника = МетодКлиента("Модуль_РаботаССерверомДиадок", "ДанныеКонтекстаДиадок", BoxId, "ДанныеСотрудника");
	
	ДополнительныеПараметры = МетодСервераБезКонтекста(, "ДополнительныеПараметрыПолученияКонтента");
	ДополнительныеПараметры.ИспользоватьСервисКонтурМаркировка = ИспользоватьКонтурМаркировку();
	ДополнительныеПараметры.РежимСовместимостиФорматов155и820 = ВключенРежимСовместимостиФорматов155и820();
	ДополнительныеПараметры.УказыватьОтсутствиеОснованияУПД = УказыватьОтсутствиеОснованияУПД();
	ДополнительныеПараметры.ДополнительныеСведенияСтрокой = ДополнительныеСведения;
	ДополнительныеПараметры.ФИОПодписанта = ФИОПодписанта;
	ДополнительныеПараметры.НомерЗаказа = НомерЗаказа;
	ДополнительныеПараметры.ПараметрыФормированияФайла = МетодСервераБезКонтекста("ГенерацияXML", "XML_ПараметрыФормированияФайла");
	ДополнительныеПараметры.ДанныеСотрудника = ДанныеСотрудника;
	
	Результат = МетодСервера(,
		"КонтентФормализованногоДокумента",
		СсылкаНаОбъект,
		ФорматОтправки,
		ДополнительныеПараметры
	);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция СтатусСогласованияДокумента(Document)
	
	Результат = Неопределено;
	
	Если Document <> Неопределено И 
		Document.ResolutionStatus <> Неопределено Тогда
		Результат = Document.ResolutionStatus.Type;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция СтатусАннулированияДокумента(Document)
	
	Результат = Неопределено;
	
	Если Document <> Неопределено Тогда
		Результат = Document.RevocationStatus;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция НужноОтразитьВУчете()
	
	Результат = Ложь;
	
	Если ЭтоВходящийДокумент()
		И Не СуществуетСвязьСДокументомУчета() Тогда
		
		ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
		Результат = ЭтоФормализованныйФормат(ФорматЭД);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

	&НаКлиенте
	Функция ТекущаяСтрокаТаблицыДокументов()
		
		Результат = Элементы.ТаблицаДокументов.ТекущиеДанные;
		
		Если Результат = Неопределено Тогда
			Результат = ТаблицаДокументов[0];
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПровестиВалидациюДокументовПакета()
		
		ФИОПодписанта = ФИОПодписанта();
		
		НомерДокумента= 0;
		ОбщееКоличествоДокументов= ТаблицаДокументов.Количество();
		_ВнешняяПечатнаяФорма = ВнешняяПечатнаяФорма;
		
		Для каждого СтрокаТаблицыДокументов из ТаблицаДокументов Цикл
			
			НомерДокумента= НомерДокумента + 1;
			
			Если ЗначениеЗаполнено(СтрокаТаблицыДокументов.ТекстОшибкиВалидации) Тогда
				СтрокаТаблицыДокументов.Вкл = Ложь;
				Продолжить;
			КонецЕсли;
			
			Если СтрокаТаблицыДокументов.ЭтоФайл Тогда
				Продолжить;
			КонецЕсли;
			
			ТекущиеОшибки = "";
			
			Парам_Документ1С	 = СтрокаТаблицыДокументов.Документ1С;
			ВнешняяПечатнаяФорма = СтрокаТаблицыДокументов.ВнешняяПечатнаяФорма;
			
			ФорматОтправки	 = СтрокаТаблицыДокументов.ФорматОтправки;
			ТипКонтента		 = ФорматОтправки.ТипКонтента;
			ИмяТипаДокумента = ФорматОтправки.ИмяТипа;
			
			Если ЭтоФормализованныйФормат(ФорматОтправки) Тогда
				
				Парам_ФункцияДокумента	= СтрокаТаблицыДокументов.ФункцияУПД;
				Парам_НомерЗаказа 		= СтрокаТаблицыДокументов.НомерЗаказа; 
				Парам_ДопСведения 		= СтрокаТаблицыДокументов.ДопСведения;
				
				ФорматОтправки = СтрокаТаблицыДокументов.ФорматОтправки;
				ТипКонтента = ФорматОтправки.ТипКонтента;
				
				DocumentContent = ПротоконтентПоДокументу(Парам_Документ1С, ФорматОтправки, ФИОПодписанта, Парам_ДопСведения, Парам_НомерЗаказа);
				ТекущиеОшибки	= ВалидироватьКонтентФормализованного(DocumentContent, ФорматОтправки, Парам_Документ1С);
				
			Иначе
				
				Попытка
					РезультатВизуализации = РезультатВизуализацииНеформализованного(ИмяТипаДокумента, Парам_Документ1С, Неопределено, ФИОПодписанта);
				Исключение
					ТекущиеОшибки = МетодКлиента("Модуль_Клиент", "ОформитьОшибкиВHTML", ОписаниеОшибки(), "Ошибка формирования печатной формы");
				КонецПопытки;
				
				Если ЗначениеЗаполнено(РезультатВизуализации) Тогда
					ТекущиеОшибки = РезультатВизуализации.СписокОшибок;
				КонецЕсли;

			КонецЕсли;
									
			СтрокаТаблицыДокументов.ТекстОшибкиВалидации= ТекущиеОшибки;
			
			//Сбрасываем галочки на невалидном документе
			СтрокаТаблицыДокументов.Вкл= (СтрокаТаблицыДокументов.Вкл = Истина) И (НЕ ЗначениеЗаполнено(ТекущиеОшибки));
						
		КонецЦикла;
		
		// Компенсируем недостатки архитектуры
		ВнешняяПечатнаяФорма = _ВнешняяПечатнаяФорма;
						
	КонецПроцедуры

	&НаКлиенте
	Функция ВалидироватьКонтентФормализованного(DocumentContent, ФорматОтправки, Парам_Документ1С, СообщитьОСтаромФормате = Ложь)
		
		БазовыеФорматы	= БазовыеФорматы();
		БазовыйФормат	= ФорматОтправки.БазовыйФормат;
				
		Если БазовыйФормат = БазовыеФорматы.utd820 Тогда
			
			ТекущиеОшибки = МетодСервераБезКонтекста(, "ВалидацияUTD820", DocumentContent);
			
		ИначеЕсли БазовыйФормат = БазовыеФорматы.utd Тогда
			
			ТекущиеОшибки = МетодСервераБезКонтекста(, "ВалидацияUTD", DocumentContent);
							
		ИначеЕсли БазовыйФормат = БазовыеФорматы.ucd736 Тогда
			
			ТекущиеОшибки = МетодСервераБезКонтекста(, "ВалидацияUCD736", DocumentContent);
			
		ИначеЕсли БазовыйФормат = БазовыеФорматы.ucd Тогда
			
			ТекущиеОшибки = МетодСервераБезКонтекста(, "ВалидацияUCD", DocumentContent);
					
		ИначеЕсли БазовыйФормат = БазовыеФорматы.torg12 Тогда
			
			ТекущиеОшибки = МетодСервераБезКонтекста(, "ВалидацияXmlTorg12Content", DocumentContent);
			
		ИначеЕсли БазовыйФормат = БазовыеФорматы.invoice Тогда
			
			ТекущиеОшибки = МетодСервераБезКонтекста(, "ВалидацияInvoiceContent", DocumentContent, Парам_Документ1С);
			
		ИначеЕсли БазовыйФормат = БазовыеФорматы.invoicecor Тогда
			
			ТекущиеОшибки =	МетодСервераБезКонтекста(, "ВалидацияInvoiceCorrectionContent", DocumentContent, Парам_Документ1С);
			
		ИначеЕсли БазовыйФормат = БазовыеФорматы.act Тогда
			
			ТекущиеОшибки = МетодСервераБезКонтекста(, "ВалидацияXmlAcceptanceCertificateContent", DocumentContent);
			
		Иначе
			
			ТекущиеОшибки = "";
			
		КонецЕсли;
		
		Если СообщитьОСтаромФормате 
			И ДокументВУстаревшемФормате(ФорматОтправки) Тогда
			HTMLТекст = "<h3> <a href=""УВЕДОМЛЕНИЕОСМЕНЕФОРМАТА""> Подробнее... </a></h3>";
			ОшибкиФормата = МетодКлиента("Модуль_Клиент", "ОформитьОшибкиВHTML", HTMLТекст, "Документ в устаревшем формате.");
		КонецЕсли;
		
		Возврат ТекущиеОшибки;
		
	КонецФункции
	
	&НаКлиенте
	Функция ДокументВУстаревшемФормате(ФорматОтправки, Знач ДатаОтправки = Неопределено)
		
		Результат = МетодКлиента("Модуль_Клиент", "ДокументВУстаревшемФормате"
			, ФорматОтправки
			, ДатаОтправки);
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПроверитьПоказатьОшибкиВСтруктуреДокумента(ПротоСтруктура)
		
		Ошибки = МетодСервераБезКонтекста(,"ВалидацияСтруктурыДокумента", ПротоСтруктура);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбновитьДокумент1С()
		
		ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
		
		ОграничитьРезультатСчетФактурой = ФорматЭД.ЕстьФункцияСФ;
		
		Документ1С = ПолучитьDocumentID_2_Документ(ЭДОбъект.DocumentID, ЭДОбъект.OrganizationID, ОграничитьРезультатСчетФактурой, ЭДОбъект.Direction);

	КонецПроцедуры
	
	&НаСервере
	Функция ПолучитьDocumentID_2_Документ(DocumentID, BoxID, ОграничитьРезультатСчетФактурой, DocumentDirection)
		
		Возврат МетодСервера(,"DocumentID_2_Документ", DocumentID, BoxID, ОграничитьРезультатСчетФактурой, DocumentDirection);

	КонецФункции
	
	&НаСервере
	Функция СопоставленныеУчетныеДокументы(ИдентификаторДокумента, ИдентификаторЯщика)
		
		Результат = МетодСервера(, "DocumentID_2_МассивДокументов"
			, ИдентификаторДокумента
			, ИдентификаторЯщика);
		
		Возврат Результат;
		
	КонецФункции
	
	// Удаляет связь учетных документов с электронным документом.
	//
	// Параметры:
	//  ДокументИлиМассив	 - ДокументСсылка, Массив - документы учета, которые нужно сопоставить с ЭД;
	//  Идентификатор		 - Структура	 - идентификатор электронного документа, с которым нужно сопоставить учетные документы;
	//                         Неопределено	 - учетные документы будут отвязаны от электронного документа;
	//
	&НаСервере
	Процедура РазорватьСвязьСЭлектроннымДокументом(ДокументИлиМассив)
		
		Если ТипЗнч(ДокументИлиМассив) = Тип("Массив") Тогда 
			ДокументыДляОбработки = ДокументИлиМассив;
		Иначе 
			ДокументыДляОбработки = Новый Массив;
			ДокументыДляОбработки.Добавить(ДокументИлиМассив);
		КонецЕсли;
		
		Для Каждого ДокументУчета Из ДокументыДляОбработки Цикл 
			
			МетодСервера(, "Установить_DocumentID_Для_Документ"
				, ДокументУчета
				, ""
				, "");
			
			МетодСервера(, "ОчиститьCustomDocumentId", ДокументУчета);
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура ОтменитьСопоставлениеДокументовНаСервере(ИдентификаторДокумента, ИдентификаторЯщика)
		
		ДокументыУчета = СопоставленныеУчетныеДокументы(ИдентификаторДокумента, ИдентификаторЯщика);
		
		НачатьТранзакцию();
		
		Попытка
			
			РазорватьСвязьСЭлектроннымДокументом(ДокументыУчета);
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			СобытиеЖР = "Диадок";
			
			КомментарийЖР = СтрШаблон("Не удалось отменить сопоставление электронного документа с документами информационной базы:
				|DocId = %1; BoxId = %2;
				|Причина ошибки:
				|%3"
				, ИдентификаторДокумента
				, ИдентификаторЯщика
				, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖР
				, УровеньЖурналаРегистрации.Ошибка
				,
				,
				, КомментарийЖР);
				
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СообщениеОбОшибкеДиадок(ТекстОшибки)
		
		ЗаголовокСообщения = ПредставлениеСервиса();
		ТекстСообщения = МодульКлиент_ТекстОшибкиДиадок(ТекстОшибки);
		Таймаут = 120;
		
		ПоказатьПредупреждение(, ТекстСообщения, Таймаут, ЗаголовокСообщения);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбновитьЭДОбъект()
		
		ЭДОбъект= ЭДОбъект.Organization.GetDocumentById(ЭДОбъект.DocumentId);
		
		ПросмотрФормы();
		
		ОбновитьДокумент1С();
		
		ПараметрыОповещения=	Новый Структура;
		ПараметрыОповещения.Вставить("BoxID", ЭДОбъект.OrganizationID);
		ПараметрыОповещения.Вставить("DocumentID", ЭДОбъект.DocumentID);
		
		МетодКлиента(,"ОповеститьФормы", "ИзменениеСтатусаДокументаДиадок", ПараметрыОповещения, ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗакэшироватьРезультатВизуализации(ТабличныйДокумент, Ошибки, ОшибкиФормата)
		
		Если ЗначениеЗаполнено(IDТекущейСтроки) Тогда
			МассивСтрок= ТаблицаДокументов.НайтиСтроки(Новый Структура("IdСтроки", IDТекущейСтроки));
			Если МассивСтрок.Количество()>0 Тогда
				ТекущаяСтрока= МассивСтрок[0];
				ТекущаяСтрока.ТекстОшибкиВалидации= Ошибки;
				ТекущаяСтрока.ТекстОшибкиФормата= ОшибкиФормата;
			КонецЕсли;
						
			КэшТабличныеДокументы.Вставить(IDТекущейСтроки, ТабличныйДокумент);
					
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура УдалитьТабличныйДокументИзКэша(IdСтроки)
		
		Если НЕ КэшТабличныеДокументы.Получить(IdСтроки) = Неопределено Тогда
			КэшТабличныеДокументы.Удалить(IdСтроки);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ВывестиОшибкуОтправкиНаФорму()
		
		Ошибки = ТекстПолнойОшибки();
				
		НастроитьЭлементыФормы();
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ТекстПолнойОшибки()
		
		Результат	 = "";
		Разделитель	 = "";
		
		Если ЗначениеЗаполнено(Ошибки) Тогда
			Результат	 = Результат + Разделитель + Ошибки;
			Разделитель	 = Символы.ПС;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОшибкиОтправки) Тогда
			Результат	 = Результат + Разделитель + ОшибкиОтправки;
			Разделитель	 = Символы.ПС;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОшибкиФормата) Тогда
			Результат	 = Результат + Разделитель + ОшибкиФормата;
			Разделитель	 = Символы.ПС;
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ВалидироватьДанныеФайла(DocumentType, ДанныеФайла);
		
		СписокОшибок = "";
		
		Если Не ЗначениеЗаполнено(ДанныеФайла) Тогда // Нечего валидировать
			Возврат СписокОшибок;
		КонецЕсли;
		
		ПроверяемыеСвойства = Новый Структура; // ключ - имя поля, значение - представление
		
		Если Ложь
			Или DocumentType = "AcceptanceCertificate"
			Или DocumentType = "Torg12"
			Или DocumentType = "Torg13"
			Или DocumentType = "ReconciliationAct"
			Или DocumentType = "ProformaInvoice"
			Или DocumentType = "SupplementaryAgreement"
			Или DocumentType = "Contract" Тогда
			
			ПроверяемыеСвойства.Вставить("DocumentNumber"	, "Номер документа"	);
			ПроверяемыеСвойства.Вставить("DocumentDate"		, "Дата документа"	);
			
		КонецЕсли;
		
		Если DocumentType = "AcceptanceCertificate"
			Или DocumentType = "Torg12"
			Или DocumentType = "Torg13"
			Или DocumentType = "ProformaInvoice" Тогда
			ПроверяемыеСвойства.Вставить("Total", "Сумма с НДС");
		КонецЕсли;
		
		Если DocumentType = "Contract" И НЕ ДанныеФайла.ЦенаНеУказана Тогда
			ПроверяемыеСвойства.Вставить("ContractPrice", "Цена");
		КонецЕсли;
		
		Для Каждого КлючИЗначение Из ПроверяемыеСвойства Цикл 
			
			ИмяСвойства = КлючИЗначение.Ключ;
			ПредставлениеСвойства = КлючИЗначение.Значение;
			
			Если Не ДанныеФайла.Свойство(ИмяСвойства) Или Не ЗначениеЗаполнено(ДанныеФайла[ИмяСвойства]) Тогда 
				СписокОшибок = СписокОшибок + "<p> - " + ПредставлениеСвойства + "</p>";
			КонецЕсли;
			
		КонецЦикла;
		
		Возврат СписокОшибок;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОбработатьИзменениеОрганизации()
		
		BoxID = МетодСервера(, "Организация_2_BoxID", Организация);
		
		Если ЗначениеЗаполнено(BoxID) Тогда
			Organization	= Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(BoxID);
			UserPermissions	= Organization.GetUserPermissions();
		Иначе
			Organization	= Неопределено;
			UserPermissions	= Неопределено;
		КонецЕсли;
							
		Если ЗначениеЗаполнено(Контрагент)
			 И UserPermissions <> Неопределено
			 И (	ПоказатьКнопкуПередачиНаСогласование(UserPermissions) 
				ИЛИ ПоказатьКнопкуПередачиНаПодпись(UserPermissions)) Тогда
				
			Элементы.ГруппаПередачаНаСогласование.Видимость		= Истина;
			Элементы.ГруппаПередачаНаСогласование.Доступность	= Истина;
		Иначе
			// только для операции "СоздатьНовыйПакет"
			Элементы.ГруппаПередачаНаСогласование.Видимость		= Истина;
			Элементы.ГруппаПередачаНаСогласование.Доступность	= Ложь;
		КонецЕсли;
		
	КонецПроцедуры //ОбработатьИзменениеОрганизации()
	
	&НаКлиенте
	Функция СтрокаПозиционированияВТаблицеДокументов()
		
		МассивТекущаяСтрока = ТаблицаДокументов.НайтиСтроки(Новый Структура("ТекущиеДанные", Истина));
		
		Возврат МассивТекущаяСтрока[0];
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьПеременныеФормыПриОткрытии(IdСтроки)
	
		Organization = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(BoxID);
		
		IDТекущейСтроки = IdСтроки;
		
	КонецПроцедуры //ЗаполнитьПеременныеФормыПриОткрытии()
	
	&НаКлиенте
	Процедура ЗаполнитьРеквизитыФормыТекущимиДаннымТаблицыДокументов(ТекущиеДанные)
		
		Если  ТекущиеДанные.ЭтоФайл
			И ТекущиеДанные.DocumentType = DocumentType Тогда
			
			Возврат;
			
		КонецЕсли;
		
		DocumentType			= ТекущиеДанные.DocumentType;
		Документ1С				= ТекущиеДанные.Документ1С;
		ТипДокумента			= ТекущиеДанные.ТипДокумента;
		ДопСведения				= ТекущиеДанные.ДопСведения;
		ВнешняяПечатнаяФорма 	= ?(ТекущиеДанные.ЭтоФайл, Неопределено, ТекущиеДанные.ВнешняяПечатнаяФорма);
		ФункцияУПД				= ТекущиеДанные.ФункцияУПД;
		
	КонецПроцедуры //ЗаполнитьРеквизитыФормыТекущимиДаннымТаблицыДокументов()
	
//} СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ ТЕЛО МОДУЛЯ

	&НаКлиенте
	Процедура СоздатьДокумент(Режим) Экспорт
		
		ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
		БазовыйФормат = ФорматЭД.БазовыйФормат;
		ИмяТипа = ФорматЭД.ИмяТипа;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ФорматДокумента", ФорматЭД);
		ПараметрыФормы.Вставить("BoxID", 				ЭДОбъект.OrganizationID);
		ПараметрыФормы.Вставить("DocumentID", 			ЭДОбъект.DocumentID);
		ПараметрыФормы.Вставить("Direction", 			ЭДОбъект.Direction);
		ПараметрыФормы.Вставить("CounteragentBoxID", 	ЭДОбъект.Counteragent.ID);
		
		ПараметрыФормы.Вставить("Контрагент", 			Контрагент);
		ПараметрыФормы.Вставить("Организация", 			Организация);
		
		Если Режим = "Ввод" Тогда
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВводаНакладной", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВводаНакладной");

		ИначеЕсли Режим = "Сопоставление" Тогда
			
			Если БазовыйФормат = БазовыеФорматы().ucd
				ИЛИ БазовыйФормат = БазовыеФорматы().ucd736 Тогда
				
				ПоказатьПредупреждение( , "Сопоставление УКД не предусмотрено", 120, Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемы);
				Возврат;
				
			КонецЕсли;
			
			ПараметрыФормы.Вставить("DocumentDate", 	ЭДОбъект.DocumentDate);
			ПараметрыФормы.Вставить("DocumentNumber", 	ЭДОбъект.DocumentNumber);
			ПараметрыФормы.Вставить("Режим", 			Режим);
			Если ЭДОбъект.Direction = НаправлениеИсходящее() И Найти(ИмяТипа, "Invoice") = 0 Тогда 
				ПараметрыФормы.Вставить("СкрытьВозможностьСоздаватьДокумент", Истина);
			КонецЕсли;
			
			Если ИмяТипа = "InvoiceCorrection" ИЛИ ИмяТипа = "InvoiceCorrectionRevision" Тогда
				ПараметрыФормы.Вставить("TotalInc",		ЭДОбъект.TotalInc);
				ПараметрыФормы.Вставить("TotalDec",		ЭДОбъект.TotalDec);
			Иначе
				ПараметрыФормы.Вставить("Total",		ЭДОбъект.Total);
			КонецЕсли;
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыбораДокумента", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВыбораДокумента");
			
		ИначеЕсли Режим = "ВводСВыборомОснований" Тогда
			
			ПараметрыФормы.Вставить("DocumentDate", 	ЭДОбъект.DocumentDate);
			ПараметрыФормы.Вставить("DocumentNumber", 	ЭДОбъект.DocumentNumber);
			ПараметрыФормы.Вставить("Режим", 			Режим);
						
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыбораДокумента", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВыбораДокументаСОснованием");

		ИначеЕсли Режим = "ВводУПД" Тогда
			
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВводаУПД", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВводаУПД");

		КонецЕсли;
		
	КонецПроцедуры
	
//} ТЕЛО МОДУЛЯ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ ОБРАБОТКА СОБЫТИЙ

	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОшибокВалидации(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		ПровестиВалидациюДокументовПакета();
		ПодключитьОбработчикОжидания("ПросмотрФормы", 0.1, Истина);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВводаНакладной(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если РезультатЗакрытия <> Неопределено Тогда
			
			Если РезультатЗакрытия.Свойство("Документ1С") Тогда
				
				ОбработчикСозданиеДокумента(РезультатЗакрытия.Документ1С);
				
			ИначеЕсли РезультатЗакрытия.Свойство("Режим") Тогда
				
				СоздатьДокумент(РезультатЗакрытия.Режим);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВыбораДокумента(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			Если ЗначениеЗаполнено(РезультатЗакрытия.Документ1С) Тогда
				ОбработчикСозданиеДокумента(РезультатЗакрытия.Документ1С);
			Иначе
				СоздатьДокумент(РезультатЗакрытия.Режим);
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВыбораДокументаСОснованием(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если РезультатЗакрытия <> Неопределено Тогда
			Если РезультатЗакрытия.СписокСсылокРНК.Количество() > 0 Тогда
				МетодКлиента("Модуль_ЛогикаПоведениеФорм","СоздатьНовыйСчетФактуру", ЭтаФорма, Контрагент, Организация, ЭДОбъект, РезультатЗакрытия.СписокСсылокРНК.ВыгрузитьЗначения());
			Иначе
				СоздатьДокумент(РезультатЗакрытия.Режим);
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВводаУПД(ПараметрыФормы, ДополнительныеПараметры) Экспорт
		
		Если НЕ ПараметрыФормы = Неопределено Тогда
			Если ЗначениеЗаполнено(ПараметрыФормы.Документ1С) Тогда
				ОбновитьДокумент1С();
				ПросмотрФормы();
			Иначе
				СоздатьДокумент(ПараметрыФормы.Режим);
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикЗакрытиеФормыСФ(РезультатЗакрытия, ДокументОбъект) Экспорт
		
		Если ЗначениеЗаполнено(ДокументОбъект.Ссылка) Тогда
			ОбработчикСозданиеДокумента(ДокументОбъект.Ссылка);
		КонецЕсли;
		
		МассивСсылокРНК=	Новый Массив;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикСозданиеДокумента(ДокументССылка) Экспорт
		
		Если ЗначениеЗаполнено(ДокументССылка) Тогда
			МетодКлиента("Модуль_Клиент","ОбработчикСозданиеДокумента", ДокументССылка, ЭДОбъект);
			ОбновитьДокумент1С();
			ПросмотрФормы();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВыбораПодразделенияОрганизации(РезультатЗакрытия, ТекущийDepartmentId) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			Если НЕ РезультатЗакрытия.DepartmentID = ТекущийDepartmentId Тогда
				
				ЭДОбъект.Move(РезультатЗакрытия.DepartmentID);
				
				ОбновитьЭДОбъект();
				
				ПараметрыОповещения=	Новый Структура;
				ПараметрыОповещения.Вставить("BoxID", 			ЭДОбъект.OrganizationID);
				ПараметрыОповещения.Вставить("DocumentId", 		ЭДОбъект.DocumentID);
				ПараметрыОповещения.Вставить("DepartmentName", 	?(ЭДОбъект.Department = Неопределено, "", ЭДОбъект.Department.Name));
				
				МетодКлиента(,"ОповеститьФормы", "ИзменениеПодразделения", ПараметрыОповещения);
			КонецЕсли;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыВыбораПолучателя(ПараметрыОтправкиНаСогласование, НаборДанных) Экспорт
		
		Если ПараметрыОтправкиНаСогласование = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ЭтоНовыйДокумент = (ЭДОбъект = Неопределено);
		
		Если ЭтоНовыйДокумент Тогда
			
			ОтправкаПрошлаУспешно = ВыполнитьОтправку(ПараметрыОтправкиНаСогласование);
			Если ОтправкаПрошлаУспешно Тогда
				Закрыть(ТаблицаДокументов);
			Иначе
				ВывестиОшибкуОтправкиНаФорму();
			КонецЕсли;
			
		Иначе
			МетодКлиента("Модуль_Клиент", "ОтправитьНаОбработку", ЭДОбъект, ПараметрыОтправкиНаСогласование);
			ОбновитьЭДОбъект();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаОтказаВПодписи(РезультатЗакрытия, ТекущийDepartmentId) Экспорт
		
		Если РезультатЗакрытия <> Неопределено Тогда
			
			Попытка
				МетодКлиента("Модуль_Клиент", "ОтказатьВПодписи", ЭДОбъект, РезультатЗакрытия.Комментарий);
			Исключение
				ОписаниеОшибки = ОписаниеОшибки();
			КонецПопытки;
			
			Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
				СообщениеОбОшибкеДиадок(ОписаниеОшибки);
			Иначе
				Элементы.ГруппаКнопокПодписания.Видимость = Истина;
				ОбновитьЭДОбъект();
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаСогласовать(РезультатЗакрытия, ТекущийDepartmentId) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			Попытка 
				ЭДОбъект.Approve(РезультатЗакрытия.Комментарий);
			Исключение
				ТекстОшибкиПодписания=	ОписаниеОшибки();
				Если НЕ Найти(ТекстОшибкиПодписания, "Duplicate resolution") = 0 Тогда
					ПоказатьПредупреждение(, "Повторное согласование не возможно", 120, ПредставлениеСервиса());
				ИначеЕсли НЕ Найти(ТекстОшибкиПодписания, "User cannot add resolution") = 0 Тогда
					ПоказатьПредупреждение(, "Вы не можете согласовать документ", 120, ПредставлениеСервиса());
				КонецЕсли;
				СообщениеОбОшибкеДиадок(ТекстОшибкиПодписания);
				Возврат;
			КонецПопытки;
			ОбновитьЭДОбъект();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаОтказатьВСогласовании(РезультатЗакрытия, ТекущийDepartmentId) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			Попытка
				ЭДОбъект.DisApprove(РезультатЗакрытия.Комментарий);
				ОбновитьЭДОбъект();
			Исключение
				ТекстОшибкиПодписания=	ОписаниеОшибки();
				Если НЕ Найти(ТекстОшибкиПодписания, "Duplicate resolution") = 0 Тогда
					ПоказатьПредупреждение(, "Повторный отказ в согласовании не возможен", 120, ПредставлениеСервиса());
					Возврат;
				ИначеЕсли НЕ Найти(ТекстОшибкиПодписания, "User cannot add resolution") = 0 Тогда
					ПоказатьПредупреждение(, "Вы не можете отказать в согласовании", 120, ПредставлениеСервиса());
					Возврат;
				КонецЕсли;
				СообщениеОбОшибкеДиадок(ТекстОшибкиПодписания);
			КонецПопытки;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикУдалитьДокумент(РезультатВопроса, ДополнительныеПараметры) Экспорт
		
		Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
			
			ИдентификаторДокумента = ЭДОбъект.DocumentId;
			ИдентификаторЯщика = ЭДОбъект.OrganizationID;
			
			ПараметрУдаляемогоДокумента = Новый Структура();
			ПараметрУдаляемогоДокумента.Вставить("DocumentId", ИдентификаторДокумента);
			ПараметрУдаляемогоДокумента.Вставить("BoxID", ИдентификаторЯщика);
			
			МассивДокументов = Новый Массив();
			МассивДокументов.Добавить(ПараметрУдаляемогоДокумента);
			
			Если ЗначениеЗаполнено(Документ1С) Тогда
				
				ОтменитьСопоставлениеДокументовНаСервере(ИдентификаторДокумента, ИдентификаторЯщика);
				
			КонецЕсли;
			
			Если НЕ ЭДОбъект.IsDeleted Тогда
				Попытка
					ЭДОбъект.Delete();
					ПоказатьПредупреждение(, "Документ " + МетодКлиента("Модуль_Клиент","ПредставлениеЭД", ЭДОбъект) + " перемещен в удаленные.", 120, ПредставлениеСервиса());
				Исключение
					
					ОписаниеОшибки=	ОписаниеОшибки();
					ТекстОшибки=	ОписаниеОшибки;
					Если Найти(ТекстОшибки, "is already delete") Тогда
						ТекстОшибки=	"Документ " + МетодКлиента("Модуль_Клиент","ПредставлениеЭД", ЭДОбъект) + " уже был удален.";
					Иначе
						ТекстОшибки=	"Ошибка удаления документа";
					КонецЕсли;
					
					ПараметрыФормы=	Новый Структура();
					ПараметрыФормы.Вставить("Заголовок", 		"Ошибка удаления");
					ПараметрыФормы.Вставить("ОписаниеОшибки", 	ТекстОшибки);
					ПараметрыФормы.Вставить("Подробности", 		ОписаниеОшибки);
					
					МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма);
					
				КонецПопытки;
				
			КонецЕсли;
			
			ПросмотрФормы();
			МетодКлиента(,"ОповеститьФормы","УдалениеДокументов", МассивДокументов);
		КонецЕсли;

	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОтменитьСопоставление(РезультатВопроса, ДополнительныеПараметры) Экспорт
		
		Если РезультатВопроса <> КодвозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
		ИдентификаторЯщика = ЭДОбъект.OrganizationID;
		ИдентификаторДокумента = ЭДОбъект.DocumentID;
		ОтменитьСопоставлениеДокументовНаСервере(ИдентификаторДокумента, ИдентификаторЯщика);
		
		Документ1С = Неопределено;
		
		ОбновитьДокумент1С();
		ПросмотрФормы();
		
		ДобавитьСтатистику_ОтменитьСопоставление();
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ТипСущности", "Документ");
		ПараметрыОповещения.Вставить("BoxID", ИдентификаторЯщика);
		ПараметрыОповещения.Вставить("DocumentID", ИдентификаторДокумента);
		ПараметрыОповещения.Вставить("Документ1С", Документ1С);
		
		МетодКлиента(, "ОповеститьФормы"
			,"ИзменениеСвязиДД1С"
			, ПараметрыОповещения
			, ЭтаФорма);
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыПодписанияДокумента(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если РезультатЗакрытия <> Неопределено Тогда
			
			Попытка
				МетодКлиента("Модуль_Клиент", "ПодписатьВДиадоке", ЭДОбъект, РезультатЗакрытия.ОтветныйТитул);
			Исключение
				ОписаниеОшибки = ОписаниеОшибки();
				СообщениеОбОшибкеДиадок(ОписаниеОшибки);
			КонецПопытки;
			
			Если НЕ ЗначениеЗаполнено(ОписаниеОшибки) Тогда
				Элементы.ГруппаКнопокПодписания.Видимость = Ложь;
				ОбновитьЭДОбъект();
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаЗапросАннулирования(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			
			ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
			ЭтоФормализованныйДокумент = ЭтоФормализованныйФормат(ФорматЭД);
			
			Попытка
				
				МетодКлиента("Модуль_Клиент", "ПроверитьСертификат", ЭДОбъект.OrganizationID, ЭтоФормализованныйДокумент);
				
				ЭДОбъект.SendRevocationRequest(РезультатЗакрытия.Комментарий);
				
				ОбновитьЭДОбъект();
				
			Исключение
				СообщениеОбОшибкеДиадок(ОписаниеОшибки());
			КонецПопытки;
			
		КонецЕсли;
		
		Если ДополнительныеПараметры = Истина Тогда
			ПереотправитьДокумент();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаАннулироватьДокумент(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если РезультатЗакрытия <> КодВозвратаДиалога.ОК Тогда
			Возврат;
		КонецЕсли;
					
		Попытка
			МетодКлиента("Модуль_Клиент", "АннулироватьДокумент", ЭДОбъект);
			ОбновитьЭДОбъект();
		Исключение 
			СообщениеОбОшибкеДиадок(ОписаниеОшибки());
		КонецПопытки;
			
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаОтказатьВАннулировании(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
					
			Попытка
				МетодКлиента("Модуль_Клиент", "ОтказатьВАннулированииДокумента", ЭДОбъект, РезультатЗакрытия.Комментарий);
				ОбновитьЭДОбъект();
			Исключение
				СообщениеОбОшибкеДиадок(ОписаниеОшибки());
			КонецПопытки;
			
		КонецЕсли;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыНастройкиПодписи(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
						
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
			
			ИмяФормыПодписи = "";
			ДополнительныеПараметры.Свойство("ИмяФормыПодписи", ИмяФормыПодписи);
			
			Если ЗначениеЗаполнено(ИмяФормыПодписи) 
				И ИмяФормыПодписи = "НастройкаПодписиУПД"
				И ТипЗнч(РезультатЗакрытия) = Тип("Структура") //были сохранены изменения
				И ЗначениеЗаполнено(РезультатЗакрытия) Тогда
				
				Ошибки 			= "";
				ОшибкиОтправки 	= "";
			КонецЕсли;
			
		КонецЕсли;
				
		ПодключитьОбработчикОжидания("ПросмотрФормы", 0.1, Истина);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыОтветаЗапроситьУточнение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если НЕ РезультатЗакрытия = Неопределено Тогда
			
			Попытка
				
				МетодКлиента("Модуль_Клиент", "ЗапроситьУточнение", ЭДОбъект, РезультатЗакрытия.Комментарий);
				
				ОбновитьЭДОбъект();
				
			Исключение
				СообщениеОбОшибкеДиадок(ОписаниеОшибки());
			КонецПопытки;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ВыполнитьОтправкуДокумента(ПодтверждениеОтправки, РежимОтправки) Экспорт
		
		Если ПодтверждениеОтправки <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
		Если РежимОтправки = "ПодписатьИОтправить" Тогда
			
			ОтправкаПрошлаУспешно = ВыполнитьОтправку();
			
			Если ОтправкаПрошлаУспешно = Истина Тогда
				
				ТекстОповещения = НСтр("ru = 'Документы успешно отправлены'");
				МетодКлиента("Модуль_Клиент", "ОповеститьПользователя", ТекстОповещения, DocumentID);
				
				МетодКлиента(, "ОповеститьФормы", "ДиадокЗакрытьФормуДокумента", DocumentID);
				
			Иначе
				ВывестиОшибкуОтправкиНаФорму();
			КонецЕсли;
			
		ИначеЕсли РежимОтправки = "ПередатьНаСогласование" Тогда
			
			МетодКлиента("Модуль_Клиент", "ВыбратьПараметрыПолучателяПриОтправкеНаСогласование", ЭтаФорма, Организация, "ApprovementRequest");
			
		ИначеЕсли РежимОтправки = "ПередатьНаПодписание" Тогда
			
			МетодКлиента("Модуль_Клиент", "ВыбратьПараметрыПолучателяПриОтправкеНаСогласование", ЭтаФорма, Организация, "SignatureRequest");
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОповещенияВопросВыбраныНеВсеФайлы(РезультатВопроса, ДополнительныеПараметры) Экспорт
		
		Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
			ПередатьФайлыДляДобавленияВПакеты();	
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОтветаВопросаЗапросаАннулирования(РезультатВопроса, ДополнительныеПараметры) Экспорт 
		
		Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
			ПереотправитьДокумент();
			Возврат;
		КонецЕсли;
		
		ЗапроситьАннулированиеДокумента(Истина);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикВыбораКаталогаСохранения(ПараметрыСохранения, ДополнительныеПараметры) Экспорт
		
		Если ПараметрыСохранения <> Неопределено Тогда
			
			ДополнительныеПараметры.Каталог = ПараметрыСохранения.Каталог;
			СформироватьФайлДокумента(ДополнительныеПараметры);
			
		КонецЕсли;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыНастройкиПодписи_СохранениеXML(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если РезультатЗакрытия <> Неопределено Тогда
			
			ДополнительныеПараметры.Вставить("ПолномочияПодписанта", РезультатЗакрытия);
			ПоказатьДиалогВыбораКаталогаСохраненияXML(ДополнительныеПараметры);
			
		КонецЕсли;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ПоказатьДиалогВыбораКаталогаСохраненияXML(ПараметрыСохранения)
		
		МетодКлиента(, "ОткрытьФормуОбработкиМодально"
			, "ФормаВыбораКаталогаСохранения"
			,
			, ЭтаФорма
			, "ОбработчикВыбораКаталогаСохранения"
			, ПараметрыСохранения);
		
	КонецПроцедуры
	
//}	ОБРАБОТКА СОБЫТИЙ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ УПРАВЛЕНИЕ ФОРМОЙ

	&НаСервере
	Процедура ПрочитатьПараметрыФормы()
		
		Параметры.Свойство("ТочкаВызова", ТочкаВызова);
		Параметры.Свойство("Операция"	, Операция);
		Параметры.Свойство("Организация", Организация);
		Параметры.Свойство("Контрагент"	, Контрагент);
		
		Параметры.Свойство("BoxID", BoxID);
		Параметры.Свойство("DepartmentKpp", DepartmentKpp);
		Параметры.Свойство("CounteragentBoxID", CounteragentBoxID);
		Параметры.Свойство("КаталогВыбораФайлов", КаталогВыбораФайлов);
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ДекорацияПодробноНажатие(Элемент)
		
		HTMLДокумент= МетодКлиента("Модуль_Клиент","СформироватьHTMLПредставлениеРезолюций", ЭДОбъект, Организация);
		
		ПараметрыФормы=	Новый Структура();
		ПараметрыФормы.Вставить("HTMLДокумент",	HTMLДокумент);
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаВыводаHTMLДокумента", ПараметрыФормы, ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СтатусДокументаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
		
		Если НавигационнаяСсылкаФорматированнойСтроки = "RoamingNotificationStatusError" Тогда
			
			СтандартнаяОбработка= Ложь;
			
			Если НРег(Лев(ПредставлениеСтатусаРоуминг, 6)) = "ошибка" Тогда
				
				ПараметрыФормы= Новый Структура("Заголовок, ОписаниеОшибки, Подробности",
				ПредставлениеСтатусаРоуминг,
				ПредставлениеСтатусаРоуминг,
				ПредставлениеСтатусаРоумингДетали);
				
				МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма);
				
			Иначе
				ОбновитьСтатусЭД();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
		
	&НаСервере
	Процедура НастроитьКнопкиКоманднойПанелиНаСервере()
		
		Команды.ПерейтиВДиадок.Подсказка= СтрЗаменить("Перейти в %НаименованиеСистемы%"
			, "%НаименованиеСистемы%"
			, МетодСервера(,"ПолучитьСловарь").НаименованиеСистемы);
		
	КонецПроцедуры
	
	&НаСервере
	Процедура ЗаполнитьТаблицуДокументовПоДаннымВыбранныхФайлов(МассивАдресовФайлов)
		
		ТекущиеДанныеУказаны= Ложь;
		Для каждого ПутьКФайлу из МассивАдресовФайлов Цикл
			
			НоваяСтрока= ТаблицаДокументов.Добавить();
			
			НоваяСтрока.Вкл= 			Истина;
			
			НоваяСтрока.IdСтроки= 		Новый УникальныйИдентификатор;
			НоваяСтрока.DocumentType= 	"Nonformalized";
			
			Если ТекущиеДанныеУказаны Тогда
				НоваяСтрока.ТекущиеДанные= 	Ложь;
			Иначе
				НоваяСтрока.ТекущиеДанные= 	Истина;
				ТекущиеДанныеУказаны= 		Истина;
			КонецЕсли;
			
			НоваяСтрока.ЭтоФайл= 	Истина;
			СвойстваФайла= Новый_СвойстваФайла();
			СвойстваФайла.ПолноеИмя= ПутьКФайлу;
			НоваяСтрока.СвойстваФайла= СвойстваФайла;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	// Производит заполнение полей таблицы документов данными по умолчанию
	//
	&НаКлиенте
	Процедура ЗаполнитьТаблицуДокументовДаннымиПоУмолчанию()

		Для каждого ЭлементТаблицыДокументов из ТаблицаДокументов Цикл
				
			ФайлДанных = Новый Файл(ЭлементТаблицыДокументов.СвойстваФайла.ПолноеИмя);
			
			ЭлементТаблицыДокументов.Представление  = "Неформализованный";
			ЭлементТаблицыДокументов.DocumentType   = "Nonformalized"; //по умолчанию новый файл имеет тип "Nonformalized" 
			ЭлементТаблицыДокументов.ФорматОтправки = МетодКлиента("Модуль_Выгрузка", "ОписаниеФорматаДляОтправкиФайла", ЭлементТаблицыДокументов.DocumentType);
			
			ЭлементТаблицыДокументов.ДанныеФайла    = МетодКлиента("Модуль_Клиент", "Новый_NonformalizedDocumentToSend");
			ЗаполнитьСвойстваФайла(ЭлементТаблицыДокументов.СвойстваФайла, ФайлДанных);
										
		КонецЦикла;	

	КонецПроцедуры // ЗаполнитьТаблицуДокументовДаннымиПоУмолчанию()
	
	&НаСервере
	Процедура ЗаполнитьТаблицуДокументов(ТочкаВызова, МассивДокументовПакета, ТекущиеДанные)
		
		ВПакетеОдинДокумент = МассивДокументовПакета.Количество()=1;
		
		Для каждого СтрокаМассива из МассивДокументовПакета Цикл
			
			ПредставлениеДокумента = СформироватьПредставлениеДокумента(СтрокаМассива);
			
			НоваяСтрока = ТаблицаДокументов.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМассива);
			
			НоваяСтрока.Вкл = ВПакетеОдинДокумент Или СтрокаМассива.Вкл;
			НоваяСтрока.Представление = ПредставлениеДокумента;
			НоваяСтрока.ТекущиеДанные = СтрокаМассива.ЭтоТекущиеДанные;
			НоваяСтрока.ТекстОшибкиВалидации = СтрокаМассива.ОшибкаВалидации;
			
			Если СтрокаМассива.ЭтоТекущиеДанные Тогда
				ТекущиеДанные = СтрокаМассива; 
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗаполнитьСписокВыбора_ТипДокумента()
		
		ТипыНеформализованных = ТипыНеформализованныхДокументов();
		
		Для каждого КлючЗначение Из ТипыНеформализованных Цикл
			Элементы.ТипДокумента.СписокВыбора.Добавить(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЦикла;
		
		Элементы.ТипДокумента.СписокВыбора.СортироватьПоПредставлению();
								
	КонецПроцедуры
	
	&НаКлиенте
	Функция ТипыНеформализованныхДокументов() 
		
		Результат = Новый Соответствие;
			
		Результат.Вставить("Nonformalized"			, "Неформализованный");
		Результат.Вставить("PriceListAgreement"		, "Протокол согласования цены");
		Результат.Вставить("CertificateRegistry"	, "Реестр сертификатов");
		Результат.Вставить("ServiceDetails"			, "Детализация");
		Результат.Вставить("ReconciliationAct"		, "Акт сверки");
		Результат.Вставить("ProformaInvoice"		, "Счет на оплату");
		Результат.Вставить("AcceptanceCertificate"	, "Акт выполненных работ");
		Результат.Вставить("Contract"				, "Договор");
		Результат.Вставить("Torg12"					, "Накладная");
		
		Возврат Результат;
					
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьСписокВыбора_Организация()
		
		Элементы.Организация.СписокВыбора.Очистить();
		Элементы.Организация.СписокВыбора.ЗагрузитьЗначения(МетодКлиента("Модуль_Клиент","ПолучитьОрганизацииНезаблокированныеПоAPI"));		
		
	КонецПроцедуры //ЗаполнитьСписокВыбора_Организация()
		
	&НаСервере
	Функция СформироватьПредставлениеДокумента(СтрокаМассива)
		
		Результат = СтрокаМассива.ФорматОтправки.Представление;
		
		Если ЗначениеЗаполнено(СтрокаМассива.НомерДокумента) Тогда
			Результат = Результат + " №" + СокрЛП(СтрокаМассива.НомерДокумента);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаМассива.ДатаДокумента) Тогда
			Результат = Результат + " от " + Формат(СтрокаМассива.ДатаДокумента, "ДЛФ=Д");
		КонецЕсли;
		
		Возврат Результат; 
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПерейтиВДиадок(Команда)
		
		Если НЕ ЭдОбъект = Неопределено Тогда
			ПоказатьДокументВДиадоке(ЭдОбъект);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура  ПоказатьДокументВДиадоке(ДокументДиадока)
		
		МетодКлиента("Модуль_Клиент","ПоказатьДокументВДиадоке", ДокументДиадока.OrganizationId, ДокументДиадока.DocumentId);
		
	КонецПроцедуры 
	
	// Устанавливает/снимает доступность для элемента формы "НастроитьПараметрыПодписи"
	//
	// Параметры:
	//  ТипДокумента - Строка	- тип обрабатываемого документа 
	//  ФункцияУПД	 - Строка	- функция универсального документа
	//
	&НаКлиенте	
	Процедура УправлениеДоступностью_Кнопка_НастроитьПараметрыПодписи(ТипДокумента, ФункцияУПД)
		
		АвторизацияПоСертификату = ПользовательАвторизованПоСертификату();
		
		Элементы.НастроитьПараметрыПодписи.Видимость = ТочкаВызова = "ТаблицаДокументовНаОтправку"
		                                                	И НЕ (Операция = "СоздатьНовыйПакет" ИЛИ Операция = "ДобавитьВВыделенныеПакеты")
															И (Найти(ТипДокумента, "Торг-12")>0
																ИЛИ (Найти(ТипДокумента, "УПД")>0 И АвторизацияПоСертификату)
																ИЛИ (Найти(ТипДокумента, "УКД")>0 И АвторизацияПоСертификату)
																ИЛИ (Найти(ТипДокумента, "Акт")>0 И Найти(ТипДокумента, "Акт сверки")=0)
																ИЛИ (Найти(ТипДокумента, "Счет-фактура")>0 
																	И (ФункцияУПД = "СЧФ" ИЛИ ФункцияУПД = "КСЧФ")));	
							
	КонецПроцедуры // УправлениеДоступностью_Кнопка_НастроитьПараметрыПодписи()
	
	&НаКлиенте
	Процедура УправлениеДоступностью_Кнопка_СохранениеДокумента()
		
		Элементы.СкачатьДокументВXML.Видимость = ТочкаВызова = "ТаблицаДокументовНаОтправку";
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПоказатьКнопкуПерейтиВДиадок()
		
		Возврат НЕ ЭДОбъект = Неопределено;

	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуСтруктураПодчиненности()
		
		Возврат НЕ ЭДОбъект = Неопределено;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуОтправитьСвязанныеДокументы()
		
		Возврат НЕ ЭДОбъект = Неопределено;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьГруппуКнопокПодписания(UserPermissions)
		
		DocumentStatus= ?(ЭДОбъект <> Неопределено, ЭДОбъект.Status, "");
		
		Возврат
		
		ЭДОбъект <> Неопределено
		
		И ЭДОбъект.Direction = НаправлениеВходящее()
		И ЭДОбъект.RevocationStatus = "RevocationStatusNone"
		И UserPermissions.CanSignDocuments
		
		И 	  DocumentType 	   <> "ProformaInvoice"
		И Лев(DocumentType, 7) <> "Invoice"
		
		И (		DocumentStatus = "InboundWaitingForRecipientSignature" 
		   ИЛИ  DocumentStatus = "InboundInvalidRecipientSignature"
		   ИЛИ (DocumentStatus = "InboundNoRecipientSignatureRequest" И DocumentType = "Nonformalized"))
		   
		И (ЭДОбъект.IsTest ИЛИ ПользовательАвторизованПоСертификату());

	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуОтменитьСопоставление()
		
		Возврат
		
		ЭДОбъект <> Неопределено
		
		И ЗначениеЗаполнено(Документ1С)
		И ЭДОбъект.Direction <> НаправлениеВнутреннее();
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьГруппуКнопокСогласования(UserPermissions)
		
		Результат = Ложь;
		
		Если ТочкаВызова = "ТаблицаДокументовНаОтправку" Тогда
			Возврат Результат;
		КонецЕсли;
		
		Если ЭДОбъект <> Неопределено
			И UserPermissions.CanAddResolutions Тогда // Пользователь имеет право согласовывать документы
			
			СтатусАннулирования = СтатусАннулированияДокумента(ЭДОбъект);
			
			ТребуетсяАннулирование	= (СтатусАннулирования = "RequestsMyRevocation");
			ОжидаетсяАннулирование 	= (СтатусАннулирования = "RevocationIsRequestedByMe");
			ДокументАннулирован		= (СтатусАннулирования = "RevocationAccepted");
			Если ОжидаетсяАннулирование Или ДокументАннулирован Или ТребуетсяАннулирование Тогда
				Возврат Результат;
			КонецЕсли;
			
			СтатусСогласования = СтатусСогласованияДокумента(ЭДОбъект);
		
			Если СтатусСогласования = Неопределено
				Или СтатусСогласования = "None" Тогда
				
				//Такой кейс возможен в двух случаях:
				//1. По документу еще не запрашивали согласование
				//2. Документ был согласован, далее подписан и отправлен. После этого ResolutionStatus возвращается Неопределено, как будто бы и не согласовывался. 
				
				Результат = МетодКлиента("Модуль_Клиент", "ДокументНеСогласованРанее", ЭДОбъект);
				
			ИначеЕсли 	СтатусСогласования = "ApprovementRequested" 	 		// Документ в состоянии согласования
				 		И (ЭДОбъект.ResolutionStatus.TargetUser = Неопределено    	 		// Документ отправлен на согласование любому пользователю подразделения
					 		ИЛИ ЭДОбъект.ResolutionStatus.TargetUser.IsCurrentUser) Тогда 	// Документ отправлен на согласование текущему пользователю
				
				Результат = Истина;
				
			ИначеЕсли СтатусСогласования = "ActionsRequested" И ЭДОбъект.Resolutions.Count > 0 // запрошено действие
				И (ЭДОбъект.ResolutionStatus.TargetUser = Неопределено // Документ отправлен на согласование любому пользователю подразделения
					Или ЭДОбъект.ResolutionStatus.TargetUser.IsCurrentUser) // Документ отправлен на согласование текущему пользователю
				И (ЭДОбъект.ResolutionStatus.TargetDepartment = Неопределено
					Или МетодКлиента("Модуль_Клиент", "УСотрудникаЕстьДоступВПодразделение", UserPermissions, ЭДОбъект.ResolutionStatus.TargetDepartment.Id)) Тогда
				
				Resolution = ЭДОбъект.Resolutions.GetItem(ЭДОбъект.Resolutions.Count - 1);
				Если (Resolution.ResolutionType = "ResolutionRequest" // Запрос согласования документа
						Или Resolution.ResolutionType = "Custom") // Запрос резолюции сформирован шагом маршрута и имеет нестандартный тип
					И ЭДОбъект.ResolutionRequests.Count > 0 Тогда
					
					ResolutionRequest = ЭДОбъект.ResolutionRequests.GetItem(ЭДОбъект.ResolutionRequests.Count - 1);
					Если ResolutionRequest.AvaliableActions.Count > 0
						И ResolutionRequest.AvaliableActions.GetItem(ResolutionRequest.AvaliableActions.Count - 1) = "ApproveAction" Тогда
						
						Результат = Истина;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
				
	&НаКлиенте
	Функция ПоказатьКнопкуПередачиНаПодпись(UserPermissions)
		
		Если ТочкаВызова = "ТаблицаДокументовНаОтправку" Тогда
			
			Если Операция = "ДобавитьВВыделенныеПакеты" Тогда
				Возврат Ложь;
			ИначеЕсли Операция = "СоздатьНовыйПакет" ИЛИ Операция = "ОткрытьПакет" Тогда
				Если UserPermissions = Неопределено Тогда
					Возврат Ложь;
				Иначе
					Возврат UserPermissions.CanRequestResolutions;	// Пользователь имеет право запросить согласование или подписание документа
				КонецЕсли;
			Иначе
				Возврат Ложь; 			
			КонецЕсли;
			
		ИначеЕсли ТочкаВызова = "ТаблицаДокументовИсходящих" Тогда 
			
			Возврат Ложь;
			
		ИначеЕсли ТочкаВызова = "ПереотправкаДокумента" Тогда 
			
			Результат = UserPermissions.CanRequestResolutions;
			
			Возврат Результат;
			
		ИначеЕсли ТочкаВызова = "ТаблицаДокументовВнутренних"
			Или ТочкаВызова = "ТаблицаДокументовВходящих" Тогда
			
			Если ЭДОбъект = Неопределено Тогда
				Результат = Ложь;
			Иначе
				
				ЕстьПрава = UserPermissions.CanRequestResolutions;
				СтатусСогласования = СтатусСогласованияДокумента(ЭДОбъект);
				
				СтатусСогласованияПодходит = СтатусСогласования = Неопределено
											ИЛИ СтатусСогласования = "None"
											ИЛИ СтатусСогласования = "Approved"
											ИЛИ СтатусСогласования = "Disapproved"
											ИЛИ СтатусСогласования = "SignatureDenied";
				
				СтатусПодходит = ЭДОбъект.Status = "OutboundWaitingForSenderSignature"
									Или ЭДОбъект.Status = "InboundWaitingForRecipientSignature";
				
				ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
				ИмяТипа	 = ФорматЭД.ИмяТипа;
				
				ТипПодходит = ИмяТипа = "XmlTorg12" 
							Или ИмяТипа = "XmlAcceptanceCertificate"
							Или ИмяТипа = "ProformaInvoice"
							Или ИмяТипа = "Nonformalized"
							Или (ЭтоУПД(ИмяТипа) И ФорматЭД.ЕстьФункцияПД)
							Или (ЭтоУКД(ИмяТипа) И ФорматЭД.ЕстьФункцияПД);
				
				Результат = ЕстьПрава
							И СтатусСогласованияПодходит
							И СтатусПодходит
							И ТипПодходит;
				
			КонецЕсли;				
			
			Возврат Результат;	
			
		Иначе
			
			Если ЭДОбъект = Неопределено Тогда
				Результат = Ложь;
			Иначе
				
				ЕстьПрава = UserPermissions.CanRequestResolutions;
				
				ДокументНеСогласовывался = СтатусСогласования = Неопределено
										ИЛИ СтатусСогласования = "None";
				
				СтатусПодходит = ЭДОбъект.Status = "InboundWaitingForRecipientSignature";
				
				ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
				ИмяТипа	 = ФорматЭД.ИмяТипа;
				
				ТипПодходит = ИмяТипа = "XmlTorg12" 
							Или ИмяТипа = "XmlAcceptanceCertificate"
							Или ИмяТипа = "ProformaInvoice"
							Или ИмяТипа = "Nonformalized"
							Или (ЭтоУПД(ИмяТипа) И ФорматЭД.ЕстьФункцияПД)
							Или (ЭтоУКД(ИмяТипа) И ФорматЭД.ЕстьФункцияПД);
				
				Результат = ЕстьПрава
							И ДокументНеСогласовывался
							И СтатусПодходит
							И ТипПодходит;
				
			КонецЕсли;				
			
			Возврат Результат;
						
		КонецЕсли;
				
	КонецФункции
			
	&НаКлиенте
	Функция ПоказатьКнопкуПередачиНаСогласование(UserPermissions)
		
		Если ТочкаВызова = "ТаблицаДокументовНаОтправку" Тогда
			
			Если Операция = "ДобавитьВВыделенныеПакеты" Тогда
				Возврат Ложь;
			ИначеЕсли Операция = "СоздатьНовыйПакет" ИЛИ Операция = "ОткрытьПакет" Тогда
				Если UserPermissions = Неопределено Тогда
					Возврат Ложь;
				Иначе
					Возврат UserPermissions.CanRequestResolutions;	// Пользователь имеет право запросить согласование или подписание документа
				КонецЕсли;
			Иначе
				Возврат Ложь; 			
			КонецЕсли;
			
		ИначеЕсли ТочкаВызова = "ТаблицаДокументовИсходящих" Тогда 
			
			Возврат Ложь;
			
		ИначеЕсли ТочкаВызова = "ПереотправкаДокумента" Тогда 
			
			Результат = UserPermissions.CanRequestResolutions;
			
			Возврат Результат;
			
		ИначеЕсли ТочкаВызова = "ТаблицаДокументовВнутренних"
			Или ТочкаВызова = "ТаблицаДокументовВходящих" Тогда 
			
			Если ЭДОбъект = Неопределено Тогда
				Результат = Ложь;
			Иначе
				
				ЕстьПрава = UserPermissions.CanRequestResolutions;
				СтатусСогласования = СтатусСогласованияДокумента(ЭДОбъект);
				
				СтатусСогласованияПодходит = СтатусСогласования = Неопределено
											ИЛИ СтатусСогласования = "None"
											ИЛИ СтатусСогласования = "Approved"
											ИЛИ СтатусСогласования = "Disapproved"
											ИЛИ СтатусСогласования = "SignatureDenied";
											
				СтатусАннулирования = СтатусАннулированияДокумента(ЭДОбъект);
				ДокументАннулирован	= (СтатусАннулирования = "RevocationAccepted");
											
				Результат = ЕстьПрава И СтатусСогласованияПодходит И НЕ ДокументАннулирован;
							
			КонецЕсли;
										
			Возврат Результат;							
							
		Иначе
			
			Возврат ЭДОбъект <> Неопределено
					И UserPermissions.CanRequestResolutions		// Пользователь имеет право запросить согласование или подписание документа
				 	И (СтатусСогласования = Неопределено ИЛИ СтатусСогласования = "None");	// Документ еще не согласован
				
		КонецЕсли;
				
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуУдалить(UserPermissions)
		
		Возврат
		
		ЭДОбъект <> Неопределено
		
		И UserPermissions.CanDeleteRestoreDocuments
		И НЕ ЭДОбъект.IsDeleted;
		
	КонецФункции
	
	&НаКлиенте
	Функция НужноПоказатьКнопкуДобавитьВВыделенныеПакеты()
		
		Если ТочкаВызова = "ТаблицаДокументовНаОтправку" И Операция = "ДобавитьВВыделенныеПакеты" Тогда
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	КонецФункции

&НаКлиенте
Функция ПоказатьКнопкуСоздатьДокумент1С()
	
	Результат = Ложь;
	
	Если НужноОтразитьВУчете() Тогда
		Результат = СоздаватьДокументыУчетаСПредварительнымПросмотром();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
	
&НаКлиенте
Функция ПоказатьКнопкуОтразитьДокументВУчете()
	
	Результат = Ложь;
	
	Если НужноОтразитьВУчете() Тогда
		Результат = Не СоздаватьДокументыУчетаСПредварительнымПросмотром();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

	&НаКлиенте
	Функция ПоказатьКнопкуЗапросаАннулирования(UserPermissions)
		
		DocumentStatus			 = ?(ЭДОбъект <> Неопределено, ЭДОбъект.Status			, "");
		DocumentRevocationStatus = ?(ЭДОбъект <> Неопределено, ЭДОбъект.RevocationStatus, "");
		
		Возврат
		
		ЭДОбъект <> Неопределено
		
		И UserPermissions.CanSignDocuments
		И ЭДОбъект.SenderSignatureStatus = "SenderSignatureCheckedAndValid" // Подпись отправителя проверена и валидна
		И ПользовательАвторизованПоСертификату()
		
		И (		DocumentRevocationStatus = "RevocationStatusNone"           // Документ не аннулирован и не было предложений об аннулировании
			ИЛИ DocumentRevocationStatus = "RevocationRejected")            // В предложении об аннулировании отказано
			
		И (		DocumentStatus = "InboundWithRecipientSignature" 
			ИЛИ DocumentStatus = "InboundRecipientSignatureRequestRejected"
			ИЛИ DocumentStatus = "OutboundWithRecipientSignature" 
			ИЛИ DocumentStatus = "OutboundRecipientSignatureRequestRejected"
			ИЛИ DocumentStatus = "OutboundWaitingForRecipientSignature"
			ИЛИ DocumentStatus = "OutboundWaitingForInvoiceReceiptAndRecipientSignature"
			ИЛИ DocumentStatus = "InboundFinished"
			ИЛИ DocumentStatus = "OutboundFinished"
			ИЛИ DocumentStatus = "Outbound"
			ИЛИ DocumentStatus = "Inbound"
			ИЛИ DocumentStatus = "OutboundWaitingForInvoiceReceipt"
			ИЛИ DocumentStatus = "OutboundWaitingForReceipt")
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуАннулирования(UserPermissions)
		
		Возврат
		
		ЭДОбъект <> Неопределено
		
		И UserPermissions.CanSignDocuments
		И ЭДОбъект.RevocationStatus = "RequestsMyRevocation"
		И ПользовательАвторизованПоСертификату()
		
	КонецФункции

	&НаКлиенте
	Функция ПоказатьКнопкуОтказаАннулирования(UserPermissions)
		
		Возврат
		
		ЭДОбъект <> Неопределено
		
		И UserPermissions.CanSignDocuments
		И ЭДОбъект.RevocationStatus = "RequestsMyRevocation"
		И ПользовательАвторизованПоСертификату()
		
	КонецФункции

	&НаКлиенте
	Функция ПоказатьКнопкуЗапросаУточнения(UserPermissions)
		
		Если ЭДОбъект = Неопределено Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ИмяТипа = ЭДОбъект.TypeNamedId;
		
		ЭтоСФ = ЛЕВ(ИмяТипа, 7) = "Invoice";
		ЭтоУПД = ЭтоУПД(ИмяТипа) Или ЭтоУКД(ИмяТипа);
		
		Если ЭтоСФ Тогда
			
			Результат = ЭДОбъект.Status = "InboundFinished"
					И НЕ ЭДОбъект.Corrected
					И НЕ ЭДОбъект.Revised
					И НЕ ЭДОбъект.AmendmentRequested
					И ПользовательАвторизованПоСертификату();
			
		ИначеЕсли ЭтоУПД Тогда
			
			Результат = ЭтоВходящийДокумент()
					И НЕ ЭДОбъект.AmendmentRequested
					И ПользовательАвторизованПоСертификату();
			
		Иначе
			Результат = Ложь;
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура НастроитьКнопкиКоманднойПанели()
		
		Если ЭДОбъект <> Неопределено Тогда
			UserPermissions= ЭДОбъект.Organization.GetUserPermissions();
		ИначеЕсли Organization <> Неопределено Тогда
			UserPermissions= Organization.GetUserPermissions();
		КонецЕсли;
		
		МассивНастройкиВидимости= Новый Массив;
				
		Если ПоказатьКнопкуПерейтиВДиадок() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПерейтиВДиадок", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПерейтиВДиадок", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуСтруктураПодчиненности() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ФормаСтруктураПодчиненности", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ФормаСтруктураПодчиненности", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуОтправитьСвязанныеДокументы() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтправитьСвязанныйДокумент", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтправитьСвязанныйДокумент", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьГруппуКнопокПодписания(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаКнопокПодписания", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаКнопокПодписания", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуОтменитьСопоставление() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтменитьСопоставление", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтменитьСопоставление", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьГруппуКнопокСогласования(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаСогласование", Истина, Истина);
		Иначе
			Если ТочкаВызова = "ТаблицаДокументовНаОтправку" Тогда
				ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаСогласование", Ложь, Ложь);
			Иначе
				ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаСогласование", Истина, Ложь);
			КонецЕсли;
		КонецЕсли;
		
		НужноПоказатьКнопкуПередачиНаСогласование= 	ПоказатьКнопкуПередачиНаСогласование(UserPermissions);
		НужноПоказатьКнопкуПередачиНаПодпись= 		ПоказатьКнопкуПередачиНаПодпись(UserPermissions);
		
		Если НужноПоказатьКнопкуПередачиНаСогласование И НужноПоказатьКнопкуПередачиНаПодпись Тогда
			
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаПередачаНаСогласование", Истина, Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаСогласование", Истина, Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаПодписание", Истина, Истина);
			
		ИначеЕсли НужноПоказатьКнопкуПередачиНаСогласование И НЕ НужноПоказатьКнопкуПередачиНаПодпись Тогда
			
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаПередачаНаСогласование", 	Истина, Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаСогласование", 	Истина, Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаПодписание", 		Ложь, 	Ложь);
			
		ИначеЕсли НЕ НужноПоказатьКнопкуПередачиНаСогласование И НужноПоказатьКнопкуПередачиНаПодпись Тогда
			
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаПередачаНаСогласование", 	Истина, Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаСогласование", 	Ложь,	Ложь);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПередатьНаПодписание", 		Истина, Истина);
			
		Иначе
			Если Операция = "СоздатьНовыйПакет" Тогда
				ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаПередачаНаСогласование", Истина, Ложь);
			Иначе
				ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаПередачаНаСогласование", Ложь, Ложь);
			КонецЕсли;
		КонецЕсли;
		
		Если НужноПоказатьКнопкуДобавитьВВыделенныеПакеты() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаДобавитьВВыделенныеПакеты", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаДобавитьВВыделенныеПакеты", Ложь, Ложь);
		КонецЕсли;
				
		Если ПоказатьКнопкуУдалить(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаУдалить", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаУдалить", Ложь, Ложь);
		КонецЕсли;
		
		ФлагВидимости = ПоказатьКнопкуСоздатьДокумент1С();
		ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаСоздатьДокумент1С", ФлагВидимости, ФлагВидимости);
		ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаСопоставитьДокумент1С", ФлагВидимости, ФлагВидимости);
		
		ФлагВидимости = ПоказатьКнопкуОтразитьДокументВУчете();
		ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтразитьДокументВУчете", ФлагВидимости, ФлагВидимости);
		
		Если ПоказатьКнопкуЗапросаАннулирования(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаЗапроситьАннулирование", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаЗапроситьАннулирование", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуАннулирования(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаАннулировать", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаАннулировать", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуОтказаАннулирования(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтказатьВАннулировании", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаОтказатьВАннулировании", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуЗапросаУточнения(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаЗапроситьУточнение", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаЗапроситьУточнение", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуПереотправить(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПереотправить", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПереотправить", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуПодписатьИОтправить(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПодписатьИОтправить", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПодписатьИОтправить", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьКнопкуПодписатьЗапрошенный(UserPermissions) Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПодписатьЗапрошенный", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаПодписатьЗапрошенный", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьСодержимоеПакета() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаСодержимоеПакета", Истина, Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаСкрытьПоказатьСоставПакета", Истина, Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаКнопкиКоманднойПанелиТаблицыДокументов", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаСодержимоеПакета", Ложь, Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаСкрытьПоказатьСоставПакета", Истина, Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаКнопкиКоманднойПанелиТаблицыДокументов", Ложь, Истина);
		КонецЕсли;
		
		Если ПоказатьГруппаДокументыПакетаКнопкиВыделения() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаДокументыПакетаКнопкиВыделения", Истина, Истина);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаСкрытьПоказатьСоставПакета", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаДокументыПакетаКнопкиВыделения", Ложь, Ложь);
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "КнопкаСкрытьПоказатьСоставПакета", Ложь, Ложь);
		КонецЕсли;
		
		Если ПоказатьГруппаДокументыПакетаКнопкиРедактированияСостоваПакета() Тогда
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаДокументыПакетаКнопкиРедактированияСостоваПакета", Истина, Истина);
		Иначе
			ДобавитьНастройкуВидимости(МассивНастройкиВидимости, "ГруппаДокументыПакетаКнопкиРедактированияСостоваПакета", Ложь, Ложь);
		КонецЕсли;
		
		НастроитьВидимостьЭлементовФормыНаСервере(МассивНастройкиВидимости);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура УправлениеДоступностью_Кнопки_РедактированиеСоставаПакета(ЗначениеДоступности)
		
		Элементы.ЗаменитьФайлВПакете.Доступность= 	ЗначениеДоступности;
		Элементы.УдалитьФайлИзПакета.Доступность= 	ЗначениеДоступности;
		
	КонецПроцедуры
		
	&НаКлиенте
	Процедура ДобавитьНастройкуВидимости(МассивНастройкиВидимости, ИмяРеквизита, Видимость, Доступность)
		
		НастройкаВидимости = Новый Структура("ИмяРеквизита, Видимость, Доступность", ИмяРеквизита, Видимость, Доступность);
		МассивНастройкиВидимости.Добавить(НастройкаВидимости);
		
	КонецПроцедуры
	
	&НаСервере
	Процедура НастроитьВидимостьЭлементовФормыНаСервере(МассивНастройкиВидимости)
		
		Для каждого НастройкаВидимости из МассивНастройкиВидимости Цикл
			
			ИмяРеквизита		= НастройкаВидимости.ИмяРеквизита;
			НастройкаВидимость 	= НастройкаВидимости.Видимость;
			НастройкаДоступность= НастройкаВидимости.Доступность;
			
			Элементы[ИмяРеквизита].Видимость 	= НастройкаВидимость;
			Элементы[ИмяРеквизита].Доступность 	= НастройкаДоступность;
			
		КонецЦикла;
		
	КонецПроцедуры
		
	&НаКлиенте
	Процедура НастроитьЭлементыФормы()
		
		Элементы.Ошибки.Видимость = ЗначениеЗаполнено(Ошибки) ИЛИ ЗначениеЗаполнено(ОшибкиОтправки);
		
		Если ЭДОбъект <> Неопределено Тогда
			
			Элементы.ГруппаКоманднаяПанельПодвал.Видимость=	  Истина;
			Если ЭтоВходящийДокумент() Тогда
				
				ПредставлениеКнопкиСоздать=	МетодКлиента("Модуль_Клиент","СформироватьПредставлениеКнопкиСоздатьДокументВ1С", Документ1С, ЭДОбъект, МассивСсылокРНК, ПодходящаяСФ);
				Если ЗначениеЗаполнено(ПредставлениеКнопкиСоздать) Тогда
					Элементы.КнопкаСоздатьДокумент1С.Заголовок=	ПредставлениеКнопкиСоздать;
				КонецЕсли;
				
			КонецЕсли;
			
			Элементы.РамкаСтатуса.Видимость= Истина;
			Элементы.ДекорацияПодробно.Видимость= ЭДОбъект.Resolutions.Count > 0;
			
			Если ЗначениеЗаполнено(Документ1С) Тогда
				Элементы.ДокументВ1С.Заголовок= Новый ФорматированнаяСтрока(Строка(Документ1С),,,,ПолучитьНавигационнуюСсылку(Документ1С));
			Иначе
				Элементы.ДокументВ1С.Заголовок= "";
			КонецЕсли;
			
			Элементы.ГруппаПодразделениеПодвал.Видимость= (ЗначениеЗаполнено(Документ1С));
			Элементы.ГруппаПодразделениеТело.Видимость=	  (НЕ ЗначениеЗаполнено(Документ1С));
			
			ПредставлениеПодразделения=	?(ЭДОбъект.Department = Неопределено, "Головное подразделение", ЭДОбъект.Department.Name);
			
		Иначе
			
			Элементы.РамкаСтатуса.Видимость= Ложь;
			Элементы.Группа1.Видимость= Ложь;
			Элементы.ГруппаПодразделениеПодвал.Видимость= Ложь;
			
			Если ТочкаВызова = "ТаблицаДокументовНаОтправку" И Операция = "СоздатьНовыйПакет" Тогда
				Элементы.ГруппаАдресаты.Видимость= Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Операция = "СоздатьНовыйПакет" ИЛИ Операция = "ДобавитьВВыделенныеПакеты" Тогда 
			
			Элементы.ГруппаПредупреждениеОНовомФормате.Видимость = Ложь;
			
		ИначеЕсли ЗначениеЗаполнено(IDТекущейСтроки) Тогда
			
			Массив = ТаблицаДокументов.НайтиСтроки(Новый Структура("IdСтроки", IDТекущейСтроки));
			Если ЗначениеЗаполнено(Массив) И Массив[0].ЭтоФайл Тогда
				Элементы.ГруппаПредупреждениеОНовомФормате.Видимость = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		ЭтаФорма.Элементы.КнопкаПерейтиВДиадок.Заголовок = "Перейти в " + ПредставлениеСервиса();
		
		НастроитьЗаголовок();
		Если Элементы.ГруппаСодержимоеПакета.Видимость = Истина Тогда
			Элементы.КнопкаСкрытьПоказатьСоставПакета.Заголовок= "<<";
		Иначе
			Элементы.КнопкаСкрытьПоказатьСоставПакета.Заголовок= ">>";
		КонецЕсли;
						
	КонецПроцедуры
	
	&НаКлиенте
	Процедура УправлениеФормой()
		
		ОбновитьСтатусЭД();
		НастроитьЗаголовок();
		НастроитьКнопкиКоманднойПанели();
		НастроитьЭлементыФормы();
		
	КонецПроцедуры	 
	
	&НаКлиенте
	Процедура ОбновитьСтатусЭД()
		
		Если НЕ ЭДОбъект = Неопределено Тогда
			
			ПредставлениеСтатуса=		 МетодКлиента("Модуль_Клиент","ПредставлениеСтатуса"			, ЭДОбъект);
			ПредставлениеСогласования=	 МетодКлиента("Модуль_Клиент","ПредставлениеСтатусаСогласования", ЭДОбъект);
			
			ПредставлениеСтатусаРоуминг= МетодКлиента("Модуль_Клиент","ПредставлениеСтатусаРоуминг"		, ЭДОбъект);
			ПредставлениеСтатусаРоумингДетали= ?(ЭДОбъект.RoamingNotificationStatus = "RoamingNotificationStatusError", ЭДОбъект.RoamingNotificationStatusDescription, "");
			
			СводныйСтатус= Новый Массив;
			Разделитель= "";
			
			Если НЕ ПустаяСтрока(ПредставлениеСтатусаРоуминг) Тогда
				СводныйСтатус.Добавить(Разделитель);
				СводныйСтатус.Добавить(ПредставлениеСтатусаРоуминг);
				Разделитель= " ";
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(ПредставлениеСтатуса) Тогда
				СводныйСтатус.Добавить(Разделитель);
				СводныйСтатус.Добавить(ПредставлениеСтатуса);
				Разделитель= ". ";
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(ПредставлениеСогласования) Тогда
				СводныйСтатус.Добавить(Разделитель);
				СводныйСтатус.Добавить(ПредставлениеСогласования);
				Разделитель= ". ";
			КонецЕсли;
			
			Элементы.СтатусДокумента.Заголовок=	Новый ФорматированнаяСтрока(СводныйСтатус);
			
			Если НЕ Элементы.ДекорацияДокументЗашифрован.Видимость И ЭДОбъект.IsEncryptedContent Тогда
				Элементы.ДекорацияДокументЗашифрован.Видимость = ЭДОбъект.IsEncryptedContent;
			КонецЕсли;
			
		Иначе
			
			Элементы.СтатусДокумента.Заголовок=	 ""; 
			
			ПредставлениеСтатусаРоуминг=	   "";
			ПредставлениеСтатусаРоумингДетали= "";
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаСервере
	Функция ПолучитьСсылкуНаОбъект(ТекстСсылки)
		
		Результат = Новый Структура("Форма, Объект");
		
		СсылкаНаЭлементДанных = СсылкаНаЭлементДанныхИзГиперссылки(ТекстСсылки);
		
		Если СсылкаНаЭлементДанных <> Неопределено Тогда
			
			Если ЭтоСемействоУТКАERP()
				И ТипЗнч(СсылкаНаЭлементДанных) = Тип("СправочникСсылка.Контрагенты")
				И ИспользоватьПартнеровКакКонтрагентов() Тогда
				
				СсылкаНаЭлементДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаЭлементДанных, "Партнер");
				
			КонецЕсли;
			
			Результат.Объект = СсылкаНаЭлементДанных;
			Результат.Форма	 = СсылкаНаЭлементДанных.Метаданные().ПолноеИмя() + ".ФормаОбъекта";
			
		ИначеЕсли Найти(ТекстСсылки, "СПРАВОЧНИКВАЛЮТ") > 0 Тогда
			
			Результат.Форма = "Справочник.Валюты.ФормаСписка";
			
		ИначеЕсли Найти(ТекстСсылки, "ОКВ") > 0 Тогда
			
			Результат.Форма = "Справочник.Валюты.Форма.ПодборВалютИзКлассификатора";
			
		ИначеЕсли Найти(ТекстСсылки, "СПРАВОЧНИКЕДИНИЦ") > 0 Тогда // TODO странно
			
			Если Объект.ПараметрыКлиентСервер.МаркерКонфигурации = "УТ11" Тогда
				
				Если НЕ Метаданные.Справочники.Найти("УпаковкиЕдиницыИзмерения") = Неопределено Тогда
					Результат.Форма = "Справочник.УпаковкиЕдиницыИзмерения.Форма.ФормаВыбораЕдиницыИзмерения";
				Иначе
					Результат.Форма = "Справочник.ЕдиницыИзмерения.ФормаСписка";
				КонецЕсли;
				
			Иначе
				Результат.Форма = "Справочник.КлассификаторЕдиницИзмерения.ФормаСписка";
			КонецЕсли;
			
		ИначеЕсли Найти(ТекстСсылки, "ОКЕИ") > 0 Тогда
			
			//Справочники.КлассификаторЕдиницИзмерения.ПолучитьФорму("ФормаПодбораИзКлассификатора",,ЭтаФорма).Открыть();  // TODO что-то не то
			
		ИначеЕсли Найти(ТекстСсылки, "СПРАВОЧНИКСТРАН") > 0 Тогда
			
			Результат.Форма = "Справочник.СтраныМира.ФормаСписка";
			
		ИначеЕсли Найти(текстСсылки, "ОКСМ") > 0 Тогда
			
			Результат.Форма = "Справочник.СтраныМира.Форма.Классификатор";
			
		ИначеЕсли Найти(ТекстСсылки, "КАРТОЧКА:") > 0 Тогда
			
			СтрокаИД = Прав(ТекстСсылки, СтрДлина(ТекстСсылки)-9);
			Если Найти(строкаИД, ":") > 0 Тогда
				BoxID = лев(строкаИД, найти(строкаИД, ":")-1);
				DocID = прав(строкаИД, стрДлина(СтрокаИД) - найти(строкаИД, ":"));
			КонецЕсли;
			
		ИначеЕсли Найти(Текстссылки, "НАСТРОЙКАПОДПИСИ") > 0 Тогда
			
			гуид = Прав(ТекстСсылки, СтрДлина(ТекстСсылки) - 17);
			СсылкаНаЭлемент = Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор(Гуид));
			Если ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
				//если Найти(ТипДокумента, "Акт") <> 0  тогда 
				//	фрм = ЭтаФорма.ПолучитьФорму("ФормаНастройкиПодписиАкт");
				//Иначе 	
				//	фрм = ЭтаФорма.ПолучитьФорму("ФормаНастройкиПодписиТОРГ12");
				//КонецЕсли;	
				//фрм.ОткрытьКарточку(СсылкаНаЭлемент);
				//ОбновитьРНК();
			КонецЕсли;
			
		Иначе
			
			Результат = Неопределено;
			
		КонецЕсли;	
		
		Возврат Результат;
		
	КонецФункции
	
	// Преобразует текст вида
	// "СПРАВОЧНИК.ДОГОВОРЫКОНТРАГЕНТОВ:35C3C512-6B05-11E9-9239-D46D6D51F7B5"
	// в ссылку на элемент справочника/документа
	// Если текст не указывает на элемент данных, вернет Неопределено
	&НаСервере
	Функция СсылкаНаЭлементДанныхИзГиперссылки(ЗНАЧ ТекстГиперссылки)
		
		Результат = Неопределено;
		
		ВидМетаданных = Неопределено;
		
		Если Лев(ТекстГиперссылки, 11) = "СПРАВОЧНИК." Тогда
			ГруппаМетаданных = Справочники;
			ТекстГиперссылки = Сред(ТекстГиперссылки, 12);
		ИначеЕсли Лев(ТекстГиперссылки, 9) = "ДОКУМЕНТ." Тогда
			ГруппаМетаданных = Документы;
			ТекстГиперссылки = Сред(ТекстГиперссылки, 10);
		КонецЕсли;	
		
		Если ГруппаМетаданных = Неопределено Тогда
			Возврат Результат;  // не удалось вычислить ссылку
		КонецЕсли;
		
		ПодгруппаМетаданных = Лев(ТекстГиперссылки, СтрДлина(ТекстГиперссылки) - 37);  // конкретный вид справочника/документа.
		ГуидСтрокой = Прав(ТекстГиперссылки, 36);
		Гуид = Новый УникальныйИдентификатор(ГуидСтрокой);
		
		Результат = ГруппаМетаданных[ПодгруппаМетаданных].ПолучитьСсылку(Гуид);
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Процедура НастроитьЗаголовок()
		
		Если ЭДОбъект = Неопределено Тогда
			Заголовок = Заголовок();
		Иначе
			Заголовок =	МетодКлиента("Модуль_Клиент","ПредставлениеЭД", ЭДОбъект);	
		КонецЕсли;
				
	КонецПроцедуры
	
	// Возвращает заголовок для формы "ФормаДокумента" в зависимости от количества документов, отображаемых в форме
	// 
	// Возвращаемое значение:
	//   - Строка 
	//
	&НаКлиенте
	Функция Заголовок()
		
		Результат = "";
		
		КоличествоДокументов = ТаблицаДокументов.Количество();
		
		Если КоличествоДокументов = 0 Тогда
			
			Результат = "Пустой пакет";
			
		Иначе
			
			Если Операция = "ДобавитьВВыделенныеПакеты" Тогда
				
				Результат = "Количество документов: " + КоличествоДокументов;
				
			Иначе
				
				Результат = "В пакете " + МетодКлиента("Модуль_Клиент", "СтрокаКоличествоДокументов", КоличествоДокументов);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаСервереБезКонтекста
	Функция ПолучитьРеквизитСсылки(СсылкаНаОбъект, ИмяРеквизита)
		
		ЗначениеРеквизита = Неопределено;
		МетаданныеОбъекта = СсылкаНаОбъект.Метаданные();
		
		Если МетаданныеОбъекта.Реквизиты.Найти(ИмяРеквизита) <> Неопределено Тогда
			ЗначениеРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, ИмяРеквизита);
		КонецЕсли;
		
		Возврат ЗначениеРеквизита;
		
	КонецФункции
	
	&НаСервереБезКонтекста
	Функция ВернутьСписокОрганизацийНеТребующихВизуализацииДопПоля_Сервер()
		
		СписокОрганизаций = новый списокЗначений;
		//Авто (тестовая)
		СписокОрганизаций.Добавить("c19b1b8c75ab4ca3a2ab8ea3771631ab@diadoc.ru");
		
		Возврат СписокОрганизаций;
		
	КонецФункции
	
	&НаСервере
	Функция Организация_2_BoxID_Форма(Организация)
		Возврат МетодСервера(,"Организация_2_BoxID", Организация);
	КонецФункции	
	
	&НаКлиенте
	Функция ВизуализироватьДопПоля(ИсточникИДОтправителя) Экспорт
		
		ВизуализироватьДопПоля=	Истина;
		
		СписокОрганизацийНеТребующихВизуализацииДопПоля = ВернутьСписокОрганизацийНеТребующихВизуализацииДопПоля_Сервер();
		
		Если ТипЗнч(ИсточникИДОтправителя) = Тип("COMОбъект") Тогда

			Если ИсточникИДОтправителя.Direction = НаправлениеИсходящее() Тогда
				ИДОтправителя=	ИсточникИДОтправителя.Organization.Id;
			Иначе
				ИДОтправителя=	ИсточникИДОтправителя.Counteragent.Id;
			КонецЕсли;
			
			Если НЕ СписокОрганизацийНеТребующихВизуализацииДопПоля.НайтиПоЗначению(ИДОтправителя) = Неопределено Тогда
				ВизуализироватьДопПоля = Ложь;
			КонецЕсли;
			
		Иначе
			
			ИДОтправителя=	Организация_2_BoxID_Форма(ПолучитьРеквизитСсылки(ИсточникИДОтправителя, "Организация"));
			Если НЕ СписокОрганизацийНеТребующихВизуализацииДопПоля.НайтиПоЗначению(ИДОтправителя) = Неопределено Тогда
				ВизуализироватьДопПоля = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат ВизуализироватьДопПоля;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ВизуализацияПечатнойФормыПоСтруктуре()
		
		ПоказыватьПредупрежденияОСтаромФормате = Ложь;
		
		Если ЭДОбъект = Неопределено Тогда
			
			ВизуализироватьДопПоля = ВизуализироватьДопПоля(Документ1С);
			
			ФИОПодписанта = ФИОПодписанта();
			
			СформироватьПечатнуюФормуПоДокументу(ФИОПодписанта, ВизуализироватьДопПоля);
			
		Иначе
			
			ТабПоле = СформироватьПечатнуюФормуПоДокументуДиадока(ЭДОбъект);
			
			ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
			ДатаОтправки = ЭДОбъект.Timestamp;
			ПоказыватьПредупрежденияОСтаромФормате = ДокументВУстаревшемФормате(ФорматЭД, ДатаОтправки);
			
		КонецЕсли;
		
		Элементы.ГруппаПредупреждениеОНовомФормате.Видимость = ПоказыватьПредупрежденияОСтаромФормате;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПросмотрФормы()
		
		ВизуализацияПечатнойФормыПоСтруктуре();
		
		УправлениеФормой();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура НастроитьПараметрыПодписи(Команда)
		
		ТекСтрока = ТекущаяСтрокаТаблицыДокументов();
		ФорматОтправки	 = ТекСтрока.ФорматОтправки;
		БазовыйФормат	 = ФорматОтправки.БазовыйФормат;
		ИмяТипаДокумента = ФорматОтправки.ИмяТипа;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("BoxId", BoxId);
		ПараметрыФормы.Вставить("ФорматЭД", ФорматОтправки);
		ПараметрыФормы.Вставить("Организация", Организация);
		
		БазовыеФорматы = БазовыеФорматы();
		
		Если БазовыйФормат = БазовыеФорматы.torg12 Тогда
			
			ИмяФормыПодписи = "НастройкаПодписиТОРГ12";
			
		ИначеЕсли БазовыйФормат = БазовыеФорматы.act Тогда
			
			ПараметрыФормы.Вставить("Заголовок", "Параметры формирования подписи Акта выполненных работ");
			ИмяФормыПодписи = "НастройкаПодписиАкт";
			
		ИначеЕсли БазовыйФормат = БазовыеФорматы.utd820
			Или БазовыйФормат = БазовыеФорматы.utd
			Или БазовыйФормат = БазовыеФорматы.ucd736
			Или БазовыйФормат = БазовыеФорматы.ucd Тогда
			
			ПараметрыФормы.Вставить("Исходящие", Истина);
			ИмяФормыПодписи = "НастройкаПодписиУПД";
			
		Иначе
			ТекстПредупреждения = СтрШаблон("Для документов с типом %1 настройка параметров подписи не предусмотрена!"
									, ИмяТипаДокумента);
			ПоказатьПредупреждение(, ТекстПредупреждения, 60, "Настройка параметров подписи");
			Возврат;
		КонецЕсли;
		
		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("ИмяФормыПодписи", ИмяФормыПодписи);
		
		МетодКлиента(, "ОткрытьФормуОбработкиМодально"
			, ИмяФормыПодписи
			, ПараметрыФормы
			, ЭтаФорма
			, "ОбработчикОткрытиеФормыНастройкиПодписи"
			, ПараметрыОбработчика);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПодразделениеНажатие(Элемент, СтандартнаяОбработка)
		
		СтандартнаяОбработка=	Ложь;
		
		ТекущийDepartmentId=	?(ЭДОбъект.Department = Неопределено, "", ЭДОбъект.Department.Id);
		
		ПараметрыФормы=	Новый Структура();
		ПараметрыФормы.Вставить("DepartmentId", 	ТекущийDepartmentId);
		ПараметрыФормы.Вставить("OrganizationId",	ЭДОбъект.Organization.Id);
		
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ВыборПодразделенияОрганизации", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыВыбораПодразделенияОрганизации", ТекущийDepartmentId);

	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОшибкиПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
		
		СтандартнаяОбработка = Ложь;
		
		href = "";
		Если (ДанныеСобытия.Anchor<> Неопределено) И ЗначениеЗаполнено(ДанныеСобытия.Anchor.NameProp) Тогда 
			href = ДанныеСобытия.Anchor.NameProp;  // поведение платформы до 8.3.14
		ИначеЕсли ЗначениеЗаполнено(ДанныеСобытия.href) Тогда
			href = ДанныеСобытия.href;  // начиная с 8.3.14 и переезда на WebKit
		Иначе
			Возврат;
		КонецЕсли;
		
		БазовыйURI = Элемент.Документ.baseURI;
		
		Если СтрНачинаетсяС(href, БазовыйURI) Тогда
			href = Сред(href, СтрДлина(БазовыйURI) + 1);
		КонецЕсли;
		
		Если Найти(href, "НАСТРОЙКАПАРАМЕТРОВПОДПИСАНИЯ" ) > 0 Тогда
			
			НастроитьПараметрыПодписи("");
			
		ИначеЕсли Найти(href, "НАСТРОЙКДОЛЖНОСТИПОДПИСАНТА") > 0 Тогда
			
			ПутьПослеСлэша = МетодКлиента("Модуль_Клиент", "РазвернутьGUID", BoxId) + "/EmployeesList";
			ТекстURL = МетодСервера(, "ПолучитьПутьКWEBСерверу") + ПутьПослеСлэша;
			ЗапуститьПриложение(ТекстURL);
			
		ИначеЕсли Найти(href, "УВЕДОМЛЕНИЕОСМЕНЕФОРМАТА") > 0 Тогда
			
			ПоказатьПредупреждениеОбУстаревшихНастройкахОтправки();
		
		ИначеЕсли Найти(href, "ИНСТРУКЦИЯКАКЗАПОЛНИТЬОСНОВАНИЕ") > 0 Тогда
			
			ПоказатьИнструкциюКакЗаполнитьОснованиеПередачи();
			
		ИначеЕсли Найти(href, "ИНСТРУКЦИЯКАКЗАПОЛНИТЬГТД") > 0 Тогда
			
			ПоказатьИнструкциюКакЗаполнитьГТД();
	
		Иначе
			
			ПараметрыСсылки = ПолучитьСсылкуНаОбъект(href);
			
			Если ЗначениеЗаполнено(ПараметрыСсылки) Тогда 
				
				СсылкаНаОбъект = ПараметрыСсылки.Объект;
				ИмяФормыОбъекта = ПараметрыСсылки.Форма;
				
				Если ЭтоСсылкаНаОбъектКМ(href) Тогда
					
					ПоказатьФормуОбъектаКМ(СсылкаНаОбъект);
					
				Иначе
					
					МетодКлиента(, "ОткрытьФормуОбъектаИБМодально"
						, СсылкаНаОбъект
						, ИмяФормыОбъекта
						,
						, ЭтаФорма
						, "ОбработчикОткрытиеФормыОшибокВалидации");
						
				КонецЕсли;
				
			ИначеЕсли ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда 
				
				ЗапуститьПриложение(ДанныеСобытия.Href, , Ложь);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры

// Проверяет, что гиперссылка указывает на объект подсистемы Контур.Маркировка.
//
// Параметры:
//  Гиперссылка - Строка - адрес гиперссылки.
// 
// Возвращаемое значение:
//  Булево - Истина, если это ссылка на объект подсистемы Контур.Маркировка.
//
&НаКлиентеНаСервереБезКонтекста
Функция ЭтоСсылкаНаОбъектКМ(Гиперссылка)
	
	Результат = Ложь;
	
	Если СтрНайти(ВРег(Гиперссылка), ВРег("Справочник.КонтурМаркировка_ДокументыУчета")) > 0 Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Открывает форму объекта из подсистемы "Контур.Маркировка"
//
// Параметры:
//  СсылкаНаОбъект - ЛюбаяСсылка - ссылка на объект подсистемы Контур.Маркировка
//
&НаКлиенте
Процедура ПоказатьФормуОбъектаКМ(СсылкаНаОбъект)
	
	МодульМаркировки = Вычислить("ВзаимодействиеКонтурМаркировкаКлиент");
	МодульМаркировки.ОткрытьФормуДокументаУчета(СсылкаНаОбъект);
	
КонецПроцедуры

//} УПРАВЛЕНИЕ ФОРМОЙ
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
//{ КОМАНДЫ

&НаКлиенте
Процедура Подписать(Команда)
	
	ФорматЭД = МетодКлиента("Модуль_Клиент", "ФорматЭлектронногоДокумента", ЭДОбъект);
	
	ПараметрыФормы = МодульКлиент_ПараметрыФормыПодписанияДокумента();
	ПараметрыФормы.ФорматДокумента = ФорматЭД;
	ПараметрыФормы.ТребуетсяОтветныйТитул = МодульКлиент_ФормироватьОтветныйТитул(ЭДОбъект.WorkflowId);
	ПараметрыФормы.ИдентификаторОрганизации = Organization.Id;
	ПараметрыФормы.ЗаголовокФормыПодписания = Заголовок;
	
	ИмяОбработчика = "ОбработчикОткрытиеФормыПодписанияДокумента";
	
	МетодКлиента("Модуль_Клиент", "ПоказатьФормуПодписания"
		, ПараметрыФормы
		, ЭтаФорма
		, ИмяОбработчика);
	
КонецПроцедуры
	
	&НаКлиенте
	Процедура ОтказатьВПодписи(Команда)
		
		ФИОПодписанта = ФИОПодписанта();
						
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		1);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	ПредставлениеПодписи());
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаОтказаВПодписи");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура Согласовать(Команда)
		
		ФИОПодписанта=	?(НЕ Organization.Certificate = Неопределено,  Organization.Certificate.Name, "");
		
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		2);
		ПараметрыФормы.Вставить("AuthenticateType", 		Organization.AuthenticateType);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	ПредставлениеПодписи());
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаСогласовать");

	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПередатьНаСогласование(Команда)
		
		МассивПомеченныеСтроки= ТаблицаДокументов.НайтиСтроки(Новый Структура("Вкл", Истина));
		Если МассивПомеченныеСтроки.Количество() = 0 Тогда
			ПоказатьПредупреждение(, "Нет документов, отмеченных для передачи на согласование", 120, ПредставлениеСервиса());
			Возврат;
		ИначеЕсли ТочкаВызова = "ТаблицаДокументовНаОтправку" И НЕ МассивПомеченныеСтроки.Количество() = ТаблицаДокументов.Количество() Тогда
			ТекстВопроса= 		"Были отмечены не все документы пакета. Передать на согласование только отмеченные?";
			ОписаниеОповещения= Новый ОписаниеОповещения("ВыполнитьОтправкуДокумента", ЭтаФорма, "ПередатьНаСогласование");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120, , ПредставлениеСервиса()); 	
		Иначе
			ВыполнитьОтправкуДокумента(КодВозвратаДиалога.Да, "ПередатьНаСогласование");
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПередатьНаПодписание(Команда)
		
		МассивПомеченныеСтроки= ТаблицаДокументов.НайтиСтроки(Новый Структура("Вкл", Истина));
		Если МассивПомеченныеСтроки.Количество() = 0 Тогда
			ПоказатьПредупреждение(, "Нет документов, отмеченных для передачи на подпись", 120, ПредставлениеСервиса());
			Возврат;
		ИначеЕсли ТочкаВызова = "ТаблицаДокументовНаОтправку" И НЕ МассивПомеченныеСтроки.Количество() = ТаблицаДокументов.Количество() Тогда
			ТекстВопроса= 		"Были отмечены не все документы пакета. Передать на подписание только отмеченные?";
			ОписаниеОповещения= Новый ОписаниеОповещения("ВыполнитьОтправкуДокумента", ЭтаФорма, "ПередатьНаПодписание");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120, , ПредставлениеСервиса()); 	
		Иначе
			ВыполнитьОтправкуДокумента(КодВозвратаДиалога.Да, "ПередатьНаПодписание");
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОтказатьВСогласовании(Команда)
		
		ФИОПодписанта=	?(НЕ Organization.Certificate = Неопределено,  Organization.Certificate.Name, "");
		
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		3);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	ПредставлениеПодписи());
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаОтказатьВСогласовании");

	КонецПроцедуры
	
	&НаКлиенте
	Процедура Удалить(Команда)
		
		Оповещение=	Новый ОписаниеОповещения("ОбработчикУдалитьДокумент", ЭтаФорма);
		ПоказатьВопрос(Оповещение, "Вы действительно хотите удалить документ?", РежимДиалогаВопрос.ДаНет, 120, КодВозвратаДиалога.Нет, ПредставлениеСервиса(), КодВозвратаДиалога.Нет);

	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОтменитьСопоставление(Команда)
		
		ДобавитьСтатистику_НажалиОтменитьСопоставление();
		
		Оповещение 		= Новый ОписаниеОповещения("ОбработчикОтменитьСопоставление", ЭтаФорма);
		ТекстВопроса 	= НСтр("ru = 'Вы действительно хотите отменить сопоставление с документом 1С?'");
		ЗаголовокОкнаВопроса = ПредставлениеСервиса();
		
		ПоказатьВопрос(	Оповещение, 
						ТекстВопроса, 
						РежимДиалогаВопрос.ДаНет, 
						120, 
						КодВозвратаДиалога.Нет, 
						ЗаголовокОкнаВопроса, 
						КодВозвратаДиалога.Нет	);

	КонецПроцедуры
	
	&НаКлиенте
	Процедура СтруктураПодчиненности(Команда)
		
		ПараметрыФормы=	Новый Структура();
		ПараметрыФормы.Вставить("Режим", "СтруктураПодчиненности");
		
		ФормаСвязейДокументов= МетодКлиента(,"ПолучитьФормуОбработки", "ФормаСвязейДокументов", ПараметрыФормы, ЭтаФорма, СокрЛП(ЭДОбъект.DocumentID) + "/" + СокрЛП(ЭДОбъект.OrganizationID));
		ФормаСвязейДокументов.ЭДОбъект=		ЭДОбъект;
		ФормаСвязейДокументов.Organization=	Organization;
		
		ОткрытьФорму(ФормаСвязейДокументов);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОтразитьДокументВУчете(Команда)
		
		МетодКлиента("Модуль_Клиент", "ОтразитьДокументВУчете", ЭтаФорма, ЭДОбъект);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СоздатьДокументУчетаЗавершение(ПараметрыВозврата, ДополнительныеПараметры) Экспорт 
		
		ОбновитьДокумент1С();
		УправлениеФормой();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СоздатьДокумент1С(Команда)
		
		ИдентификаторФункциональности = "СоздатьДокумент1С_СтарыйИнтерфейс";
		Если ФункциональностьОграничена(ИдентификаторФункциональности) Тогда
			ПоказатьПредупреждениеОбОграниченнииФункциональности(ИдентификаторФункциональности);
			Возврат;
		КонецЕсли;
		
		ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
		
		Если ФорматЭД.ЭтоКорректировка Тогда
			
			ПоказатьПредупреждение( , "Создание корректировочных документов не поддерживается"
				,
				, Платформа.ПараметрыКлиент.СловарьWL.КраткоеНаименованиеСистемы);
			
			Возврат;
			
		КонецЕсли;
		
		Если ФорматЭД.ЕстьФункцияПД И ФорматЭД.ЕстьФункцияСФ Тогда
			
			СоздатьДокумент("ВводУПД");
			
		ИначеЕсли ФорматЭД.ЕстьФункцияПД Тогда
			
			СоздатьДокумент("Ввод");
			
		Иначе
			
			Если ЗначениеЗаполнено(ПодходящаяСФ) Тогда
				
				СозданныйДокумент = ПодходящаяСФ;
				Если ЗначениеЗаполнено(СозданныйДокумент) Тогда
					ОбработчикСозданиеДокумента(СозданныйДокумент);
				КонецЕсли;
				
			ИначеЕсли ЗначениеЗаполнено(МассивСсылокРНК) Тогда
				
				МетодКлиента("Модуль_ЛогикаПоведениеФорм", "СоздатьНовыйСчетФактуру"
					, ЭтаФорма
					, Контрагент
					, Организация
					, ЭДОбъект
					, МассивСсылокРНК);
				
			Иначе
				
				СоздатьДокумент("ВводСВыборомОснований");
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СопоставитьДокумент1С(Команда)
		
		ИдентификаторФункциональности = "СопоставитьДокумент1С_СтарыйИнтерфейс";
		Если ФункциональностьОграничена(ИдентификаторФункциональности) Тогда
			ПоказатьПредупреждениеОбОграниченнииФункциональности(ИдентификаторФункциональности);
			Возврат;
		КонецЕсли;
		
		СоздатьДокумент("Сопоставление");
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОтправитьСвязанныйДокумент(Команда)
		
		ИдентификаторФункциональности = "ОтправкаСвязанногоДокумента";
		Если ФункциональностьОграничена(ИдентификаторФункциональности) Тогда
			ПоказатьПредупреждениеОбОграниченнииФункциональности(ИдентификаторФункциональности);
			Возврат;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
								
			ШаблонПредупреждения	= НСтр("ru = 'С %1 не сопоставлено ни одной карточки контрагента в 1С'");
			ТекстПредупреждения		= СтрШаблон(ШаблонПредупреждения, ЭДОбъект.Counteragent.Name);
			
			ПоказатьПростоеПредупреждение(ТекстПредупреждения);
			
			Возврат;
			
		КонецЕсли;		
		
		ПараметрыФормы = Новый Структура;
		
		ПараметрыФормы.Вставить("InitialDocumentID", ЭДОбъект.DocumentID);
		ПараметрыФормы.Вставить("Организация", Организация);
		
		ОписаниеКонтрагента = Новый Структура;
		ОписаниеКонтрагента.Вставить("Контрагент"		, Контрагент);
		ОписаниеКонтрагента.Вставить("CounteragentName"	, ЭДОбъект.Counteragent.Name);
		ОписаниеКонтрагента.Вставить("CounteragentID"	, ЭДОбъект.Counteragent.ID);
		
		ПараметрыФормы.Вставить("ОписаниеКонтрагента", ОписаниеКонтрагента);
		
		Форма_Выгрузка = МетодКлиента(, "ОткрытьФормуОбработки", "Форма_Выгрузка", ПараметрыФормы, ЭтаФорма,,,, Истина);
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ЗапроситьАннулирование(Команда)
		
		ЗапроситьАннулированиеДокумента();
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ЗапроситьАннулированиеДокумента(ПереотправитьДокумент = Ложь)
		
		ФИОПодписанта = ФИОПодписанта();
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования",		 5);
		ПараметрыФормы.Вставить("ПредставлениеДокумента",	 Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи",		 ПредставлениеПодписи());
		
		МетодКлиента(, "ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаЗапросАннулирования", ПереотправитьДокумент);
		
	КонецПроцедуры

	&НаКлиенте
	Процедура АннулироватьДокумент(Команда)
		
		ПредставлениеДокумента = Заголовок;
		
		Подстроки = Новый Массив;
		Подстроки.Добавить(ПредставлениеДокумента);
		
		Если ПользовательАвторизованПоСертификату() Тогда
			
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(Новый ФорматированнаяСтрока("Соглашение об аннулировании будет подписано", , WebЦвета.ТемноКрасный));
			Подстроки.Добавить(Символы.ПС);
			Подстроки.Добавить(ПредставлениеПодписи());
			
		КонецЕсли;
		
		ТекстВопроса = Новый ФорматированнаяСтрока(Подстроки);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.ОК, "Аннулировать");
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		ОбработкаОтвета = Новый ОписаниеОповещения("ОбработчикОткрытиеФормыОтветаАннулироватьДокумент", ЭтаФорма);
		
		ПоказатьВопрос(ОбработкаОтвета
			, ТекстВопроса
			, Кнопки
			,
			,
			, "Аннулирование");
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ОтказатьВАннулировании(Команда)
		
		ФИОПодписанта = ФИОПодписанта();
						
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		6);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	ПредставлениеПодписи());
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаОтказатьВАннулировании");

	КонецПроцедуры

	&НаКлиенте
	Процедура ЗапроситьУточнение(Команда)
		
		ФИОПодписанта = ФИОПодписанта();
						
		ПараметрыФормы=		Новый Структура;
		ПараметрыФормы.Вставить("РежимИспользования", 		7);
		ПараметрыФормы.Вставить("ПредставлениеДокумента", 	Заголовок);
		ПараметрыФормы.Вставить("ПредставлениеПодписи", 	ПредставлениеПодписи());
		МетодКлиента(,"ОткрытьФормуОбработкиМодально", "ФормаОтветаПоДокументу", ПараметрыФормы, ЭтаФорма, "ОбработчикОткрытиеФормыОтветаЗапроситьУточнение", Новый Структура("Organization", Organization));

	КонецПроцедуры

	&НаКлиенте
	Процедура Декорация4Нажатие(Элемент)
		
		ПоказатьПредупреждениеОбУстаревшемФормате();
		
	КонецПроцедуры
	
	&НаКлиенте 
	Функция ПропатчитьInvoice(DocumentType)
		Если Лев( DocumentType, 7) = "Invoice" Тогда 
			Возврат  Лев( DocumentType, 7)
		Иначе	
			Возврат  DocumentType;
		КонецЕсли;	
	КонецФункции	
	
	&НаКлиенте
	Процедура Переотправить(Команда)
		
		ЗаголовокБлокирующихОкон = ПредставлениеСервиса();
		
		Если Не ЗначениеЗаполнено(Документ1С) Тогда
			ПоказатьПредупреждение(, "Для переотправки следует создать документ в 1С.", 120, ЗаголовокБлокирующихОкон);
			Возврат;
		КонецЕсли;
		
		ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
		
		Если Не ПоддерживаетсяОтправкаФормата(ФорматЭД) Тогда
			
			ТекстПредупреждения = СтрШаблон("Отправка документов в формате ""%1"" не поддерживается!"
				, ФорматЭД.Представление);
			
			ПоказатьПредупреждение(, ТекстПредупреждения
				, 120
				, ЗаголовокБлокирующихОкон);
			
			Возврат;
			
		КонецЕсли;
		
		Если НужноПредложитьЗапросНаАннулирование() Тогда
			ТекстВопроса = "Запросить аннулирование предыдущего документа?";
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОтветаВопросаЗапросаАннулирования", ЭтаФорма);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120,, ЗаголовокБлокирующихОкон);
		Иначе
			ПереотправитьДокумент();
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция НужноПредложитьЗапросНаАннулирование()
		
		Результат = Ложь;
		
		// для зашифрованных документов в статусах, отличных от "Подпись отправителя проверена и валидна",
		// отправку запроса на аннулирование предлагать не нужно
		Если ЭДОбъект.IsEncryptedContent 
			И ЭДОбъект.SenderSignatureStatus <> "SenderSignatureCheckedAndValid" Тогда
			Возврат Результат;
		КонецЕсли;
		
		// если ранее по документу уже отправлялся/получался запрос на аннулирование,
		// то новый запрос на аннулирование предлагать не нужно
		Если СтатусыАнулированияДокументовДиадок()[ЭДОбъект.RevocationStatus] <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
		
		ПроверяемыеСтатусы = Новый Соответствие;
		
		ПроверяемыеСтатусы.Вставить("Outbound" 	, Истина); //"Отправлен"
		
		ПроверяемыеСтатусы.Вставить("OutboundNotFinished"	, Истина); //"Документооборот не завершен"
		ПроверяемыеСтатусы.Вставить("OutboundFinished"		, Истина); //"Документооборот завершен"
		
		ПроверяемыеСтатусы.Вставить("OutboundWithRecipientSignature"	, Истина); //"Подписан"
		ПроверяемыеСтатусы.Вставить("OutboundWaitingForInvoiceReceipt"	, Истина); //"Ожидается извещение от покупателя"
		ПроверяемыеСтатусы.Вставить("OutboundWaitingForReceipt"			, Истина); //"Ожидается извещение от покупателя"
		
		ПроверяемыеСтатусы.Вставить("OutboundWaitingForRecipientSignature" 	, Истина); //"Требуется подпись"
		ПроверяемыеСтатусы.Вставить("OutboundNoRecipientSignatureRequest"	, Истина); //"Доставлен"
		
		ПроверяемыеСтатусы.Вставить("OutboundWaitingForInvoiceReceiptAndRecipientSignature"	, Истина); //"Ожидается извещение и подпись от покупателя"
		
		// если документ находится в перечисленных выше статусах,
		// то нужно предложить отправку запроса на аннулирование
		Если ПроверяемыеСтатусы[ЭДОбъект.Status] <> Неопределено Тогда
			Результат = Истина;
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция СтатусыАнулированияДокументовДиадок() Экспорт
		
		Результат= Новый Соответствие;
		
		Результат.Вставить("RevocationIsRequestedByMe",	 "Ожидается аннулирование");
		Результат.Вставить("RequestsMyRevocation",		 "Требуется аннулирование");
		Результат.Вставить("RevocationRejected",		 "Отказано в аннулировании");
		Результат.Вставить("RevocationAccepted",		 "Аннулирован");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция НовоеОписаниеДокументаПакета()
		
		Результат = МетодКлиента("Модуль_Клиент", "НовоеОписаниеДокументаПакета");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПереотправитьДокумент() Экспорт 
		
		МассивДокументовПакета = Новый Массив;
		
		ФорматОтправки = ФорматЭлектронногоДокумента(ЭДОбъект);
		
		ЭлементМассива = НовоеОписаниеДокументаПакета();
		
		ЭлементМассива.Вкл = Ложь;
		ЭлементМассива.Документ1С		 = Документ1С;
		ЭлементМассива.ТипДокумента		 = ТипДокумента;
		ЭлементМассива.ДопСведения		 = ДопСведения;
		ЭлементМассива.CounteragentBoxID = CounteragentBoxID;
		ЭлементМассива.DepartmentID		 = BoxID;
		ЭлементМассива.isTest			 = ЭДОбъект.isTest;
		ЭлементМассива.DocumentID		 = ЭДОбъект.documentID;
		ЭлементМассива.ФорматОтправки	 = ФорматОтправки;
		ЭлементМассива.DocumentType		 = ФорматОтправки.ИмяТипа;
		ЭлементМассива.DocumentFunction	 = ФорматОтправки.ФункцияДокумента;
		ЭлементМассива.ФункцияУПД		 = МетодКлиента("Модуль_Клиент", "ФункцияДокументаДляСбораКонтента", ФорматОтправки);
		
		МассивДокументовПакета.Добавить(ЭлементМассива);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("BoxID",					BoxID);
		ПараметрыФормы.Вставить("CounteragentBoxID",		CounteragentBoxID);
		ПараметрыФормы.Вставить("МассивДокументовПакета",	МассивДокументовПакета);
		ПараметрыФормы.Вставить("ТочкаВызова",				"ПереотправкаДокумента");
		
		МетодКлиента(, "ОткрытьФормуОбработкиМодально", "ФормаДокумента", ПараметрыФормы, ЭтаФорма);
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПоказатьКнопкуПодписатьЗапрошенный(UserPermissions)
		
		Результат = Ложь;
		
		Если Истина
			И ТочкаВызова = "ТаблицаДокументовВнутренних"
			И UserPermissions.CanSignDocuments // есть право подписи документов
			И ЭДОбъект <> Неопределено
			И ЭДОбъект.Status = "OutboundWaitingForSenderSignature" // Документ ожидает подписания перед отправкой (не отправлен и не подписан)
			Тогда 
			
			// основные условия выполнены, проверим, доступно ли подписание в текущем статусе согласования
			СтатусСогласования = СтатусСогласованияДокумента(ЭДОбъект);
			
			Если СтатусСогласования = Неопределено Тогда 
				
				// согласование не требуется
				Результат = Истина;
				
			ИначеЕсли СтатусСогласования = "Approved" Тогда 
				
				// документ уже согласован
				Результат = Истина;
				
			ИначеЕсли СтатусСогласования = "SignatureRequested" Тогда
				
				// Документ отправлен на подпись - проверяем, может ли его подписать текущий пользователь
				TargetUser = ЭДОбъект.ResolutionStatus.TargetUser;
				Результат = TargetUser = Неопределено 		// направлен на подпись любому пользователю подразделения
							Или TargetUser.IsCurrentUser; 	// направлен на подпись текущему пользователю
				
			Иначе 
				
				Результат = Ложь;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат Результат;
						
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьКнопкуПереотправить(UserPermissions)
		
		Результат = Ложь;
		
		Если ЭДОбъект <> Неопределено
			И ЭДОбъект.Direction = НаправлениеИсходящее()
			И UserPermissions.CanSignDocuments
			И (ЭДОбъект.isTest ИЛИ ПользовательАвторизованПоСертификату()) Тогда
			
			ФорматОтправки = ФорматЭлектронногоДокумента(ЭДОбъект);
			Результат = ПоддерживаетсяОтправкаФормата(ФорматОтправки);
			
		КонецЕсли;
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Функция ПоказатьКнопкуПодписатьИОтправить(UserPermissions)
		
		Если ТочкаВызова = "ТаблицаДокументовНаОтправку" Тогда
			Если Операция = "СоздатьНовыйПакет" Тогда 
				Возврат Истина;
			ИначеЕсли Операция = "ДобавитьВВыделенныеПакеты" Тогда
				Возврат Ложь;
			Иначе
				Возврат Истина;
			КонецЕсли;	
		ИначеЕсли ТочкаВызова = "ПереотправкаДокумента" Тогда
			Если ЗначениеЗаполнено(DocumentID) Тогда
				Возврат Истина;
			КонецЕсли;
		Иначе
			Возврат Ложь;
		КонецЕсли;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьСодержимоеПакета()
		
		Если ТочкаВызова = "ТаблицаДокументовНаОтправку" Тогда
			
			Если Операция = "СоздатьНовыйПакет" ИЛИ Операция = "ДобавитьВВыделенныеПакеты" Тогда
				Возврат Истина;
			Иначе
				
				// Панель со списком документов пакета покажем в двух случаях:
				ЭтоЕдиничныйДокумент = (ТаблицаДокументов.Количество() = 1);
				
				Если ЭтоЕдиничныйДокумент
					И ЗначениеЗаполнено(ТаблицаДокументов[0].ТекстОшибкиВалидации) Тогда
					
					Возврат Истина;  // Если единственный документ пакета содержит ошибки валидации
					
				Иначе
					
					Возврат (ТаблицаДокументов.Количество() > 1);  // Или в пакете несколько документов
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			Возврат Ложь;
		КонецЕсли;	
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьГруппаДокументыПакетаКнопкиВыделения()
		
		Возврат ТочкаВызова = "ТаблицаДокументовНаОтправку";
		
	КонецФункции
	
	&НаКлиенте
	Функция ПоказатьГруппаДокументыПакетаКнопкиРедактированияСостоваПакета()
		
		Возврат ТочкаВызова = "ТаблицаДокументовНаОтправку";
		
	КонецФункции
		
	&НаКлиенте
	Процедура ПодписатьИОтправить(Команда)
		
		ТекстОшибки = НеЗаполненныеПоляФайла();
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			Ошибки = МетодКлиента("Модуль_Клиент", "ОформитьОшибкиВHTML", ТекстОшибки, "Перед отправкой необходимо:");
			Элементы.Ошибки.Видимость = Истина;
			Возврат;
		КонецЕсли;
		
		Если Organization = Неопределено Тогда 
			Organization = Платформа.ПараметрыКлиент.КонтекстРаботаССерверомДиадок.DiadocConnection.GetOrganizationById(BoxID);
		КонецЕсли;
		
		Counteragent = Organization.GetCounteragentById(CounteragentBoxID);
		
		ЭтоТестоваяОрганизация	= МетодКлиента("Модуль_Клиент", "ЭтоТестоваяОрганизация", Organization);
		ЭтоТестовыйКонтрагент 	= МетодКлиента("Модуль_Клиент", "ЭтоТестоваяОрганизация", Counteragent);
		
		ЭтоТестовый = (ЭтоТестоваяОрганизация ИЛИ ЭтоТестовыйКонтрагент);
		
		Если  НЕ ЭтоТестовый 
			И НЕ ПользовательАвторизованПоСертификату() Тогда
			
			ПоказатьПредупреждение(,"Отправка юридически значимого документа возможна только по сертификату", 120, ПредставлениеСервиса());
			Возврат;
			
		КонецЕсли;
				
		МассивПомеченныеСтроки= ТаблицаДокументов.НайтиСтроки(Новый Структура("Вкл", Истина));
		Если МассивПомеченныеСтроки.Количество() = 0 Тогда
			ПоказатьПредупреждение(,"Нет документов, отмеченных для отправки", 120, ПредставлениеСервиса());
			Возврат;
		ИначеЕсли НЕ МассивПомеченныеСтроки.Количество() = ТаблицаДокументов.Количество() Тогда
			ТекстВопроса= 		"Были отмечены не все документы пакета. Продолжить отправку?";
			ОписаниеОповещения= Новый ОписаниеОповещения("ВыполнитьОтправкуДокумента", ЭтаФорма, "ПодписатьИОтправить");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120, , ПредставлениеСервиса()); 	
		Иначе
			ВыполнитьОтправкуДокумента(КодВозвратаДиалога.Да, "ПодписатьИОтправить");
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Функция НеЗаполненныеПоляФайла()
		
		СписокОшибок = "";
		
		Если ТочкаВызова = "ТаблицаДокументовНаОтправку" Тогда
			
			Если Операция = "СоздатьНовыйПакет" Тогда
			
				Если НЕ ЗначениеЗаполнено(Организация) Тогда
					СписокОшибок = СписокОшибок + "<p> - Организация</p>";
				КонецЕсли;
							
				Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
					СписокОшибок = СписокОшибок + "<p> - Контрагент</p>";
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СписокОшибок) Тогда
					СписокОшибок = "<p> </p>" + "<H3>Заполнить общие поля:</H3>" + СписокОшибок;
				КонецЕсли;
				
			КонецЕсли;
			
			ВсеОшибкиДокументов = "";
			
			Для каждого Документ из ТаблицаДокументов Цикл
				
				ОшибкиДокумента = ВалидироватьДанныеФайла(Документ.DocumentType, Документ.ДанныеФайла);
				
				Если ЗначениеЗаполнено(ОшибкиДокумента) Тогда
					ТекстЗаголовка = "<H3>" + Документ.Представление + "</H3>"; 
					Если ЗначениеЗаполнено(ВсеОшибкиДокументов) Тогда
						ВсеОшибкиДокументов = ВсеОшибкиДокументов + "<p> </p>";
					КонецЕсли;
					ВсеОшибкиДокументов = ВсеОшибкиДокументов + ТекстЗаголовка + ОшибкиДокумента;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ЗначениеЗаполнено(ВсеОшибкиДокументов) Тогда
				Если ЗначениеЗаполнено(СписокОшибок) Тогда
					СписокОшибок = СписокОшибок + "<p> </p>"; 
				КонецЕсли;
				
				СписокОшибок = СписокОшибок + "<H3>Для следующих документов заполнить поля:</H3>" + ВсеОшибкиДокументов;		
			КонецЕсли;
									
		КонецЕсли;
					
		Возврат СписокОшибок;
				
	КонецФункции
		
	&НаКлиенте
	Функция ВыполнитьОтправку(ПараметрыОтправкиНаСогласование = Неопределено)
		
		Результат = Истина;
		
		Попытка 
			
			ПараметрыPackageSendTask = ПараметрыPackageSendTask(ПараметрыОтправкиНаСогласование);

			Если ПараметрыОтправкиНаСогласование = Неопределено Тогда
				МетодКлиента("Модуль_Клиент", "ПроверитьСертификатПоМассивуДокументов", ПараметрыPackageSendTask.МассивДокументовПакета, Organization.Id);
			КонецЕсли;
			
			СоответствиеCustomIdСсылке1С = Новый Соответствие;
			
			ЗаполненныйPackageSendTask = МетодКлиента("Модуль_Выгрузка", "ПолучитьЗаполненныйPackageSendTask", ПараметрыPackageSendTask, СоответствиеCustomIdСсылке1С);
			
			СоответствиеВПФСсылке = МетодКлиента("Модуль_Выгрузка", "PackageSendTask_В_Соответствие", ЗаполненныйPackageSendTask.PackageSendTask, СоответствиеCustomIdСсылке1С);
			
			МетодКлиента("Модуль_Выгрузка", "ПередОтправкойСервер", СоответствиеВПФСсылке, ЗаполненныйPackageSendTask.PackageSendTask.DocumentsToSend.Count, ЗаполненныйPackageSendTask.PackageSendTask.OperationId);
			
			DocumentPackage = ЗаполненныйPackageSendTask.PackageSendTask.Send();
			
			ПослеОтправкиПакета(DocumentPackage, СоответствиеCustomIdСсылке1С, СоответствиеВПФСсылке, ПараметрыОтправкиНаСогласование);
			
		Исключение
			
			Результат	 = Ложь;
			
			Ошибка = ИнформацияОбОшибке();
			ТекстОшибки = КраткоеПредставлениеОшибки(Ошибка);
			
			Если МетодКлиента("Модуль_Клиент", "ЭтоИнформацияОбОшибкахВалидации", Ошибка) Тогда
				
				Ошибки = ТекстОшибки;
				
			Иначе
				
				ОшибкиОтправки = МодульКлиент_ТекстОшибкиДиадок(ТекстОшибки);
				ОшибкиОтправки = МетодКлиента("Модуль_Клиент", "HTMLТекстОшибкиОтправки", ОшибкиОтправки);
				
			КонецЕсли;
			
			Если НЕ ЗаполненныйPackageSendTask = Неопределено Тогда
				МетодКлиента("Модуль_Выгрузка", "ПриОшибкеОтправкиСервер", ТекстОшибки, СоответствиеВПФСсылке, ЗаполненныйPackageSendTask.PackageSendTask.DocumentsToSend.Count);
			КонецЕсли;
			
			Для каждого ДокументПакета из ТаблицаДокументов Цикл
				ДокументПакета.Вкл = Ложь;
			КонецЦикла;
			
		КонецПопытки;
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция ПараметрыPackageSendTask(ПараметрыОтправкиНаСогласование)
		
		ПараметрыPackageSendTask= Новый Структура();
		
		ПараметрыPackageSendTask.Вставить("Получатель", 						Контрагент);
		
		FnsParticipantIdПолучателя = МетодСервера(, "ПолучитьИдентификаторЭДОДляКонтрагента", Контрагент);
		ПараметрыPackageSendTask.Вставить("FnsParticipantIdПолучателя", ?(ЗначениеЗаполнено(FnsParticipantIdПолучателя), FnsParticipantIdПолучателя, ""));
		
		ПараметрыPackageSendTask.Вставить("ПараметрыОтправкиНаСогласование", 	ПараметрыОтправкиНаСогласование);
		ПараметрыPackageSendTask.Вставить("ФИОПодписанта",                    	ФИОПодписанта());
		
		ПараметрыPackageSendTask.Вставить("IdОтправленого", 					"");
		ПараметрыPackageSendTask.Вставить("Организация", 						Организация);
		
		ПараметрыPackageSendTask.Вставить("Organization", 						Organization);
		ПараметрыPackageSendTask.Вставить("CounteragentID",						CounteragentBoxID);
		
		СтруктураПодразделения= Неопределено;
		
		МассивДокументовПакета= Новый Массив;
		Для каждого ДокументТаблицы из ТаблицаДокументов Цикл
			
			Если ДокументТаблицы.Вкл Тогда
				СтрокаМассива= Новый Структура();
				СтрокаМассива.Вставить("Документ", 						ДокументТаблицы.Документ1С);
				СтрокаМассива.Вставить("DocumentType", 					ДокументТаблицы.DocumentType);
				СтрокаМассива.Вставить("ТипДокумента", 					ДокументТаблицы.ТипДокумента);
				СтрокаМассива.Вставить("ДопПоле", 						ДокументТаблицы.ДопСведения);
				СтрокаМассива.Вставить("ВнешняяПечатнаяФорма", 			ДокументТаблицы.ВнешняяПечатнаяФорма);
				СтрокаМассива.Вставить("CustomDocumentId", 				"");
				СтрокаМассива.Вставить("DocumentId", 					ДокументТаблицы.DocumentId);
				СтрокаМассива.Вставить("СвязующийИдентификаторСтроки", 	ДокументТаблицы.IdСтроки);
				СтрокаМассива.Вставить("ФункцияУПД", 					ДокументТаблицы.ФункцияУПД);
				СтрокаМассива.Вставить("Организация", 					Организация);
				
				МассивДокументовПакета.Добавить(СтрокаМассива);
				
				Если СтруктураПодразделения = Неопределено И ЗначениеЗаполнено(ДокументТаблицы.Документ1С) Тогда
					СтруктураПодразделения= МетодСервераБезКонтекста("Модуль_ИнтеграцияУниверсальный", "ПолучитьПодразделениеПолучателя", ДокументТаблицы.Документ1С, Контрагент);	
				КонецЕсли;
				
				СтрокаМассива.Вставить("ЭтоФайл", 		ДокументТаблицы.ЭтоФайл);
				СтрокаМассива.Вставить("ДанныеФайла", 	ДокументТаблицы.ДанныеФайла);
				СтрокаМассива.Вставить("СвойстваФайла",	ДокументТаблицы.СвойстваФайла);
				
				СтрокаМассива.Вставить("ФорматОтправки", ДокументТаблицы.ФорматОтправки);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ПараметрыPackageSendTask.Вставить("МассивДокументовПакета", 			МассивДокументовПакета);
		
		Если СтруктураПодразделения = Неопределено Тогда
			ПараметрыPackageSendTask.Вставить("ReceiverDepartmentId", "");
		Иначе
			ПараметрыPackageSendTask.Вставить("ReceiverDepartmentId", СтруктураПодразделения.ToDepartmentID);
		КонецЕсли;
		
		ПараметрыPackageSendTask.Вставить("ЭтоПереотправка", ТочкаВызова = "ПереотправкаДокумента");
		
		Возврат ПараметрыPackageSendTask; 
		
	КонецФункции
	
	&НаКлиенте
	Процедура ПослеОтправкиПакета(DocumentPackage, СоответствиеCustomIdСсылке1С, СоответствиеВПФСсылке, ПараметрыОтправкиНаСогласование)
		
		МетодКлиента("Модуль_Выгрузка", "ОбработатьОтправленныйПакетДокументов", DocumentPackage, СоответствиеCustomIdСсылке1С, ПараметрыОтправкиНаСогласование); 
		
		Для каждого СтрокаТЧ из ТаблицаДокументов Цикл
			
			Если СтрокаТЧ.Вкл Тогда
				
				// Для очистки списка документов на отправку на форме выгрузки.
				СтрокаТЧ.Действие = "удалить";
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытияФормыОшибки(РезультатВыбора, ЗакрытьФорму) Экспорт
		
		Если ЗакрытьФорму = Истина Тогда
			Закрыть();
		КонецЕсли;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ПодписатьЗапрошенный(Команда)
		
		СтруктураSigner = МетодСервера(, "ПолучитьДанныеПодписиСогласующим", Организация);
		
		Если НЕ ЗначениеЗаполнено(СтруктураSigner.ДолжностьПодписанта) Тогда
			
			ФорматЭД = ФорматЭлектронногоДокумента(ЭДОбъект);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("BoxId", BoxId);
			ПараметрыФормы.Вставить("ФорматЭД", ФорматЭД);
			ПараметрыФормы.Вставить("Организация", Организация);
			
			МетодКлиента(, "ОткрытьФормуОбработкиМодально", "НастройкаПодписиАкт"
				, ПараметрыФормы
				, ЭтаФорма
				, "ОбработчикОткрытиеФормыНастройкиПодписи_ПодписаниеЗапрошенного"
				, Новый Структура("Document", ЭДОбъект));
			
		Иначе
			ПодписатьИОтправить_ПодписаниеЗапрошенного(СтруктураSigner, ЭДОбъект);	
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ОбработчикОткрытиеФормыНастройкиПодписи_ПодписаниеЗапрошенного(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
		
		Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
			ФИОПодписанта= 		РезультатЗакрытия.ФИО; 
			СтруктураSigner= 	МетодСервера(, "ПолучитьДанныеПодписиСогласующим", Организация);
			ПодписатьИОтправить_ПодписаниеЗапрошенного(СтруктураSigner, ДополнительныеПараметры.Document);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ПодписатьИОтправить_ПодписаниеЗапрошенного(СтруктураSigner, Document) 
		
		СтруктураРезультатОперации= МетодКлиента("Модуль_РаботаССерверомДиадок", "ПодписатьИОтправить_ПодписаниеЗапрошенного", СтруктураSigner, Document);
		
		ИмяТипа = Document.TypeNamedId;
		
		Если СтруктураРезультатОперации.ВыполненоУспешно Тогда
			
			Если ЛЕВ(ИмяТипа, 7) = "Invoice" Тогда
			
				ДокументСсылка1С = МетодСервера(,"DocumentID_2_Документ", Document.documentId, Document.OrganizationId, ИмяТипа = "Invoice");
				Если ЗначениеЗаполнено(ДокументСсылка1С) Тогда
					
					ТекстОшибки= ВнестиИзмененияВДокумент(ДокументСсылка1С);
					Если ЗначениеЗаполнено(ТекстОшибки) Тогда
						Сообщить("Не удалось изменить реквизиты документа Счет-фактура по причине: " + ТекстОшибки);	
					КонецЕсли;
					
				КонецЕсли;
							
			КонецЕсли;
			
			ОбновитьЭДОбъект();
			
		Иначе
			
			ТекстОшибкиПодписания = МодульКлиент_ТекстОшибкиДиадок(СтруктураРезультатОперации.ТекстОшибки);
			
			РасшифровкаОшибки = Новый Массив;
			РасшифровкаОшибки.Добавить(ТекстОшибкиПодписания);
			РасшифровкаОшибки.Добавить("BoxId: " + Document.OrganizationId);
			РасшифровкаОшибки.Добавить("DocumentId: " + Document.DocumentId);
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("Заголовок", 		"Ошибка при подписании документа");
			ПараметрыФормы.Вставить("ОписаниеОшибки", 	"Не удалось подписать документ");
			ПараметрыФормы.Вставить("Подробности", 		СтрСоединить(РасшифровкаОшибки, Символы.ПС));
			МетодКлиента(,"ОткрытьФормуОбработкиМодально", "Форма_ВыводОшибки", ПараметрыФормы, ЭтаФорма);
			
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаСервереБезКонтекста	
	Функция ВнестиИзмененияВДокумент(ДокументСсылка1С)
		
		ТекстОшибки= 		"";
		ВнесеныИзменения= 	Ложь;
		
		Попытка
			ДокументОбъект1С= ДокументСсылка1С.ПолучитьОбъект();
			
			Если НЕ ДокументОбъект1С.Метаданные().Реквизиты.Найти("КодСпособаВыставления") = Неопределено Тогда 
				Если НЕ ДокументОбъект1С.КодСпособаВыставления = 2 Тогда 
					ДокументОбъект1С.КодСпособаВыставления= 2;
					ВнесеныИзменения= Истина;
				КонецЕсли;
			КонецЕсли;
			Если НЕ ДокументОбъект1С.Метаданные().Реквизиты.Найти("ДатаВыставления") = Неопределено Тогда 
				Если НЕ ДокументОбъект1С.ДатаВыставления = НачалоДня(ТекущаяДата()) Тогда
					ДокументОбъект1С.ДатаВыставления= НачалоДня(ТекущаяДата());
					ВнесеныИзменения= Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если ВнесеныИзменения Тогда 
				Если ДокументОбъект1С.Проведен Тогда
					ДокументОбъект1С.Записать(РежимЗаписиДокумента.Проведение);
				Иначе
					ДокументОбъект1С.Записать();
				КонецЕсли;
			КонецЕсли;
			
		Исключение
			ТекстОшибки= ОписаниеОшибки();
		КонецПопытки;
		
		Возврат ТекстОшибки;
		
	КонецФункции
			
	&НаКлиенте
	Процедура Скрыть_Показать_Список(Команда)
		
		Если Элементы.ГруппаСодержимоеПакета.Видимость = Истина Тогда
			Элементы.КнопкаСкрытьПоказатьСоставПакета.Заголовок= ">>";
			УстановитьПодсказкуКомандыНаСервере("Скрыть_Показать_Список", "Показать список документов пакета");
			
			Элементы.ГруппаСодержимоеПакета.Видимость= 		Ложь;
			Элементы.ГруппаКнопкиКоманднойПанелиТаблицыДокументов.Видимость= Ложь;
		Иначе
			Элементы.КнопкаСкрытьПоказатьСоставПакета.Заголовок= "<<";
			УстановитьПодсказкуКомандыНаСервере("Скрыть_Показать_Список", "Скрыть список документов пакета");
			
			Элементы.ГруппаСодержимоеПакета.Видимость= 		Истина;
			Элементы.ГруппаКнопкиКоманднойПанелиТаблицыДокументов.Видимость= Истина;
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаСервере
	Процедура УстановитьПодсказкуКомандыНаСервере(ИмяКоманды, ТекстПодсказки)
		
		НашаКоманда= Команды.Найти(ИмяКоманды);
		НашаКоманда.Подсказка= ТекстПодсказки;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ТаблицаДокументовПакетаПриАктивизацииСтроки(Элемент)
				
		//перезаполняем реквизиты формы и кэшируем результат визуализации
		ТекущиеДанные = Элемент.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		УправлениеДоступностью_Кнопка_НастроитьПараметрыПодписи(ТекущиеДанные.ТипДокумента, ТекущиеДанные.ФункцияУПД);
		УправлениеДоступностью_Кнопки_РедактированиеСоставаПакета(ТекущиеДанные.ЭтоФайл);
		УправлениеДоступностью_Кнопка_СохранениеДокумента();
		
		Если IDТекущейСтроки = ТекущиеДанные.IdСтроки Тогда
			Возврат;
		КонецЕсли;
				
		IDТекущейСтроки = ТекущиеДанные.IdСтроки;
				
		ЗаполнитьРеквизитыФормыТекущимиДаннымТаблицыДокументов(ТекущиеДанные);
				
		Если ТекущиеДанные.ЭтоФайл Тогда
			
			Элементы.ГруппаВизуализацияДокумента.ТекущаяСтраница = Элементы.СтраницаБезВизуализации;
												
			ДанныеФайлаНаФорму(ТекущиеДанные);
			
			УстановитьВидимостьПоТипуДокумента();
					
			Если ЗначениеЗаполнено(ТекущиеДанные.ТекстОшибкиВалидации) Тогда
				Ошибки = ТекущиеДанные.ТекстОшибкиВалидации;
			Иначе
				Ошибки = "";
			КонецЕсли;
			
			Элементы.Ошибки.Видимость = ЗначениеЗаполнено(ТекущиеДанные.ТекстОшибкиВалидации);
								
		Иначе
			
			Элементы.ГруппаВизуализацияДокумента.ТекущаяСтраница = Элементы.СтраницаТабличноеПоле;
			
			Если КэшТабличныеДокументы = Неопределено Тогда
				ТабДокументЗакэширован = Ложь;
			Иначе
				ТабДокументЗакэширован = (НЕ КэшТабличныеДокументы.Получить(IDТекущейСтроки) = Неопределено);
			КонецЕсли;
			
			Ошибки = "";
			
			//При наличии ошибки, сознательно повторяем валидацию и построение табличного документа
			Если НЕ ТабДокументЗакэширован
				ИЛИ ЗначениеЗаполнено(ТекущиеДанные.ТекстОшибкиВалидации)
				ИЛИ ЗначениеЗаполнено(ТекущиеДанные.ТекстОшибкиФормата) Тогда
				
				ВизуализацияПечатнойФормыПоСтруктуре();
				
			Иначе
				
				ТабПоле = КэшТабличныеДокументы.Получить(IDТекущейСтроки);
				
				ТекстПолнойОшибки 	= ТекстПолнойОшибки();
				Ошибки 				= ТекстПолнойОшибки; 
				
			КонецЕсли;
			
			
			Элементы.Ошибки.Видимость = 	ЗначениеЗаполнено(ТекущиеДанные.ТекстОшибкиВалидации)
									   	ИЛИ ЗначениеЗаполнено(ОшибкиОтправки) 
									   	ИЛИ ЗначениеЗаполнено(ТекущиеДанные.ТекстОшибкиФормата);
						
		КонецЕсли;
						
		НастроитьЗаголовок();
								
	КонецПроцедуры

	&НаКлиенте
	Процедура ВыделитьВсеДокументыПакета(Команда)
		
		Для каждого СтрокаТаблицыДокументов из ТаблицаДокументов Цикл
						
			Если ЗначениеЗаполнено(СтрокаТаблицыДокументов.ВнешняяПечатнаяФорма) Тогда
							
				НаборСтрок= ТаблицаДокументов.НайтиСтроки(Новый Структура("Документ1С", СтрокаТаблицыДокументов.Документ1С));
				
				Для каждого Строка из НаборСтрок Цикл
														
					Если ЗначениеЗаполнено(Строка.ТекстОшибкиВалидации) Тогда
						Строка.Вкл= 				 Ложь;
						СтрокаТаблицыДокументов.Вкл= Ложь;
					ИначеЕсли МетодКлиента("Модуль_Клиент", "НеобходимоОграничениеНаОтправку", СтрокаТаблицыДокументов.Документ1С, СтрокаТаблицыДокументов.ВнешняяПечатнаяФорма, СтрокаТаблицыДокументов.Проведен) Тогда
						Строка.Вкл= 				 Ложь;
						СтрокаТаблицыДокументов.Вкл= Ложь;
					Иначе
						Строка.Вкл= ?(ЗначениеЗаполнено(Строка.DocumentID) = Истина, Строка.Вкл, Истина);	
					КонецЕсли;
										
				КонецЦикла;
				
			Иначе
				
				Если МетодКлиента("Модуль_Клиент", "НеобходимоОграничениеНаОтправку", СтрокаТаблицыДокументов.Документ1С, СтрокаТаблицыДокументов.ВнешняяПечатнаяФорма, СтрокаТаблицыДокументов.Проведен) Тогда 
					СтрокаТаблицыДокументов.Вкл= Ложь;
				Иначе
					СтрокаТаблицыДокументов.Вкл= ?(ЗначениеЗаполнено(СтрокаТаблицыДокументов.ТекстОшибкиВалидации), Ложь, Истина);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура СнятьВыделениеСДокументовПакета(Команда)
		
		Для каждого СтрокаТаблицыДокументов из ТаблицаДокументов Цикл
			СтрокаТаблицыДокументов.Вкл= Ложь;
		КонецЦикла;
		
	КонецПроцедуры

	&НаКлиенте
	Процедура ТаблицаДокументовПакетаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
		Отказ= Истина;
	КонецПроцедуры

	&НаКлиенте
	Процедура ТаблицаДокументовПакетаПередУдалением(Элемент, Отказ)
		Отказ= Истина;
	КонецПроцедуры

	&НаКлиенте
	Процедура ТаблицаДокументовПакетаВклПриИзменении(Элемент)
		
		ТекущаяСтрока= 	Элементы.ТаблицаДокументов.ТекущиеДанные;
		Документ= 		ТекущаяСтрока.Документ1С;
		
		Если ЗначениеЗаполнено(ТекущаяСтрока.DocumentID) = Ложь Тогда
			
			Если ЗначениеЗаполнено(ТекущаяСтрока.ТекстОшибкиВалидации) И ТекущаяСтрока.Вкл = Истина Тогда
				ТекущаяСтрока.Вкл= Ложь;
				ТекстПредупреждения= "При проверке документа: """ +  ТекущаяСтрока.Представление + """ обнаружены ошибки. После исправления ошибок, документ будет доступен для отправки.";
				ПоказатьПредупреждение(, ТекстПредупреждения, 120, ПредставлениеСервиса());
				Возврат;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекущаяСтрока.ВнешняяПечатнаяФорма) = Истина И ТекущаяСтрока.Вкл = Истина Тогда
							
				НаборСтрок= ТаблицаДокументов.НайтиСтроки(Новый Структура("Документ1С", Документ));
				
				Для каждого Строка из НаборСтрок Цикл
					
					Если ЗначениеЗаполнено(Строка.ТекстОшибкиВалидации) Тогда
						
						Строка.Вкл= 		Ложь;
						ТекущаяСтрока.Вкл= 	Ложь;
																		
						ТекстПредупреждения= "При проверке документа основания: """ + Строка.Представление + """ обнаружены ошибки. "
											 "После исправления ошибок, внешняя печатная форма и документ основание будут доступны для отправки.";
						ПоказатьПредупреждение(, ТекстПредупреждения, 120, ПредставлениеСервиса());
						
						Возврат;
					
					ИначеЕсли МетодКлиента("Модуль_Клиент", "НеобходимоОграничениеНаОтправку", Документ, ТекущаяСтрока.ВнешняяПечатнаяФорма, ТекущаяСтрока.Проведен) Тогда
						
						Строка.Вкл= 		Ложь;
						ТекущаяСтрока.Вкл= 	Ложь;
						
						ТекстПредупреждения= "Документ основание: """ + Строка.Представление + """ валютный и не проведен." + Символы.ПС +
											 "Для отправки валютного документа, необходимо выполнить его проведение.";
						
						ПоказатьПредупреждение(, ТекстПредупреждения, 120, ПредставлениеСервиса());
						
						Возврат;
					Иначе
						Строка.Вкл= ?(ЗначениеЗаполнено(Строка.DocumentID) = Истина, Строка.Вкл, Истина);	
					КонецЕсли;
										
				КонецЦикла;
										
			ИначеЕсли ЗначениеЗаполнено(ТекущаяСтрока.ВнешняяПечатнаяФорма) = Ложь И ТекущаяСтрока.Вкл = Ложь Тогда
									
				НаборСтрок= ТаблицаДокументов.НайтиСтроки(Новый Структура("Документ1С", Документ));
									
				Для каждого Строка из НаборСтрок Цикл
					Если ЗначениеЗаполнено(Строка.ВнешняяПечатнаяФорма) = Истина Тогда
						Строка.Вкл= Ложь;
					КонецЕсли;
				КонецЦикла;
								
			КонецЕсли;
					
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ДобавитьФайлыВПакет(Команда)
		
		ДиалогВыбора = МетодКлиента("Модуль_Клиент", "ЗаполненныйДиалогВыбораФайла", Истина, КаталогВыбораФайлов);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Организация"	, Организация);
		ДополнительныеПараметры.Вставить("Контрагент"	, Контрагент);
		
		ОбработкаВыбораФайла = Новый ОписаниеОповещения("ВыполнитьДействиеПослеВыбораФайла"
			, ЭтаФорма
			, ДополнительныеПараметры);
		
		ДиалогВыбора.Показать(ОбработкаВыбораФайла);
		
	КонецПроцедуры
			
	&НаКлиенте
	Процедура ЗаменитьФайлВПакете(Команда)
		
		ДиалогВыбора = МетодКлиента("Модуль_Клиент", "ЗаполненныйДиалогВыбораФайла", Ложь, КаталогВыбораФайлов);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Организация"	, Организация);
		ДополнительныеПараметры.Вставить("Контрагент"	, Контрагент);
		
		ОбработкаВыбораФайла = Новый ОписаниеОповещения("ВыполнитьДействиеПослеРедактированияВыбранногоФайла"
			, ЭтаФорма
			, ДополнительныеПараметры);
		
		ДиалогВыбора.Показать(ОбработкаВыбораФайла);
		
	КонецПроцедуры
		
	&НаКлиенте
	Процедура УдалитьФайлВПакете(Команда)
		
		ТекстВопроса= "Вы действительно хотите удалить файл из пакета?";
		ДополнительныеПараметры= Новый Структура("ИдентификаторТекущейСтроки", Элементы.ТаблицаДокументов.ТекущаяСтрока);
		
		ОписаниеОповещения= Новый ОписаниеОповещения("ОбработчикОповещенияВопросУдалитьФайлИзПакета", ЭтаФорма, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120, , ПредставлениеСервиса()); 
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ДобавитьВВыделенныеПакеты(Команда)
		
		ТекстОшибки= НеЗаполненныеПоляФайла();
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			Ошибки= МетодКлиента("Модуль_Клиент", "ОформитьОшибкиВHTML", ТекстОшибки, "Перед добавлением в пакет необходимо:");
			Элементы.Ошибки.Видимость= Истина;
			Возврат;
		КонецЕсли;
				
		ОтмеченныеФайлы= ТаблицаДокументов.НайтиСтроки(Новый Структура("Вкл", Истина));
		Если ОтмеченныеФайлы.Количество() = 0 Тогда
			ПоказатьПредупреждение(, "Укажите флажками документы, которые необходимо добавить в выделенные ранее пакеты", 120, ПредставлениеСервиса());
			Возврат;
		ИначеЕсли НЕ ОтмеченныеФайлы.Количество() = ТаблицаДокументов.Количество() Тогда
			ТекстВопроса= 		"Были отмечены не все файлы в списке. Продолжить операцию?";
			ОписаниеОповещения= Новый ОписаниеОповещения("ОбработчикОповещенияВопросВыбраныНеВсеФайлы", ЭтаФорма);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 120, , ПредставлениеСервиса());	
		Иначе
			ПередатьФайлыДляДобавленияВПакеты(Истина);
		КонецЕсли;
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СохранитьДокумент(Команда)
		
		ТекСтрока 		= ТекущаяСтрокаТаблицыДокументов();
		ФорматОтправки	= ТекСтрока.ФорматОтправки;
		
		ФорматДоступенДляВыгрузки = ФорматДокументаДляВыгрузки(ФорматОтправки);
		
		Если ФорматДоступенДляВыгрузки Тогда
			ВыполнитьСохранениеДокументаXML();
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Скачивание документов в XML доступно только для 155, 820, 189 формата.'")
									+ Символы.ПС + НСтр("ru = 'Документ можно скачать из веб-интерфейса после отправки в Диадок.'"));
		КонецЕсли;
		
	КонецПроцедуры
	
//} КОМАНДЫ
////////////////////////////////////////////////////////////////////////////////

//{ РЕДАКТИРОВАНИЕ ПАКЕТА
////////////////////////////////////////////////////////////////////////////////

	&НаКлиенте
	Процедура УстановитьВидимостьПоТипуДокумента()
		
		Элементы.ГруппаNonformalized.Видимость 			= Видимость_ГруппаNonformalized();
		Элементы.ГруппаNonformalizedProforma.Видимость	= (DocumentType = "ProformaInvoice");
		Элементы.Группа_PriceList.Видимость				= (DocumentType = "PriceList");
		Элементы.Группа_AcceptanceCertificate.Видимость	= Видимость_ГруппаAcceptanceCertificate();
		Элементы.Группа_Contract.Видимость				= (DocumentType = "Contract");
		
		Элементы.Неформализованный_ЗапроситьПодписьКонтрагента.Видимость = Видимость_НеформализованныйЗапроситьПодписьКонтрагента();
		
		Если DocumentType = "Contract" Тогда 
			Элементы.Договор_Цена.Доступность = НЕ Договор_ЦенаНеУказана;
		КонецЕсли;
					
	КонецПроцедуры
	
	&НаКлиенте
	Функция Видимость_ГруппаNonformalized()
		
		Результат = 	DocumentType = "Nonformalized" 
					ИЛИ DocumentType = "PriceListAgreement" 
					ИЛИ DocumentType = "CertificateRegistry" 
					ИЛИ DocumentType = "ServiceDetails" 
					ИЛИ DocumentType = "ReconciliationAct";
					
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Видимость_НеформализованныйЗапроситьПодписьКонтрагента()
		
		Результат = 	DocumentType = "Nonformalized" 
					ИЛИ DocumentType = "PriceListAgreement" 
					ИЛИ DocumentType = "CertificateRegistry";
					
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Функция Видимость_ГруппаAcceptanceCertificate()
		
		Результат = 	DocumentType = "Torg12"
					ИЛИ DocumentType = "AcceptanceCertificate";  // у этих документов идентичные метаданные
					
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ОчиститьРеквизитыФормы(ОбрабатываемыйТип)
		
		Если 	ОбрабатываемыйТип = "Torg12"
			ИЛИ ОбрабатываемыйТип = "AcceptanceCertificate" Тогда
				
			ЗапроситьПодписьКонтрагента = Ложь;
			
			ДокументНомер		= "";
			ДокументДата		= '00010101';
			ДокументСуммаСНДС	= 0;
			ДокументСуммаНДС	= 0;
			ДокументОснования	= "";
			ДокументБезНДС		= Ложь;
			Комментарий			= "";		
			
		ИначеЕсли ОбрабатываемыйТип = "Contract" Тогда
			
			Договор_Тип		= "";
			ДокументНомер	= "";
			ДокументДата	= '00010101';
			ДокументЦена	= 0;
			Комментарий		= "";
			Договор_ЦенаНеУказана = Ложь;
			
		ИначеЕсли ОбрабатываемыйТип = "ProformaInvoice" Тогда
			
			ДокументНомер		= "";
			ДокументДата		= '00010101';
			ДокументСуммаСНДС	= 0;
			ДокументСуммаНДС	= 0;
			ДокументОснования	= "";
			ДокументБезНДС		= Ложь;
			Комментарий			= "";
			
		ИначеЕсли ОбрабатываемыйТип = "PriceList" Тогда
			
			ДокументНомер				= "";
			ДокументДата				= '00010101';
			ЦеновойЛист_ВступаетВСилу	= '00010101';
			ДокументНомерДоговора		= "";
			ДокументДатаДоговора		= "";
			Комментарий					= "";		
			
		ИначеЕсли 	ОбрабатываемыйТип = "Nonformalized"
			ИЛИ 	ОбрабатываемыйТип = "PriceListAgreement"
			ИЛИ		DocumentType = "CertificateRegistry" Тогда
			
			ЗапроситьПодписьКонтрагента = Ложь;
			ДокументНомер	= "";
			ДокументДата	= '00010101';
			Комментарий		= ""; 
			
		ИначеЕсли DocumentType = "ServiceDetails" ИЛИ DocumentType = "ReconciliationAct" Тогда 	
			
			ДокументНомер	= "";
			ДокументДата	= '00010101';
			Комментарий		= "";
			
		Иначе
			
			СообщитьОбОтсутствииКэширования();
									
		КонецЕсли;	
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура ИзменитьПредставлениеДокументаВТаблице()
		
		ТекущиеДанные = Элементы.ТаблицаДокументов.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ТипыНеформализованных = ТипыНеформализованныхДокументов();
		
		Представление = ТипыНеформализованных.Получить(DocumentType);
		
		Если Представление = Неопределено Тогда
			Представление = ТипыНеформализованных.Получить("Nonformalized");
		КонецЕсли;
								
		Если ЗначениеЗаполнено(ДокументНомер) Тогда
			Представление = Представление + " № " + ДокументНомер;
		КонецЕсли;
		Если ЗначениеЗаполнено(ДокументДата) Тогда
			Представление = Представление + " от " + Формат(ДокументДата, "ДЛФ=Д");	
		КонецЕсли;
		
		ТекущиеДанные.Представление = Представление; 
							
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПредставлениеРазмераФайла(РазмерВБайтах)
		
		Если РазмерВБайтах >= 1048576 Тогда // Размер больше или равен 1 мегабайт
			Возврат Строка(ОКР(РазмерВБайтах / 1048576, 1)) + " МБ";
		ИначеЕсли РазмерВБайтах >= 1024 Тогда // Размер больше или равен 1 килобайт
			Возврат Строка(ОКР(РазмерВБайтах / 1024, 1)) + " КБ";
		Иначе
			Возврат Строка(РазмерВБайтах) + " Б";
		КонецЕсли;
		
	КонецФункции
				
	&НаКлиенте
	Процедура ПередатьФайлыДляДобавленияВПакеты(ПередатьВсе= Ложь)
		
		ПараметрЗакрытия= Новый Структура;
						
		КоллекцияФайлов= Новый Массив;
				
		Для каждого СтрокаТаблицыДокументов из ТаблицаДокументов Цикл
			
			Если НЕ ПередатьВсе И НЕ СтрокаТаблицыДокументов.Вкл Тогда
				Продолжить;
			КонецЕсли;
			
			ЭлементКоллекции= Новый Структура("IdСтроки, СвойстваФайла, ДанныеФайла, DocumentType, ФорматОтправки");
			
			ЭлементКоллекции.Вставить("IdСтроки", 		СтрокаТаблицыДокументов.IdСтроки);
			ЭлементКоллекции.Вставить("СвойстваФайла", 	СтрокаТаблицыДокументов.СвойстваФайла);
			ЭлементКоллекции.Вставить("ДанныеФайла", 	СтрокаТаблицыДокументов.ДанныеФайла);
			ЭлементКоллекции.Вставить("DocumentType", 	СтрокаТаблицыДокументов.DocumentType);
			ЭлементКоллекции.Вставить("ФорматОтправки", СтрокаТаблицыДокументов.ФорматОтправки);
			
			КоллекцияФайлов.Добавить(ЭлементКоллекции);
			
		КонецЦикла;
		
		ПараметрЗакрытия.Вставить("МассивНеформализованныхДокументов", КоллекцияФайлов);
				
		Закрыть(ПараметрЗакрытия);
		
	КонецПроцедуры
	
	////////////////////////////////////////////////////////////////////////////////
	//{ ЗАГОТОВКИ

		&НаКлиентеНаСервереБезКонтекста
		Функция Новый_СвойстваФайла()
			
			Возврат Новый Структура("Имя, ИмяБезРасширения, ПолноеИмя, Путь, Расширение, РазмерВБайтах");
			
		КонецФункции
		
		&НаКлиенте
		Процедура ОрганизацияПриИзменении(Элемент)
			
			ОбработатьИзменениеОрганизации();
								
		КонецПроцедуры
		
		&НаКлиенте
		Процедура КонтрагентПриИзменении(Элемент)
			
			CounteragentBoxID = МетодСервера(, "Контрагент_2_CounteragentBoxID", Контрагент);
			
			Если Organization = Неопределено ИЛИ НЕ ЗначениеЗаполнено(CounteragentBoxID) Тогда
				// только для операции "СоздатьНовыйПакет"
				Элементы.ГруппаПередачаНаСогласование.Видимость=  	Истина;
				Элементы.ГруппаПередачаНаСогласование.Доступность=  Ложь;	
			Иначе
				Если UserPermissions = Неопределено Тогда
					UserPermissions= Organization.GetUserPermissions();	
				КонецЕсли;
				Если ПоказатьКнопкуПередачиНаСогласование(UserPermissions) ИЛИ ПоказатьКнопкуПередачиНаПодпись(UserPermissions) Тогда
					Элементы.ГруппаПередачаНаСогласование.Видимость=  	Истина;
					Элементы.ГруппаПередачаНаСогласование.Доступность=  Истина;	
				Иначе
					Элементы.ГруппаПередачаНаСогласование.Видимость=  	Истина;
					Элементы.ГруппаПередачаНаСогласование.Доступность=  Ложь;	
				КонецЕсли;
			КонецЕсли;
								
		КонецПроцедуры
	
		&НаКлиенте
		Процедура БезВизуализации_FileNameНажатие(Элемент)
			
			Если НЕ Элементы.ТаблицаДокументов.ТекущиеДанные = Неопределено Тогда
				
				СтруктураСвойстваФайла= Элементы.ТаблицаДокументов.ТекущиеДанные.СвойстваФайла;
				ЗапуститьПриложение(СтруктураСвойстваФайла.ПолноеИмя);
				
			КонецЕсли;
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура ТипДокументаПриИзменении(Элемент)
			
			ТекущиеДанные = Элементы.ТаблицаДокументов.ТекущиеДанные;
			
			Если ТекущиеДанные = Неопределено Тогда
				
				DocumentType = ТипДокумента;
				УстановитьВидимостьПоТипуДокумента();
				
				Возврат;
				
			КонецЕсли;
						
			Если НЕ ТекущиеДанные.DocumentType = ТипДокумента Тогда
				
				ОчиститьРеквизитыФормы(ТекущиеДанные.DocumentType);
				
				ТекущиеДанные.DocumentType = ТипДокумента;
				DocumentType = ТипДокумента;
				
				ТекущиеДанные.ДанныеФайла    = Новый_ДанныеФайла();
				ТекущиеДанные.ФорматОтправки = МетодКлиента("Модуль_Выгрузка", "ОписаниеФорматаДляОтправкиФайла", ТекущиеДанные.DocumentType);
				
				УстановитьВидимостьПоТипуДокумента();
				
				ДанныеФайлаНаФорму(ТекущиеДанные);
				
				ИзменитьПредставлениеДокументаВТаблице();
				
				СообщитьОЗаполненииОбязательныхПолей(ТекущиеДанные,DocumentType);
								
			КонецЕсли;
			
		КонецПроцедуры
		
		&НаКлиенте
		Процедура СообщитьОЗаполненииОбязательныхПолей(ТекущиеДанные, DocumentType)
			
			ДанныеФайла= ТекущиеДанные.ДанныеФайла;
			
			СписокОшибок = ВалидироватьДанныеФайла(DocumentType, ДанныеФайла);
														
			Если ЗначениеЗаполнено(СписокОшибок) Тогда
				ОшибкиВHTML = МетодКлиента("Модуль_Клиент", "ОформитьОшибкиВHTML", СписокОшибок, "Необходимо заполнить поля:");	
				ТекущиеДанные.ТекстОшибкиВалидации = ОшибкиВHTML;
				Ошибки = ОшибкиВHTML;
			Иначе
				ТекущиеДанные.ТекстОшибкиВалидации = "";
				Ошибки = "";
			КонецЕсли;
						
			Элементы.Ошибки.Видимость= ЗначениеЗаполнено(СписокОшибок);
			
		КонецПроцедуры
			
		// Nonfomalized
	
		&НаКлиенте
		Процедура Неформализованный_ЗапроситьПодписьКонтрагентаПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("NeedRecipientSignature", ЗапроситьПодписьКонтрагента);
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Неформализованный_НомерПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("DocumentNumber", ДокументНомер);
			ИзменитьПредставлениеДокументаВТаблице();
			
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
										
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Неформализованный_ДатаПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("DocumentDate", ДокументДата);
			ИзменитьПредставлениеДокументаВТаблице();
			
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
										
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Неформализованный_КомментарийПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("Comment", СокрЛП(Элемент.ТекстРедактирования));
			
		КонецПроцедуры
	
		//Nonfomalized Proforma
	
		&НаКлиенте
		Процедура Счет_НомерПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("DocumentNumber", ДокументНомер);
			ИзменитьПредставлениеДокументаВТаблице();
			
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
								
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Счет_ДатаПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("DocumentDate", ДокументДата);
			ИзменитьПредставлениеДокументаВТаблице();
			
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
						
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Счет_СуммаСНДСПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("Total", ДокументСуммаСНДС);
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Счет_СуммаНДСПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("Vat", ДокументСуммаНДС);
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Счет_БезНДСПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("БезНДС", ДокументБезНДС);
			
			Если ДокументБезНДС Тогда
				ДокументСуммаНДС= 0;
			КонецЕсли;
			
			Элементы.Счет_СуммаНДС.Доступность= НЕ ДокументБезНДС;
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Счет_ОснованиеПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("Grounds", ДокументОснования);
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Счет_КомментарийПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("Comment", Комментарий);
			
		КонецПроцедуры
	
		//Acceptance Certificate
	
		&НаКлиенте
		Процедура АктВыпРабот_ЗапроситьПодписьКонтрагентаПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("NeedRecipientSignature", ЗапроситьПодписьКонтрагента);
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура АктВыпРабот_НомерПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("DocumentNumber", ДокументНомер);
			ИзменитьПредставлениеДокументаВТаблице();
			
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
						
		КонецПроцедуры
	
		&НаКлиенте
		Процедура АктВыпРабот_ДатаПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("DocumentDate", ДокументДата);
			ИзменитьПредставлениеДокументаВТаблице();
			
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
						
		КонецПроцедуры
	
		&НаКлиенте
		Процедура АктВыпРабот_СуммаСНДСПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("Total", ДокументСуммаСНДС);
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура АктВыпРабот_СуммаНДСПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("Vat", ДокументСуммаНДС);
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура ДокументБезНДСПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("БезНДС", ДокументБезНДС);
			
			Если ДокументБезНДС Тогда
				ДокументСуммаНДС= 0;
			КонецЕсли;
			
			Элементы.АктВыпРабот_СуммаНДС.Доступность= НЕ ДокументБезНДС;
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура АктВыпРабот_ОснованиеПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("Grounds", ДокументОснования);
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура АктВыпРабот_КомментарийПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("Comment", Комментарий);	
			
		КонецПроцедуры
	
		//Contract
	
		&НаКлиенте
		Процедура Договор_ТипПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("ContractType", Договор_Тип);	
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Договор_НомерПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("DocumentNumber", ДокументНомер);
			ИзменитьПредставлениеДокументаВТаблице();
			
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
						
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Договор_ДатаПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("DocumentDate", ДокументДата);
			ИзменитьПредставлениеДокументаВТаблице();
			
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
						
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Договор_ЦенаПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("ContractPrice", ДокументЦена);
			
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
						
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Договор_КомментарийПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("Comment", Комментарий);
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура Договор_ЦенаНеУказанаПриИзменении(Элемент)
			
			ИзменитьДанныеФайла("ЦенаНеУказана", Договор_ЦенаНеУказана);
			
			Если Договор_ЦенаНеУказана Тогда
				ДокументЦена= 0;
			КонецЕсли;
			
			Элементы.Договор_Цена.Доступность= НЕ Договор_ЦенаНеУказана;
			
			СообщитьОЗаполненииОбязательныхПолей(Элементы.ТаблицаДокументов.ТекущиеДанные, DocumentType);
						
		КонецПроцедуры

	
	////////////////////////////////////////////////////////////////////////////////
	//} ОБРАБОТЧИКИ СОБЫТИЙ
	
	////////////////////////////////////////////////////////////////////////////////
	//{ ОБРАБОТЧИКИ ОПОВЕЩЕНИЙ

		&НаКлиенте
		Процедура ВыполнитьДействиеПослеВыбораФайла(МассивАдресовФайлов, ДополнительныеПараметры) Экспорт
			
			Если Не ЗначениеЗаполнено(МассивАдресовФайлов) Тогда
				Возврат;
			КонецЕсли;
			
			ПервыйФайл = Новый Файл(МассивАдресовФайлов[0]);
			КаталогВыбораФайлов = ПервыйФайл.Путь;
			
			МассивБольшиеФайлы = Новый Массив;
			МетодКлиента("Модуль_Клиент", "ПроверитьРазмерВыбранныхФайлов", МассивАдресовФайлов, МассивБольшиеФайлы);
			
			Если МассивБольшиеФайлы.Количество() > 0 Тогда
				МетодКлиента("Модуль_Клиент", "ОбработатьМассивБольшихФайлов", МассивБольшиеФайлы, ЭтаФорма);
				МассивБольшиеФайлы = Неопределено;
			КонецЕсли;
			
			Для Каждого ВыбранныйФайл Из МассивАдресовФайлов Цикл
				
				ФайлДанных = Новый Файл(ВыбранныйФайл);
				
				НоваяСтрока = ТаблицаДокументов.Добавить();
				
				НоваяСтрока.Вкл                  = Истина;
				НоваяСтрока.Представление        = "Неформализованный";
				НоваяСтрока.DocumentType         = "Nonformalized"; //по умолчанию добавляемый файл имеет тип "Nonformalized", далее пользователь настраивает сам
				НоваяСтрока.ТекущиеДанные        = Ложь;
				НоваяСтрока.IdСтроки             = Новый УникальныйИдентификатор;
				НоваяСтрока.ТекстОшибкиВалидации = "";
				НоваяСтрока.ЭтоФайл              = Истина; 
				
				НоваяСтрока.ДанныеФайла		 = МетодКлиента("Модуль_Клиент", "Новый_NonformalizedDocumentToSend");
				НоваяСтрока.СвойстваФайла	 = Новый_СвойстваФайла();
				НоваяСтрока.ФорматОтправки	 = МетодКлиента("Модуль_Выгрузка", "ОписаниеФорматаДляОтправкиФайла", НоваяСтрока.DocumentType);
				
				ЗаполнитьСвойстваФайла(НоваяСтрока.СвойстваФайла, ФайлДанных);
				
			КонецЦикла;
			
			НастроитьЗаголовок();
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура ВыполнитьДействиеПослеРедактированияВыбранногоФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
			
			Если Не ЗначениеЗаполнено(ВыбранныеФайлы) Тогда
				Возврат;
			КонецЕсли;
			
			ФайлДанных = Новый Файл(ВыбранныеФайлы[0]);
			КаталогВыбораФайлов = ФайлДанных.Путь;
			
			Если ФайлДанных.Размер() > (5*1024*1024) Тогда
				
				ТекстПредупреждения= "Размер отправляемого файла не должен превышать 5Мб. Размер выбранного файла " + ПредставлениеРазмераФайла(ФайлДанных.Размер());
				ПоказатьПредупреждение(, ТекстПредупреждения, 120, ПредставлениеСервиса()); 
				
			Иначе
				
				ТекущиеДанные = Элементы.ТаблицаДокументов.ТекущиеДанные;
				ЗаполнитьЗначенияСвойств(ТекущиеДанные.СвойстваФайла, ФайлДанных);
				ТекущиеДанные.СвойстваФайла.РазмерВБайтах = ФайлДанных.Размер();
			
				Элементы.БезВизуализации_FileName.Заголовок = ФайлДанных.Имя;
				Элементы.БезВизуализации_РазмерФайла.Заголовок = ПредставлениеРазмераФайла(ФайлДанных.Размер());
				
			КонецЕсли;
			
		КонецПроцедуры
	
		&НаКлиенте
		Процедура ОбработчикОповещенияВопросУдалитьФайлИзПакета(РезультатВопроса, ДополнительныеПараметры) Экспорт
			
			Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
				
				ИдентификаторТекущейСтроки= ДополнительныеПараметры.ИдентификаторТекущейСтроки;
				
				ТекущаяСтрока= ТаблицаДокументов.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
				
				ТаблицаДокументов.Удалить(ТекущаяСтрока);
							
				НастроитьЗаголовок();
				
				Если ТаблицаДокументов.Количество() = 0 Тогда
					ОчиститьРеквизитыФормы(ТипДокумента);
					Элементы.БезВизуализации_FileName.Заголовок= 	"";
					Элементы.БезВизуализации_РазмерФайла.Заголовок= "";
				КонецЕсли;
								
			КонецЕсли;
			
		КонецПроцедуры
		
	
	////////////////////////////////////////////////////////////////////////////////
	//} ОБРАБОТЧИКИ ОПОВЕЩЕНИЙ
	
	////////////////////////////////////////////////////////////////////////////////
	//{ РАБОТА С КЭШ

		&НаКлиенте
		Процедура ИнициализироватьКэшТабличныеДокументы()
			
			Если КэшТабличныеДокументы = Неопределено Тогда
				КэшТабличныеДокументы= Новый Соответствие;
			КонецЕсли;
			
		КонецПроцедуры
		
		&НаКлиенте
		Процедура ЗаполнитьСвойстваФайла(СтруктураСвойстваФайла, ФайлДанных)
							
			ЗаполнитьЗначенияСвойств(СтруктураСвойстваФайла, ФайлДанных);
			СтруктураСвойстваФайла.РазмерВБайтах= ФайлДанных.Размер();
										
		КонецПроцедуры
	
		&НаКлиенте
		Процедура ИзменитьДанныеФайла(ИмяСвойстваОбъекта, ЗначениеСвойства)
			
			ДанныеТекущейСтроки= Элементы.ТаблицаДокументов.ТекущиеДанные;
			
			Если ДанныеТекущейСтроки = Неопределено Тогда
				Возврат;
			КонецЕсли;
						
			ДанныеТекущейСтроки.ДанныеФайла[ИмяСвойстваОбъекта]= ЗначениеСвойства;
						
		КонецПроцедуры
	
		&НаКлиенте
		Процедура ДанныеФайлаНаФорму(ТекущиеДанные)
			
			ТипДокумента = DocumentType;
			
			СтруктураДанныеФайла	= ТекущиеДанные.ДанныеФайла;
			СтруктураСвойстваФайла	= ТекущиеДанные.СвойстваФайла;
			
			Если 	DocumentType = "AcceptanceCertificate"
				Или DocumentType = "Torg12" Тогда
				
				ЗапроситьПодписьКонтрагента = СтруктураДанныеФайла.NeedRecipientSignature;
			
				ДокументНомер		= СтруктураДанныеФайла.DocumentNumber;
				ДокументДата		= СтруктураДанныеФайла.DocumentDate;
				ДокументСуммаСНДС	= СтруктураДанныеФайла.Total;
				ДокументСуммаНДС	= СтруктураДанныеФайла.Vat;
				ДокументБезНДС		= СтруктураДанныеФайла.БезНДС;
				ДокументОснования	= СтруктураДанныеФайла.Grounds;
				Комментарий			= СтруктураДанныеФайла.Comment;
				
			ИначеЕсли DocumentType = "Contract" Тогда
				
				Договор_Тип				= СтруктураДанныеФайла.ContractType;
				ДокументНомер			= СтруктураДанныеФайла.DocumentNumber;
				ДокументДата			= СтруктураДанныеФайла.DocumentDate;
				ДокументЦена			= СтруктураДанныеФайла.ContractPrice;
				Договор_ЦенаНеУказана	= СтруктураДанныеФайла.ЦенаНеУказана;
				Комментарий				= СтруктураДанныеФайла.Comment;
				
			ИначеЕсли DocumentType = "ProformaInvoice" Тогда
				
				ДокументНомер		= СтруктураДанныеФайла.DocumentNumber;
				ДокументДата		= СтруктураДанныеФайла.DocumentDate;
				ДокументСуммаНДС	= СтруктураДанныеФайла.Vat;
				ДокументСуммаСНДС	= СтруктураДанныеФайла.Total;
				ДокументОснования	= СтруктураДанныеФайла.Grounds;
				ДокументБезНДС		= СтруктураДанныеФайла.БезНДС;
				Комментарий			= СтруктураДанныеФайла.Comment;
				
			ИначеЕсли DocumentType = "Nonformalized" 
				ИЛИ DocumentType = "PriceListAgreement" 
				ИЛИ DocumentType = "CertificateRegistry" Тогда
				
				ЗапроситьПодписьКонтрагента = СтруктураДанныеФайла.NeedRecipientSignature;
				ДокументНомер	= СтруктураДанныеФайла.DocumentNumber;
				ДокументДата	= СтруктураДанныеФайла.DocumentDate;
				Комментарий		= СтруктураДанныеФайла.Comment; 
										
			ИначеЕсли DocumentType = "ServiceDetails" 
				ИЛИ DocumentType = "ReconciliationAct" Тогда
				
				ДокументНомер	= СтруктураДанныеФайла.DocumentNumber;
				ДокументДата	= СтруктураДанныеФайла.DocumentDate;
				Комментарий		= СтруктураДанныеФайла.Comment;	
				
			Иначе
				
				СообщитьОбОтсутствииКэширования();
				
			КонецЕсли;
			
			Элементы.БезВизуализации_FileName.Заголовок = СтруктураСвойстваФайла.Имя;
				
			РазмерФайлаНаФорму = "";
			РазмерФайлаНаФорму = ПредставлениеРазмераФайла(СтруктураСвойстваФайла.РазмерВБайтах);
			
			Элементы.БезВизуализации_РазмерФайла.Заголовок = РазмерФайлаНаФорму;
						
		КонецПроцедуры
		
		&НаКлиенте
		Процедура СообщитьОбОтсутствииКэширования()
			
			ТекстСообщения = НСтр("ru = 'Кэширование для данного типа документа еще не реализовано'");
			МетодКлиента("Модуль_Клиент", "СообщитьПользователю", ТекстСообщения);
			
		КонецПроцедуры
			
		&НаКлиенте
		Функция Новый_ДанныеФайла()
			
			Если ТипДокумента = "Nonformalized" Тогда
				
				Результат = МетодКлиента("Модуль_Клиент", "Новый_NonformalizedDocumentToSend");
				
			ИначеЕсли ТипДокумента = "PriceListAgreement" Тогда
				
				Результат = МетодКлиента("Модуль_Клиент", "Новый_NonformalizedDocumentToSend");
				Результат.NeedRecipientSignature = Истина;
				
			ИначеЕсли ТипДокумента = "CertificateRegistry" Тогда 
				
				Результат = МетодКлиента("Модуль_Клиент", "Новый_NonformalizedDocumentToSend");
				
			ИначеЕсли ТипДокумента = "ServiceDetails" ИЛИ ТипДокумента = "ReconciliationAct" Тогда
				
				Результат = МетодКлиента("Модуль_Клиент", "Новый_ServiceDetails");
				
			ИначеЕсли ТипДокумента = "ProformaInvoice" Тогда
				
				Результат = МетодКлиента("Модуль_Клиент", "Новый_NonformalizedProforma");
				
			ИначеЕсли ТипДокумента = "PriceList" Тогда
				
				Результат = МетодКлиента("Модуль_Клиент", "Новый_PriceList");
				
			ИначеЕсли ТипДокумента = "AcceptanceCertificate" Тогда
				
				Результат = МетодКлиента("Модуль_Клиент", "Новый_AcceptanceCertificate");
				
			ИначеЕсли ТипДокумента = "Torg12" Тогда
				
				Результат = МетодКлиента("Модуль_Клиент", "Новый_Torg12");
				
			ИначеЕсли ТипДокумента = "Contract" Тогда
				
				Результат = МетодКлиента("Модуль_Клиент", "Новый_Contract");
				
			Иначе
				
				Результат = Неопределено;
										
			КонецЕсли;
			
			Возврат Результат;
									
		КонецФункции
	
	////////////////////////////////////////////////////////////////////////////////
	//} РАБОТА С КЭШ 


//} РЕДАКТИРОВАНИЕ ПАКЕТА
////////////////////////////////////////////////////////////////////////////////

	////////////////////////////////////////////////////////////////////////////////
	//{ СОХРАНЕНИЕ ДОКУМЕНТА В XML
	
	&НаКлиенте
	Функция ФорматДокументаДляВыгрузки(ФорматОтправки)
		
		БазовыеФорматы = БазовыеФорматы();
		
		ДопустимыеФорматы = Новый Структура;
		
		ДопустимыеФорматы.Вставить(БазовыеФорматы.utd820);
		ДопустимыеФорматы.Вставить(БазовыеФорматы.ucd736);
		ДопустимыеФорматы.Вставить(БазовыеФорматы.utd);
		ДопустимыеФорматы.Вставить(БазовыеФорматы.ucd);
				
		Результат = ДопустимыеФорматы.Свойство(ФорматОтправки.БазовыйФормат);
		
		Возврат Результат;
		
	КонецФункции

	&НаКлиенте
	Процедура СохранениеДокументаXML(ТипАвторизации) Экспорт
				
		ПараметрыСохранения = ПараметрыСохраненияДокументаXML();
		
		Если ЗначениеЗаполнено(ПараметрыСохранения.ПолномочияПодписанта) Или ТипАвторизации = "Login" Тогда
			
			ПоказатьДиалогВыбораКаталогаСохраненияXML(ПараметрыСохранения);
			
		Иначе
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПолномочияПодписантаДляСохраненияXML",
															ЭтаФорма,
															ПараметрыСохранения);
			
			КнопкиДиалога = Новый СписокЗначений;
			КнопкиДиалога.Добавить(КодВозвратаДиалога.ОК, "Заполнить полномочия");
			КнопкиДиалога.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
			
			ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Требуется заполнить полномочия подписанта'")
				, КнопкиДиалога
				, 60
				, КодВозвратаДиалога.ОК
				, "Диадок"
				, КодВозвратаДиалога.Отмена);
			
		КонецЕсли;
	
	КонецПроцедуры
	
	&НаКлиенте
	Функция ПараметрыСохраненияДокументаXML()
		
		Результат = Новый Структура;
		
		ТекСтрока 					= ТекущаяСтрокаТаблицыДокументов();
		ПолномочияПодписанта		= МетодКлиента("Модуль_РаботаССерверомДиадок",
													"ПрочитатьДанныеПодписанта",
													BoxId,
													ТекСтрока.ФорматОтправки.БазовыйФормат,
													Истина);
				
		Результат.Вставить("ПредставлениеДокумента",	ТекСтрока.Представление);
		Результат.Вставить("ФорматОтправки", 			ТекСтрока.ФорматОтправки);
		Результат.Вставить("ПолномочияПодписанта", 		ПолномочияПодписанта);
		Результат.Вставить("Каталог", 					"");
		
		Возврат Результат;
		
	КонецФункции
	
	&НаКлиенте
	Процедура ЗаполнитьПолномочияПодписантаДляСохраненияXML(РезультатВопроса, ПараметрыСохранения) Экспорт
		
		Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
			Возврат;
		КонецЕсли;
		
		ИмяФормыПодписи = "НастройкаПодписиУПД";
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("BoxId", BoxId);
		ПараметрыФормы.Вставить("ФорматЭД", ПараметрыСохранения.ФорматОтправки);
		ПараметрыФормы.Вставить("Исходящие", Истина);
		ПараметрыФормы.Вставить("Организация", Организация);
		
		МетодКлиента(, "ОткрытьФормуОбработкиМодально"
					, ИмяФормыПодписи
					, ПараметрыФормы
					, ЭтаФорма
					, "ОбработчикОткрытиеФормыНастройкиПодписи_СохранениеXML"
					, ПараметрыСохранения);
		
	КонецПроцедуры
	
	&НаКлиенте
	Процедура СформироватьФайлДокумента(ДополнительныеПараметры)
		
		ДанныеСотрудника	 = МетодКлиента("Модуль_РаботаССерверомДиадок", "ДанныеКонтекстаДиадок", BoxId, "ДанныеСотрудника");
		ДанныеОрганизации	 = МетодКлиента("Модуль_РаботаССерверомДиадок", "ДанныеКонтекстаДиадок", BoxId, "ДанныеОрганизации");
		
		ПараметрыСогласования = Неопределено;
		ДанныеПодписанта = МетодКлиента("Модуль_Выгрузка", "ДанныеПодписанта"
			, ДанныеСотрудника
			, ПараметрыСогласования);
		
		ПараметрыPackageSendTask = ПараметрыPackageSendTask(Неопределено);
		ИдентификаторОтправителя = ПараметрыPackageSendTask.Organization.FnsParticipantId;
		ИдентификаторПолучателя	 = ПараметрыPackageSendTask.FnsParticipantIdПолучателя;
		
		ПараметрыФормированияФайла = МетодСервераБезКонтекста("ГенерацияXML", "XML_ПараметрыФормированияФайла");
		ПараметрыФормированияФайла.ИдентификаторОтправителя	 = ИдентификаторОтправителя;
		ПараметрыФормированияФайла.ИдентификаторПолучателя	 = ИдентификаторПолучателя;
		
		ТекСтрока = ТекущаяСтрокаТаблицыДокументов();
		ФорматОтправки = ТекСтрока.ФорматОтправки;
		ПредставлениеДокумента = ТекСтрока.Представление;
		ДополнительныеСведенияСтрокой = ТекСтрока.ДопСведения;
		
		ПараметрыПолученияКонтента = МетодСервераБезКонтекста(, "ДополнительныеПараметрыПолученияКонтента");
		ПараметрыПолученияКонтента.ИспользоватьСервисКонтурМаркировка = ИспользоватьКонтурМаркировку();
		ПараметрыПолученияКонтента.РежимСовместимостиФорматов155и820 = ВключенРежимСовместимостиФорматов155и820();
		ПараметрыПолученияКонтента.УказыватьОтсутствиеОснованияУПД = УказыватьОтсутствиеОснованияУПД();
		ПараметрыПолученияКонтента.ДополнительныеСведенияСтрокой = ДополнительныеСведенияСтрокой;
		ПараметрыПолученияКонтента.ПараметрыФормированияФайла = ПараметрыФормированияФайла;
		ПараметрыПолученияКонтента.ДанныеСотрудника = ДанныеСотрудника;
		
		КонтентДокумента = МетодСервера(,
			"КонтентФормализованногоДокумента",
			Документ1С,
			ФорматОтправки,
			ПараметрыПолученияКонтента
		);
		
		ПодписантВПрото = МетодКлиента("Модуль_Выгрузка"
			, "ЗаполнитьПодписантаВПротоКонтенте"
			, КонтентДокумента
			, ФорматОтправки
			, ДанныеОрганизации
			, ДанныеПодписанта
			, ДополнительныеПараметры.ПолномочияПодписанта);
		
		ДвоичныеДанные = ДвоичныеДанныеФайлаДляСохранения(КонтентДокумента
			, ПараметрыПолученияКонтента.ПараметрыФормированияФайла
			, ФорматОтправки);
		
		ЧастиИмениФайла = Новый Массив;
		ЧастиИмениФайла.Добавить(ДополнительныеПараметры.Каталог);
		ЧастиИмениФайла.Добавить(ПолучитьРазделительПути());
		ЧастиИмениФайла.Добавить(ПредставлениеДокумента);
		ЧастиИмениФайла.Добавить(".xml");
		ИмяФайла = СтрСоединить(ЧастиИмениФайла);
		
		ДвоичныеДанные.Записать(ИмяФайла);
		
	КонецПроцедуры
	
	&НаСервере
	Функция ДвоичныеДанныеФайлаДляСохранения(КонтентДокумента, ПараметрыФормирования, ФорматОтправки)

		Результат = Неопределено;
		
		БазовыеФорматы = МетодСервера(, "БазовыеФорматы");
		
		XDTO = МетодСервера("ГенерацияXML", "ПротоКонтент_В_ПротоКонтентXDTO", КонтентДокумента, ФорматОтправки.ТипКонтента);
		
		Если ФорматОтправки.БазовыйФормат = БазовыеФорматы.utd820 Тогда
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ПолучитьXML_УПД_820", XDTO, ПараметрыФормирования);
		ИначеЕсли ФорматОтправки.БазовыйФормат = БазовыеФорматы.utd Тогда
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ПолучитьXML_УПД_155", XDTO, ПараметрыФормирования);
		ИначеЕсли ФорматОтправки.БазовыйФормат = БазовыеФорматы.ucd Тогда
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ПолучитьXML_УКД", XDTO, ПараметрыФормирования);
		ИначеЕсли ФорматОтправки.БазовыйФормат = БазовыеФорматы.ucd736 Тогда
			ОбъектXDTO = МетодСервера("ГенерацияXML", "XML_ПолучитьXML_УКД_736", XDTO, ПараметрыФормирования); 
		Иначе
			ТекстИсключения = НСтр("ru='Неизвестный формат документа'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		Результат = МетодСервера("ГенерацияXML", "ОбъектXDTO_В_ДвоичныеДанные", ОбъектXDTO);
		
		Возврат Результат;
	
	КонецФункции
	
	&НаКлиенте
	Процедура ВыполнитьСохранениеДокументаXML()
		
		Если Organization.AuthenticateType = "Login" Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("СохранениеДокументаXML", ЭтаФорма, Organization.AuthenticateType);
			ПоказатьПредупреждение(ОписаниеОповещения, НСтр("ru = 'Не удалось заполнить сведения о подписанте, так как вы авторизованы по логину и паролю'"));
		Иначе
			СохранениеДокументаXML(Organization.AuthenticateType);
		КонецЕсли;
		
	КонецПроцедуры
	
	////////////////////////////////////////////////////////////////////////////////
	//} СОХРАНЕНИЕ ДОКУМЕНТА В XML
	
//{ ОБЕРТКИ

&НаКлиенте
Функция ФункциональностьОграничена(ИдентификаторФункциональности)
	
	Результат = МетодКлиента("Модуль_Клиент", "ФункциональностьОграничена", ИдентификаторФункциональности);
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ПоказатьПредупреждениеОбОграниченнииФункциональности(ИдентификаторФункциональности)
	
	МетодКлиента("Модуль_Клиент", "ПоказатьПредупреждениеОбОграниченнииФункциональности", ИдентификаторФункциональности);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПростоеПредупреждение(ТекстПредупреждения)
	
	МетодКлиента("Модуль_Клиент", "ПоказатьПростоеПредупреждение", ТекстПредупреждения);
	
КонецПроцедуры

//} ОБЕРТКИ

#Область Метрики

&НаКлиенте
Процедура ДобавитьСтатистику_НажалиОтменитьСопоставление()
	
	Категории = МетодКлиента("Модуль_Клиент", "Метрика_Категории");
		
	НазваниеФормы 		= "Форма документа";
	НазваниеКатегории 	= Категории.ОтражениеВУчете;
	НазваниеДействия 	= "Отменить сопоставление с документом 1С";
	
	МетодКлиента(	"Модуль_Клиент",
					"Метрика_ДобавитьПоведение_НажатиеКнопки",
					НазваниеФормы,
					НазваниеКатегории,
					НазваниеДействия	);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтатистику_ОтменитьСопоставление()
	
	Если ЭДОбъект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлектронныйДокумент = МетодКлиента("Модуль_Клиент", "ЭлектронныйДокумент", ЭДОбъект);
	
	ФорматЭД = ЭлектронныйДокумент.Формат;
	
	Направления = Новый Структура;
	
	Направления.Вставить("Inbound"	, "Входящий");
	Направления.Вставить("Outbound"	, "Исходящий");
	Направления.Вставить("Internal"	, "Внутренний");
	
	Направление = Направления[ЭДОбъект.Direction];
	
	ИдентификаторыЭД 	= ЭлектронныйДокумент.Идентификатор;
	BoxGuid 			= МетодКлиента("Модуль_Клиент", "ПолучитьBoxGuidПоId", ИдентификаторыЭД.ИдентификаторОрганизации);
	
	Переменные = Новый Структура;
	
	Переменные.Вставить("BoxID"				, BoxGuid);
	Переменные.Вставить("Direction"			, Направление);
	Переменные.Вставить("DocumentID"		, ИдентификаторыЭД.СоставнойИдентификатор);
	Переменные.Вставить("LetterID"			, ИдентификаторыЭД.ИдентификаторСообщения);
	Переменные.Вставить("ВерсияФормата"		, ФорматЭД.ВерсияФормата);
	Переменные.Вставить("ИмяТипа"			, ФорматЭД.ИмяТипа);
	Переменные.Вставить("ФункцияДокумента"	, ФорматЭД.ФункцияДокумента);
	                                                                  	
	МетодКлиента(	"Модуль_Клиент",
					"Метрика_ДобавитьСтатистику_ОтменитьСопоставление",
					Переменные	);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли