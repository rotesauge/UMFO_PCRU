
#Область ПЕРЕМЕННЫЕ_ПЛАТФОРМЫ

&НаКлиенте
Перем Платформа Экспорт;

&НаСервере
Перем ОбработкаОбъект;

&НаКлиенте
Перем КонтекстМЧД;

#КонецОбласти

#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ_ПЛАТФОРМЫ

&НаКлиенте
Функция МетодКлиента(ИмяМодуля= "", ИмяМетода, 
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL,
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат  Платформа.МетодКлиента(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаКлиенте
Функция МетодСервераБезКонтекста(ИмяМодуля= "", ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат Платформа.МетодСервераБезКонтекста(ИмяМодуля, ИмяМетода,
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция МетодСервера(Знач ИмяМодуля= "", Знач ИмяМетода,
		Параметр0= NULL, Параметр1= NULL, Параметр2= NULL, Параметр3= NULL, Параметр4= NULL, 
		Параметр5= NULL, Параметр6= NULL, Параметр7= NULL, Параметр8= NULL, Параметр9= NULL) Экспорт
	
	Возврат ОбработкаОбъект().МетодСервера(ИмяМодуля, ИмяМетода, 
	Параметр0, Параметр1, Параметр2, Параметр3, Параметр4,
	Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	
КонецФункции

&НаСервере
Функция ОбработкаОбъект() Экспорт
	
	Если ОбработкаОбъект = Неопределено Тогда
		
		СтруктураОбработки= ПолучитьИзВременногоХранилища(Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
		
		Если СтруктураОбработки <> Неопределено Тогда
			ОбработкаОбъект= СтруктураОбработки.ОбработкаОбъект;
		КонецЕсли;
		
		Если ОбработкаОбъект = Неопределено Тогда
			
			ОбработкаОбъект= РеквизитФормыВЗначение("Объект");
			
			Попытка
				ПоместитьВоВременноеХранилище(Новый Структура("ОбработкаОбъект", ОбработкаОбъект), Объект.ПараметрыКлиентСервер.ВременноеХранилище.АдресОбработкаОбъект);
			Исключение КонецПопытки;
		
		Иначе
			ОбработкаОбъект.ПараметрыКлиентСервер= Объект.ПараметрыКлиентСервер;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОбработкаОбъект;
	
КонецФункции

&НаКлиенте
Функция ОсновнаяФорма(ТекущийВладелецФормы)
	
	Если ТекущийВладелецФормы = Неопределено Тогда
		Возврат Неопределено
	ИначеЕсли Прав(ТекущийВладелецФормы.ИмяФормы, 14) = "Форма_Основная" Тогда
		Возврат ТекущийВладелецФормы;
	Иначе
		Возврат ОсновнаяФорма(ТекущийВладелецФормы.ВладелецФормы);
	КонецЕсли;
	
КонецФункции


&НаСервере
Процедура ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОбъектПараметрыКлиентСервер", Объект.ПараметрыКлиентСервер);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриОткрытии(Отказ)
	
	ОсновнаяФорма = ОсновнаяФорма(ВладелецФормы);
	
	Если ОсновнаяФорма <> Неопределено Тогда
		Платформа = ОсновнаяФорма.Платформа;
	КонецЕсли;
		
	Платформа.ПриОткрытииФормыОбработки(ЭтаФорма, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатформаПриЗакрытии()
	
	Платформа.ПриЗакрытииФормыОбработки(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти
 
#Область ПРЕДОПРЕДЕЛЕННЫЕ_СОБЫТИЯ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПлатформаПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
	
	ИнициализироватьФорму(Параметры);

	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПлатформаПриОткрытии(Отказ);
	
	ЗаполнитьСписокДоступныхМЧД();
   
	ПредставлениеПодписанта = МетодКлиента("Модуль_Клиент", "ПредставлениеПодписи", АдресЯщика);

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьФорму(ПараметрыФормы)

	Заголовок 				= Параметры.ЗаголовокФормы;
	АдресЯщика 				= Параметры.АдресЯщика;
	ЗаголовокКнопкиДействия = Параметры.ЗаголовокКнопкиДействия;
	ДействиеЭДО				= Параметры.ДействиеЭДО;
	
	ПредставлениеПодписанта = "Нужно добавить";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Платформа.ПриЗакрытииФормыОбработки(ЭтаФорма);

КонецПроцедуры

#КонецОбласти

#Область ОБРАБОТЧИКИ_КОМАНД

&НаКлиенте
Процедура Отмена(Команда)
	
	Результат = Новый Структура("РазрешитьДействие", Ложь);
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ВыполнитьДействиеПослеПроверкиМЧД", ЭтаФорма);
	КонтрактМЧД = КонтекстМЧД.СписокМЧД.Получить(ИдентификаторВыбраннойМЧД);
	МетодКлиента("Модуль_Клиент", "ПроверитьМЧДИВыполнитьОповещение", Оповещение, КонтрактМЧД, АдресЯщика, ДействиеЭДО);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействиеПослеПроверкиМЧД(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.РазрешитьДействие Тогда
		Закрыть(Результат);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОБРАБОТЧИКИ_ЭЛЕМЕНТОВ_ФОРМЫ

&НаКлиенте
Процедура НастроитьМЧД(Команда)
	
	МетодКлиента("Модуль_Клиент", "ОткрытьФормуСпискаДоверенностей", АдресЯщика, ЭтотОбъект)
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокДоступныхМЧД()
	
	КонтекстМЧД	= МетодКлиента("Модуль_Клиент", "МЧД_ДанныеПоИспользованиюДоверенностей", АдресЯщика);
	
	СписокВыбораМЧД	= Элементы.ИдентификаторВыбраннойМЧД.СписокВыбора;
	
	Для Каждого КлючЗначение Из КонтекстМЧД.СписокМЧД Цикл
		КонтрактМЧД = КлючЗначение.Значение;
		СписокВыбораМЧД.Добавить(КонтрактМЧД.Идентификатор, КонтрактМЧД.ПредставлениеМЧД);
	КонецЦикла;
	
	ИдентификаторВыбраннойМЧД = КонтекстМЧД.МЧД;

КонецПроцедуры

#КонецОбласти

#Область СЛУЖЕБНЫЕ_ПРОЦЕДУРЫ_И_ФУНКЦИИ
	
&НаСервере
Процедура НастроитьЭлементыФормы()

	КоллекцияКартинок = МетодСервера(, "ЭДО_БиблиотекаКартинок");
	
	КартинкаСертификата = КоллекцияКартинок.КартинкаЛокальныйСертификат;
	АдресКартинкиПодписанта = ПоместитьВоВременноеХранилище(КартинкаСертификата, УникальныйИдентификатор);

	Элементы.ВыполнитьДействие.Заголовок = ЗаголовокКнопкиДействия;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСпискаДоверенностей()
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("АдресЯщика", АдресЯщика);	
	
	МетодКлиента(, "ОткрытьФормуОбработкиМодально"
		, "НастройкаМашиночитаемыхДоверенностей"
		, ПараметрыОткрытияФормы
		, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти