
&Вместо("ПодготовитьТаблицуАмортизацияНМА")
Функция Расш1_ПодготовитьТаблицуАмортизацияНМА(ТаблицаНМА, ТаблицаРеквизиты, Отказ)
	
	Параметры = ПодготовитьПараметрыАмортизацияНМА(ТаблицаНМА, ТаблицаРеквизиты);
	Реквизиты = Параметры.Реквизиты[0];
	
	ТаблицаАмортизации = ПолучитьПустуюТаблицуАмортизацииНМА();
	
	ОтражатьВНалоговомУчете 		=	УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ПоддержкаПБУ18					=	УчетнаяПолитика.ПоддержкаПБУ18(Реквизиты.Организация, Реквизиты.Период);
	МетодНачисленияАмортизацииНУ	=	УчетнаяПолитика.МетодНачисленияАмортизацииНУ(Реквизиты.Организация, Реквизиты.Период);
	ПоддержкаПоложения492П			=	БНФОУправлениеВнеоборотнымиАктивамиПереопределяемый.ПрименениеПоложения492П(Реквизиты.ДатаРасчета);
	ВключатьДеньВыбытияВнеоборотногоАктиваВРасчетАмортизации = БНФОУчетнаяПолитика.ВключатьДеньВыбытияВнеоборотногоАктиваВРасчетАмортизации(Реквизиты.Организация, Реквизиты.Период);

	Если ОтражатьВНалоговомУчете И НЕ ЗначениеЗаполнено(МетодНачисленияАмортизацииНУ) Тогда
		Если Реквизиты.ВыдаватьСообщения Тогда
			ШаблонСообщения = НСтр("ru = 'Не указан метод начисления амортизации (НУ) в учетной политике организации %1
				|Укажите метод начисления амортизации на %2.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Реквизиты.Организация,
				Формат(НачалоГода(Реквизиты.ДатаРасчета)));
			СсылкаДляПерехода = БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьНавигационнуюСсылкуНаУчетнуюПолитикуПоНалогуНаПрибыль();
			БухгалтерскийУчетПереопределяемый.СообщитьОбОшибкеРегОперацииСНавигацией(ТекстСообщения, СсылкаДляПерехода, Отказ,
				Реквизиты.Регистратор);
		КонецЕсли;
		Возврат ТаблицаАмортизации;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация",                  Реквизиты.Организация);
	Запрос.УстановитьПараметр("НачалоГода",                   НачалоГода(Реквизиты.ДатаРасчета));
	Запрос.УстановитьПараметр("НачалоПериодаДата",            НачалоМесяца(Реквизиты.ДатаРасчета));
	Запрос.УстановитьПараметр("ПоддержкаПоложения492П",       ПоддержкаПоложения492П);
	
	Если ПоддержкаПоложения492П Тогда
		Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.БНФОАмортизацияНМА") Тогда
			// Расчет ведется по календарным дням, поэтому при расчете амортизации нужны данные на конец периода
			Запрос.УстановитьПараметр("НачалоПериодаСведений",       Новый Граница(КонецМесяца(Реквизиты.ДатаРасчета), ВидГраницы.Исключая));
		Иначе
			// Проверки для амортизации осуществляются на дату проведения документа.
			Запрос.УстановитьПараметр("НачалоПериодаСведений",       Новый Граница(НачалоДня(Реквизиты.ДатаРасчета), ВидГраницы.Исключая));
		КонецЕсли;
	Иначе
		Запрос.УстановитьПараметр("НачалоПериодаСведений",       Новый Граница(НачалоМесяца(Реквизиты.ДатаРасчета), ВидГраницы.Исключая));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоПериода",                Новый Граница(НачалоМесяца(Реквизиты.ДатаРасчета), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецПериода",                 КонецМесяца(Реквизиты.ДатаРасчета));
	Запрос.УстановитьПараметр("КонецПериодаДата",         	  КонецМесяца(Реквизиты.ДатаРасчета));

	Если ПоддержкаПоложения492П И ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.БНФОАмортизацияНМА") Тогда
		// Расчет ведется по календарным дням, поэтому при расчете амортизации нужны данные на конец периода
		Запрос.УстановитьПараметр("Период",                       КонецДня(Реквизиты.ДатаРасчета));
	Иначе
	Запрос.УстановитьПараметр("Период",                       Реквизиты.ДатаРасчета);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаПереходаКЛинейномуМетоду", УправлениеВнеоборотнымиАктивами.ПолучитьДатуПереходаКЛинейномуМетоду(Реквизиты.ДатаРасчета, Реквизиты.Организация));
	Запрос.УстановитьПараметр("Регистратор",                  Реквизиты.Регистратор);
	Запрос.УстановитьПараметр("МетодНачисленияАмортизацииНУ", МетодНачисленияАмортизацииНУ);
	
	// Получим список нематериальных активов для расчета амортизации
	Если Параметры.ТаблицаНМА.Количество() > 0 Тогда
		Запрос.УстановитьПараметр("ТаблицаНМА", Параметры.ТаблицаНМА);
		Запрос.Текст = ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАПоТаблице();
	Иначе
		Запрос.Текст = ПолучитьТекстЗапросаАмортизацияНМАПринятыКУчетуНМАВсе();
	КонецЕсли;
	Запрос.Выполнить();
	
	// Получим историю изменения параметров амортизации
	ТаблицаИзмененияПараметровАмортизации = ПолучитьТаблицуИзмененияПараметровАмортизации(Запрос);
	
	// Получим счета учета нематериальных активов
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета КАК СчетУчета,
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации КАК СчетНачисленияАмортизации,
	|	ВЫРАЗИТЬ(СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета КАК Справочник.БНФОСчетаАналитическогоУчета).Владелец КАК СчетУчетаВладелец,
	|	ВЫРАЗИТЬ(СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации КАК Справочник.БНФОСчетаАналитическогоУчета).Владелец КАК СчетНачисленияАмортизацииВладелец
	|ПОМЕСТИТЬ СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|ИЗ
	|	РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|			&НачалоПериодаСведений,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|"
	;
	
	Если ТранзакцияАктивна() Тогда
		
		Запрос.Текст = Запрос.Текст + "
		|;
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
		|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета КАК СчетАналитическогоУчета,
		|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчетаВладелец КАК СчетУчетаНМА
		|ИЗ
		|	СчетаБухгалтерскогоУчетаНМАСрезПоследних КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
		|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации,
		|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизацииВладелец
		|ИЗ
		|	СчетаБухгалтерскогоУчетаНМАСрезПоследних КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних";
		
		ТаблицаСчетов = Запрос.Выполнить().Выгрузить();
		
		// Блокируем регистр БНФОБанковский для получения остатков
		//Блокировка = Новый БлокировкаДанных;
		//
		//ЭлементБлокировки = Блокировка.Добавить("РегистрБухгалтерии.БНФОБанковский");
		//ЭлементБлокировки.УстановитьЗначение("Период",      Новый Диапазон(, КонецМесяца(Реквизиты.ДатаРасчета)));
		//ЭлементБлокировки.УстановитьЗначение("Организация", Реквизиты.Организация);
		//ЭлементБлокировки.ИсточникДанных = ТаблицаСчетов;
		//ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Счет", "СчетУчетаНМА");
		//ЭлементБлокировки.ИспользоватьИзИсточникаДанных("СчетАналитическогоУчета", "СчетАналитическогоУчета");
		//ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ПланыВидовХарактеристик.БНФОВидыСубконтоБанковские.НематериальныеАктивы, "НематериальныйАктив");
		//
		//Блокировка.Заблокировать();
	Иначе
		ТаблицаСчетов = Запрос.Выполнить();
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Период КАК Период,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Коэффициент КАК Коэффициент,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НачислятьАмортизацию КАК НачислятьАмортизацию
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|			&НачалоПериодаСведений,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних
	|ГДЕ
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НачислятьАмортизацию = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БНФОПараметрыАмортизацииНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	БНФОПараметрыАмортизацииНМАБухгалтерскийУчетСрезПоследних.СрокИспользованияДляВычисленияАмортизации,
	|	БНФОПараметрыАмортизацииНМАБухгалтерскийУчетСрезПоследних.СтоимостьДляВычисленияАмортизации
	|ПОМЕСТИТЬ ПараметыАмортизацииНМАБУ
	|ИЗ
	|	РегистрСведений.БНФОПараметрыАмортизацииНМАБухгалтерскийУчет.СрезПоследних(
	|			&НачалоПериодаСведений,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК БНФОПараметрыАмортизацииНМАБухгалтерскийУчетСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.Период КАК Период,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НачислятьАмортизацию КАК НачислятьАмортизацию
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних
	|ГДЕ
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НачислятьАмортизацию = ИСТИНА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.СпособОтраженияРасходов КАК СпособОтраженияРасходов
	|ПОМЕСТИТЬ СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.СрезПоследних(
	|			&НачалоПериодаСведений,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.Ссылка КАК СпособОтраженияРасходов,
	|	КОЛИЧЕСТВО(СпособыОтраженияРасходовПоАмортизацииСпособы.Коэффициент) КАК КоличествоКоэффициентов
	|ПОМЕСТИТЬ СпособыОтраженияРасходовПоАмортизацииСпособы
	|ИЗ
	|	Справочник.СпособыОтраженияРасходовПоАмортизации.Способы КАК СпособыОтраженияРасходовПоАмортизацииСпособы
	|ГДЕ
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.СпособОтраженияРасходов
	|			ИЗ
	|				СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпособыОтраженияРасходовПоАмортизацииСпособы.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СпособОтраженияРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних.НематериальныйАктив КАК НематериальныйАктив,
	|	НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних.СпециальныйКоэффициент КАК СпециальныйКоэффициент
	|ПОМЕСТИТЬ НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних
	|ИЗ
	|	РегистрСведений.НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет.СрезПоследних(
	|			&НачалоПериода,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БНФОБанковскийОстаткиИОбороты.Субконто1 КАК НематериальныйАктив,
	|	БНФОБанковскийОстаткиИОбороты.Счет КАК Счет,
	|	БНФОБанковскийОстаткиИОбороты.Подразделение КАК Подразделение,
	|	БНФОБанковскийОстаткиИОбороты.СуммаНачальныйОстатокДт КАК СуммаНачальныйОстатокДт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаНУНачальныйОстатокДт КАК СуммаНУНачальныйОстатокДт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаПРНачальныйОстатокДт КАК СуммаПРНачальныйОстатокДт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаВРНачальныйОстатокДт КАК СуммаВРНачальныйОстатокДт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаНачальныйОстатокКт КАК СуммаНачальныйОстатокКт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаНУНачальныйОстатокКт КАК СуммаНУНачальныйОстатокКт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаПРНачальныйОстатокКт КАК СуммаПРНачальныйОстатокКт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаВРНачальныйОстатокКт КАК СуммаВРНачальныйОстатокКт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаКонечныйОстатокДт КАК СуммаКонечныйОстатокДт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаНУКонечныйОстатокДт КАК СуммаНУКонечныйОстатокДт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаПРКонечныйОстатокДт КАК СуммаПРКонечныйОстатокДт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаВРКонечныйОстатокДт КАК СуммаВРКонечныйОстатокДт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаКонечныйОстатокКт КАК СуммаКонечныйОстатокКт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаНУКонечныйОстатокКт КАК СуммаНУКонечныйОстатокКт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаПРКонечныйОстатокКт КАК СуммаПРКонечныйОстатокКт,
	|	БНФОБанковскийОстаткиИОбороты.СуммаВРКонечныйОстатокКт КАК СуммаВРКонечныйОстатокКт,
	|	БНФОБанковскийОстаткиИОбороты.СчетАналитическогоУчета
	|ПОМЕСТИТЬ ХозрасчетныйОстаткиИОбороты
	|ИЗ
	|	РегистрБухгалтерии.БНФОБанковский.ОстаткиИОбороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			,
	|			Счет В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчетаВладелец
	|				ИЗ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|			
	|				ОБЪЕДИНИТЬ ВСЕ
	|			
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизацииВладелец
	|				ИЗ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.БНФОВидыСубконтоБанковские.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)
	|				И СчетАналитическогоУчета В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета
	|					ИЗ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|			
	|					ОБЪЕДИНИТЬ ВСЕ
	|			
	|					ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации
	|					ИЗ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних)) КАК БНФОБанковскийОстаткиИОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БНФОБанковскийОбороты.Субконто1 КАК НематериальныйАктив,
	|	СУММА(БНФОБанковскийОбороты.СуммаОборотКт) КАК СуммаОборотКт,
	|	СУММА(БНФОБанковскийОбороты.СуммаНУОборотКт) КАК СуммаНУОборотКт,
	|	СУММА(БНФОБанковскийОбороты.СуммаПРОборотКт) КАК СуммаПРОборотКт,
	|	СУММА(БНФОБанковскийОбороты.СуммаВРОборотКт) КАК СуммаВРОборотКт
	|ПОМЕСТИТЬ БНФОБанковскийОбороты
	|ИЗ
	|	РегистрБухгалтерии.БНФОБанковский.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Регистратор,
	|			Счет В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизацииВладелец
	|				ИЗ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.БНФОВидыСубконтоБанковские.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)
	|				И СчетАналитическогоУчета В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации
	|					ИЗ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних),
	|			,
	|			) КАК БНФОБанковскийОбороты
	|ГДЕ
	|	БНФОБанковскийОбороты.Регистратор <> &Регистратор
	|	И НЕ БНФОБанковскийОбороты.Регистратор ССЫЛКА Документ.БНФОПереоценкаОбесценениеНМА
	|	И НЕ БНФОБанковскийОбороты.Регистратор ССЫЛКА Документ.БНФОМемориальныйОрдер
	|
	|СГРУППИРОВАТЬ ПО
	|	БНФОБанковскийОбороты.Субконто1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БНФОБанковскийОстатки.Субконто1 КАК НематериальныйАктив,
	|	БНФОБанковскийОстатки.СуммаОстаток КАК СтоимостьНаНачалоГода
	|ПОМЕСТИТЬ СтоимостьНМАНаНачалоГода
	|ИЗ
	|	РегистрБухгалтерии.БНФОБанковский.Остатки(
	|			&НачалоГода,
	|			Счет В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчетаВладелец
	|				ИЗ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|			
	|				ОБЪЕДИНИТЬ ВСЕ
	|			
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизацииВладелец
	|				ИЗ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.БНФОВидыСубконтоБанковские.НематериальныеАктивы),
	|			Организация = &Организация
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)
	|				И СчетАналитическогоУчета В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета
	|					ИЗ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|			
	|					ОБЪЕДИНИТЬ ВСЕ
	|			
	|					ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации
	|					ИЗ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних)) КАК БНФОБанковскийОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыработкаНМАОбороты.НематериальныйАктив,
	|	ВыработкаНМАОбороты.КоличествоОборот
	|ПОМЕСТИТЬ ВыработкаНМАОбороты
	|ИЗ
	|	РегистрНакопления.ВыработкаНМА.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Период,
	|			Организация = &Организация
	|				И НематериальныйАктив В
	|					(ВЫБРАТЬ
	|						СписокНМА.НематериальныйАктив
	|					ИЗ
	|						СписокНМА КАК СписокНМА)) КАК ВыработкаНМАОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияНМАОрганизаций.НематериальныйАктив,
	|	МАКСИМУМ(СостоянияНМАОрганизаций.Период) КАК ДатаВводаВЭксплуатацию
	|ПОМЕСТИТЬ СостоянияНМАОрганизаций
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|ГДЕ
	|	СостоянияНМАОрганизаций.Организация = &Организация
	|	И СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|
	|СГРУППИРОВАТЬ ПО
	|	СостоянияНМАОрганизаций.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НематериальныеАктивы.Ссылка КАК НематериальныйАктив
	|ПОМЕСТИТЬ НМАТолькоЛинейныйМетодНУ
	|ИЗ
	|	Справочник.НематериальныеАктивы КАК НематериальныеАктивы
	|ГДЕ
	|	НематериальныеАктивы.Ссылка В
	|			(ВЫБРАТЬ
	|				СписокНМА.НематериальныйАктив
	|			ИЗ
	|				СписокНМА КАК СписокНМА)
	|	И НематериальныеАктивы.АмортизационнаяГруппа В (ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВосьмаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДевятаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ДесятаяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ОтдельнаяГруппа))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БНФОБанковскийОстатки.Субконто1 КАК НематериальныйАктив,
	|	БНФОБанковскийОстатки.СуммаНУОстатокКт КАК АмортизацияНачальныйОстаток
	|ПОМЕСТИТЬ АмортизацияНМАПриПереходеКЛинейномуМетоду
	|ИЗ
	|	РегистрБухгалтерии.БНФОБанковский.Остатки(
	|			&ДатаПереходаКЛинейномуМетоду,
	|			Счет В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизацииВладелец
	|				ИЗ
	|					СчетаБухгалтерскогоУчетаНМАСрезПоследних),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.БНФОВидыСубконтоБанковские.НематериальныеАктивы),
	|			Организация = &Организация
	|				И НЕ Субконто1 В
	|						(ВЫБРАТЬ
	|							НМАТолькоЛинейныйМетодНУ.НематериальныйАктив
	|						ИЗ
	|							НМАТолькоЛинейныйМетодНУ)
	|				И СчетАналитическогоУчета В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации
	|					ИЗ
	|						СчетаБухгалтерскогоУчетаНМАСрезПоследних)) КАК БНФОБанковскийОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СписокНМА.НематериальныйАктив,
	|	ПРЕДСТАВЛЕНИЕ(СписокНМА.НематериальныйАктив) КАК НематериальныйАктивПредставление,
	|	СписокНМА.НематериальныйАктив.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	СписокНМА.НематериальныйАктив.Код КАК Код,
	|	СтоимостьНМА.Подразделение КАК ПодразделениеОрганизации,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаНачальныйОстатокДт, 0) КАК СтоимостьНачальныйОстаток,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаНУНачальныйОстатокДт, 0) КАК СтоимостьНУНачальныйОстаток,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаПРНачальныйОстатокДт, 0) КАК СтоимостьПРНачальныйОстаток,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаНачальныйОстатокКт, 0) КАК АмортизацияНачальныйОстаток,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаКонечныйОстатокДт, 0) КАК СтоимостьКонечныйОстаток,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаНУКонечныйОстатокДт, 0) КАК СтоимостьНУКонечныйОстаток,
	|	ЕСТЬNULL(СтоимостьНМА.СуммаПРКонечныйОстатокДт, 0) КАК СтоимостьПРКонечныйОстаток,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаКонечныйОстатокКт, 0) КАК АмортизацияКонечныйОстаток,
	|	ЕСТЬNULL(БНФОБанковскийОбороты.СуммаОборотКт, 0) КАК АмортизацияОборот,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаНУНачальныйОстатокКт, 0) КАК АмортизацияНУНачальныйОстаток,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаПРНачальныйОстатокКт, 0) КАК АмортизацияПРНачальныйОстаток,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаНУКонечныйОстатокКт, 0) КАК АмортизацияНУКонечныйОстаток,
	|	ЕСТЬNULL(АмортизацияНМА.СуммаПРКонечныйОстатокКт, 0) КАК АмортизацияПРКонечныйОстаток,
	|	ЕСТЬNULL(БНФОБанковскийОбороты.СуммаНУОборотКт, 0) КАК АмортизацияНУОборот,
	|	ЕСТЬNULL(ВыработкаНМАОбороты.КоличествоОборот, 0) КАК Количество,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ПервоначальнаяСтоимость, 0) КАК ПервоначальнаяСтоимость,
	|	ВЫБОР
	|		КОГДА &ПоддержкаПоложения492П
	|			ТОГДА ЕСТЬNULL(ПараметыАмортизацииНМАБУ.СрокИспользованияДляВычисленияАмортизации, 0)
	|		ИНАЧЕ ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования, 0)
	|	КОНЕЦ КАК СрокИспользованияДляВычисленияАмортизации,
	|	ВЫБОР
	|		КОГДА &ПоддержкаПоложения492П
	|			ТОГДА ЕСТЬNULL(ПараметыАмортизацииНМАБУ.СтоимостьДляВычисленияАмортизации, 0)
	|		ИНАЧЕ ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ПервоначальнаяСтоимость, 0)
	|	КОНЕЦ КАК СтоимостьДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРаботДляВычисленияАмортизации, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Коэффициент, 0) КАК Коэффициент,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Период КАК Период,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользованияБУ,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) КАК НачислятьАмортизациюБУ,
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета,
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации,
	|	СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.СпособОтраженияРасходов КАК СпособыОтраженияРасходовПоАмортизации,
	|	ЕСТЬNULL(СпособыОтраженияРасходовПоАмортизацииСпособы.КоличествоКоэффициентов, 0) КАК КоличествоКоэффициентов,
	|	ЕСТЬNULL(СтоимостьНМАНаНачалоГода.СтоимостьНаНачалоГода, 0) КАК СтоимостьНаНачалоГода,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.Период КАК ДатаПринятияКУчету,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.СрокПолезногоИспользования,
	|	ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) КАК НачислятьАмортизациюНУ,
	|	ВЫБОР
	|		КОГДА ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НематериальныйАктив В
	|						(ВЫБРАТЬ
	|							НМАТолькоЛинейныйМетодНУ.НематериальныйАктив
	|						ИЗ
	|							НМАТолькоЛинейныйМетодНУ)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный)
	|				ИНАЧЕ &МетодНачисленияАмортизацииНУ
	|			КОНЕЦ
	|	КОНЕЦ КАК МетодНачисленияАмортизации,
	|	ЕСТЬNULL(НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних.СпециальныйКоэффициент, 0) КАК СпециальныйКоэффициент,
	|	ВЫБОР
	|		КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.АмортизацияДо2009, 0)
	|		ИНАЧЕ ЕСТЬNULL(АмортизацияНМАПриПереходеКЛинейномуМетоду.АмортизацияНачальныйОстаток, 0)
	|	КОНЕЦ КАК АмортизацияПриПереходеКЛинейномуМетоду,
	|	ВЫБОР
	|		КОГДА ГОД(&ДатаПереходаКЛинейномуМетоду) = 2009
	|			ТОГДА ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.ФактическийСрокИспользованияДо2009, 0)
	|		ИНАЧЕ (ГОД(&ДатаПереходаКЛинейномуМетоду) - ГОД(СостоянияНМАОрганизаций.ДатаВводаВЭксплуатацию)) * 12 + (МЕСЯЦ(&ДатаПереходаКЛинейномуМетоду) - МЕСЯЦ(СостоянияНМАОрганизаций.ДатаВводаВЭксплуатацию)) - 1
	|	КОНЕЦ КАК ФактическийСрокПриПереходеКЛинейномуМетоду
	|ИЗ
	|	СписокНМА КАК СписокНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних КАК ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних
	|		ПО СписокНМА.НематериальныйАктив = ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних КАК СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних
	|		ПО СписокНМА.НематериальныйАктив = СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходовПоАмортизацииСпособы КАК СпособыОтраженияРасходовПоАмортизацииСпособы
	|		ПО (СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчетСрезПоследних.СпособОтраженияРасходов = СпособыОтраженияРасходовПоАмортизацииСпособы.СпособОтраженияРасходов)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаБухгалтерскогоУчетаНМАСрезПоследних КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты КАК СтоимостьНМА
	|			ПО СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив = СтоимостьНМА.НематериальныйАктив
	|				И СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчетаВладелец = СтоимостьНМА.Счет
	|				И СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета = СтоимостьНМА.СчетАналитическогоУчета
	|			ЛЕВОЕ СОЕДИНЕНИЕ ХозрасчетныйОстаткиИОбороты КАК АмортизацияНМА
	|			ПО СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив = АмортизацияНМА.НематериальныйАктив
	|				И СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизацииВладелец = АмортизацияНМА.Счет
	|				И СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетНачисленияАмортизации = АмортизацияНМА.СчетАналитическогоУчета
	|		ПО СписокНМА.НематериальныйАктив = СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ БНФОБанковскийОбороты КАК БНФОБанковскийОбороты
	|		ПО СписокНМА.НематериальныйАктив = БНФОБанковскийОбороты.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВыработкаНМАОбороты КАК ВыработкаНМАОбороты
	|		ПО СписокНМА.НематериальныйАктив = ВыработкаНМАОбороты.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтоимостьНМАНаНачалоГода КАК СтоимостьНМАНаНачалоГода
	|		ПО СписокНМА.НематериальныйАктив = СтоимостьНМАНаНачалоГода.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних КАК ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних
	|		ПО СписокНМА.НематериальныйАктив = ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних КАК НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних
	|		ПО СписокНМА.НематериальныйАктив = НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчетСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияНМАПриПереходеКЛинейномуМетоду КАК АмортизацияНМАПриПереходеКЛинейномуМетоду
	|		ПО СписокНМА.НематериальныйАктив = АмортизацияНМАПриПереходеКЛинейномуМетоду.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|		ПО СписокНМА.НематериальныйАктив = СостоянияНМАОрганизаций.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПараметыАмортизацииНМАБУ КАК ПараметыАмортизацииНМАБУ
	|		ПО СписокНМА.НематериальныйАктив = ПараметыАмортизацииНМАБУ.НематериальныйАктив
	|ГДЕ
	|	НЕ(ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) = ЛОЖЬ
	|				И ЕСТЬNULL(ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) = ЛОЖЬ)";
	
	ВыборкаПоНМА = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаПоНМА.Следующий() Цикл

		СуммаАмортизацииБУ = 0;
		СуммаАмортизацииНУ = 0;
		СуммаАмортизацииПР = 0;
		СуммаАмортизацииВР = 0;
		
		Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.СчетНачисленияАмортизации) Тогда
			Если Реквизиты.ВыдаватьСообщения Тогда
				СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Не указан счет для начисления амортизации'"),
					Реквизиты.Регистратор, Отказ, Реквизиты.Регистратор);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Пропустить = Ложь;
		
		Если ВыборкаПоНМА.СтоимостьКонечныйОстаток - ВыборкаПоНМА.АмортизацияНачальныйОстаток <= 0 Тогда
				Пропустить = Истина; // остаточная стоимость на начало месяца равна 0
		КонецЕсли;

		
		Если ВыборкаПоНМА.НачислятьАмортизациюБУ Тогда

			Если ТипЗнч(Реквизиты.Регистратор) <> Тип("ДокументСсылка.БНФОМемориальныйОрдер") И ВыборкаПоНМА.АмортизацияОборот <> 0 Тогда
				Если Реквизиты.ВыдаватьСообщения И ТаблицаНМА.Количество() > 0 Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Амортизация в этом месяце уже начислялась '"),
						Реквизиты.Регистратор, Отказ, Реквизиты.Регистратор);
				КонецЕсли; 
				Пропустить = Истина;
			КонецЕсли;

			Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.СпособНачисленияАмортизации) Тогда
				Если Реквизиты.ВыдаватьСообщения Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Не указан способ амортизации '"),
						Реквизиты.Регистратор, Отказ, Реквизиты.Регистратор);
				КонецЕсли; 
				Пропустить = Истина;
			КонецЕсли;

			Если ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации = 0 И НЕ Пропустить Тогда
				Если Реквизиты.ВыдаватьСообщения Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Стоимость равна нулю '"),
						Реквизиты.Регистратор, Отказ, Реквизиты.Регистратор);
				КонецЕсли; 
				Пропустить = Истина;
			КонецЕсли;

			Если ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации = 0 И НЕ Пропустить Тогда
				Если Реквизиты.ВыдаватьСообщения Тогда
					СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Срок использования равен нулю '"),
						Реквизиты.Регистратор, Отказ, Реквизиты.Регистратор);
				КонецЕсли;
				Пропустить = Истина;
			КонецЕсли;

			Если НЕ ПоддержкаПоложения492П Тогда
				
				#Область АмортизацияБУПоПравиламДо492П
				
			Если НЕ Пропустить Тогда
			
				СуммаАмортизации = 0;

				Если ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный Тогда
		 
					СуммаГодовойАмортизации = ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации / ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации * 12;
					СуммаАмортизации        = СуммаГодовойАмортизации / 12;

				ИначеЕсли ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда

					Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.Количество) Тогда
						Пропустить = Истина;
					КонецЕсли;

					Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.ОбъемПродукцииРаботДляВычисленияАмортизации) Тогда
						Если Реквизиты.ВыдаватьСообщения Тогда
							СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Объем продукции для вычисления не определен '"),
								Реквизиты.Регистратор, Отказ);
						КонецЕсли; 
						Пропустить = Истина;
					КонецЕсли;

					Если Пропустить Тогда
						СуммаАмортизации = 0 ;
					Иначе
						СуммаАмортизации = ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации * ВыборкаПоНМА.Количество / ВыборкаПоНМА.ОбъемПродукцииРаботДляВычисленияАмортизации;
					КонецЕсли;

				ИначеЕсли ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка Тогда

					ОстаточнаяСтоимость = Макс(0, ВыборкаПоНМА.СтоимостьНачальныйОстаток - ВыборкаПоНМА.АмортизацияНачальныйОстаток);
					ОставшийсяСрокПолезногоИспользования = ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации 
														 - УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВыборкаПоНМА.Период, Реквизиты.ДатаРасчета)
														 + 1;
					Коэффициент = ?(ВыборкаПоНМА.Коэффициент = 0, 1, ВыборкаПоНМА.Коэффициент);
					
					СуммаАмортизации = ?(ОставшийсяСрокПолезногоИспользования > 0,
										 ОстаточнаяСтоимость * (Коэффициент / ОставшийсяСрокПолезногоИспользования),
										 0);

				КонецЕсли;

				ВсегоОсталосьСписать = ВыборкаПоНМА.СтоимостьНачальныйОстаток - ВыборкаПоНМА.АмортизацияНачальныйОстаток;
				
				Если ВсегоОсталосьСписать - СуммаАмортизации < 0.01 
					И ВсегоОсталосьСписать - СуммаАмортизации > 0 Тогда 
					СуммаАмортизации = ВсегоОсталосьСписать; // погрешности при вычислениях
				КонецЕсли;
				
				БУНеОкругленная = Мин(СуммаАмортизации, ВсегоОсталосьСписать);
				ПРНеОкругленная = ВыборкаПоНМА.СтоимостьПРНачальныйОстаток * (СуммаАмортизации / ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации);
				РазницаНеОкругленная = БУНеОкругленная - ПРНеОкругленная;
				СуммаАмортизацииБУ = Окр(БУНеОкругленная, 2);
				
				Если ВыборкаПоНМА.СтоимостьПРНачальныйОстаток = ВыборкаПоНМА.АмортизацияПРНачальныйОстаток Тогда 
					СуммаАмортизацииПР = 0;
				ИначеЕсли РазницаНеОкругленная < 0.005 И РазницаНеОкругленная > -0.005 Тогда 
					СуммаАмортизацииПР = СуммаАмортизацииБУ;
				Иначе
					СуммаАмортизацииПР = Окр(ПРНеОкругленная, 2);
				КонецЕсли;
				
			КонецЕсли;

				#КонецОбласти
				
			Иначе
				
				#Область АмортизацияБУПоПравилам492П
				
				Если НЕ Пропустить Тогда
					
					ДатаНачала		= Макс(НачалоДня(ВыборкаПоНМА.Период),НачалоМесяца(Реквизиты.ДатаРасчета));
					
					ВыбытиеАктивов = ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.ПередачаНМА")
								 ИЛИ ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.СписаниеНМА");
								 
					Если ТипЗнч(Реквизиты.Регистратор) = Тип("ДокументСсылка.БНФОАмортизацияНМА") Тогда
						ДатаОкончания	= КонецМесяца(Реквизиты.ДатаРасчета);
					ИначеЕсли ВыбытиеАктивов И ВключатьДеньВыбытияВнеоборотногоАктиваВРасчетАмортизации Тогда
						ДатаОкончания	= КонецДня(Реквизиты.ДатаРасчета);
					Иначе
						ДатаОкончания	= КонецДня(Реквизиты.ДатаРасчета) - 24*60*60;
					КонецЕсли;
					
					ЧислоДнейНачисления		= БНФООбщегоНазначенияНФОКлиентСервер.РазностьДатВКалендарныхДнях(ДатаНачала, ДатаОкончания);
					ЧислоДнейВГоду			= БНФООбщегоНазначенияНФОКлиентСервер.РазностьДатВКалендарныхДнях(НачалоГода(ДатаНачала), КонецГода(ДатаОкончания));
					
					СуммаАмортизации = 0;
					
					Если ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.Линейный Тогда
						
						// используем алгоритм расчета амортизации "по частям" с учетом изменения параметров амортизации за период
						СуммаАмортизации = РассчитатьСуммуАмортизацииЗаПериод(ТаблицаИзмененияПараметровАмортизации, ВыборкаПоНМА.НематериальныйАктив, ДатаНачала, ДатаОкончания);							
						
						// если фактический срок наступил - списываем весь остаток
						Если КонецМесяца(ДобавитьМесяц(ВыборкаПоНМА.Период, ВыборкаПоНМА.СрокПолезногоИспользованияБУ)) = КонецМесяца(Реквизиты.ДатаРасчета) Тогда 
							СуммаАмортизации = ВыборкаПоНМА.СтоимостьКонечныйОстаток - ВыборкаПоНМА.АмортизацияКонечныйОстаток;
						КонецЕсли;
						
					ИначеЕсли ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.ПропорциональноОбъемуПродукции Тогда
						
						Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.Количество) Тогда
							Пропустить = Истина;
						КонецЕсли;
						
						Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.ОбъемПродукцииРаботДляВычисленияАмортизации) Тогда
							Если Реквизиты.ВыдаватьСообщения Тогда
								СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Объем продукции для вычисления не определен '"),
								Реквизиты.Регистратор, Отказ);
							КонецЕсли; 
							Пропустить = Истина;
						КонецЕсли;
						
						Если Пропустить Тогда
							СуммаАмортизации = 0 ;
						Иначе
							СуммаАмортизации = ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации * ВыборкаПоНМА.Количество / ВыборкаПоНМА.ОбъемПродукцииРаботДляВычисленияАмортизации;
						КонецЕсли;
						
					ИначеЕсли ВыборкаПоНМА.СпособНачисленияАмортизации = Перечисления.СпособыНачисленияАмортизацииНМА.УменьшаемогоОстатка Тогда
						
						ОстаточнаяСтоимость = Макс(0, ВыборкаПоНМА.СтоимостьКонечныйОстаток - ВыборкаПоНМА.АмортизацияКонечныйОстаток);
						ОставшийсяСрокПолезногоИспользования = ВыборкаПоНМА.СрокИспользованияДляВычисленияАмортизации 
						- УправлениеВнеоборотнымиАктивами.ОпределитьФактическийСрокИспользования(ВыборкаПоНМА.Период, Реквизиты.ДатаРасчета)
						+ 1;
						Коэффициент = ?(ВыборкаПоНМА.Коэффициент = 0, 1, ВыборкаПоНМА.Коэффициент);
						
						СуммаАмортизации = ?(ОставшийсяСрокПолезногоИспользования > 0,
						ОстаточнаяСтоимость * (Коэффициент / ОставшийсяСрокПолезногоИспользования),
						0);
						
					КонецЕсли;
					
					ВсегоОсталосьСписать = ВыборкаПоНМА.СтоимостьКонечныйОстаток - ВыборкаПоНМА.АмортизацияКонечныйОстаток;
					
					Если ВсегоОсталосьСписать - СуммаАмортизации < 0.01 
						И ВсегоОсталосьСписать - СуммаАмортизации > 0 Тогда 
						СуммаАмортизации = ВсегоОсталосьСписать; // погрешности при вычислениях
					КонецЕсли;
					
					БУНеОкругленная = Мин(СуммаАмортизации, ВсегоОсталосьСписать);
					ПРНеОкругленная = ВыборкаПоНМА.СтоимостьПРКонечныйОстаток * (СуммаАмортизации / ВыборкаПоНМА.СтоимостьДляВычисленияАмортизации);
					РазницаНеОкругленная = БУНеОкругленная - ПРНеОкругленная;
					СуммаАмортизацииБУ = Окр(БУНеОкругленная, 2);
					
					Если ВыборкаПоНМА.СтоимостьПРКонечныйОстаток = ВыборкаПоНМА.АмортизацияПРКонечныйОстаток Тогда 
						СуммаАмортизацииПР = 0;
					ИначеЕсли РазницаНеОкругленная < 0.005 И РазницаНеОкругленная > -0.005 Тогда 
						СуммаАмортизацииПР = СуммаАмортизацииБУ;
					Иначе
						СуммаАмортизацииПР = Окр(ПРНеОкругленная, 2);
					КонецЕсли;
					
				КонецЕсли;
				
				#КонецОбласти
				
			КонецЕсли;
			
		КонецЕсли;
		
		Пропустить = Ложь;
		
		Если ОтражатьВНалоговомУчете Тогда
			
			Если ВыборкаПоНМА.НачислятьАмортизациюНУ Тогда
				
				Если ВыборкаПоНМА.АмортизацияНУОборот <> 0 Тогда
					Если Реквизиты.ВыдаватьСообщения И ТаблицаНМА.Количество() > 0 Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Амортизация (НУ) в этом месяце уже начислялась '"),
							Реквизиты.Регистратор, Отказ, Реквизиты.Регистратор);
					КонецЕсли; 
					Пропустить = Истина; 
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(ВыборкаПоНМА.МетодНачисленияАмортизации) Тогда
					Если Реквизиты.ВыдаватьСообщения Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Не указан метод начисления амортизации (НУ) в учетной политике организации '"),
							Реквизиты.Регистратор, Отказ, Реквизиты.Регистратор);
					КонецЕсли;
					Пропустить = Истина;                                                                                     
				КонецЕсли;
				
				Если НЕ ПоддержкаПоложения492П И НЕ ЗначениеЗаполнено(ВыборкаПоНМА.СтоимостьНУНачальныйОстаток) Тогда
					Пропустить = Истина;
				ИначеЕсли ПоддержкаПоложения492П И НЕ ЗначениеЗаполнено(ВыборкаПоНМА.СтоимостьНУКонечныйОстаток) Тогда
					Пропустить = Истина;
				КонецЕсли;
				
				Если ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Линейный
					И ВыборкаПоНМА.СрокПолезногоИспользования = 0 Тогда
					Если Реквизиты.ВыдаватьСообщения Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'Срок использования (НУ) равен нулю '"),
							Реквизиты.Регистратор, Отказ, Реквизиты.Регистратор);
					КонецЕсли; 
					Пропустить = Истина;
				КонецЕсли;
				
				Если ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Нелинейный
					И НЕ ЗначениеЗаполнено(ВыборкаПоНМА.АмортизационнаяГруппа) Тогда
					Если Реквизиты.ВыдаватьСообщения Тогда
						СообщитьОбОшибкеПриНачисленииАмортизации(ВыборкаПоНМА, НСтр("ru = 'В справочнике не указана амортизационная группа '"),
							Реквизиты.Регистратор, Отказ, Реквизиты.Регистратор);
					КонецЕсли; 
					Пропустить = Истина;
				КонецЕсли;
				
				
				#Область АмортизацияБУПоПравиламДо492П
				
				Если НЕ Пропустить Тогда
					
					СуммаАмортизации = 0;
					
					Если ВыборкаПоНМА.СчетУчета = ВыборкаПоНМА.СчетНачисленияАмортизации Тогда // НИОКР.
						СтоимостьДляВычисления = ВыборкаПоНМА.ПервоначальнаяСтоимостьНУ;
					Иначе
						СтоимостьДляВычисления = ВыборкаПоНМА.СтоимостьНУНачальныйОстаток;
					КонецЕсли; 
					
					СрокИспользованияДляВычисления = Макс(1, ВыборкаПоНМА.СрокПолезногоИспользования);
					
					СпециальныйКоэффициент = ?(ВыборкаПоНМА.СпециальныйКоэффициент = 0, 1, ВыборкаПоНМА.СпециальныйКоэффициент);
					
					Если ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Линейный Тогда
						
						СтоимостьДляВычисления         = СтоимостьДляВычисления - ВыборкаПоНМА.АмортизацияПриПереходеКЛинейномуМетоду;
						СрокИспользованияДляВычисления = СрокИспользованияДляВычисления - ВыборкаПоНМА.ФактическийСрокПриПереходеКЛинейномуМетоду;
						
						СуммаАмортизации = СтоимостьДляВычисления / СрокИспользованияДляВычисления * СпециальныйКоэффициент;
						
					ИначеЕсли ВыборкаПоНМА.МетодНачисленияАмортизации = Перечисления.МетодыНачисленияАмортизации.Нелинейный Тогда
						
						НормаАмортизации = УправлениеВнеоборотнымиАктивами.ПолучитьНормуПоАмортизационнойГруппе(ВыборкаПоНМА.АмортизационнаяГруппа);
						
						Если НЕ (НормаАмортизации = Неопределено) Тогда
							
							СуммаАмортизации = (ВыборкаПоНМА.СтоимостьНУНачальныйОстаток - ВыборкаПоНМА.АмортизацияНУНачальныйОстаток)
							* НормаАмортизации / 100 
							* СпециальныйКоэффициент;
							
						КонецЕсли;
						
					КонецЕсли;
					
					ВсегоОсталосьСписать = ВыборкаПоНМА.СтоимостьНУНачальныйОстаток - ВыборкаПоНМА.АмортизацияНУНачальныйОстаток;
					
					СуммаАмортизацииНУ = Окр(Мин(ВсегоОсталосьСписать, СуммаАмортизации), 2);
					
				КонецЕсли;
				
				#КонецОбласти
					
			КонецЕсли;
			
		КонецЕсли;
		
		СуммаАмортизацииВР = СуммаАмортизацииБУ - СуммаАмортизацииНУ - СуммаАмортизацииПР;			
		
		Если СуммаАмортизацииБУ > 0
		 ИЛИ СуммаАмортизацииНУ > 0
		 ИЛИ СуммаАмортизацииПР <> 0
		 ИЛИ СуммаАмортизацииВР <> 0 Тогда
	
			СтрокаАмортизации = ТаблицаАмортизации.Добавить();

			СтрокаАмортизации.ОбъектУчета               = ВыборкаПоНМА.НематериальныйАктив;
			СтрокаАмортизации.СчетУчета                 = ВыборкаПоНМА.СчетУчета;
			СтрокаАмортизации.СчетНачисленияАмортизации = ВыборкаПоНМА.СчетНачисленияАмортизации;
			СтрокаАмортизации.НаправлениеАмортизации    = ВыборкаПоНМА.СпособыОтраженияРасходовПоАмортизации;
			СтрокаАмортизации.СуммаАмортизацииБУ        = СуммаАмортизацииБУ;
			СтрокаАмортизации.СуммаАмортизацииНУ        = СуммаАмортизацииНУ;
			СтрокаАмортизации.СуммаАмортизацииПР        = ?(ПоддержкаПБУ18, СуммаАмортизацииПР, 0);
			СтрокаАмортизации.СуммаАмортизацииВР        = ?(ПоддержкаПБУ18, СуммаАмортизацииВР, 0);
			СтрокаАмортизации.Подразделение             = ВыборкаПоНМА.ПодразделениеОрганизации;
	
		КонецЕсли;
			
	КонецЦикла;

	Возврат ТаблицаАмортизации;
КонецФункции

