//проверим и создадим строковый доп. реквизит к документу заказ покупателя
Процедура ПроверитьДобавитьСтроковыйДопРеквизитДоговорыКонтрагентов(ИмяРеквизита, ДлинаСтроки)

    //пробуем найти доп. реквизит по заголовку
    ЭлПВХ_Ссылка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Заголовок",ИмяРеквизита);
    Если ЭлПВХ_Ссылка.Пустая() Тогда
        //если не найдено, то создадим новый
        ЭлПВХ = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
        ЭлПВХ.Заголовок = ИмяРеквизита;
        ЭлПВХ.НаборСвойств = Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_ДоговорыКонтрагентов;
        ЭлПВХ.Наименование = ЭлПВХ.Заголовок+" ("+ЭлПВХ.НаборСвойств+")";
        ЭлПВХ.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(ДлинаСтроки));
        ЭлПВХ.Записать();
        ЭлПВХ_Ссылка = ЭлПВХ.Ссылка;
    КонецЕсли;

    //проверим, что реквизит указан в наборе доп. реквизитов заказа покупателя
    НаборОб = Справочники.НаборыДополнительныхРеквизитовИСведений.Документ_ЗаказПокупателя.ПолучитьОбъект();
    Если НаборОб.ДополнительныеРеквизиты.Найти(ЭлПВХ_Ссылка, "Свойство") = Неопределено Тогда
        //добавим реквизит в набор
        СтрНаборОб = НаборОб.ДополнительныеРеквизиты.Добавить();
        СтрНаборОб.Свойство = ЭлПВХ_Ссылка;
    КонецЕсли;
    Если НаборОб.Модифицированность() Тогда
        //запишем при необходимости
        НаборОб.Записать();
    КонецЕсли;

КонецПроцедуры

//проверим и создадим строковый доп. реквизит к документу заказ покупателя
Процедура ПроверитьДобавитьЧисловойДопДоговорыКонтрагентов(ИмяРеквизита, Разрядность = 10, ЧислоЗнаковПослеЗапятой = 2) Экспорт 

    //пробуем найти доп. реквизит по заголовку
    ЭлПВХ_Ссылка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Заголовок",ИмяРеквизита);
    Если ЭлПВХ_Ссылка.Пустая() Тогда
        //если не найдено, то создадим новый
        ЭлПВХ = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
        ЭлПВХ.Заголовок = ИмяРеквизита;
        ЭлПВХ.НаборСвойств = Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_ДоговорыКонтрагентов;
        ЭлПВХ.Наименование = ЭлПВХ.Заголовок+" ("+ЭлПВХ.НаборСвойств+")";
        ЭлПВХ.ТипЗначения = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(Разрядность,ЧислоЗнаковПослеЗапятой));
        ЭлПВХ.Записать();
        ЭлПВХ_Ссылка = ЭлПВХ.Ссылка;
    КонецЕсли;

    //проверим, что реквизит указан в наборе доп. реквизитов заказа покупателя
    НаборОб = Справочники.НаборыДополнительныхРеквизитовИСведений.Документ_ЗаказПокупателя.ПолучитьОбъект();
    Если НаборОб.ДополнительныеРеквизиты.Найти(ЭлПВХ_Ссылка, "Свойство") = Неопределено Тогда
        //добавим реквизит в набор
        СтрНаборОб = НаборОб.ДополнительныеРеквизиты.Добавить();
        СтрНаборОб.Свойство = ЭлПВХ_Ссылка;
    КонецЕсли;
    Если НаборОб.Модифицированность() Тогда
        //запишем при необходимости
        НаборОб.Записать();
    КонецЕсли;

КонецПроцедуры


//ДокОб = Документы.ЗаказПокупателя.СоздатьДокумент();
//.....
//записать в доп. реквизиты ORDER_ID и другие данные
//УстановитьДополнительныйРеквизитОбъекта(ДокОб, "ORDER_ID", "32312312");
//.....
//////////////////////////
Процедура УстановитьДополнительныйРеквизитОбъекта(Объект, ИмяРеквизита, ЗначениеРеквизита) Экспорт

    ЭлПВХ_Ссылка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Заголовок",ИмяРеквизита);
    Если ЭлПВХ_Ссылка.Пустая() Тогда
        Сообщить("Не найден доп. реквизит "+ИмяРеквизита+", значение не установлено!");
        Возврат;
    КонецЕсли;

    СтрДопРеквизиты = Объект.ДополнительныеРеквизиты.Найти(ЭлПВХ_Ссылка, "Свойство");
    Если СтрДопРеквизиты = Неопределено Тогда
        СтрДопРеквизиты = Объект.ДополнительныеРеквизиты.Добавить();
    КонецЕсли;
    СтрДопРеквизиты.Свойство = ЭлПВХ_Ссылка;
    СтрДопРеквизиты.Значение = ЗначениеРеквизита;

КонецПроцедуры

Функция  ПрочитатьДополнительныйРеквизитОбъекта(Объект, ИмяРеквизита) Экспорт
    ЭлПВХ_Ссылка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Заголовок",ИмяРеквизита);
    Если ЭлПВХ_Ссылка.Пустая() Тогда
        //Сообщить("Не найден доп. реквизит "+ИмяРеквизита+", значение не установлено!");
        Возврат Неопределено;
    КонецЕсли;
    СтрДопРеквизиты = Объект.ДополнительныеРеквизиты.Найти(ЭлПВХ_Ссылка, "Свойство");
    Если СтрДопРеквизиты = Неопределено Тогда
        Возврат Неопределено;
    КонецЕсли;
    Возврат СтрДопРеквизиты.Значение;
КонецФункции

Функция ПолучитьЗначениеДопСведений(Ссылка ,Свойство) Экспорт

    Запрос = Новый Запрос;

    Запрос.УстановитьПараметр("Ссылка", Ссылка);
    Запрос.УстановитьПараметр("Свойство", Свойство);

    Запрос.Текст = "ВЫБРАТЬ
               |    ДополнительныеСведения.Значение
               |ИЗ
               |    РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
               |ГДЕ
               |    ДополнительныеСведения.Объект = &Ссылка
               |    И ДополнительныеСведения.Свойство = &Свойство";   

    Результат = Запрос.Выполнить().Выбрать();

    Если Результат.Количество() = 0 Тогда
        Возврат Неопределено;
    Иначе
        Результат.Следующий();

        Возврат Результат.Значение;
    КонецЕсли;
КонецФункции

Функция ЗаписатьЗначениеДопСведения(Объект, Свойство, Значение)
    Попытка
        НаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();

        НаборЗаписей.Отбор.Объект.Установить(Объект);
        НаборЗаписей.Отбор.Свойство.Установить(Свойство);

        НоваяСтрокаНабора = НаборЗаписей.Добавить();

        НоваяСтрокаНабора.Объект = Объект;
        НоваяСтрокаНабора.Свойство = Свойство;

        НоваяСтрокаНабора.Значение = Значение;

        НаборЗаписей.Записать();

        Возврат Истина;
    Исключение
        Возврат Ложь;
    КонецПопытки;
КонецФункции